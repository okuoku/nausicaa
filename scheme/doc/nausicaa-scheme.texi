\input texinfo.tex
@c %**start of header
@setfilename scheme.info
@settitle Scheme language for Nausicaa
@c %**end of header

@ignore

List of things to do
====================


@end ignore


@c page
@c ------------------------------------------------------------
@c Macros.
@c ------------------------------------------------------------

@include version.texiinc

@macro gnu{}
@acronym{GNU}
@end macro

@macro gpl{}
@acronym{GPL}
@end macro

@macro fdl{}
@acronym{FDL}
@end macro

@c ------------------------------------------------------------
@c Special notes.
@c ------------------------------------------------------------

@macro forunix{}
@strong{Unix:}
@end macro

@macro fixme{TEXT}
@strong{FIXME: \TEXT\}
@end macro

@c ------------------------------------------------------------
@c Scheme related macros.
@c ------------------------------------------------------------

@macro rnrs{VERSION}
@acronym{R\VERSION\RS}
@end macro

@macro rsix{}
Revised^6 Report on the algorithmic language Scheme
@end macro

@macro repl{}
@acronym{REPL}
@end macro

@macro srfi{}
@acronym{SRFI}
@end macro

@c Separating the @srfi{} macro from the number with a '--' rather than
@c a '-'  makes the expansion look  ugly in menu entries  under the Info
@c reader.  IMHO  this should not happen,  but it does; so  we live with
@c this, because the main purpose of this document is to provide an Info
@c version.
@macro ansrfi{NUM}
@srfi{}-\NUM\
@end macro

@macro clos{}
@acronym{CLOS}
@end macro

@macro library{SPEC}
@code{(\SPEC\)}
@end macro

@macro ffi{}
@acronym{FFI}
@end macro


@c ------------------------------------------------------------
@c Miscellaneous acronyms.
@c ------------------------------------------------------------

@macro ansi{}
@acronym{ANSI}
@end macro

@macro api{}
@acronym{API}
@end macro

@c Remember that @url is already used by Texinfo.
@macro aurl{}
@acronym{URL}
@end macro

@macro ascii{}
@acronym{ASCII}
@end macro

@macro cpu{}
@acronym{CPU}
@end macro

@macro ieee{}
@acronym{IEEE}
@end macro

@macro iso{}
@acronym{ISO}
@end macro

@macro posix{}
@acronym{POSIX}
@end macro

@macro utf{}
@acronym{UTF}
@end macro

@c ------------------------------------------------------------
@c Software acronyms.
@c ------------------------------------------------------------

@macro gmp{}
@acronym{GMP}
@end macro

@macro gcc{}
@acronym{GCC}
@end macro

@c ------------------------------------------------------------
@c Network related acronyms.
@c ------------------------------------------------------------

@macro http{}
@acronym{HTTP}
@end macro

@macro ftp{}
@acronym{FTP}
@end macro

@macro tcp{}
@acronym{TCP}
@end macro

@macro udp{}
@acronym{UDP}
@end macro

@macro smtp{}
@acronym{SMTP}
@end macro

@c ------------------------------------------------------------
@c Scheme language macros.
@c ------------------------------------------------------------

@macro func{NAME}
@code{\NAME\}
@end macro

@macro snull{}
@code{()}
@end macro

@macro true{}
@code{#t}
@end macro

@macro false{}
@code{#f}
@end macro

@macro keyword{NAME}
@code{#:\NAME\}
@end macro

@c ------------------------------------------------------------
@c C language macros.
@c ------------------------------------------------------------

@macro cfunc{NAME}
@code{\NAME\()}
@end macro

@macro cnull{}
@code{NULL}
@end macro

@c ------------------------------------------------------------
@c Macros for references to external documents.
@c ------------------------------------------------------------

@macro glibcref{NODE, TITLE}
@ref{\NODE\,\TITLE\,\TITLE\,libc}
@end macro

@c @macro rsixref{NODE, TITLE}
@c @ref{\NODE\,\TITLE\,\TITLE\,r6rs}
@c @end macro

@macro bibref{TAG}
@code{[\TAG\]}
@end macro

@c ------------------------------------------------------------
@c RNRS document macros.
@c ------------------------------------------------------------

@macro rsixlibrary{THING}
@code{(rnrs \THING\ (6))}
@end macro

@macro arbno{THING}
\THING\*
@end macro

@macro atleastone{THING}
\THING\+
@end macro

@c ------------------------------------------------------------

@macro meta{THING}
<\THING\>
@end macro

@macro metai{THING}
<\THING\1>
@end macro

@macro metaii{THING}
<\THING\2>
@end macro

@macro metaiii{THING}
<\THING\3>
@end macro

@c ------------------------------------------------------------

@macro hyper{THING}
<\THING\>
@end macro

@macro hyperi{THING}
<\THING\1>
@end macro

@macro hyperii{THING}
<\THING\2>
@end macro

@macro hyperiii{THING}
<\THING\3>
@end macro

@macro hypern{THING}
<\THING\n>
@end macro

@c ------------------------------------------------------------

@macro vari{THING}
@var{\THING\1}
@end macro

@macro varii{THING}
@var{\THING\2}
@end macro

@macro variii{THING}
@var{\THING\3}
@end macro

@macro variv{THING}
@var{\THING\4}
@end macro

@macro varn{THING}
@var{\THING\n}
@end macro

@macro vark{THING}
@var{\THING\k}
@end macro

@macro varj{THING}
@var{\THING\j}
@end macro

@c page
@c ------------------------------------------------------------
@c Values.
@c ------------------------------------------------------------

@set TITLE                      Scheme language for Nausicaa

@c To be used as @value{PACKAGE} whenever we need to include the full
@c name of this package.
@set PACKAGE                    Nausicaa/Scheme

@c To be used as @value{PACKAGE_NICKNAME} whenever we need to include
@c the nickname of the project: the name that is used to compose the
@c distribution tarball or the web address.
@set PACKAGE_NICKNAME           nausicaa-scheme

@c To be used as @value{AUTHOR} whenever we need to include the list of
@c authors of this document.
@set AUTHOR                     Marco Maggi

@c To be used as @value{AUTHOR_EMAIL} whenever we need to include the
@c email of the *single* author of this document.
@set AUTHOR_EMAIL               @email{marcomaggi@@gna.org}

@c To be used as @value{COPYRIGHT_YEARS} whenever we need to include the
@c list of copyright years.
@set COPYRIGHT_YEARS            2008, 2009


@c page
@c ------------------------------------------------------------
@c Copyright notice.
@c ------------------------------------------------------------

@copying
@noindent
This document describes version @version{} of @value{PACKAGE}, a set of
Scheme libraries defining a slightly modified @rnrs{6} Scheme language
and augmenting the features of the base and standard @rnrs{6} libraries.

@noindent
Copyright @copyright{} @value{COPYRIGHT_YEARS} by @value{AUTHOR} @value{AUTHOR_EMAIL}.

@quotation
Permission is granted to copy, distribute and/or modify this document
under the terms of the @gnu{} Free Documentation License, Version 1.2 or
any later version published by the Free Software Foundation; with no
Invariant Sections being ``@gnu{} Free Documentation License'' and
``@gnu{} General Public License'', no Front--Cover Texts, and no
Back--Cover Texts.  A copy of the license is included in the section
entitled ``@gnu{} Free Documentation License''.
@end quotation

This document embeds an unofficial assemblage of several documents
reformatted in Texinfo; the reformatting author and maintainer is
@value{AUTHOR} @value{AUTHOR_EMAIL}.  See the appendix ``Credits'' for
the list of original documents and their authors.
@end copying

@c page
@c ------------------------------------------------------------
@c Headers.
@c ------------------------------------------------------------

@titlepage
@title @value{TITLE}
@subtitle Version @version{}
@page
@vskip 0pt plus 1filll
@insertcopying
@end titlepage

@c ------------------------------------------------------------

@ifinfo
@dircategory Development
@direntry
* scheme: (scheme).             @value{TITLE}.
@end direntry
@end ifinfo

@c ------------------------------------------------------------

@ignore
@syncodeindex tp cp
@syncodeindex vr cp
@syncodeindex fn cp
@end ignore


@c page
@ifnottex
@node Top
@top @value{TITLE}

@insertcopying

@menu
* overview::                    Overview of the package.
* lang::                        The Scheme language.
* baselib::                     Base library.
* stdlib::                      Standard libraries.
* naulib::                      The @library{nausicaa} language.
* more::                        More libraries.
* foreign::                     Foreign functions interface.

Appendices

* binfmt::                      Launching programs directly.
* credits::                     Credits.
* Package License::             @gnu{} General Public License.
* Documentation License::       @gnu{} Free Documentation License.
* references::                  Bibliography and references.

Indexes

* examples::                    Scheme programming examples.
* concept index::               An entry for each concept.
* function index::              An entry for each function.
* variable index::              An entry for each variable.
* type index::                  An entry for each type.

@detailmenu
 --- The Detailed Node Listing ---

Overview of the package

* overview copying::            Copying this document.
* overview implementation::     Supported implementations.
* overview porting::            On writing portable code.
* overview libraries::          Other libraries.
* overview binfmt::             Launching Scheme programs directly.

The Scheme language

* lang overview::               Overview of Scheme.
* lang requirements::           Requirement levels.
* lang numbers::                Numbers.
* lang lex::                    Lexical syntax and datum syntax.
* lang basic::                  Semantic concepts.
* lang entry::                  Entry format.
* lang library::                Libraries.
* lang programs::               Top-level programs.
* lang syntax::                 Primitive syntax.
* lang expansion::              Expansion process.
* lang derived::                Sample definitions for derived forms.

Overview of Scheme

* lang overview basic types::        Basic types.
* lang overview expressions::        Expressions.
* lang overview variables::          Variables and binding.
* lang overview definitions::        Definitions.
* lang overview forms::              Forms.
* lang overview procedures::         Procedures.
* lang overview syntax::             Procedure calls and syntactic keywords.
* lang overview assignment::         Assignment.
* lang overview macros::             Derived forms and macros.
* lang overview data::               Syntactic data and datum values.
* lang overview continuations::      Continuations.
* lang overview libraries::          Libraries.
* lang overview programs::           Top--level programs.

Numbers

* lang numbers tower::          Numerical tower.
* lang numbers exactness::      Exactness.
* lang numbers fix flo::        Fixnums and flonums.
* lang numbers requirements::   Implementation requirements.
* lang numbers inf nan::        Infinities and NaNs.
* lang numbers minus zero::     Distinguished @math{-0.0}.

Lexical syntax and datum syntax

* lang lex notation::           Notation.
* lang lex syntax::             Lexical syntax.
* lang lex datum::              Datum syntax.

Lexical syntax

* lang lex syntax formal account::           Formal account.
* lang lex syntax line endings::             Line endings.
* lang lex syntax whitespace and comments::  Whitespace and comments.
* lang lex syntax identifiers::              Identifiers.
* lang lex syntax booleans::                 Booleans.
* lang lex syntax characters::               Characters.
* lang lex syntax strings::                  Strings.
* lang lex syntax numbers::                  Numbers.

Datum syntax

* lang lex datum formal account::       Formal account.
* lang lex datum pairs and lists::      Pairs and lists.
* lang lex datum vectors::              Vectors.
* lang lex datum bytevectors::          Bytevectors.
* lang lex datum abbreviations::        Abbreviations.

Semantic concepts

* lang basic programs and libraries::        Programs and libraries.
* lang basic variables keywords regions::    Variables, keywords,
                                             and regions.
* lang basic exceptions::                    Exceptional situations.
* lang basic argument checking::             Argument checking.
* lang basic syntax violations::             Syntax violations.
* lang basic safety::                        Safety.
* lang basic boolean values::                Boolean values.
* lang basic multiple return values::        Multiple return values.
* lang basic unspecified behavior::          Unspecified behavior.
* lang basic storage model::                 Storage model.
* lang basic proper tail recursion::         Proper tail recursion.
* lang basic dynamic extent::                Dynamic extent and the
                                             dynamic environment.

Entry format

* lang entry syntax::                Syntax entries.
* lang entry procedure::             Procedure entries.
* lang entry implementation resp::   Implementation responsibilities.
* lang entry other kinds::           Other kinds of entries.
* lang entry equivalent::            Equivalent entries.
* lang entry evaluation examples::   Evaluation examples.
* lang entry naming conventions::    Naming conventions.

Libraries

* lang library form::           Library form.
* lang library import export::  Import and export levels.
* lang library examples::       Examples.

Library form

* lang library form decl::      Library declaration.
* lang library form names::     Library names.
* lang library form export::    The export specification.
* lang library form import::    The import specification.
* lang library form version::   The version reference.
* lang library form conflicts:: Handling conflicts.
* lang library form body::      Library bodies.

Top--level programs

* lang programs syntax::        Top-level program syntax.
* lang programs semantics::     Top-level program semantics.

Primitive syntax

* lang syntax primitive::       Primitive expression types.
* lang syntax macros::          Macros.

Base library

* baselib types::               Base types.
* baselib definitions::         Definitions.
* baselib bodies::              Bodies.
* baselib expressions::         Expressions.
* baselib predicates::          Equivalence and procedure predicates.
* baselib math::                Mathematics.
* baselib booleans::            Booleans.
* baselib lists::               Pairs and lists.
* baselib symbols::             Symbols.
* baselib characters::          Characters.
* baselib strings::             Strings.
* baselib vectors::             Vectors.
* baselib errors::              Errors and violations.
* baselib control::             Control features.
* baselib iteration::           Iteration.
* baselib quasiquotation::      Quasiquotation.
* baselib syntax binding::      Binding constructs for syntactic keywords.
* baselib transformers::        Macro transformers.
* baselib tail call::           Tail calls and tail contexts.

Definitions

* baselib definitions variables::  Variables definitions.
* baselib definitions syntax::     Syntax definitions.

Expressions

* baselib expressions quotation::       Quotation.
* baselib expressions procedures::      Procedures.
* baselib expressions conditionals::    Conditionals.
* baselib expressions assignments::     Assignments.
* baselib expressions derived cond::    Derived conditionals.
* baselib expressions binding::         Binding constructs.
* baselib expressions sequencing::      Sequencing.

Mathematics

* baselib math exactness::      Propagation of exactness and inexactness.
* baselib math infinities::     Representability of infinities and NaNs.
* baselib math semantics::      Semantics of common operations.
* baselib math ops::            Numerical operations.
* baselib math string::         Numerical input and output.

Semantics of common operations

* baselib math semantics integer::      Integer division.
* baselib math semantics trascend::     Transcendental functions.

Numerical operations

* baselib math ops type pred::          Numerical type predicates.
* baselib math ops exactness::          Generic conversion.
* baselib math ops value pred::         Value predicates.
* baselib math ops comparison::         Comparison.
* baselib math ops arithmetic::         Arithmetic operations.
* baselib math ops part::               Part separation.
* baselib math ops trascend::           Trascendental functions.
* baselib math ops exponentiation::     Exponentiation functions.
* baselib math ops complex::            Complex numbers functions.

Standard libraries

* stdlib unicode::              Unicode.
* stdlib bytevector::           Bytevectors.
* stdlib list::                 List utilities.
* stdlib sorting::              Sorting.
* stdlib control::              Control structures.
* stdlib records::              Records.
* stdlib exceptions::           Exceptions and conditions.
* stdlib io::                   Input/output.
* stdlib files::                File system.
* stdlib programlib::           Command line access and exit values.
* stdlib arithmetics::          Arithmetics.
* stdlib syntax-case::          Syntax-case.
* stdlib hashtable::            Hashtables.
* stdlib enum::                 Enumerations.
* stdlib complib::              Composite library.
* stdlib eval::                 Evaluation.
* stdlib mutable pairs::        Mutable pairs.
* stdlib mutable strings::      Mutable strings.
* stdlib rfive compat::         @rnrs{5} compatibility.

Unicode

* stdlib unicode characters::   Characters.
* stdlib unicode strings::      Strings.

Bytevectors

* stdlib bytevector endianness::        Endianness.
* stdlib bytevector general::           General operations.
* stdlib bytevector bytes and octets::  Operations on bytes and octets.
* stdlib bytevector integers::          Operations on integers of
                                        arbitrary size.
* stdlib bytevector integers 16::       Operations on 16-bit integers.
* stdlib bytevector integers 32::       Operations on 32-bit integers.
* stdlib bytevector integers 64::       Operations on 64-bit integers.
* stdlib bytevector flonum::            Operations on @ieee{} 754
                                        representations.
* stdlib bytevector strings::           Operations on strings.

Records

* stdlib records intro::                Introduction.
* stdlib records mutability::           Mutability and equivalence
                                        of records.
* stdlib records syntactic layer::      Syntactic layer.
* stdlib records procedural layer::     Procedural layer.
* stdlib records inspection::           Inspection.

Exceptions and conditions

* stdlib exceptions exceptions::        Exceptions.
* stdlib exceptions conditions::        Conditions.

Conditions

* stdlib exceptions conditions objects::        Condition objects.
* stdlib exceptions conditions types::          Standard condition types.

Input/output

* stdlib io conditions::        Condition types.
* stdlib io port::              Port input/output.
* stdlib io simple::            Simple input/output.

Port input/output

* stdlib io port file names::           File names.
* stdlib io port file options::         File options.
* stdlib io port buffer modes::         Buffer modes.
* stdlib io port transcoders::          Transcoders.
* stdlib io port eof object::           End of file object.
* stdlib io port io ports::             Input and output ports.
* stdlib io port input ports::          Input ports.
* stdlib io port binary input::         Binary input.
* stdlib io port textual input::        Textual input.
* stdlib io port output ports::         Output ports.
* stdlib io port binary output::        Binary output.
* stdlib io port textual output::       Textual output.
* stdlib io port input output ports::   Input/output ports.

Arithmetics

* stdlib arithmetics bitwise::          Bitwise operations.
* stdlib arithmetics fixnum::           Fixnums.
* stdlib arithmetics flonum::           Flonums.
* stdlib arithmetics exact bitwise::    Exact bitwise arithmetic.

Syntax--case

* stdlib syntax-case hygiene::          Hygiene.
* stdlib syntax-case objects::          Syntax objects.
* stdlib syntax-case transformers::     Transformers.
* stdlib syntax-case parsing::          Parsing input and producing
                                        output.
* stdlib syntax-case identifier::       Identifier predicates.
* stdlib syntax-case conversion::       Syntax-object and datum
                                        conversions.
* stdlib syntax-case temporaries::      Generating lists of temporaries.
* stdlib syntax-case derived::          Derived forms and procedures.
* stdlib syntax-case violations::       Syntax violations.

Examples

* stdlib syntax-case define-macro::     Common Lisp style macros.

Hashtables

* stdlib hashtable constructors::       Constructors.
* stdlib hashtable procedures::         Procedures.
* stdlib hashtable inspection::         Inspection.
* stdlib hashtable hash functions::     Hash functions.

The @library{nausicaa} language

* naulib features::             Feature--based conditional expansion.
* naulib funcs::                Functions.
* naulib macros::               Syntactic abstractions.
* naulib parameters::           Paremeter objects.
* naulib shared::               External representation for data
                                with shared structure.
* naulib environment::          Accessing environment variables.
* naulib unimplemented::        Unimplemented error condition.

More libraries

* lists::                       Lists library.
* char-sets::                   Character sets library.
* strings::                     Strings library.
* vectors::                     Vectors library.
* checks::                      Lightweight testing.

Low level libraries

* one-dimension::               One dimensional extended ranges.

List library

* lists intro::                 Introduction.
* lists linear update::         Linear update procedures.
* lists improper::              Improper lists.
* lists cons::                  Constructors.
* lists pred::                  Predicates.
* lists select::                Selectors.
* lists misc::                  Length, append, concatenate,
                                reverse, zip and count.
* lists fold::                  Fold, unfold and map.
* lists filter::                Filtering and partitioning.
* lists search::                Searching.
* lists delete::                Deletion.
* lists alist::                 Association lists.
* lists set::                   Set operations on lists.

Constructors

* lists cons pair::             Pair constructors.
* lists cons list::             List constructors.

Predicates

* lists pred kind::             Kinds of list.
* lists pred null::             Null predicates.
* lists pred pair::             Pair predicates.
* lists pred cmp::              Comparison.

Fold, unfold and map

* lists fold fold::             Folding.
* lists fold reduce::           Reducing.
* lists fold unfold::           Unfolding.
* lists fold map::              Mapping.

Character sets

* char-sets intro::             Introduction.
* char-sets make::              Constructors.
* char-sets pred::              Predicates.
* char-sets inspect::           Inspection.
* char-sets ops::               Operations.
* char-sets std::               Standard char sets.

Strings library

* strings intro::               Introduction.
* strings convention::          Interface conventions.
* strings views::               Views over strings.
* strings pred::                Predicates.
* strings compar::              Comparison.
* strings map::                 Mapping functions.
* strings case::                Case mapping.
* strings fold::                Fold, unfold.
* strings select::              Selection.
* strings prefix::              Prefixes and suffixes.
* strings search::              Searching.
* strings filter::              Filtering and deleting.
* strings list::                List and string conversion.
* strings replicate::           Replicate and rotate.
* strings misc::                Miscellaneous functions.
* strings kmp::                 Knuth--Morris--Pratt searching.

Knuth-Morris-Pratt searching

* strings kmp rv::              The restart vector.
* strings kmp step::            Single step of the search.
* strings kmp partial::         Partial string search.
* strings kmp string::          Full string search.

Vectors library

* vectors intro::               Introduction.
* vectors view::                Views over vectors.
* vectors cons::                Constructors.
* vectors pred::                Predicates.
* vectors map::                 Mapping functions.
* vectors fold::                Folding and unfolding.
* vectors search::              Searching.
* vectors mutators::            Mutators.
* vectors conv::                Conversion.

Lightweight testing

* checks select::               Selecting tests to be run.
* checks eval::                 Evaluating code.
* checks result::               Handling multiple results.
* checks debug::                Printing debug messages.

One dimensional extended ranges

* one-dimension intro::         Data types and conventions.
* one-dimension make::          Constructors.
* one-dimension pred::          Predicates.
* one-dimension inspect::       Inspection.
* one-dimension ops::           Operations.

Launching programs directly

* binfmt overview::             Overview of the mechanism.
* binfmt loading::              Loading the module.
* binfmt registry::             The registry.
* binfmt using::                Using the mechanism.
* binfmt control::              Controlling the mechanism.
* binfmt hints::                Hints.
* binfmt scheme::               Running Scheme programs.

Scheme programming examples

* examples macros::             Exploring macro mechanisms.

Exploring macro mechanisms

* examples macros phases::      Bits about phases.
* examples macros underscore::  About the underscore.
* examples macros patterns::    About writing patterns.
* examples macros literals::    About literal identifiers.
* examples macros syntax::      Syntax objects and you.

Syntax objects and you

* examples macros syntax not::   What we do not do.

@end detailmenu
@end menu

@end ifnottex


@c page
@node overview
@chapter Overview of the package


@value{PACKAGE} is a set of Scheme libraries defining a slightly
modified @rnrs{6} Scheme language and augmenting the features of the
base and standard @rnrs{6} libraries.  To use it when writing a library
or program, we import @library{nausicaa} in place of @library{rnrs}:

@example
(import (nausicaa)
  ---)
@end example

@noindent
the only way to use the @library{nausicaa} library is as language
definition, if we try to import both @library{nausicaa} and @library{rnrs}
conflicts may arise.


@menu
* overview copying::            Copying this document.
* overview implementation::     Supported implementations.
* overview porting::            On writing portable code.
* overview libraries::          Other libraries.
* overview binfmt::             Launching Scheme programs directly.
@end menu

@c page
@node overview copying
@section Copying this document

@insertcopying

@c page
@node overview implementation
@section Supported implementations


@value{PACKAGE} supports the following Scheme implementations:

@table @strong
@item Larceny
It is an also @rnrs{6} compliant implementation of the Scheme
programming language.  It is the creation of William D.  Clinger, Lars T
Hansen and Lightship Software, Incorporated, with contributions by other
people.

@center @url{http://www.ccs.neu.edu/home/will/Larceny/}

@item Ikarus
It is an almost @rnrs{6} compliant implementation of the Scheme
programming language.  It is the creation of Abdulaziz Ghuloum.

@center @url{http://launchpad.net/ikarus}

@item Mosh
It is an @rnrs{6} compliant implementation of the Scheme programming
language.  It is the creation of Taro Minowa (higepon).

@center @url{http://code.google.com/p/mosh-scheme/}

@item Ypsilon
It is an @rnrs{6} compliant implementation of the Scheme programming
language.  It is the creation of Yoshikatsu Fujita at LittleWing
Company.

@center @url{http://code.google.com/p/ypsilon/}
@end table

@noindent
when referring to all of these we can use the @acronym{LIMY}
abbreviation.

@c page
@node overview porting
@section On writing portable code


Some Scheme implementation defines itself as @rnrs{6} compliant, but
then does not provide a compliant implementation of some of the
features.  Also, the @rnrs{6} language has some limitations and
underspecifications.

@value{PACKAGE} tries, through the @library{nausicaa} language, to fill
some (not all) of the gaps and to provide a base language to write
portable programs among the supported Scheme implementations.

@library{nausicaa} has the following purposes:

@itemize
@item
To make available all the bindings from @library{rnrs}.

@item
To make self--defining @rnrs{6} Scheme implementations more @rnrs{6}
compliant, in the sense of ``programs that work''.  This means, for
example: If an underlying implementation does not provide a requested
function, @library{nausicaa} tries to provide a working replacement.

@item
To offer by default some features from the @srfi{} libraries which where
voted to @emph{final} status.  It may be that those features are
redefined or extended.

@item
To change the specification of some function to make it more usable.
@end itemize

This document describes the features of the @rnrs{6} language by
embedding the original @rnrs{6} documents; these specifications are
available in the chapters:

@itemize -
@item
@ref{lang,The Scheme language}.

@item
@ref{baselib,Base library}.

@item
@ref{stdlib,Standard libraries}.
@end itemize

@noindent
the features of @library{nausicaa} that extend the @library{rnrs} language
are described in the chapter @ref{naulib,The augmented language}.


@c page
@node overview libraries
@section Other libraries


In addition to the focus on code portability among @rnrs{6} Scheme
implementations, the Nausicaa distribution is concerned to provide a
good base of features and interfaces to foreign libraries (mostly coded
in the C language).

With these purposes, @value{PACKAGE} installs libraries implementing and
extending some of the @srfi{}s that were voted to @emph{final} status,
and providing a unified @api{} to the foreign functions interfaces of
the underlying implementations.

These specifications are described in the chapters:

@itemize -
@item
@ref{more,More libraries}

@item
@ref{foreign,Foreign functions interface}
@end itemize

@c page
@node overview binfmt
@section Launching Scheme programs directly


The appendix @ref{binfmt,Launching programs directly} describes an
optional feature of @value{PACKAGE} that activates Scheme programs
execution through the mechanism of the @code{binfmt_misc} Linux kernel
module.  It is available only on Unix--like platforms running the Linux
kernel.

@c page
@node lang
@chapter The Scheme language


@menu
* lang overview::               Overview of Scheme.
* lang requirements::           Requirement levels.
* lang numbers::                Numbers.
* lang lex::                    Lexical syntax and datum syntax.
* lang basic::                  Semantic concepts.
* lang entry::                  Entry format.
* lang library::                Libraries.
* lang programs::               Top-level programs.
* lang syntax::                 Primitive syntax.
* lang expansion::              Expansion process.
* lang derived::                Sample definitions for derived forms.
@end menu


@c page
@node lang overview
@section Overview of Scheme

@c originally this chapter was the "struct" file.

@menu
* lang overview basic types::        Basic types.
* lang overview expressions::        Expressions.
* lang overview variables::          Variables and binding.
* lang overview definitions::        Definitions.
* lang overview forms::              Forms.
* lang overview procedures::         Procedures.
* lang overview syntax::             Procedure calls and syntactic keywords.
* lang overview assignment::         Assignment.
* lang overview macros::             Derived forms and macros.
* lang overview data::               Syntactic data and datum values.
* lang overview continuations::      Continuations.
* lang overview libraries::          Libraries.
* lang overview programs::           Top--level programs.
@end menu


@noindent
This chapter gives an overview of Scheme's semantics.  The purpose of
this overview is to explain enough about the basic concepts of the
language to facilitate understanding of the subsequent chapters of the
report, which are organized as a reference manual.  Consequently, this
overview is not a complete introduction to the language, nor is it
precise in all respects or normative in any way.

Following Algol, Scheme is a statically scoped programming language.
Each use of a variable is associated with a lexically apparent binding
of that variable.

Scheme has latent as opposed to manifest types.  Types are associated
with objects (also called values) rather than with variables.  (Some
authors refer to languages with latent types as untyped, weakly typed or
dynamically typed languages.)  Other languages with latent types are
Python, Ruby, Smalltalk, and other dialects of Lisp.  Languages with
manifest types (sometimes referred to as strongly typed or statically
typed languages) include Algol 60, C, C#, Java, Haskell, and ML.

All objects created in the course of a Scheme computation, including
procedures and continuations, have unlimited extent.  No Scheme object
is ever destroyed.  The reason that implementations of Scheme do not
(usually!) run out of storage is that they are permitted to reclaim the
storage occupied by an object if they can prove that the object cannot
possibly matter to any future computation.  Other languages in which
most objects have unlimited extent include C#, Java, Haskell, most Lisp
dialects, ML, Python, Ruby, and Smalltalk.

Implementations of Scheme must be properly tail--recursive.  This allows
the execution of an iterative computation in constant space, even if the
iterative computation is described by a syntactically recursive
procedure.  Thus with a properly tail--recursive implementation,
iteration can be expressed using the ordinary procedure--call mechanics,
so that special iteration constructs are useful only as syntactic sugar.

Scheme was one of the first languages to support procedures as objects
in their own right.  Procedures can be created dynamically, stored in
data structures, returned as results of procedures, and so on.  Other
languages with these properties include Common Lisp, Haskell, ML, Ruby,
and Smalltalk.

One distinguishing feature of Scheme is that continuations, which in
most other languages only operate behind the scenes, also have
``first--class'' status.  First--class continuations are useful for
implementing a wide variety of advanced control constructs, including
non--local exits, backtracking, and coroutines.

In Scheme, the argument expressions of a procedure call are evaluated
before the procedure gains control, whether the procedure needs the
result of the evaluation or not.  C, C#, Common Lisp, Python, Ruby, and
Smalltalk are other languages that always evaluate argument expressions
before invoking a procedure.  This is distinct from the lazy--evaluation
semantics of Haskell, or the call--by--name semantics of Algol 60, where
an argument expression is not evaluated unless its value is needed by
the procedure.

Scheme's model of arithmetic provides a rich set of numerical types and
operations on them.  Furthermore, it distinguishes @emph{exact} and
@emph{inexact} number objects: Essentially, an exact number object
corresponds to a number exactly, and an inexact number object is the
result of a computation that involved rounding or other errors.


@c page
@node lang overview basic types
@subsection Basic types


Scheme programs manipulate @emph{objects}, which are also referred to as
@emph{values}.  Scheme objects are organized into sets of values called
@emph{types}.  This section gives an overview of the fundamentally
important types of the Scheme language.  More types are described in
later chapters.

@quotation
@emph{Note:} As Scheme is latently typed, the use of the term
@emph{type} in this report differs from the use of the term in the
context of other languages, particularly those with manifest typing.
@end quotation

@subsubheading Booleans

A boolean is a truth value, and can be either true or false.  In Scheme,
the object for ``false'' is written @false{}.  The object for ``true''
is written @true{}.  In most places where a truth value is expected,
however, any object different from @false{} counts as true.

@subsubheading Numbers

Scheme supports a rich variety of numerical data types, including
objects representing integers of arbitrary precision, rational numbers,
complex numbers, and inexact numbers of various kinds.  @ref{lang numbers}
gives an overview of the structure of Scheme's numerical tower.

@subsubheading Characters

Scheme characters mostly correspond to textual characters.  More
precisely, they are isomorphic to the @emph{scalar values} of the
Unicode standard.

@subsubheading Strings

Strings are finite sequences of characters with fixed length and thus
represent arbitrary Unicode texts.

@subsubheading Symbols

A symbol is an object representing a string, the symbol's @emph{name}.
Unlike strings, two symbols whose names are spelled the same way are
never distinguishable.  Symbols are useful for many applications; for
instance, they may be used the way enumerated values are used in other
languages.

@subsubheading Pairs and lists

A pair is a data structure with two components.  The most common use of
pairs is to represent (singly linked) lists, where the first component
(the ``car'') represents the first element of the list, and the second
component (the ``cdr'') the rest of the list.  Scheme also has a
distinguished empty list, which is the last cdr in a chain of pairs that
form a list.

@subsubheading Vectors

Vectors, like lists, are linear data structures representing finite
sequences of arbitrary objects.  Whereas the elements of a list are
accessed sequentially through the chain of pairs representing it, the
elements of a vector are addressed by integer indices.  Thus, vectors
are more appropriate than lists for random access to elements.

@subsubheading Procedures

Procedures are values in Scheme.


@c page
@node lang overview expressions
@subsection Expressions


The most important elements of Scheme code are @emph{expressions}.
Expressions can be @emph{evaluated}, producing a @emph{value}.
(Actually, any number of values; @ref{lang basic multiple return values}.)
The most fundamental expressions are literal expressions:

@example
#t @result{} #t
23 @result{} 23
@end example

This notation means that the expression @true{} evaluates to @true{},
that is, the value for ``true'', and that the expression @code{23}
evaluates to a number object representing the number 23.

Compound expressions are formed by placing parentheses around their
subexpressions.  The first subexpression identifies an operation; the
remaining subexpressions are operands to the operation:

@example
(+ 23 42) @result{} 65
(+ 14 (* 23 42)) @result{} 980
@end example

In the first of these examples, @func{+} is the name of the built--in
operation for addition, and @code{23} and @code{42} are the operands.
The expression @code{(+ 23 42)} reads as ``the sum of 23 and 42''.
Compound expressions can be nested---the second example reads as ``the
sum of 14 and the product of 23 and 42''.

As these examples indicate, compound expressions in Scheme are always
written using the same prefix notation.  As a consequence, the
parentheses are needed to indicate structure.  Consequently,
``superfluous'' parentheses, which are often permissible in mathematical
notation and also in many programming languages, are not allowed in
Scheme.

As in many other languages, whitespace (including line endings) is not
significant when it separates subexpressions of an expression, and can
be used to indicate structure.


@c page
@node lang overview variables
@subsection Variables and binding


Scheme allows identifiers to stand for locations containing values.
These identifiers are called variables.  In many cases, specifically
when the location's value is never modified after its creation, it is
useful to think of the variable as standing for the value directly.

@example
(let ((x 23)
      (y 42))
  (+ x y))
@result{} 65
@end example

In this case, the expression starting with @func{let} is a binding
construct.  The parenthesized structure following the @func{let} lists
variables alongside expressions: the variable @code{x} alongside
@code{23}, and the variable @code{y} alongside @code{42}.  The
@func{let} expression binds @code{x} to 23, and @code{y} to 42.  These
bindings are available in the @emph{body} of the @func{let} expression,
@code{(+ x y)}, and only there.


@c page
@node lang overview definitions
@subsection Definitions


The variables bound by a @func{let} expression are @emph{local}, because
their bindings are visible only in the @func{let}'s body.  Scheme also
allows creating top--level bindings for identifiers as follows:

@example
(define x 23)
(define y 42)
(+ x y) @result{} 65
@end example

@noindent
(These are actually ``top--level'' in the body of a top--level program
or library; @ref{lang library}.)

The first two parenthesized structures are @emph{definitions}; they
create top--level bindings, binding @code{x} to 23 and @code{y} to 42.
Definitions are not expressions, and cannot appear in all places where
an expression can occur.  Moreover, a definition has no value.

Bindings follow the lexical structure of the program: When several
bindings with the same name exist, a variable refers to the binding that
is closest to it, starting with its occurrence in the program and going
from inside to outside, and referring to a top--level binding if no
local binding can be found along the way:

@example
(define x 23)
(define y 42)
(let ((y 43))
  (+ x y)) @result{} 66

(let ((y 43))
  (let ((y 44))
    (+ x y))) @result{} 67
@end example


@c page
@node lang overview forms
@subsection Forms


While definitions are not expressions, compound expressions and
definitions exhibit similar syntactic structure:

@example
(define x 23)
(* x 2)
@end example

While the first line contains a definition, and the second an
expression, this distinction depends on the bindings for @func{let}
and @func{*}.  At the purely syntactical level, both are @emph{forms},
and @emph{form} is the general name for a syntactic part of a Scheme
program.  In particular, @code{23} is a @emph{subform} of the form
@code{(define x 23)}.


@c page
@node lang overview procedures
@subsection Procedures


Definitions can also be used to define procedures:

@example
(define (f x)
  (+ x 42))

(f 23) @result{} 65
@end example

A procedure is, slightly simplified, an abstraction of an expression
over objects.  In the example, the first definition defines a procedure
called @func{f}.  (Note the parentheses around @code{f x}, which
indicate that this is a procedure definition.)  The expression @code{(f
23)} is a procedure call, meaning, roughly, ``evaluate @code{(+ x 42)}
(the body of the procedure) with @var{x} bound to 23''.

As procedures are objects, they can be passed to other procedures:

@example
(define (f x)
  (+ x 42))

(define (g p x)
  (p x))

(g f 23) @result{} 65
@end example

In this example, the body of @func{g} is evaluated with @var{p} bound to
@func{f} and @var{x} bound to 23, which is equivalent to @code{(f 23)},
which evaluates to 65.

In fact, many predefined operations of Scheme are provided not by
syntax, but by variables whose values are procedures.  The @func{+}
operation, for example, which receives special syntactic treatment in
many other languages, is just a regular identifier in Scheme, bound to a
procedure that adds number objects.  The same holds for @func{*} and
many others:

@example
(define (h op x y)
  (op x y))

(h + 23 42) @result{} 65
(h * 23 42) @result{} 966
@end example

Procedure definitions are not the only way to create procedures.  A
@func{lambda} expression creates a new procedure as an object, with no
need to specify a name:

@example
((lambda (x) (+ x 42)) 23) @result{} 65
@end example

The entire expression in this example is a procedure call; @code{(lambda
(x) (+ x 42))}, evaluates to a procedure that takes a single number
object and adds 42 to it.


@c page
@node lang overview syntax
@subsection Procedure calls and syntactic keywords


Whereas @code{(+ 23 42)}, @code{(f 23)}, and @code{((lambda (x) (+ x
42)) 23)} are all examples of procedure calls, @func{lambda} and
@func{let} expressions are not.  This is because @func{let}, even though
it is an identifier, is not a variable, but is instead a @emph{syntactic
keyword}.  A form that has a syntactic keyword as its first
subexpression obeys special rules determined by the keyword.  The
@func{let} identifier in a definition is also a syntactic keyword.
Hence, definitions are also not procedure calls.

The rules for the @func{lambda} keyword specify that the first subform
is a list of parameters, and the remaining subforms are the body of the
procedure.  In @func{let} expressions, the first subform is a list of
binding specifications, and the remaining subforms constitute a body of
expressions.

Procedure calls can generally be distinguished from these @emph{special
forms} by looking for a syntactic keyword in the first position of a
form: if the first position does not contain a syntactic keyword, the
expression is a procedure call.  (So--called @emph{identifier macros}
allow creating other kinds of special forms, but are comparatively
rare.)  The set of syntactic keywords of Scheme is fairly small, which
usually makes this task fairly simple.  It is possible, however, to
create new bindings for syntactic keywords; @ref{lang overview macros} below.


@c page
@node lang overview assignment
@subsection Assignment


Scheme variables bound by definitions or @func{let} or @func{lambda}
expressions are not actually bound directly to the objects specified in
the respective bindings, but to locations containing these objects.  The
contents of these locations can subsequently be modified destructively
via @emph{assignment}:

@example
(let ((x 23))
  (set! x 42)
  x) @result{} 42
@end example

In this case, the body of the @func{let} expression consists of two
expressions which are evaluated sequentially, with the value of the
final expression becoming the value of the entire @func{let} expression.
The expression @code{(set! x 42)} is an assignment, saying ``replace the
object in the location referenced by @code{x} with 42''.  Thus, the
previous value of @code{x}, 23, is replaced by 42.


@c page
@node lang overview macros
@subsection Derived forms and macros


Many of the special forms specified in this report can be translated
into more basic special forms.  For example, a @func{let} expression can
be translated into a procedure call and a @func{lambda} expression.  The
following two expressions are equivalent:

@example
(let ((x 23)
      (y 42))
  (+ x y))
@result{} 65

((lambda (x y) (+ x y)) 23 42)
@result{} 65
@end example

Special forms like @func{let} expressions are called @emph{derived
forms} because their semantics can be derived from that of other kinds
of forms by a syntactic transformation.  Some procedure definitions are
also derived forms.  The following two definitions are equivalent:

@example
(define (f x)
  (+ x 42))

(define f
  (lambda (x)
    (+ x 42)))
@end example

In Scheme, it is possible for a program to create its own derived forms
by binding syntactic keywords to macros:

@example
(define-syntax def
  (syntax-rules ()
    ((def f (p ...) body)
     (define (f p ...)
       body))))

(def f (x)
  (+ x 42))
@end example

The @func{define-syntax} construct specifies that a parenthesized
structure matching the pattern @code{(def f (p ...) body)}, where
@func{f}, @code{p}, and @code{body} are pattern variables, is translated
to @code{(define (f p ...) body)}.  Thus, the @code{def} form appearing
in the example gets translated to:

@example
(define (f x)
  (+ x 42))
@end example

The ability to create new syntactic keywords makes Scheme extremely
flexible and expressive, allowing many of the features built into other
languages to be derived forms in Scheme.


@c page
@node lang overview data
@subsection Syntactic data and datum values


A subset of the Scheme objects is called @emph{datum values}.  These
include booleans, number objects, characters, symbols, and strings as
well as lists and vectors whose elements are data.  Each datum value may
be represented in textual form as a @emph{syntactic datum}, which can be
written out and read back in without loss of information.  A datum value
may be represented by several different syntactic data.  Moreover, each
datum value can be trivially translated to a literal expression in a
program by prepending a @code{'} (single quote) to a corresponding
syntactic datum:

@example
'23             @result{} 23
'@true{}        @result{} @true{}
'foo            @result{} foo
'(1 2 3)        @result{} (1 2 3)
'#(1 2 3)       @result{} #(1 2 3)
@end example

The @code{'} shown in the previous examples is not needed for
representations of number objects or booleans.  The syntactic datum
@code{foo} represents a symbol with name ``foo'', and @code{'foo} is a
literal expression with that symbol as its value.  @code{(1 2 3)} is a
syntactic datum that represents a list with elements 1, 2, and 3, and
@code{'(1 2 3)} is a literal expression with this list as its value.
Likewise, @code{#(1 2 3)} is a syntactic datum that represents a vector
with elements 1, 2 and 3, and @code{'#(1 2 3)} is the corresponding
literal.

The syntactic data are a superset of the Scheme forms.  Thus, data can
be used to represent Scheme forms as data objects.  In particular,
symbols can be used to represent identifiers.

@example
'(+ 23 42)                      @result{} (+ 23 42)
'(define (f x) (+ x 42))        @result{} (define (f x) (+ x 42))
@end example

This facilitates writing programs that operate on Scheme source code, in
particular interpreters and program transformers.


@c page
@node lang overview continuations
@subsection Continuations


Whenever a Scheme expression is evaluated there is a @emph{continuation}
wanting the result of the expression.  The continuation represents an
entire (default) future for the computation.  For example, informally
the continuation of @code{3} in the expression

@example
(+ 1 3)
@end example

@noindent
adds 1 to it.  Normally these ubiquitous continuations are hidden behind
the scenes and programmers do not think much about them.  On rare
occasions, however, a programmer may need to deal with continuations
explicitly.

The @func{call-with-current-continuation} procedure (@ref{baselib control})
allows Scheme programmers to do that by creating a procedure that
reinstates the current continuation.

The @func{call-with-current-continuation} procedure accepts a procedure,
calls it immediately with an argument that is an @emph{escape
procedure}.  This escape procedure can then be called with an argument
that becomes the result of the call to
@func{call-with-current-continuation}.  That is, the escape procedure
abandons its own continuation, and reinstates the continuation of the
call to @func{call-with-current-continuation}.

In the following example, an escape procedure representing the
continuation that adds 1 to its argument is bound to @code{escape}, and
then called with 3 as an argument.  The continuation of the call to
@code{escape} is abandoned, and instead the 3 is passed to the
continuation that adds 1:

@example
(+ 1 (call-with-current-continuation
       (lambda (escape)
         (+ 2 (escape 3)))))
@result{} 4
@end example

An escape procedure has unlimited extent: It can be called after the
continuation it captured has been invoked, and it can be called multiple
times.  This makes @func{call-with-current-continuation} significantly
more powerful than typical non--local control constructs such as
exceptions in other languages.


@c page
@node lang overview libraries
@subsection Libraries


Scheme code can be organized in components called @emph{libraries}.
Each library contains definitions and expressions.  It can import
definitions from other libraries and export definitions to other
libraries.

The following library called @code{(hello)} exports a definition called
@func{hello-world}, and imports the base library (@ref{baselib}) and the
simple I/O library (@ref{stdlib io simple}).  The @func{hello-world}
export is a procedure that displays @code{Hello World} on a separate
line:

@example
(library (hello)
  (export hello-world)
  (import (rnrs base)
          (rnrs io simple))
  (define (hello-world)
    (display "Hello World")
    (newline)))
@end example


@c page
@node lang overview programs
@subsection Top--level programs


A Scheme program is invoked via a @emph{top--level program}.  Like a
library, a top--level program contains imports, definitions and
expressions, and specifies an entry point for execution.  Thus a
top--level program defines, via the transitive closure of the libraries
it imports, a Scheme program.

The following top--level program obtains the first argument from the
command line via the @code{command-line} procedure from the
@rsixlibrary{programs} library (@ref{stdlib programlib}).  It then opens
the file using @func{open-file-input-port} (@ref{stdlib io port input
ports}), yielding a @emph{port}, i.e. a connection to the file as a data
source, and calls the @func{get-bytes-all} procedure to obtain the
contents of the file as binary data.  It then uses @func{put-bytes} to
output the contents of the file to standard output:

@example
#!r6rs
(import (rnrs base)
        (rnrs io ports)
        (rnrs programs))
(let ((p (standard-output-port)))
  (put-bytevector p
                  (call-with-port
                      (open-file-input-port
                        (cadr (command-line)))
                    get-bytevector-all))
  (close-port p))
@end example


@c page
@node lang requirements
@section Requirement levels


The key words ``must'', ``must not'', ``should'', ``should not'',
``recommended'', ``may'', and ``optional'' in this report are to be
interpreted as described in @acronym{RFC} 2119.  Specifically:

@table @strong
@item must
This word means that a statement is an absolute requirement of the
specification.

@item must not
This phrase means that a statement is an absolute prohibition of the
specification.

@item should
This word, or the adjective ``recommended'', means that valid reasons
may exist in particular circumstances to ignore a statement, but that
the implications must be understood and weighed before choosing a
different course.

@item should not
This phrase, or the phrase ``not recommended'', means that valid reasons
may exist in particular circumstances when the behavior of a statement
is acceptable, but that the implications should be understood and
weighed before choosing the course described by the statement.

@item may
This word, or the adjective ``optional'', means that an item is truly
optional.
@end table

In particular, this report occasionally uses ``should'' to designate
circumstances that are outside the specification of this report, but
cannot be practically detected by an implementation; @ref{lang basic
argument checking}.  In such circumstances, a particular implementation
may allow the programmer to ignore the recommendation of the report and
even exhibit reasonable behavior.  However, as the report does not
specify the behavior, these programs may be unportable, that is, their
execution might produce different results on different implementations.

Moreover, this report occasionally uses the phrase ``not required'' to
note the absence of an absolute requirement.


@c page
@node lang numbers
@section Numbers


This chapter describes Scheme's model for numbers.  It is important to
distinguish between the mathematical numbers, the Scheme objects that
attempt to model them, the machine representations used to implement the
numbers, and notations used to write numbers.

In this report, the term @emph{number} refers to a mathematical number,
and the term @emph{number object} refers to a Scheme object representing
a number.  This report uses the types @emph{complex}, @emph{real},
@emph{rational}, and @emph{integer} to refer to both mathematical
numbers and number objects.  The @emph{fixnum} and @emph{flonum} types
refer to special subsets of the number objects, as determined by common
machine representations, as explained below.


@menu
* lang numbers tower::          Numerical tower.
* lang numbers exactness::      Exactness.
* lang numbers fix flo::        Fixnums and flonums.
* lang numbers requirements::   Implementation requirements.
* lang numbers inf nan::        Infinities and NaNs.
* lang numbers minus zero::     Distinguished @math{-0.0}.
@end menu


@c page
@node lang numbers tower
@subsection Numerical tower


Numbers may be arranged into a tower of subsets in which each level is a
subset of the level above it:

@example
number
  complex
  real
  rational
  integer
@end example

For example, 5 is an integer.  Therefore 5 is also a rational, a real,
and a complex.  The same is true of the number objects that model 5.

Number objects are organized as a corresponding tower of subtypes
defined by the predicates @code{number?}, @code{complex?}, @code{real?},
@code{rational?}, and @code{ integer?}; @ref{baselib math ops type pred}.
Integer number objects are also called @emph{integer objects}.

There is no simple relationship between the subset that contains a
number and its representation inside a computer.  For example, the
integer 5 may have several representations.  Scheme's numerical
operations treat number objects as abstract data, as independent of
their representation as possible.  Although an implementation of Scheme
may use many different representations for numbers, this should not be
apparent to a casual programmer writing simple programs.


@c page
@node lang numbers exactness
@subsection Exactness


It is useful to distinguish between number objects that are known to
correspond to a number exactly, and those number objects whose
computation involved rounding or other errors.  For example, index
operations into data structures may need to know the index exactly, as
may some operations on polynomial coefficients in a symbolic algebra
system.  On the other hand, the results of measurements are inherently
inexact, and irrational numbers may be approximated by rational and
therefore inexact approximations.  In order to catch uses of numbers
known only inexactly where exact numbers are required, Scheme explicitly
distinguishes @emph{exact} from @emph{inexact} number objects.  This
distinction is orthogonal to the dimension of type.

A number object is exact if it is the value of an exact numerical
literal or was derived from exact number objects using only exact
operations.  Exact number objects correspond to mathematical numbers in
the obvious way.

Conversely, a number object is inexact if it is the value of an inexact
numerical literal, or was derived from inexact number objects, or was
derived using inexact operations.  Thus inexactness is contagious.

Exact arithmetic is reliable in the following sense: If exact number
objects are passed to any of the arithmetic procedures described in
section ``Propagation of exactness and inexactness'', and an exact
number object is returned, then the result is mathematically correct.
This is generally not true of computations involving inexact number
objects because approximate methods such as floating--point arithmetic
may be used, but it is the duty of each implementation to make the
result as close as practical to the mathematically ideal result.


@c page
@node lang numbers fix flo
@subsection Fixnums and flonums


A @emph{fixnum} is an exact integer object that lies within a certain
implementation--dependent subrange of the exact integer objects.
(Library section on ``Fixnums'' describes a library for computing with
fixnums.)  Likewise, every implementation must designate a subset of its
inexact real number objects as @emph{flonums}, and to convert certain
external representations into flonums.  (Library section on ``Flonums''
describes a library for computing with flonums.)  Note that this does
not imply that an implementation must use floating--point
representations.


@c page
@node lang numbers requirements
@subsection Implementation requirements


Implementations of Scheme must support number objects for the entire
tower of subtypes given in section ``Numerical tower''.  Moreover,
implementations must support exact integer objects and exact rational
number objects of practically unlimited size and precision, and to
implement certain procedures (listed in ``Propagation of exactness and
inexactness'') so they always return exact results when given exact
arguments.  (``Practically unlimited'' means that the size and precision
of these numbers should only be limited by the size of the available
memory.)

Implementations may support only a limited range of inexact number
objects of any type, subject to the requirements of this section.  For
example, an implementation may limit the range of the inexact real
number objects (and therefore the range of inexact integer and rational
number objects) to the dynamic range of the flonum format.  Furthermore
the gaps between the inexact integer objects and rationals are likely to
be very large in such an implementation as the limits of this range are
approached.

An implementation may use floating point and other approximate
representation strategies for @emph{inexact} numbers.  This report
recommends, but does not require, that the @ieee{} floating--point
standards be followed by implementations that use floating--point
representations, and that implementations using other representations
should match or exceed the precision achievable using these
floating--point standards.

In particular, implementations that use floating--point representations
must follow these rules: A floating--point result must be represented
with at least as much precision as is used to express any of the inexact
arguments to that operation.  Potentially inexact operations such as
@func{sqrt}, when applied to exact arguments, should produce exact
answers whenever possible (for example the square root of an exact 4
ought to be an exact 2).  However, this is not required.  If, on the
other hand, an exact number object is operated upon so as to produce an
inexact result (as by @func{sqrt}), and if the result is represented in
floating point, then the most precise floating--point format available
must be used; but if the result is represented in some other way then
the representation must have at least as much precision as the most
precise floating--point format available.

It is the programmer's responsibility to avoid using inexact number
objects with magnitude or significand too large to be represented in the
implementation.


@c page
@node lang numbers inf nan
@subsection Infinities and NaNs


Some Scheme implementations, specifically those that follow the @ieee{}
floating--point standards, distinguish special number objects called,
@emph{negative infinity}, and @emph{NaN}.

Positive infinity is regarded as an inexact real (but not rational)
number object that represents an indeterminate number greater than the
numbers represented by all rational number objects.  Negative infinity
is regarded as an inexact real (but not rational) number object that
represents an indeterminate number less than the numbers represented by
all rational numbers.

A NaN is regarded as an inexact real (but not rational) number object so
indeterminate that it might represent any real number, including
positive or negative infinity, and might even be greater than positive
infinity or less than negative infinity.


@c page
@node lang numbers minus zero
@subsection Distinguished @math{-0.0}


Some Scheme implementations, specifically those that follow the @ieee{}
floating--point standards, distinguish between number objects for
@math{0.0} and @math{-0.0}, i.e., positive and negative inexact zero.
This report will sometimes specify the behavior of certain arithmetic
operations on these number objects.  These specifications are marked
with ``if @math{-0.0} is distinguished'' or ``implementations that
distinguish @math{-0.0}''.


@c page
@node lang lex
@section Lexical syntax and datum syntax


@menu
* lang lex notation::           Notation.
* lang lex syntax::             Lexical syntax.
* lang lex datum::              Datum syntax.
@end menu

@noindent
The syntax of Scheme code is organized in three levels:

@enumerate
@item
the @emph{lexical syntax} that describes how a program text is split
into a sequence of lexemes,

@item
the @emph{datum syntax}, formulated in terms of the lexical syntax, that
structures the lexeme sequence as a sequence of @emph{syntactic data},
where a syntactic datum is a recursively structured entity,

@item
the @emph{program syntax} formulated in terms of the datum syntax,
imposing further structure and assigning meaning to syntactic data.
@end enumerate

Syntactic data (also called @emph{external representations}) double as a
notation for objects, and Scheme's @rsixlibrary{io ports} library
(library section ``Port I/O'') provides the @func{get-datum} and
@func{put-datum} procedures for reading and writing syntactic data,
converting between their textual representation and the corresponding
objects.  Each syntactic datum represents a corresponding @emph{datum
value}.  A syntactic datum can be used in a program to obtain the
corresponding datum value using @code{quote} (@ref{baselib expressions
quotation}).

Scheme source code consists of syntactic data and (non--significant)
comments.  Syntactic data in Scheme source code are called @emph{forms}.
(A form nested inside another form is called a @emph{subform}.)
Consequently, Scheme's syntax has the property that any sequence of
characters that is a form is also a syntactic datum representing some
object.  This can lead to confusion, since it may not be obvious out of
context whether a given sequence of characters is intended to be a
representation of objects or the text of a program.  It is also a source
of power, since it facilitates writing programs such as interpreters or
compilers that treat programs as objects (or vice versa).

A datum value may have several different external representations.  For
example, both @code{#e28.000} and @code{#x1c} are syntactic data
representing the exact integer object 28, and the syntactic data
@code{(8 13)}, @code{( 08 13 )}, @code{(8 . (13 . ()))} all represent a
list containing the exact integer objects 8 and 13.  Syntactic data that
represent equal objects (in the sense of @func{equal?}; @ref{baselib
predicates}) are always equivalent as forms of a program.

Because of the close correspondence between syntactic data and datum
values, this report sometimes uses the term @emph{datum} for either a
syntactic datum or a datum value when the exact meaning is apparent from
the context.

An implementation must not extend the lexical or datum syntax in any
way, with one exception: it need not treat the syntax
@code{#!<identifier>}, for any <identifier> (@ref{lang lex syntax
identifiers}) that is not @code{r6rs}, as a syntax violation, and it may
use specific @code{#!}--prefixed identifiers as flags indicating that
subsequent input contains extensions to the standard lexical or datum
syntax.  The syntax @code{#!r6rs} may be used to signify that the input
afterward is written with the lexical syntax and datum syntax described
by this report.  @code{#!r6rs} is otherwise treated as a comment;
@ref{lang lex syntax whitespace and comments}.


@c page
@node lang lex notation
@subsection Notation


The formal syntax for Scheme is written in an extended @acronym{BNF}.
Non--terminals are written using angle brackets.  Case is insignificant
for non--terminal names.

All spaces in the grammar are for legibility.  @meta{Empty} stands for
the empty string.

The following extensions to @acronym{BNF} are used to make the
description more concise: @arbno{@meta{thing}} means zero or more
occurrences of @meta{thing}, and @atleastone{@meta{thing}} means at
least one @meta{thing}.

Some non-terminal names refer to the Unicode scalar values of the same
name: @meta{character tabulation} (U+0009), @meta{linefeed} (U+000A),
@meta{carriage return} (U+000D), @meta{line tabulation} (U+000B),
@meta{form feed} (U+000C), @meta{space} (U+0020), @meta{next line}
(U+0085), @meta{line separator} (U+2028), and @meta{paragraph separator}
(U+2029).


@c page
@node lang lex syntax
@subsection Lexical syntax


The lexical syntax determines how a character sequence is split into a
sequence of lexemes, omitting non--significant portions such as comments
and whitespace.  The character sequence is assumed to be text according
to the Unicode standard.  Some of the lexemes, such as identifiers,
representations of number objects, strings etc., of the lexical syntax
are syntactic data in the datum syntax, and thus represent objects.
Besides the formal account of the syntax, this section also describes
what datum values are represented by these syntactic data.

The lexical syntax, in the description of comments, contains a forward
reference to @meta{datum}, which is described as part of the datum
syntax.  Being comments, however, these @meta{datum}s do not play a
significant role in the syntax.

Case is significant except in representations of booleans, number
objects, and in hexadecimal numbers specifying Unicode scalar values.
For example, @code{#x1A} and @code{#X1a} are equivalent.  The identifier
@code{Foo} is, however, distinct from the identifier @code{FOO}.


@menu
* lang lex syntax formal account::           Formal account.
* lang lex syntax line endings::             Line endings.
* lang lex syntax whitespace and comments::  Whitespace and comments.
* lang lex syntax identifiers::              Identifiers.
* lang lex syntax booleans::                 Booleans.
* lang lex syntax characters::               Characters.
* lang lex syntax strings::                  Strings.
* lang lex syntax numbers::                  Numbers.
@end menu


@c page
@node lang lex syntax formal account
@subsubsection Formal account


@noindent
@meta{Interlexeme space} may occur on either side of any lexeme, but not
within a lexeme.

@meta{Identifier}s, @code{.}, @meta{number}s, @meta{character}s, and
@meta{boolean}s, must be terminated by a @meta{delimiter} or by the end
of the input.

The following two characters are reserved for future extensions to the
language: @code{@{ @}}

@example
<lexeme> -> <identifier> | <boolean> | <number>
         | <character> | <string>
         | ( | ) | [ | ] | #( | #vu8( | ' | ` | , | ,@@ | .
         | #' | #` | #, | #,@@
<delimiter> -> ( | ) | [ | ] | " | ; | #
         | <whitespace>
<whitespace> -> <character tabulation>
         | <linefeed> | <line tabulation> | <form feed>
         | <carriage return> | <next line>
         | <any character whose category is Zs, Zl, or Zp>
<line ending> -> <linefeed> | <carriage return>
         | <carriage return> <linefeed> | <next line>
         | <carriage return> <next line> | <line separator>
<comment> -> ; <all subsequent characters up to a <line ending>
                or <paragraph separator> >
         | <nested comment>
         | #; <interlexeme space> <datum>
         | #!r6rs
<nested comment> -> #| <comment text>
         <comment cont>* |#
<comment text> -> character sequence not containing #| or |#
<comment cont> -> <nested comment> <comment text>
<atmosphere> -> <whitespace> | <comment>
<interlexeme space> -> <atmosphere>*

<identifier> -> <initial> <subsequent>*
         | <peculiar identifier>
<initial> -> <constituent> | <special initial>
         | <inline hex escape>
<letter> -> a | b | c | ... | z
         | A | B | C | ... | Z
<constituent> -> <letter>
         | <any character whose Unicode scalar value is greater than
             127, and whose category is Lu, Ll, Lt, Lm, Lo, Mn,
             Nl, No, Pd, Pc, Po, Sc, Sm, Sk, So, or Co>
<special initial> -> ! | $ | % | & | * | / | : | < | =
         | > | ? | ^ | _ | ~
<subsequent> -> <initial> | <digit>
         | <any character whose category is Nd, Mc, or Me>
         | <special subsequent>
<digit> -> 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
<hex digit> -> <digit>
         | a | A | b | B | c | C | d | D | e | E | f | F
<special subsequent> -> + | - | . | @@
<inline hex escape> -> \x<hex scalar value>;
<hex scalar value> -> <hex digit>+
<peculiar identifier> -> + | - | ... | -> <subsequent>*
<boolean> -> #t | #T | #f | #F
<character> -> #\<any character>
         | #\<character name>
         | #\x<hex scalar value>
<character name> -> nul | alarm | backspace | tab
         | linefeed | newline | vtab | page | return
         | esc | space | delete
<string> -> " <string element>* "
<string element> -> <any character other than " or \>
         | \a | \b | \t | \n | \v | \f | \r
         | \" | \\
         | \<intraline whitespace>* <line ending>
            <intraline whitespace>*
         | <inline hex escape>
<intraline whitespace> -> <character tabulation>
         | <any character whose category is Zs>
@end example

A @meta{hex scalar value} represents a Unicode scalar value between
@code{0} and @code{#x10FFFF}, excluding the range @code{[#D800,
#xDFFF]}.

The rules for @meta{num R}, @meta{complex R}, @meta{real R}, @meta{ureal
R}, @meta{uinteger R}, and @meta{prefix R} below should be replicated
for R = 2, 8, 10, and 16.  There are no rules for @meta{decimal 2},
@meta{decimal 8}, and @meta{decimal 16}, which means that number
representations containing decimal points or exponents must be in
decimal radix.

@example
<number> -> <num 2> | <num 8>
         | <num 10> | <num 16>
<num R> -> <prefix R> <complex R>
<complex R> -> <real R> | <real R> @@ <real R>
         | <real R> + <ureal R> i | <real R> - <ureal R> i
         | <real R> + <naninf> i | <real R> - <naninf> i
         | <real R> + i | <real R> - i
         | + <ureal R> i | - <ureal R> i
         | + <naninf> i | - <naninf> i
         | + i | - i
<real R> -> <sign> <ureal R>
         | + <naninf> | - <naninf>
<naninf> -> nan.0 | inf.0
<ureal R> -> <uinteger R>
         | <uinteger R> / <uinteger R>
         | <decimal R> <mantissa width>
<decimal 10> -> <uinteger 10> <suffix>
         | . <digit 10>+ <suffix>
         | <digit 10>+ . <digit 10>* <suffix>
         | <digit 10>+ . <suffix>
<uinteger R> -> <digit R>+
<prefix R> -> <radix R> <exactness>
         | <exactness> <radix R>

<suffix> -> <empty>
         | <exponent marker> <sign> <digit 10>+
<exponent marker> -> e | E | s | S | f | F
         | d | D | l | L
<mantissa width> -> <empty>
         | | <digit 10>+
<sign> -> <empty> | + | -
<exactness> -> <empty>
         | #i| #I | #e| #E
<radix 2> -> #b| #B
<radix 8> -> #o| #O
<radix 10> -> <empty> | #d | #D
<radix 16> -> #x| #X
<digit 2> -> 0 | 1
<digit 8> -> 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7
<digit 10> -> <digit>
<digit 16> -> <hex digit>
@end example


@c page
@node lang lex syntax line endings
@subsubsection Line endings


Line endings are significant in Scheme in single--line comments
(@ref{lang lex syntax whitespace and comments}) and within string
literals.  In Scheme source code, any of the line endings in @meta{line
ending} marks the end of a line.  Moreover, the two--character line
endings @meta{carriage return} @meta{linefeed} and @meta{carriage
return} @meta{next line} each count as a single line ending.

In a string literal, a @meta{line ending} not preceded by a @code{\}
stands for a linefeed character, which is the standard line--ending
character of Scheme.


@c page
@node lang lex syntax whitespace and comments
@subsubsection Whitespace and comments


@emph{Whitespace} characters are spaces, linefeeds, carriage returns,
character tabulations, form feeds, line tabulations, and any other
character whose category is Zs, Zl, or Zp.  Whitespace is used for
improved readability and as necessary to separate lexemes from each
other.  Whitespace may occur between any two lexemes, but not within a
lexeme.  Whitespace may also occur inside a string, where it is
significant.

The lexical syntax includes several comment forms.  In all cases,
comments are invisible to Scheme, except that they act as delimiters,
so, for example, a comment cannot appear in the middle of an identifier
or representation of a number object.

A semicolon (@code{;}) indicates the start of a line comment.  The
comment continues to the end of the line on which the semicolon appears.

Another way to indicate a comment is to prefix a @meta{datum} (cf.
section ``Formal account'') with @code{#;}, possibly with
@meta{interlexeme space} before the @meta{datum}.  The comment consists
of the comment prefix @code{#;} and the @meta{datum} together.  This
notation is useful for ``commenting out'' sections of code.

Block comments may be indicated with properly nested @code{#|} and
@code{|#} pairs.

@example
#|
   The FACT procedure computes the factorial of a
   non-negative integer.
|#
(define fact
  (lambda (n)
    ;; base case
    (if (= n 0)
        #;(= n 1)
        1       ; identity of *
        (* n (fact (- n 1))))))
@end example

The lexeme @code{#!r6rs}, which signifies that the program text that
follows is written with the lexical and datum syntax described in this
report, is also otherwise treated as a comment.


@c page
@node lang lex syntax identifiers
@subsubsection Identifiers


@cindex Identifier, definition of
@cindex Definition of identifier


Most identifiers allowed by other programming languages are also
acceptable to Scheme.  In general, a sequence of letters, digits, and
``extended alphabetic characters'' is an identifier when it begins with
a character that cannot begin a representation of a number object.  In
addition, @code{+}, @code{-}, and @code{...} are identifiers, as is a
sequence of letters, digits, and extended alphabetic characters that
begins with the two--character sequence @code{->}.  Here are some
examples of identifiers:

@example
lambda         q                soup
list->vector   +                V17a
<=             a34kTMNs         ->-
the-word-recursion-has-many-meanings
@end example

Extended alphabetic characters may be used within identifiers as if they
were letters.  The following are extended alphabetic characters:

@example
! $ % & * + - . / : < = > ? @@ ^ _ ~
@end example

Moreover, all characters whose Unicode scalar values are greater than
127 and whose Unicode category is Lu, Ll, Lt, Lm, Lo, Mn, Mc, Me, Nd,
Nl, No, Pd, Pc, Po, Sc, Sm, Sk, So, or Co can be used within
identifiers.  In addition, any character can be used within an
identifier when specified via an @meta{inline hex escape}.  For example,
the identifier @code{H\x65;llo} is the same as the identifier
@code{Hello}.
@c , and the identifier @code{\x3BB;} is the same as the
@c identifier $\lambda$.

Any identifier may be used as a variable or as a syntactic keyword
(@ref{lang basic variables keywords regions} and @ref{lang syntax
macros}) in a Scheme program.  Any identifier may also be used as a
syntactic datum, in which case it represents a @emph{symbol}
(@ref{baselib symbols}).


@c page
@node lang lex syntax booleans
@subsubsection Booleans


The standard boolean objects for true and false have external
representations @true{} and @false{}.


@c page
@node lang lex syntax characters
@subsubsection Characters


Characters are represented using the notation @code{#\<character>} or
@code{#<character name>} or @code{#x<hex scalar value>}.

For example:

@example
#\a             lower case letter a
#\A             upper case letter A
#\(             left parenthesis
#\              space character
#\nul           U+0000
#\alarm         U+0007
#\backspace     U+0008
#\tab           U+0009
#\linefeed      U+000A
#\newline       U+000A
#\vtab          U+000B
#\page          U+000C
#\return        U+000D
#\esc           U+001B
#\space         U+0020 preferred way to write a space
#\delete        U+007F
#\xFF           U+00FF
#\x03BB         U+03BB
#\x00006587     U+6587
#\x0001z        &lexical exception
#\alarmx        &lexical exception
#\alarm x       U+0007 followed by x
#\Alarm         &lexical exception
#\alert         &lexical exception
#\xA            U+000A
#\xFF           U+00FF
#\xff           U+00FF
#\x ff          U+0078 followed by another datum, ff
#\x(ff)         U+0078 followed by another datum, a parenthesized ff
#\(x)           &lexical exception
#\(x            &lexical exception
#\((x)          U+0028 followed by another datum, parenthesized x
#\x00110000     &lexical exception out of range
#\x000000001    U+0001
#\xD800         &lexical exception in excluded range
@end example

(The notation @code{&lexical} means that the line in question is a
lexical syntax violation.)

Case is significant in @code{#\<character>}, and in @code{#\<character
name>}, but not in the @code{<hex scalar value>} of @code{#\x<hex scalar
value>}.  A @meta{character} must be followed by a @meta{delimiter} or
by the end of the input.  This rule resolves various ambiguous cases
involving named characters, requiring, for example, the sequence of
characters @code{#\space} to be interpreted as the space character
rather than as the character @code{#\s} followed by the identifier
@code{pace}.

@quotation
@emph{Note:} The @code{#\newline} notation is retained for backward
compatibility.  Its use is deprecated; @code{#\linefeed} should be used
instead.
@end quotation


@c page
@node lang lex syntax strings
@subsubsection Strings


String are represented by sequences of characters enclosed within
doublequotes (@code{"}).  Within a string literal, various escape
sequences represent characters other than themselves.  Escape sequences
always start with a backslash (@code{\}):

@table @code
@item \a
alarm, U+0007
@item \b
backspace, U+0008
@item \t
character tabulation, U+0009
@item \n
linefeed, U+000A
@item \v
line tabulation, U+000B
@item \f
formfeed, U+000C
@item \r
return, U+000D
@item \"
doublequote, U+0022
@item \
backslash, U+005C
@item \<intraline whitespace><line ending> <intraline whitespace>
nothing
@item \x<hex scalar value>;
specified character (note the terminating semi--colon).
@end table

These escape sequences are case-sensitive, except that the alphabetic
digits of a @meta{hex scalar value} can be uppercase or lowercase.

Any other character in a string after a backslash is a syntax violation.
Except for a line ending, any character outside of an escape sequence
and not a doublequote stands for itself in the string literal.
@c For example the single--character string literal {\tt "$\lambda$"}
@c (doublequote, a lower case lambda, doublequote) represents the same
@c string as {\tt "\{}x03bb;"}.
A line ending that does not follow a backslash stands for a linefeed
character.

Examples:

@example
"abc"           U+0061, U+0062, U+0063
"\x41;bc"       "Abc" ; U+0041, U+0062, U+0063
"\x41; bc"      "A bc"
                U+0041, U+0020, U+0062, U+0063
"\x41bc;"       U+41BC
"\x41"          &lexical exception
"\x;"           &lexical exception
"\x41bx;"       &lexical exception
"\x00000041;"   "A" ; U+0041
"\x0010FFFF;"   U+10FFFF
"\x00110000;"   &lexical exception
                out of range
"\x000000001;"  U+0001
"\xD800;"       &lexical exception
                in excluded range
"A
bc"             U+0041, U+000A, U+0062, U+0063
                if no space occurs after the A
@end example


@c page
@node lang lex syntax numbers
@subsubsection Numbers


The syntax of external representations for number objects is described
formally by the @meta{number} rule in the formal grammar.  Case is not
significant in external representations of number objects.

A representation of a number object may be written in binary, octal,
decimal, or hexadecimal by the use of a radix prefix.  The radix
prefixes are @code{#b} (binary), @code{#o} (octal), @code{#d} (decimal),
and @code{#x} (hexadecimal).  With no radix prefix, a representation of
a number object is assumed to be expressed in decimal.

A representation of a number object may be specified to be either exact
or inexact by a prefix.  The prefixes are @code{#e} for exact, and
@code{#i} for inexact.  An exactness prefix may appear before or after
any radix prefix that is used.  If the representation of a number object
has no exactness prefix, the constant is inexact if it contains a
decimal point, an exponent, or a nonempty mantissa width; otherwise it
is exact.

In systems with inexact number objects of varying precisions, it may be
useful to specify the precision of a constant.  For this purpose,
representations of number objects may be written with an exponent marker
that indicates the desired precision of the inexact representation.  The
letters @code{s}, @code{f}, @code{d}, and @code{l} specify the use of
@emph{short}, @emph{single}, @emph{double}, and @emph{long} precision,
respectively.  (When fewer than four internal inexact representations
exist, the four size specifications are mapped onto those available.
For example, an implementation with two internal representations may map
short and single together and long and double together.)  In addition,
the exponent marker @code{e} specifies the default precision for the
implementation.  The default precision has at least as much precision as
@emph{double}, but implementations may wish to allow this default to be
set by the user.

@example
3.1415926535898F0
       Round to single, perhaps 3.141593
0.6L0
       Extend to long, perhaps .600000000000000
@end example

A representation of a number object with nonempty mantissa width,
@code{x|p}, represents the best binary floating--point approximation of
@emph{x} using a @emph{p}--bit significand.  For example, @code{1.1|53}
is a representation of the best approximation of 1.1 in @ieee{} double
precision.  If @emph{x} is an external representation of an inexact real
number object that contains no vertical bar, then its numerical value
should be computed as though it had a mantissa width of 53 or more.

Implementations that use binary floating--point representations of real
number objects should represent @code{x|p} using a @emph{p}--bit
significand if practical, or by a greater precision if a @emph{p}--bit
significand is not practical, or by the largest available precision if
@emph{p} or more bits of significand are not practical within the
implementation.

@quotation
@emph{Note} The precision of a significand should not be confused with
the number of bits used to represent the significand.  In the @ieee{}
floating--point standards, for example, the significand's most
significant bit is implicit in single and double precision but is
explicit in extended precision.  Whether that bit is implicit or
explicit does not affect the mathematical precision.  In implementations
that use binary floating point, the default precision can be calculated
by calling the following procedure:

@example
(define (precision)
  (do ([n 0 (+ n 1)]
       [x 1.0 (/ x 2.0)])
    ((= 1.0 (+ 1.0 x)) n)))
@end example
@end quotation

@quotation
@emph{Note} When the underlying floating--point representation is
@ieee{} double precision, the @code{|p} suffix should not always be
omitted: Denormalized floating--point numbers have diminished precision,
and therefore their external representations should carry a @code{|p}
suffix with the actual width of the significand.
@end quotation

The literals @code{+inf.0} and @code{-inf.0} represent positive and
negative infinity, respectively.  The @code{+nan.0} literal represents
the NaN that is the result of @code{(/ 0.0 0.0)}, and may represent
other NaNs as well.  The @code{-nan.0} literal also represents a NaN.

If @emph{x} is an external representation of an inexact real number
object and contains no vertical bar and no exponent marker other than
@code{e}, the inexact real number object it represents is a flonum (see
library section ``Flonums'').  Some or all of the other external
representations of inexact real number objects may also represent
flonums, but that is not required by this report.


@c page
@node lang lex datum
@subsection Datum syntax


The datum syntax describes the syntax of syntactic data in terms of a
sequence of @meta{lexeme}s, as defined in the lexical syntax.

Syntactic data include the lexeme data described in the previous section
as well as the following constructs for forming compound data:

@itemize
@item
pairs and lists, enclosed by @code{( )} or @code{[ ]};

@item
vectors;

@item
bytevectors.
@end itemize


@menu
* lang lex datum formal account::       Formal account.
* lang lex datum pairs and lists::      Pairs and lists.
* lang lex datum vectors::              Vectors.
* lang lex datum bytevectors::          Bytevectors.
* lang lex datum abbreviations::        Abbreviations.
@end menu


@c page
@node lang lex datum formal account
@subsubsection Formal account


The following grammar describes the syntax of syntactic data in terms of
various kinds of lexemes defined in the grammar in @ref{lang lex
syntax,Lexical Syntax}

@example
<datum> -> <lexeme datum>
         | <compound datum>
<lexeme datum> -> <boolean> | <number>
         | <character> | <string> | <symbol>
<symbol> -> <identifier>
<compound datum> -> <list> | <vector> | <bytevector>
<list> -> (<datum>*) | [<datum>*]
         | (<datum>+ . <datum>) | [<datum>+ . <datum>]
         | <abbreviation>
<abbreviation> -> <abbrev prefix> <datum>
<abbrev prefix> -> ' | ` | , | ,@@
         | #' | #` | #, | #,@@
<vector> -> #(<datum>*)
<bytevector> -> #vu8(<u8>*)
<u8> -> <any <number> representing an exact integer in @{0, ..., 255@}>
@end example


@c page
@node lang lex datum pairs and lists
@subsubsection Pairs and lists


List and pair data, representing pairs and lists of values are
represented using parentheses or brackets.  Matching pairs of brackets
that occur in the rules of @meta{list} are equivalent to matching pairs
of parentheses.

The most general notation for Scheme pairs as syntactic data is the
``dotted'' notation @code{(<datum1> . <datum2>)} where @meta{datum1} is
the representation of the value of the car field and @meta{datum2} is
the representation of the value of the cdr field.  For example @code{(4
. 5)} is a pair whose car is 4 and whose cdr is 5.

A more streamlined notation can be used for lists: the elements of the
list are simply enclosed in parentheses and separated by spaces.  The
empty list is represented by @code{( )}.  For example,

@example
(a b c d e)
@end example

@noindent
and:

@example
(a . (b . (c . (d . (e . ())))))
@end example

@noindent
are equivalent notations for a list of symbols.

The general rule is that, if a dot is followed by an open parenthesis,
the dot, open parenthesis, and matching closing parenthesis can be
omitted in the external representation.

The sequence of characters @code{(4 . 5)} is the external representation
of a pair, not an expression that evaluates to a pair.  Similarly, the
sequence of characters @code{(+ 2 6)} is @emph{not} an external
representation of the integer 8, even though it @emph{is} an expression
(in the language of the @rsixlibrary{base} library) evaluating to the
integer 8; rather, it is a syntactic datum representing a three--element
list, the elements of which are the symbol @code{+} and the integers 2
and 6.


@c page
@node lang lex datum vectors
@subsubsection Vectors


Vector data, representing vectors of objects, are represented using the
notation @code{#(<datum> ...)}.  For example, a vector of length 3
containing the number object for zero in element 0, the list @code{(2 2
2 2)} in element 1, and the string @code{"Anna"} in element 2 can be
represented as follows:

@example
#(0 (2 2 2 2) "Anna")
@end example

This is the external representation of a vector, not an expression that
evaluates to a vector.


@c page
@node lang lex datum bytevectors
@subsubsection Bytevectors


Bytevector data, representing bytevectors (@ref{stdlib bytevector}), are
represented using the notation @code{#vu8(<u8> ...)}, where the
@meta{u8}s represent the octets of the bytevector.  For example, a
bytevector of length 3 containing the octets 2, 24, and 123 can be
represented as follows:

@example
#vu8(2 24 123)
@end example

This is the external representation of a bytevector, and also an
expression that evaluates to a bytevector.


@c page
@node lang lex datum abbreviations
@subsubsection Abbreviations


@example
 '<datum>        `<datum>        ,<datum>
,@@<datum>       #'<datum>       #`<datum>
#,<datum>      #,@@<datum>
@end example

Each of these is an abbreviation:

@table @code
@item '<datum>
for @code{(quote <datum>)},

@item `<datum>
for @code{(quasiquote <datum>)},

@item ,<datum>
for @code{(unquote <datum>)},

@item ,@@<datum>
for @code{(unquote-splicing <datum>)},

@item #'<datum>
for @code{(syntax <datum>)},

@item #`<datum>
for @code{(quasisyntax <datum>)},

@item #,<datum>
for @code{(unsyntax <datum>)}, and

@item #,@@<datum>
for @code{(unsyntax-splicing <datum>)}.
@end table


@c page
@node lang basic
@section Semantic concepts

@ignore
In the original document this chapter was the "basic.tex" file.
@end ignore

@menu
* lang basic programs and libraries::        Programs and libraries.
* lang basic variables keywords regions::    Variables, keywords,
                                             and regions.
* lang basic exceptions::                    Exceptional situations.
* lang basic argument checking::             Argument checking.
* lang basic syntax violations::             Syntax violations.
* lang basic safety::                        Safety.
* lang basic boolean values::                Boolean values.
* lang basic multiple return values::        Multiple return values.
* lang basic unspecified behavior::          Unspecified behavior.
* lang basic storage model::                 Storage model.
* lang basic proper tail recursion::         Proper tail recursion.
* lang basic dynamic extent::                Dynamic extent and the
                                             dynamic environment.
@end menu


@c page
@node lang basic programs and libraries
@subsection Programs and libraries


A Scheme program consists of a @emph{top--level} program together with a
set of @emph{libraries}, each of which defines a part of the program
connected to the others through explicitly specified exports and
imports.

@itemize
@item
A library consists of a set of export and import specifications and a
body, which consists of definitions, and expressions.

@item
A top--level program is similar to a library, but has no export
specifications.
@end itemize

Chapters ``Libraries'' and ``Top--level programs'' describe the syntax
and semantics of libraries and top--level programs, respectively.
Chapter ``Base library'' describes a base library that defines many of
the constructs traditionally associated with Scheme.  A separate report
describes the various @emph{standard libraries} provided by a Scheme
system.

The division between the base library and the other standard libraries
is based on use, not on construction.  In particular, some facilities
that are typically implemented as ``primitives'' by a compiler or the
run--time system rather than in terms of other standard procedures or
syntactic forms are not part of the base library, but are defined in
separate libraries.  Examples include the fixnums and flonums libraries,
the exceptions and conditions libraries, and the libraries for records.


@c page
@node lang basic variables keywords regions
@subsection Variables, keywords, and regions


@cindex Keyword, definition of
@cindex Definition of keyword

@cindex Variable, definition of
@cindex Definition of variable

@cindex Environment, definition of
@cindex Definition of environment

@cindex Binding constructs, definition of
@cindex Definition of binding constructs

@cindex Definitions, definition of
@cindex Definition of definitions

@cindex Region, definition of
@cindex Definition of region


Within the body of a library or top--level program, an identifier may
name a kind of syntax, or it may name a location where a value can be
stored.  An identifier that names a kind of syntax is called a
@emph{keyword}, and is said to be @emph{bound} to that kind of syntax
(or, in the case of a syntactic abstraction, a @emph{transformer} that
translates the syntax into more primitive forms).  An identifier that
names a location is called a @emph{variable} and is said to be
@emph{bound} to that location.  At each point within a top--level
program or a library, a specific, fixed set of identifiers is bound.
The set of these identifiers, the set of @emph{visible bindings}, is
known as the @emph{environment} in effect at that point.

Certain forms are used to create syntactic abstractions and to bind
keywords to transformers for those new syntactic abstractions, while
other forms create new locations and bind variables to those locations.
Collectively, these forms are called @emph{binding constructs}.  Some
binding constructs take the form of @emph{definitions}, while others are
expressions.  With the exception of exported library bindings, a binding
created by a definition is visible only within the body in which the
definition appears, e.g. the body of a library, top--level program, or
@func{lambda} expression.  Exported library bindings are also visible
within the bodies of the libraries and top--level programs that import
them.

Expressions that bind variables include the @func{lambda}, @func{let},
@func{let*}, @func{letrec}, @func{letrec*}, @func{let-values}, and
@func{let*-values} forms from the base library.  Of these, @func{lambda}
is the most fundamental.  Variable definitions appearing within the body
of such an expression, or within the bodies of a library or top--level
program, are treated as a set of @func{letrec*} bindings.  In addition,
for library bodies, the variables exported from the library can be
referenced by importing libraries and top--level programs.

Expressions that bind keywords include the @func{let-syntax} and
@func{letrec-syntax} forms.  A @func{define} form is a definition that
creates a variable binding, and a @code{define-syntax} form is a
definition that creates a keyword binding.

Scheme is a statically scoped language with block structure.  To each
place in a top--level program or library body where an identifier is
bound there corresponds a @emph{region} of code within which the binding
is visible.  The region is determined by the particular binding
construct that establishes the binding; if the binding is established by
a @func{lambda} expression, for example, then its region is the entire
@func{lambda} expression.  Every mention of an identifier refers to the
binding of the identifier that establishes the innermost of the regions
containing the use.  If a use of an identifier appears in a place where
none of the surrounding expressions contains a binding for the
identifier, the use may refer to a binding established by a definition
or import at the top of the enclosing library or top--level program.  If
there is no binding for the identifier, it is said to be @emph{unbound}.


@c page
@node lang basic exceptions
@subsection Exceptional situations


A variety of exceptional situations are distinguished in this report,
among them violations of syntax, violations of a procedure's
specification, violations of implementation restrictions, and
exceptional situations in the environment.  When an exceptional
situation is detected by the implementation, an @emph{exception is
raised}, which means that a special procedure called the @emph{current
exception handler} is called.  A program can also raise an exception,
and override the current exception handler; @ref{stdlib exceptions}.

When an exception is raised, an object is provided that describes the
nature of the exceptional situation.  The report uses the condition
system described in library section ``Conditions'' to describe
exceptional situations, classifying them by condition types.

Some exceptional situations allow continuing the program if the
exception handler takes appropriate action.  The corresponding
exceptions are called @emph{continuable}.  For most of the exceptional
situations described in this report, portable programs cannot rely upon
the exception being continuable at the place where the situation was
detected.  For those exceptions, the exception handler that is invoked
by the exception should not return.  In some cases, however, continuing
is permissible, and the handler may return.  @ref{stdlib exceptions}

Implementations must raise an exception when they are unable to continue
correct execution of a correct program due to some @emph{implementation
restriction}.  For example, an implementation that does not support
infinities must raise an exception with condition type
@code{&implementation-restriction} when it evaluates an expression whose
result would be an infinity.

Some possible implementation restrictions such as the lack of
representations for NaNs and infinities are anticipated by this report,
and implementations typically must raise an exception of the appropriate
condition type if they encounter such a situation.

This report uses the phrase ``an exception is raised'' synonymously with
``an exception must be raised''.  This report uses the phrase ``an
exception with condition type @var{t}'' to indicate that the object
provided with the exception is a condition object of the specified type.
The phrase ``a continuable exception is raised'' indicates an
exceptional situation that permits the exception handler to return.


@c page
@node lang basic argument checking
@subsection Argument checking


Many procedures specified in this report or as part of a standard
library restrict the arguments they accept.  Typically, a procedure
accepts only specific numbers and types of arguments.  Many syntactic
forms similarly restrict the values to which one or more of their
subforms can evaluate.  These restrictions imply responsibilities for
both the programmer and the implementation.  Specifically, the
programmer is responsible for ensuring that the values indeed adhere to
the restrictions described in the specification.  The implementation
must check that the restrictions in the specification are indeed met, to
the extent that it is reasonable, possible, and necessary to allow the
specified operation to complete successfully.

Note that it is not always possible for an implementation to completely
check the restrictions set forth in a specification.  For example, if an
operation is specified to accept a procedure with specific properties,
checking of these properties is undecidable in general.  Similarly, some
operations accept both lists and procedures that are called by these
operations.  Since lists can be mutated by the procedures through the
@rsixlibrary{mutable-pairs} library, an argument that is a list when the
operation starts may become a non--list during the execution of the
operation.

Also, the procedure might escape to a different continuation, preventing
the operation from performing more checks.  Requiring the operation to
check that the argument is a list after each call to such a procedure
would be impractical.  Furthermore, some operations that accept lists
only need to traverse these lists partially to perform their function;
requiring the implementation to traverse the remainder of the list to
verify that all specified restrictions have been met might violate
reasonable performance assumptions.  For these reasons, the programmer's
obligations may exceed the checking obligations of the implementation.

When an implementation detects a violation of a restriction for an
argument, it must raise an exception with condition type
@code{&assertion} in a way consistent with the safety of execution as
described in section ``Safety''.


@c page
@node lang basic syntax violations
@subsection Syntax violations


The subforms of a special form usually need to obey certain syntactic
restrictions.  As forms may be subject to macro expansion, which may not
terminate, the question of whether they obey the specified restrictions
is undecidable in general.

When macro expansion terminates, however, implementations must detect
violations of the syntax.  A @emph{syntax violation} is an error with
respect to the syntax of library bodies, top--level bodies, or the
``syntax'' entries in the specification of the base library or the
standard libraries.  Moreover, attempting to assign to an immutable
variable (i.e. the variables exported by a library) is also considered a
syntax violation.

If a top--level or library form in a program is not syntactically
correct, then the implementation must raise an exception with condition
type @code{&syntax}, and execution of that top--level program or library
must not be allowed to begin.


@c page
@node lang basic safety
@subsection Safety


The standard libraries whose exports are described by this document are
said to be @emph{safe libraries}.  Libraries and top--level programs
that import only from safe libraries are also said to be safe.

As defined by this document, the Scheme programming language is safe in
the following sense: The execution of a safe top--level program cannot
go so badly wrong as to crash or to continue to execute while behaving
in ways that are inconsistent with the semantics described in this
document, unless an exception is raised.

Violations of an implementation restriction must raise an exception with
condition type @code{&implementation-restriction}, as must all
violations and errors that would otherwise threaten system integrity in
ways that might result in execution that is inconsistent with the
semantics described in this document.

The above safety properties are guaranteed only for top--level programs
and libraries that are said to be safe.  In particular, implementations
may provide access to unsafe libraries in ways that cannot guarantee
safety.


@c page
@node lang basic boolean values
@subsection Boolean values


Although there is a separate boolean type, any Scheme value can be used
as a boolean value for the purpose of a conditional test.  In a
conditional test, all values count as true in such a test except for
@false{}.  This report uses the word ``true'' to refer to any Scheme
value except @false{}, and the word ``false'' to refer to @false{}.


@c page
@node lang basic multiple return values
@subsection Multiple return values


A Scheme expression can evaluate to an arbitrary finite number of
values.  These values are passed to the expression's continuation.

Not all continuations accept any number of values. For example, a
continuation that accepts the argument to a procedure call is guaranteed
to accept exactly one value.  The effect of passing some other number of
values to such a continuation is unspecified.  The
@func{call-with-values} procedure makes it possible to create
continuations that accept specified numbers of return values.  If the
number of return values passed to a continuation created by a call to
@func{call-with-values} is not accepted by its consumer that was passed
in that call, then an exception is raised.  A more complete description
of the number of values accepted by different continuations and the
consequences of passing an unexpected number of values is given in the
description of the @func{values} procedure.

A number of forms in the base library have sequences of expressions as
subforms that are evaluated sequentially, with the return values of all
but the last expression being discarded.  The continuations discarding
these values accept any number of values.


@c page
@node lang basic unspecified behavior
@subsection Unspecified behavior


If an expression is said to ``return unspecified values'', then the
expression must evaluate without raising an exception, but the values
returned depend on the implementation; this report explicitly does not
say how many or what values should be returned.  Programmers should not
rely on a specific number of return values or the specific values
themselves.


@c page
@node lang basic storage model
@subsection Storage model


Variables and objects such as pairs, vectors, bytevectors, strings,
hashtables, and records implicitly refer to locations or sequences of
locations.  A string, for example, contains as many locations as there
are characters in the string.  (These locations need not correspond to a
full machine word.) A new value may be stored into one of these
locations using the @func{string-set!} procedure, but the string
contains the same locations as before.

An object fetched from a location, by a variable reference or by a
procedure such as @func{car}, @func{vector-ref}, or @func{string-ref},
is equivalent in the sense of @func{eqv?} to the object last stored in
the location before the fetch.

Every location is marked to show whether it is in use.  No variable or
object ever refers to a location that is not in use.  Whenever this
report speaks of storage being allocated for a variable or object, what
is meant is that an appropriate number of locations are chosen from the
set of locations that are not in use, and the chosen locations are
marked to indicate that they are now in use before the variable or
object is made to refer to them.

It is desirable for constants (i.e. the values of literal expressions)
to reside in read-only memory.  To express this, it is convenient to
imagine that every object that refers to locations is associated with a
flag telling whether that object is mutable.  Literal constants, the
strings returned by @func{symbol->string}, records with no mutable
fields, and other values explicitly designated as immutable are
immutable objects, while all objects created by the other procedures
listed in this report are mutable.  An attempt to store a new value into
a location referred to by an immutable object should raise an exception
with condition type @code{&assertion}.


@c page
@node lang basic proper tail recursion
@subsection Proper tail recursion


Implementations of Scheme must be @emph{properly tail--recursive}.
Procedure calls that occur in certain syntactic contexts called
@emph{tail contexts} are @emph{tail calls}.

A Scheme implementation is properly tail--recursive if it supports an
unbounded number of active tail calls.  A call is @emph{active} if the
called procedure may still return.  Note that this includes regular
returns as well as returns through continuations captured earlier by
@func{call-with-current-continuation} that are later invoked.  In the
absence of captured continuations, calls could return at most once and
the active calls would be those that had not yet returned.  A formal
definition of proper tail recursion can be found in Clinger's paper
``Proper tail recursion and and space efficiency''.  The rules for
identifying tail calls in constructs from the @rsixlibrary{base} library
are described in section ``Tail calls and tail contexts''.


@c page
@node lang basic dynamic extent
@subsection Dynamic extent and the dynamic environment


@cindex Dynamic extent, definition of
@cindex Definition of dynamic extent

@cindex Dynamic environment, definition of
@cindex Definition of dynamic environment


For a procedure call, the time between when it is initiated and when it
returns is called its @emph{dynamic extent}.  In Scheme,
@func{call-with-current-continuation} allows reentering a dynamic extent
after its procedure call has returned.  Thus, the dynamic extent of a
call may not be a single, connected time period.

Some operations described in the report acquire information in addition
to their explicit arguments from the @emph{dynamic environment}.  For
example, @func{call-with-current-continuation} accesses an implicit
context established by @func{dynamic-wind}, and the @func{raise}
procedure accesses the current exception handler.

The operations that modify the dynamic environment do so dynamically,
for the dynamic extent of a call to a procedure like @func{dynamic-wind}
or @func{with-exception-handler}.  When such a call returns, the
previous dynamic environment is restored.  The dynamic environment can
be thought of as part of the dynamic extent of a call.  Consequently, it
is captured by @func{call-with-current-continuation}, and restored by
invoking the escape procedure it creates.



@c page
@node lang entry
@section Entry format

@ignore
In the original document, this chapter was the "entry.tex" file.
@end ignore

The chapters that describe bindings in the base library and the standard
libraries are organized into entries.  Each entry describes one language
feature or a group of related features, where a feature is either a
syntactic construct or a built--in procedure.  An entry begins with one
or more header lines of the form

@deffn Category template
@dots{}
@end deffn

The @emph{Category} defines the kind of binding described by the entry,
typically either ``Syntax'' or ``Procedure''.  An entry may specify
various restrictions on subforms or arguments.


@menu
* lang entry syntax::                Syntax entries.
* lang entry procedure::             Procedure entries.
* lang entry implementation resp::   Implementation responsibilities.
* lang entry other kinds::           Other kinds of entries.
* lang entry equivalent::            Equivalent entries.
* lang entry evaluation examples::   Evaluation examples.
* lang entry naming conventions::    Naming conventions.
@end menu


@c page
@node lang entry syntax
@subsection Syntax entries


If @emph{category} is ``Syntax'', the entry describes a special
syntactic construct, and the template gives the syntax of the forms of
the construct.  The template is written in a notation similar to a
right--hand side of the @acronym{BNF} rules in chapter ``Lexical syntax
and datum syntax'', and describes the set of forms equivalent to the
forms matching the template as syntactic data.  Some ``Syntax'' entries
carry a suffix (@code{expand}), specifying that the syntactic keyword of
the construct is exported with level 1.  Otherwise, the syntactic
keyword is exported with level 0; @ref{lang library import export}.

Components of the form described by a template are designated by
syntactic variables, which are written using angle brackets, for
example, @meta{expression}, @meta{variable}.  Case is insignificant in
syntactic variables.  Syntactic variables stand for other forms, or
sequences of them.  A syntactic variable may refer to a non--terminal in
the grammar for syntactic data, in which case only forms matching that
non--terminal are permissible in that position.  For example,
@meta{identifier} stands for a form which must be an identifier.  Also,
@meta{expression} stands for any form which is a syntactically valid
expression.  Other non--terminals that are used in templates are defined
as part of the specification.

The notation

@example
@hyperi{thing} ...
@end example

@noindent
indicates zero or more occurrences of a @meta{thing}, and

@example
@hyperi{thing} @hyperii{thing} ...
@end example

@noindent
indicates one or more occurrences of a @meta{thing}.

It is the programmer's responsibility to ensure that each component of a
form has the shape specified by a template.  Descriptions of syntax may
express other restrictions on the components of a form.  Typically, such
a restriction is formulated as a phrase of the form ``@meta{x} must be a
@dots{}''.  Again, these specify the programmer's responsibility.  It is
the implementation's responsibility to check that these restrictions are
satisfied, as long as the macro transformers involved in expanding the
form terminate.  If the implementation detects that a component does not
meet the restriction, an exception with condition type @code{&syntax}
is raised.


@c page
@node lang entry procedure
@subsection Procedure entries


If @emph{Category} is ``Procedure'', then the entry describes a
procedure, and the header line gives a template for a call to the
procedure.  Parameter names in the template are shown like this:
@var{parm}.  Thus the header line:

@deffn Procedure vector-ref @var{vector} @var{k}
@dots{}
@end deffn


@noindent
indicates that the built--in procedure @func{vector-ref} takes two
arguments, a vector @var{vector} and an exact non--negative integer
object @var{k} (see below).  The header lines:

@deffn Procedure procname @var{k}
@dots{}
@end deffn

@deffn Procedure procname @var{k} @var{fill}
@dots{}
@end deffn

@noindent
indicate that the @func{make-vector} procedure takes either one or two
arguments.  The parameter names are case--insensitive.

As with syntax templates, an ellipsis @dots{} at the end of a header
line, as in:

@deffn Procedure the-proc @vari{z} @varii{z} @variii{z} ...
@dots{}
@end deffn

@noindent
indicates that the procedure takes arbitrarily many arguments of the
same type as specified for the last parameter name.  In this case,
@code{=} accepts two or more arguments that must all be complex number
objects.

A procedure that detects an argument that it is not specified to handle
must raise an exception with condition type @code{&assertion}.  Also,
the argument specifications are exhaustive: if the number of arguments
provided in a procedure call does not match any number of arguments
accepted by the procedure, an exception with condition type
@code{&assertion} must be raised.

For succinctness, the report follows the convention that if a parameter
name is also the name of a type, then the corresponding argument must be
of the named type.  For example, the header line for @func{vector-ref}
given above dictates that the first argument to @func{vector-ref} must
be a vector.  The following naming conventions imply type restrictions:

@table @var
@item obj
any object;

@item z
complex number object;

@item x
real number object;

@item y
real number object;

@item q
rational number object;

@item n
integer object;

@item k
exact non--negative integer object;

@item bool
boolean (@false{} or @true{});

@item octet
exact integer object in @{0, @dots{}, 255@};

@item byte
exact integer object in @{-128, @dots{}, 127@};

@item char
character;

@item pair
pair;

@item vector
vector;

@item string
string;

@item condition
condition;

@item bytevector
bytevector;

@item proc
procedure.
@end table

Other type restrictions are expressed through parameter--naming
conventions that are described in specific chapters.  For example,
library chapter ``Arithmetic'' uses a number of special parameter
variables for the various subsets of the numbers.

With the listed type restrictions, it is the programmer's responsibility
to ensure that the corresponding argument is of the specified type.  It
is the implementation's responsibility to check for that type.

A parameter called @var{list} means that it is the programmer's
responsibility to pass an argument that is a list.  It is the
implementation's responsibility to check that the argument is
appropriately structured for the operation to perform its function, to
the extent that this is possible and reasonable.  The implementation
must at least check that the argument is either an empty list or a pair.

Descriptions of procedures may express other restrictions on the
arguments of a procedure.  Typically, such a restriction is formulated
as a phrase of the form ``@var{x} must be a @dots{}'' (or otherwise
using the word ``must'').


@c page
@node lang entry implementation resp
@subsection Implementation responsibilities


In addition to the restrictions implied by naming conventions, an entry
may list additional explicit restrictions.  These explicit restrictions
usually describe both the programmer's responsibilities, who must ensure
that the subforms of a form are appropriate, or that an appropriate
argument is passed, and the implementation's responsibilities, which
must check that subform adheres to the specified restrictions (if macro
expansion terminates), or if the argument is appropriate.

A description may explicitly list the implementation's responsibilities
for some arguments or subforms in a paragraph labeled ``Implementation
responsibilities''.  In this case, the responsibilities specified for
these subforms or arguments in the rest of the description are only for
the programmer.  A paragraph describing implementation responsibility
does not affect the implementation's responsibilities for checking
subforms or arguments not mentioned in the paragraph.


@c page
@node lang entry other kinds
@subsection Other kinds of entries


If @emph{Category} is something other than ``Syntax'' and ``Procedure'',
then the entry describes a non--procedural value, and the
@emph{category} describes the type of that value.  The header line:

@deffn {Condition Type} {&who}
@dots{}
@end deffn

@noindent
indicates that @code{&who} is a condition type.  The header line:

@deffn {Auxiliary Syntax} unquote
@dots{}
@end deffn

@noindent
indicates that @func{unquote} is a syntax binding that may occur only as
part of specific surrounding expressions.  Any use as an independent
syntactic construct or identifier is a syntax violation.  As with
``Syntax'' entries, some ``Auxiliary Syntax'' entries carry a suffix
(@code{expand}), specifying that the syntactic keyword of the construct
is exported with level 1.


@c page
@node lang entry equivalent
@subsection Equivalent entries


The description of an entry occasionally states that it is @emph{the
same} as another entry.  This means that both entries are equivalent.
Specifically, it means that if both entries have the same name and are
thus exported from different libraries, the entries from both libraries
can be imported under the same name without conflict.


@c page
@node lang entry evaluation examples
@subsection Evaluation examples


The symbol @code{@result{}} used in program examples can be read
``evaluates to''.  For example:

@example
(* 5 8)
@result{} 40
@end example

@noindent
means that the expression @code{(* 5 8)} evaluates to the object
@code{40}.  Or, more precisely: the expression given by the sequence of
characters @code{(* 5 8)} evaluates, in an environment that imports the
relevant library, to an object that may be represented externally by the
sequence of characters @code{40}.

The @code{@result{}} symbol is also used when the evaluation of an
expression causes a violation.  For example:

@example
(integer->char #xD800)
@result{} &assertion exception
@end example

@noindent
means that the evaluation of the expression @code{(integer->char
#xD800)} must raise an exception with condition type @code{&assertion}.

Moreover, the @code{@result{}} symbol is also used to explicitly say
that the value of an expression in unspecified.  For example:

@example
(eqv? "" "")
@result{} unspecified
@end example

Mostly, examples merely illustrate the behavior specified in the entry.
In some cases, however, they disambiguate otherwise ambiguous
specifications and are thus normative.  Note that, in some cases,
specifically in the case of inexact number objects, the return value is
only specified conditionally or approximately.  For example:

@example
(atan -inf.0)
@result{} -1.5707963267948965 ; approximately
@end example


@c page
@node lang entry naming conventions
@subsection Naming conventions


By convention, the names of procedures that store values into previously
allocated locations usually end in @code{!}.

By convention, @code{->} appears within the names of procedures that
take an object of one type and return an analogous object of another
type.  For example, @func{list->vector} takes a list and returns a
vector whose elements are the same as those of the list.

By convention, the names of predicates (procedures that always return a
boolean value) end in @code{?} when the name contains any letters;
otherwise, the predicate's name does not end with a question mark.

By convention, the components of compound names are separated by
@code{-}.  In particular, prefixes that are actual words or can be
pronounced as though they were actual words are followed by a hyphen,
except when the first character following the hyphen would be something
other than a letter, in which case the hyphen is omitted.  Short,
unpronounceable prefixes (@code{fx} and @code{fl}) are not followed by a
hyphen.

By convention, the names of condition types start with @code{&}.


@c page
@node lang library
@section Libraries


@ignore
In the original document this chapter was the "library.tex" file.
@end ignore

Libraries are parts of a program that can be distributed independently.
The library system supports macro definitions within libraries, macro
exports, and distinguishes the phases in which definitions and imports
are needed.  This chapter defines the notation for libraries and a
semantics for library expansion and execution.

@menu
* lang library form::           Library form.
* lang library import export::  Import and export levels.
* lang library examples::       Examples.
@end menu


@c page
@node lang library form
@subsection Library form


@findex library
@cindex library form, the
@cindex The library form


A library definition must have the following form:

@example
(library @meta{library name}
  (export @meta{export spec} @dots{})
  (import @meta{import spec} @dots{})
  @meta{library body})
@end example

It is a syntax violation if a constraint given in this section is not
met.


@menu
* lang library form decl::      Library declaration.
* lang library form names::     Library names.
* lang library form export::    The export specification.
* lang library form import::    The import specification.
* lang library form version::   The version reference.
* lang library form conflicts:: Handling conflicts.
* lang library form body::      Library bodies.
@end menu


@c page
@node lang library form decl
@subsubsection Library declaration


A library declaration contains the following elements:

@itemize
@item
The @meta{library name} specifies the name of the library (possibly with
version).

@item
The @func{export} subform specifies a list of exports, which names a
subset of the bindings defined within or imported into the library.

@item
The @func{import} subform specifies the imported bindings as a list of
import dependencies, where each dependency specifies:

@itemize -
@item
the imported library's name, and, optionally, constraints on its
version,

@item
the relevant levels, e.g. expand or run time,

@item
the subset of the library's exports to make available within the
importing library, and the local names to use within the importing
library for each of the library's exports.
@end itemize

@item
The @meta{library body} is the library body, consisting of a sequence of
definitions followed by a sequence of expressions.  The definitions may
be both for local (unexported) and exported bindings, and the
expressions are initialization expressions to be evaluated for their
effects.
@end itemize

An identifier can be imported with the same local name from two or more
libraries or for two levels from the same library only if the binding
exported by each library is the same (i.e., the binding is defined in
one library, and it arrives through the imports only by exporting and
re--exporting).  Otherwise, no identifier can be imported multiple
times, defined multiple times, or both defined and imported.  No
identifiers are visible within a library except for those explicitly
imported into the library or defined within the library.


@c page
@node lang library form names
@subsubsection Library names


A @meta{library name} uniquely identifies a library within an
implementation, and is globally visible in the @func{import} clauses of
all other libraries within an implementation.  A @meta{library name} has
the following form:

@example
(@hyperi{identifier} @hyperii{identifier} @dots{} @meta{version})
@end example

@noindent
where @meta{version} is empty or has the following form:

@example
(@meta{sub-version} @dots{})
@end example

Each @meta{sub-version} must represent an exact nonnegative integer
object.  An empty @meta{version} is equivalent to @code{()}.


@c page
@node lang library form export
@subsubsection The export specification


An @meta{export spec} names a set of imported and locally defined
bindings to be exported, possibly with different external names.  An
@meta{export spec} must have one of the following forms:

@example
@meta{identifier}
(rename (@hyperi{identifier} @hyperii{identifier})
        @dots{})
@end example

In an @meta{export spec}, an @meta{identifier} names a single binding
defined within or imported into the library, where the external name for
the export is the same as the name of the binding within the library.  A
@func{rename} spec exports the binding named by @hyperi{identifier} in
each @code{(@hyperi{identifier} @hyperii{identifier})} pairing, using
@hyperii{identifier} as the external name.


@c page
@node lang library form import
@subsubsection The import specification


Each @meta{import spec} specifies a set of bindings to be imported into
the library, the levels at which they are to be available, and the local
names by which they are to be known.  An @meta{import spec} must be one
of the following:

@example
@meta{import set}
(for @meta{import set} @meta{import level} @dots{})
@end example

An @meta{import level} is one of the following:

@example
run
expand
(meta @meta{level})
@end example

@noindent
where @meta{level} represents an exact integer object.

As an @meta{import level}, @func{run} is an abbreviation for @code{(meta
0)}, and @func{expand} is an abbreviation for @code{(meta 1)}.

An @meta{import set} names a set of bindings from another library and
possibly specifies local names for the imported bindings.  It must be
one of the following:

@example
@meta{library reference}
(library @meta{library reference})
(only @meta{import set} @meta{identifier} ...)
(except @meta{import set} @meta{identifier} ...)
(prefix @meta{import set} @meta{identifier})
(rename @meta{import set} (@hyperi{identifier} @hyperii{identifier}) ...)
@end example

A @meta{library reference} identifies a library by its name and
optionally by its version.  It has one of the following forms:

@example
(@hyperi{identifier} @hyperii{identifier} ...)
(@hyperi{identifier} @hyperii{identifier} ... @meta{version reference})
@end example

A @meta{library reference} whose first @meta{identifier} is @func{for},
@func{library}, @func{only}, @func{except}, @func{prefix}, or
@func{rename} is permitted only within a @func{library} @meta{import
set}.  The @meta{import set} @code{(library @meta{library reference})}
is otherwise equivalent to @meta{library reference}.

A @meta{library reference} with no @meta{version reference} (first form
above) is equivalent to a @meta{library reference} with a @meta{version
reference} of @code{()}.


@c page
@node lang library form version
@subsubsection The version reference


A @meta{version reference} specifies a set of @meta{version}s that it
matches.  The @meta{library reference} identifies all libraries of the
same name and whose version is matched by the @meta{version reference}.
A @meta{version reference} has the following form:

@example
(@hyperi{sub-version reference} ... @hypern{sub-version reference})
(and @meta{version reference} ...)
(or @meta{version reference} @dots{})
(not @meta{version reference})
@end example

A @meta{version reference} of the first form matches a @meta{version}
with at least @var{n} elements, whose @meta{sub-version reference}s
match the corresponding @meta{sub-version}s.  An @func{and}
@hyper{version reference} matches a version if all @meta{version
references} following the @func{and} match it.  Correspondingly, an
@func{or} @meta{version reference} matches a version if one of
@meta{version references} following the @func{or} matches it, and a
@func{not} @meta{version reference} matches a version if the
@meta{version reference} following it does not match it.

A @meta{sub-version reference} has one of the following forms:

@example
@meta{sub-version}
(>= @meta{sub-version})
(<= @meta{sub-version})
(and @meta{sub-version reference} @dots{})
(or @meta{sub-version reference} @dots{})
(not @meta{sub-version reference})
@end example

A @meta{sub-version reference} of the first form matches a
@meta{sub-version} if it is equal to it.  A @code{>=} @meta{sub-version
reference} of the first form matches a sub--version if it is greater or
equal to the @meta{sub-version} following it; analogously for @code{<=}.
An @func{and} @meta{sub-version reference} matches a sub--version if all
of the subsequent @hyper{sub-version reference}s match it.

Correspondingly, an @func{or} @meta{sub-version reference} matches a
sub--version if one of the subsequent @meta{sub-version reference}s
matches it, and a @func{not} @meta{sub-version reference} matches a
sub--version if the subsequent @meta{sub-version reference} does not
match it.

Examples:

@multitable {abcdefghilmnopqrstuvz} {abcdefghi} {match?}
@headitem version reference @tab version @tab match?
@item @code{()} @tab @code{(1)} @tab yes
@item @code{(1)} @tab @code{(1)} @tab yes
@item @code{(1)} @tab @code{(2)} @tab no
@item @code{(2 3)} @tab @code{(2)} @tab no
@item @code{(2 3)} @tab @code{(2 3)} @tab yes
@item @code{(2 3)} @tab @code{(2 3 5)} @tab yes
@item @code{(or (1 (>= 1)) (2))} @tab @code{(2)} @tab yes
@item @code{(or (1 (>= 1)) (2))} @tab @code{(1 1)} @tab yes
@item @code{(or (1 (>= 1)) (2))} @tab @code{(1 0)} @tab no
@item @code{((or 1 2 3))} @tab @code{(1)} @tab yes
@item @code{((or 1 2 3))} @tab @code{(2)} @tab yes
@item @code{((or 1 2 3))} @tab @code{(3)} @tab yes
@item @code{((or 1 2 3))} @tab @code{(4)} @tab no
@end multitable


@c page
@node lang library form conflicts
@subsubsection Handling conflicts


When more than one library is identified by a library reference, the
choice of libraries is determined in some implementation--dependent
manner.

To avoid problems such as incompatible types and replicated state,
implementations should prohibit the two libraries whose library names
consist of the same sequence of identifiers but whose versions do not
match to co--exist in the same program.

By default, all of an imported library's exported bindings are made
visible within an importing library using the names given to the
bindings by the imported library.  The precise set of bindings to be
imported and the names of those bindings can be adjusted with the
@func{only}, @func{except}, @func{prefix}, and @func{rename} forms as
described below.

@table @func
@item only
Produces a subset of the bindings from another @meta{import set},
including only the listed @meta{identifier}s.  The included
@meta{identifier}s must be in the original @meta{import set}.

@item except
Produces a subset of the bindings from another @meta{import set},
including all but the listed @meta{identifier}s.  All of the excluded
@meta{identifier}s must be in the original @meta{import set}.

@item prefix
Adds the @meta{identifier} prefix to each name from another @meta{import
set}.

@item rename
Used as:

@example
(rename (@hyperi{identifier} @hyperii{identifier})
        @dots{})
@end example

@noindent
removes the bindings for @code{@hyperi{identifier} @dots{}} to form an
intermediate @meta{import set}, then adds the bindings back for the
corresponding @code{@hyperii{identifier} @dots{}} to form the final
@meta{import set}.  Each @hyperi{identifier} must be in the original
@meta{import set}, each @hyperii{identifier} must not be in the
intermediate @meta{import set}, and the @hyperii{identifier}s must be
distinct.
@end table


@c page
@node lang library form body
@subsubsection Library bodies


The @meta{library body} of a @func{library} form consists of forms that
are classified as @emph{definitions} or @emph{expressions}.  Which forms
belong to which class depends on the imported libraries and the result
of expansion.  Generally, forms that are not definitions are
expressions.

A @meta{library body} is like a @meta{body} except that a @meta{library
body}s need not include any expressions.  It must have the following
form:

@example
@meta{definition} @dots{} @meta{expression} @dots{}
@end example

When @func{begin}, @func{let-syntax}, or @func{letrec-syntax} forms
occur in a top--level body prior to the first expression, they are
spliced into the body.  Some or all of the body, including portions
wrapped in @func{begin}, @func{let-syntax}, or @func{letrec-syntax}
forms, may be specified by a syntactic abstraction.

The transformer expressions and bindings are evaluated and created from
left to right, as described in @ref{lang expansion,Expansion process}
The expressions of variable definitions are evaluated from left to
right, as if in an implicit @func{letrec*}, and the body expressions are
also evaluated from left to right after the expressions of the variable
definitions.  A fresh location is created for each exported variable and
initialized to the value of its local counterpart.  The effect of
returning twice to the continuation of the last body expression is
unspecified.

@quotation
@emph{Note:} The names @func{library}, @func{export}, @func{import},
@func{for}, @func{run}, @func{expand}, @func{meta}, @func{import},
@func{export}, @func{only}, @func{except}, @func{prefix}, @func{rename},
@func{and}, @func{or}, @func{not}, @func{>=}, and @func{<=} appearing in
the library syntax are part of the syntax and are not reserved, i.e.,
the same names can be used for other purposes within the library or even
exported from or imported into a library with different meanings,
without affecting their use in the @func{library} form.
@end quotation

Bindings defined with a library are not visible in code outside of the
library, unless the bindings are explicitly exported from the library.
An exported macro may, however, @emph{implicitly export} an otherwise
unexported identifier defined within or imported into the library.  That
is, it may insert a reference to that identifier into the output code it
produces.

All explicitly exported variables are immutable in both the exporting
and importing libraries.  It is thus a syntax violation if an explicitly
exported variable appears on the left--hand side of a @func{set!}
expression, either in the exporting or importing libraries.

All implicitly exported variables are also immutable in both the
exporting and importing libraries.  It is thus a syntax violation if a
variable appears on the left--hand side of a @func{set!}  expression in
any code produced by an exported macro outside of the library in which
the variable is defined.  It is also a syntax violation if a reference
to an assigned variable appears in any code produced by an exported
macro outside of the library in which the variable is defined, where an
assigned variable is one that appears on the left--hand side of a
@func{set!} expression in the exporting library.

All other variables defined within a library are mutable.


@c page
@node lang library import export
@subsection Import and export levels


Expanding a library may require run--time information from another
library.  For example, if a macro transformer calls a procedure from
library @math{A}, then the library @math{A} must be instantiated before
expanding any use of the macro in library @math{B}.  Library @math{A}
may not be needed when library @math{B} is eventually run as part of a
program, or it may be needed for run time of library @math{B}, too.  The
library mechanism distinguishes these times by phases, which are
explained in this section.

Every library can be characterized by expand--time information
(minimally, its imported libraries, a list of the exported keywords, a
list of the exported variables, and code to evaluate the transformer
expressions) and run--time information (minimally, code to evaluate the
variable definition right--hand--side expressions, and code to evaluate
the body expressions).  The expand--time information must be available
to expand references to any exported binding, and the run--time
information must be available to evaluate references to any exported
variable binding.

A @emph{phase} is a time at which the expressions within a library are
evaluated.  Within a library body, top--level expressions and the
right--hand sides of @func{define} forms are evaluated at run time,
i.e. phase @math{0}, and the right--hand sides of @func{define-syntax}
forms are evaluated at expand time, i.e. phase @math{1}.  When
@func{define-syntax}, @func{let-syntax}, or @func{letrec-syntax} forms
appear within code evaluated at phase @math{n}, the right--hand sides
are evaluated at phase @math{n+1}.

These phases are relative to the phase in which the library itself is
used.  An @emph{instance} of a library corresponds to an evaluation of
its variable definitions and expressions in a particular phase relative
to another library---a process called @emph{instantiation}.  For
example, if a top--level expression in a library @math{B} refers to a
variable export from another library @math{A}, then it refers to the
export from an instance of @math{A} at phase @math{0} (relative to the
phase of @math{B}).  But if a phase @math{1} expression within @math{B}
refers to the same binding from @math{A}, then it refers to the export
from an instance of @math{A} at phase @math{1} (relative to the phase of
@math{B}).

A @emph{visit} of a library corresponds to the evaluation of its syntax
definitions in a particular phase relative to another library; a process
called @emph{visiting}.  For example, if a top--level expression in a
library @math{B} refers to a macro export from another library @math{A},
then it refers to the export from a visit of @math{A} at phase @math{0}
(relative to the phase of @math{B}), which corresponds to the evaluation
of the macro's transformer expression at phase @math{1}.

A @emph{level} is a lexical property of an identifier that determines in
which phases it can be referenced.  The level for each identifier bound
by a definition within a library is @math{0}; that is, the identifier
can be referenced only at phase @math{0} within the library.  The level
for each imported binding is determined by the enclosing @func{for} form
of the @func{import} in the importing library, in addition to the levels
of the identifier in the exporting library.  Import and export levels
are combined by pairwise addition of all level combinations.  For
example, references to an imported identifier exported for levels
@math{p_a} and @math{p_b} and imported for levels @math{q_a},
@math{q_b}, and @math{q_c} are valid at levels @math{p_a + q_a},
@math{p_a + q_b}, @math{p_a + q_c}, @math{p_b + q_a}, @math{p_b + q_b},
and @math{p_b + q_c}.  An @meta{import set} without an enclosing
@func{for} is equivalent to @code{(for @meta{import set} run)}, which is
the same as @code{(for @meta{import set} (meta 0))}.

The export level of an exported binding is @math{0} for all bindings
that are defined within the exporting library.  The export levels of a
reexported binding, i.e. an export imported from another library, are
the same as the effective import levels of that binding within the
reexporting library.

For the libraries defined in the library report, the export level is
@math{0} for nearly all bindings.  The exceptions are
@func{syntax-rules}, @func{identifier-syntax}, @code{...}, and @code{_}
from the @rsixlibrary{base} library, which are exported with level
@math{1}, @func{set!} from the @rsixlibrary{base} library, which is
exported with levels @math{0} and @math{1}, and all bindings from the
composite @code{(rnrs (6))} library, which are exported with levels
@math{0} and @math{1}.

Macro expansion within a library can introduce a reference to an
identifier that is not explicitly imported into the library.  In that
case, the phase of the reference must match the identifier's level as
shifted by the difference between the phase of the source library (i.e.,
the library that supplied the identifier's lexical context) and the
library that encloses the reference.  For example, suppose that
expanding a library invokes a macro transformer, and the evaluation of
the macro transformer refers to an identifier that is exported from
another library (so the phase @math{1} instance of the library is used);
suppose further that the value of the binding is a syntax object
representing an identifier with only a level @math{n} binding; then, the
identifier must be used only at phase @math{n+1} in the library being
expanded.  This combination of levels and phases is why negative levels
on identifiers can be useful, even though libraries exist only at
non--negative phases.

If any of a library's definitions are referenced at phase @math{0} in
the expanded form of a program, then an instance of the referenced
library is created for phase @math{0} before the program's definitions
and expressions are evaluated.  This rule applies transitively: if the
expanded form of one library references at phase @math{0} an identifier
from another library, then before the referencing library is
instantiated at phase @math{n}, the referenced library must be
instantiated at phase @math{n}.  When an identifier is referenced at any
phase @math{n} greater than @math{0}, in contrast, then the defining
library is instantiated at phase @math{n} at some unspecified time
before the reference is evaluated.  Similarly, when a macro keyword is
referenced at phase @math{n} during the expansion of a library, then the
defining library is visited at phase @math{n} at some unspecified time
before the reference is evaluated.

An implementation may distinguish instances/visits of a library for
different phases or to use an instance/visit at any phase as an
instance/visit at any other phase.  An implementation may further expand
each @code{library} form with distinct visits of libraries in any phase
and/or instances of libraries in phases above @math{0}.  An
implementation may create instances/visits of more libraries at more
phases than required to satisfy references.  When an identifier appears
as an expression in a phase that is inconsistent with the identifier's
level, then an implementation may raise an exception either at expand
time or run time, or it may allow the reference.  Thus, a library whose
meaning depends on whether the instances of a library are distinguished
or shared across phases or @func{library} expansions may be unportable.


@c page
@node lang library examples
@subsection Examples


Examples for various @meta{import spec}s and @meta{export spec}s:

@example
(library (stack)
  (export make push! pop! empty!)
  (import (rnrs)
          (rnrs mutable-pairs))

  (define (make)
    (list '()))
  (define (push! s v)
    (set-car! s (cons v (car s))))
  (define (pop! s)
    (let ([v (caar s)])
      (set-car! s (cdar s))
      v))
  (define (empty! s)
    (set-car! s '())))

(library (balloons)
  (export make push pop)
  (import (rnrs))

  (define (make w h)
    (cons w h))
  (define (push b amt)
    (cons (- (car b) amt)
          (+ (cdr b) amt)))
  (define (pop b)
    (display "Boom! ")
    (display (* (car b) (cdr b)))
    (newline)))

(library (party)
  ;; Total exports:
  ;; make, push, push!, make-party, pop!
  (export (rename (balloon:make make)
                  (balloon:push push))
          push!
          make-party
          (rename (party-pop! pop!)))
  (import (rnrs)
          (only (stack) make push! pop!) ; not empty!
          (prefix (balloons) balloon:))

  ;; Creates a party as a stack of balloons,
  ;; starting with two balloons
  (define (make-party)
    (let ([s (make)]) ; from stack
      (push! s (balloon:make 10 10))
      (push! s (balloon:make 12 9))
      s))
  (define (party-pop! p)
    (balloon:pop (pop! p))))


(library (main)
  (export)
  (import (rnrs) (party))

  (define p (make-party))
  (pop! p)        ; displays "Boom! 108"
  (push! p (push (make 5 5) 1))
  (pop! p))       ; displays "Boom! 24"
@end example

Examples for macros and phases:

@example
(library (my-helpers id-stuff)
  (export find-dup)
  (import (rnrs))

  (define (find-dup l)
    (and (pair? l)
         (let loop ((rest (cdr l)))
           (cond
            [(null? rest) (find-dup (cdr l))]
            [(bound-identifier=? (car l) (car rest))
             (car rest)]
            [else (loop (cdr rest))])))))

(library (my-helpers values-stuff)
  (export mvlet)
  (import (rnrs) (for (my-helpers id-stuff) expand))

  (define-syntax mvlet
    (lambda (stx)
      (syntax-case stx ()
        [(_ [(id ...) expr] body0 body ...)
         (not (find-dup (syntax (id ...))))
         (syntax
           (call-with-values
               (lambda () expr)
             (lambda (id ...) body0 body ...)))]))))

(library (let-div)
  (export let-div)
  (import (rnrs)
          (my-helpers values-stuff)
          (rnrs r5rs))

  (define (quotient+remainder n d)
    (let ([q (quotient n d)])
      (values q (- n (* q d)))))
  (define-syntax let-div
    (syntax-rules ()
      [(_ n d (q r) body0 body ...)
       (mvlet [(q r) (quotient+remainder n d)]
         body0 body ...)])))
@end example


@c page
@node lang programs
@section Top--level programs


@ignore
In the original document this chapter is the "programs.tex" file.
@end ignore

A ``top--level program'' specifies an entry point for defining and
running a Scheme program.  A top--level program specifies a set of
libraries to import and code to run.  Through the imported libraries,
whether directly or through the transitive closure of importing, a
top--level program defines a complete Scheme program.


@menu
* lang programs syntax::        Top-level program syntax.
* lang programs semantics::     Top-level program semantics.
@end menu


@c page
@node lang programs syntax
@subsection Top--level program syntax


A top--level program is a delimited piece of text, typically a file,
that has the following form:

@example
@meta{import form} @hyper{top-level body}
@end example

An @meta{import form} has the following form:

@example
(import @meta{import spec} @dots{})
@end example

A @meta{top-level body} has the following form:

@example
@meta{top-level body form} @dots{}
@end example

A @meta{top-level body form} is either a @hyper{definition} or an
@meta{expression}.

The @meta{import form} is identical to the import clause in libraries,
and specifies a set of libraries to import.  A @meta{top-level body} is
like a @meta{library body}, except that definitions and expressions may
occur in any order.  Thus, the syntax specified by @meta{top-level body
form} refers to the result of macro expansion.

When uses of @func{begin}, @func{let-syntax}, or @func{letrec-syntax}
from the @rsixlibrary{base} library occur in a top--level body prior to
the first expression, they are spliced into the body.  Some or all of
the body, including portions wrapped in @func{begin}, @func{let-syntax},
or @func{letrec-syntax} forms, may be specified by a syntactic
abstraction.


@c page
@node lang programs semantics
@subsection Top--level program semantics


A top--level program is executed by treating the program similarly to a
library, and evaluating its definitions and expressions.  The semantics
of a top--level body may be roughly explained by a simple translation
into a library body: Each @meta{expression} that appears before a
definition in the top-level body is converted into a dummy definition

@example
(define @meta{variable}
  (begin @meta{expression} @meta{unspecified}))
@end example

where @meta{variable} is a fresh identifier and @meta{unspecified} is a
side--effect--free expression returning an unspecified value.  (It is
generally impossible to determine which forms are definitions and
expressions without concurrently expanding the body, so the actual
translation is somewhat more complicated.)

On platforms that support it, a top--level program may access its
command line by calling the @func{command-line} procedure, @ref{stdlib
programlib,Command line access and exit values}


@c page
@node lang syntax
@section Primitive syntax


@ignore
In the original document this chapter is the "syntax.tex" file.
@end ignore

After the @func{import} form within a @func{library} form or a
top--level program, the forms that constitute the body of the library or
the top--level program depend on the libraries that are imported.  In
particular, imported syntactic keywords determine the available
syntactic abstractions and whether each form is a definition or
expression.  A few form types are always available independent of
imported libraries, however, including constant literals, variable
references, procedure calls, and macro uses.

@menu
* lang syntax primitive::       Primitive expression types.
* lang syntax macros::          Macros.
@end menu


@c page
@node lang syntax primitive
@subsection Primitive expression types


The entries in this section all describe expressions, which may occur in
the place of @meta{expression} syntactic variables.


@subsubheading Constant literals

@deffn Syntax @meta{number}
@deffnx Syntax @meta{boolean}
@deffnx Syntax @meta{character}
@deffnx Syntax @meta{string}
@deffnx Syntax @meta{bytevector}
An expression consisting of a representation of a number object, a
boolean, a character, a string, or a bytevector, evaluates ``to
itself''.

@example
145932          @result{}  145932
#t              @result{}  #t
"abc"           @result{}  "abc"
#vu8(2 24 123)  @result{} #vu8(2 24 123)
@end example

The value of a literal expression is immutable, @ref{lang basic storage
model,Storage model}
@end deffn

@c ------------------------------------------------------------

@subsubheading Variable references

@deffn Syntax @meta{variable}
An expression consisting of a variable is a variable reference if it is
not a macro use (see below).  The value of the variable reference is the
value stored in the location to which the variable is bound.  It is a
syntax violation to reference an unbound variable.

The following example examples assumes the base library has been
imported:

@example
(define x 28)
x @result{} 28
@end example
@end deffn

@c ------------------------------------------------------------

@subsubheading Procedure calls

@deffn Syntax @meta{operator} @hyperi{operand} @dots{}
A procedure call consists of expressions for the procedure to be called
and the arguments to be passed to it, with enclosing parentheses.  A
form in an expression context is a procedure call if @meta{operator} is
not an identifier bound as a syntactic keyword.

When a procedure call is evaluated, the operator and operand expressions
are evaluated (in an unspecified order) and the resulting procedure is
passed the resulting arguments.

The following examples assume the @rsixlibrary{base} library has been
imported:

@example
(+ 3 4)                 @result{} 7
((if #f + *) 3 4)       @result{} 12
@end example

If the value of @meta{operator} is not a procedure, an exception with
condition type @code{&assertion} is raised.  Also, if @meta{operator}
does not accept as many arguments as there are @meta{operand}s, an
exception with condition type @code{&assertion} is raised.

@quotation
@emph{Note:} In contrast to other dialects of Lisp, the order of
evaluation is unspecified, and the operator expression and the operand
expressions are always evaluated with the same evaluation rules.

Although the order of evaluation is otherwise unspecified, the effect of
any concurrent evaluation of the operator and operand expressions is
constrained to be consistent with some sequential order of evaluation.
The order of evaluation may be chosen differently for each procedure
call.
@end quotation

@quotation
@emph{Note:} In many dialects of Lisp, the form @code{()} is a
legitimate expression.  In Scheme, expressions written as list/pair
forms must have at least one subexpression, so @code{()} is not a
syntactically valid expression.
@end quotation
@end deffn

@c page
@node lang syntax macros
@subsection Macros


@cindex Transformer, definition of
@cindex Definition of transformer
@cindex Macro transformer, definition of
@cindex Definition of macro transformer


Libraries and top--level programs can define and use new kinds of
derived expressions and definitions called @emph{syntactic abstractions}
or @emph{macros}.  A syntactic abstraction is created by binding a
keyword to a @emph{macro transformer} or, simply, @emph{transformer}.

The transformer determines how a use of the macro (called a @emph{macro
use}) is transcribed into a more primitive form.

Most macro uses have the form:

@example
(@meta{keyword} @meta{datum} @dots{})
@end example

@noindent
where @meta{keyword} is an identifier that uniquely determines the kind
of form.  This identifier is called the @emph{syntactic keyword}, or
simply @emph{keyword}.  The number of @meta{datum}s and the syntax of
each depends on the syntactic abstraction.

Macro uses can also take the form of improper lists, singleton
identifiers, or @func{set!} forms, where the second subform of the
@func{set!} is the keyword:

@example
(@meta{keyword} @meta{datum} @dots{} . @meta{datum})
@meta{keyword}
(set! @meta{keyword} @meta{datum})
@end example

The @func{define-syntax}, @func{let-syntax} and @func{letrec-syntax}
forms create bindings for keywords, associate them with macro
transformers, and control the scope within which they are visible.

The @func{syntax-rules} and @func{identifier-syntax} forms create
transformers via a pattern language.  Moreover, the @func{syntax-case}
form allows creating transformers via arbitrary Scheme code.

Keywords occupy the same name space as variables.  That is, within the
same scope, an identifier can be bound as a variable or keyword, or
neither, but not both, and local bindings of either kind may shadow
other bindings of either kind.

Macros defined using @func{syntax-rules} and @func{identifier-syntax}
are ``hygienic'' and ``referentially transparent'' and thus preserve
Scheme's lexical scoping.

@itemize
@item
If a macro transformer inserts a binding for an identifier (variable or
keyword) not appearing in the macro use, the identifier is in effect
renamed throughout its scope to avoid conflicts with other identifiers.

@item
If a macro transformer inserts a free reference to an identifier, the
reference refers to the binding that was visible where the transformer
was specified, regardless of any local bindings that may surround the
use of the macro.
@end itemize

Macros defined using the @func{syntax-case} facility are also hygienic
unless @func{datum->syntax} is used.

@c page
@node lang expansion
@section Expansion process


@ignore
In the original document this chapter is the file "expansion.tex".
@end ignore


Macro uses are expanded into @emph{core forms} at the start of
evaluation (before compilation or interpretation) by a syntax
@emph{expander}.  The set of core forms is implementation--dependent, as
is the representation of these forms in the expander's output.  If the
expander encounters a syntactic abstraction, it invokes the associated
transformer to expand the syntactic abstraction, then repeats the
expansion process for the form returned by the transformer.  If the
expander encounters a core form, it recursively processes its subforms
that are in expression or definition context, if any, and reconstructs
the form from the expanded subforms.  Information about identifier
bindings is maintained during expansion to enforce lexical scoping for
variables and keywords.

To handle definitions, the expander processes the initial forms in a
@meta{body} or @meta{library body} from left to right.  How the expander
processes each form encountered depends upon the kind of form.

@table @asis
@item macro use
The expander invokes the associated transformer to transform the macro
use, then recursively performs whichever of these actions are
appropriate for the resulting form.

@item @func{define-syntax} form
The expander expands and evaluates the right--hand--side expression and
binds the keyword to the resulting transformer.

@item @func{define} form
The expander records the fact that the defined identifier is a variable
but defers expansion of the right--hand--side expression until after all
of the definitions have been processed.

@item @func{begin} form
The expander splices the subforms into the list of body forms it is
processing.

@item @func{let-syntax} or @func{letrec-syntax} form
The expander splices the inner body forms into the list of (outer) body
forms it is processing, arranging for the keywords bound by the
@func{let-syntax} and @func{letrec-syntax} to be visible only in the
inner body forms.

@item expression, i.e., nondefinition
The expander completes the expansion of the deferred right--hand--side
expressions and the current and remaining expressions in the body, and
then creates the equivalent of a @func{letrec*} form from the defined
variables, expanded right--hand--side expressions, and expanded body
expressions.
@end table

For the right--hand side of the definition of a variable, expansion is
deferred until after all of the definitions have been seen.
Consequently, each keyword and variable reference within the right--hand
side resolves to the local binding, if any.

A definition in the sequence of forms must not define any identifier
whose binding is used to determine the meaning of the undeferred
portions of the definition or any definition that precedes it in the
sequence of forms.  For example, the bodies of the following expressions
violate this restriction.

@example
(let ()
  (define define 17)
  (list define))

(let-syntax ([def0 (syntax-rules ()
                     [(_ x) (define x 0)])])
  (let ([z 3])
    (def0 z)
    (define def0 list)
    (list z)))

(let ()
  (define-syntax foo
    (lambda (e)
      (+ 1 2)))
  (define + 2)
  (foo))
@end example

The following do not violate the restriction.

@example
(let ([x 5])
  (define lambda list)
  (lambda x x))         @result{}  (5 5)

(let-syntax ([def0 (syntax-rules ()
                     [(_ x) (define x 0)])])
  (let ([z 3])
    (define def0 list)
    (def0 z)
    (list z)))          @result{}  (3)

(let ()
  (define-syntax foo
    (lambda (e)
      (let ([+ -]) (+ 1 2))))
  (define + 2)
  (foo))                @result{}  -1
@end example

The implementation should treat a violation of the restriction as a
syntax violation.

Note that this algorithm does not directly reprocess any form.  It
requires a single left--to--right pass over the definitions followed by
a single pass (in any order) over the body expressions and deferred
right--hand sides.

Example:

@example
(lambda (x)
  (define-syntax defun
    (syntax-rules ()
      [(_ x a e) (define x (lambda a e))]))
  (defun even? (n) (or (= n 0) (odd? (- n 1))))
  (define-syntax odd?
    (syntax-rules () [(_ n) (not (even? n))]))
  (odd? (if (odd? x) (* x x) x)))
@end example

In the example:

@enumerate
@item
The definition of @func{defun} is encountered first, and the keyword
@func{defun} is associated with the transformer resulting from the
expansion and evaluation of the corresponding right--hand side.

@item
A use of @func{defun} is encountered next and expands into a
@func{define} form.  Expansion of the right--hand side of this
@func{define} form is deferred.

@item
The definition of @func{odd?} is next and results in the association of
the keyword @func{odd?} with the transformer resulting from expanding
and evaluating the corresponding right--hand side.

@item
A use of @func{odd?}  appears next and is expanded; the resulting call
to @func{not} is recognized as an expression because @func{not} is bound
as a variable.

@item
At this point, the expander completes the expansion of the current
expression (the call to @func{not}) and the deferred right--hand side of
the @func{even?}  definition; the uses of @func{odd?} appearing in these
expressions are expanded using the transformer associated with the
keyword @func{odd?}.

@item
The final output is the equivalent of:

@example
(lambda (x)
  (letrec* ([even?
              (lambda (n)
                (or (= n 0)
                    (not (even? (- n 1)))))])
    (not (even? (if (not (even? x)) (* x x) x)))))
@end example

@noindent
although the structure of the output is implementation-dependent.
@end enumerate

Because definitions and expressions can be interleaved in a
@meta{top-level body}, the expander's processing of a @meta{top-level
body} is somewhat more complicated.  It behaves as described above for a
@meta{body} or @meta{library body} with the following exceptions:

@itemize
@item
When the expander finds a nondefinition, it defers its expansion and
continues scanning for definitions.

@item
Once it reaches the end of the set of forms, it processes the deferred
right--hand--side and body expressions, then generates the equivalent of
a @func{letrec*} form from the defined variables, expanded
right--hand--side expressions, and expanded body expressions.

@item
For each body expression @meta{expression} that appears before a
variable definition in the body, a dummy binding is created at the
corresponding place within the set of @func{letrec*} bindings, with a
fresh temporary variable on the left--hand side and the equivalent of:

@example
(begin @meta{expression} @meta{unspecified})
@end example

@noindent
where @meta{unspecified} is a side--effect--free expression returning an
unspecified value, on the right--hand side, so that left--to--right
evaluation order is preserved.  The @func{begin} wrapper allows
@meta{expression} to evaluate to an arbitrary number of values.
@end itemize

@c page
@node lang derived
@section Sample definitions for derived forms


@ignore
In the original document this appendix is the file "derived.tex".
@end ignore

This appendix contains sample definitions for some of the keywords
described in this report in terms of simpler forms:

@c ------------------------------------------------------------

@subsubheading @func{cond}

The @func{cond} keyword could be defined in terms of @func{if},
@func{let} and @func{begin} using @func{syntax-rules} as follows:

@example
(define-syntax cond
  (syntax-rules (else =>)
    ((cond (else result1 result2 ...))
     (begin result1 result2 ...))
    ((cond (test => result))
     (let ((temp test))
       (if temp (result temp))))
    ((cond (test => result) clause1 clause2 ...)
     (let ((temp test))
       (if temp
           (result temp)
           (cond clause1 clause2 ...))))
    ((cond (test)) test)
    ((cond (test) clause1 clause2 ...)
     (let ((temp test))
       (if temp
           temp
           (cond clause1 clause2 ...))))
    ((cond (test result1 result2 ...))
     (if test (begin result1 result2 ...)))
    ((cond (test result1 result2 ...)
           clause1 clause2 ...)
     (if test
         (begin result1 result2 ...)
         (cond clause1 clause2 ...)))))
@end example

@c ------------------------------------------------------------

@subsubheading @func{case}

The @func{case} keyword could be defined in terms of @func{let},
@func{cond}, and @func{memv} using @code{syntax-rules} as follows:

@example
(define-syntax case
  (syntax-rules (else)
    ((case expr0
       ((key ...) res1 res2 ...)
       ...
       (else else-res1 else-res2 ...))
     (let ((tmp expr0))
       (cond
         ((memv tmp '(key ...)) res1 res2 ...)
         ...
         (else else-res1 else-res2 ...))))
    ((case expr0
       ((keya ...) res1a res2a ...)
       ((keyb ...) res1b res2b ...)
       ...)
     (let ((tmp expr0))
       (cond
         ((memv tmp '(keya ...)) res1a res2a ...)
         ((memv tmp '(keyb ...)) res1b res2b ...)
         ...)))))
@end example

@c ------------------------------------------------------------

@subsubheading @func{let*}

The @func{let*} keyword could be defined in terms of @func{let} using
@func{syntax-rules} as follows:

@example
(define-syntax let*
  (syntax-rules ()
    ((let* () body1 body2 ...)
     (let () body1 body2 ...))
    ((let* ((name1 expr1) (name2 expr2) ...)
       body1 body2 ...)
     (let ((name1 expr1))
       (let* ((name2 expr2) ...)
         body1 body2 ...)))))
@end example

@c ------------------------------------------------------------

@subsubheading @func{letrec}

The @func{letrec} keyword could be defined approximately in terms of
@func{let} and @func{set!} using @func{syntax-rules}, using a helper to
generate the temporary variables needed to hold the values before the
assignments are made, as follows:

@example
(define-syntax letrec
  (syntax-rules ()
    ((letrec () body1 body2 ...)
     (let () body1 body2 ...))
    ((letrec ((var init) ...) body1 body2 ...)
     (letrec-helper
       (var ...)
       ()
       ((var init) ...)
       body1 body2 ...))))

(define-syntax letrec-helper
  (syntax-rules ()
    ((letrec-helper
       ()
       (temp ...)
       ((var init) ...)
       body1 body2 ...)
     (let ((var <undefined>) ...)
       (let ((temp init) ...)
         (set! var temp)
         ...)
       (let () body1 body2 ...)))
    ((letrec-helper
       (x y ...)
       (temp ...)
       ((var init) ...)
       body1 body2 ...)
     (letrec-helper
       (y ...)
       (newtemp temp ...)
       ((var init) ...)
       body1 body2 ...))))
@end example

The syntax @code{<undefined>} represents an expression that returns
something that, when stored in a location, causes an exception with
condition type @code{&assertion} to be raised if an attempt to read from
or write to the location occurs before the assignments generated by the
@func{letrec} transformation take place.  (No such expression is defined
in Scheme.)

@c ------------------------------------------------------------

@subsubheading @func{letrec*}

The @func{letrec*} keyword could be defined approximately in terms of
@func{let} and @func{set!} using @func{syntax-rules} as follows:

@example
(define-syntax letrec*
  (syntax-rules ()
    ((letrec* ((var1 init1) ...) body1 body2 ...)
     (let ((var1 <undefined>) ...)
       (set! var1 init1)
       ...
       (let () body1 body2 ...)))))
@end example

The syntax @code{<undefined>} is as in the definition of @func{letrec}
above.

@c ------------------------------------------------------------

@subsubheading @func{let-values}

The following definition of @func{let-values} using @func{syntax-rules}
employs a pair of helpers to create temporary names for the formals.

@example
(define-syntax let-values
  (syntax-rules ()
    ((let-values (binding ...) body1 body2 ...)
     (let-values-helper1
       ()
       (binding ...)
       body1 body2 ...))))

(define-syntax let-values-helper1
  ;; map over the bindings
  (syntax-rules ()
    ((let-values
       ((id temp) ...)
       ()
       body1 body2 ...)
     (let ((id temp) ...) body1 body2 ...))
    ((let-values
       assocs
       ((formals1 expr1) (formals2 expr2) ...)
       body1 body2 ...)
     (let-values-helper2
       formals1
       ()
       expr1
       assocs
       ((formals2 expr2) ...)
       body1 body2 ...))))

(define-syntax let-values-helper2
  ;; create temporaries for the formals
  (syntax-rules ()
    ((let-values-helper2
       ()
       temp-formals
       expr1
       assocs
       bindings
       body1 body2 ...)
     (call-with-values
       (lambda () expr1)
       (lambda temp-formals
         (let-values-helper1
           assocs
           bindings
           body1 body2 ...))))
    ((let-values-helper2
       (first . rest)
       (temp ...)
       expr1
       (assoc ...)
       bindings
       body1 body2 ...)
     (let-values-helper2
       rest
       (temp ... newtemp)
       expr1
       (assoc ... (first newtemp))
       bindings
       body1 body2 ...))
    ((let-values-helper2
       rest-formal
       (temp ...)
       expr1
       (assoc ...)
       bindings
       body1 body2 ...)
     (call-with-values
       (lambda () expr1)
       (lambda (temp ... . newtemp)
         (let-values-helper1
           (assoc ... (rest-formal newtemp))
           bindings
           body1 body2 ...))))))
@end example

@c ------------------------------------------------------------

@subsubheading @func{let*-values}

The following macro defines @func{let*-values} in terms of @func{let}
and @func{let-values} using @func{syntax-rules}:

@example
(define-syntax let*-values
  (syntax-rules ()
    ((let*-values () body1 body2 ...)
     (let () body1 body2 ...))
    ((let*-values (binding1 binding2 ...)
       body1 body2 ...)
     (let-values (binding1)
       (let*-values (binding2 ...)
         body1 body2 ...)))))
@end example


@c ------------------------------------------------------------

@subsubheading @func{let}

The @func{let} keyword could be defined in terms of @func{lambda} and
@func{letrec} using @func{syntax-rules} as follows:

@example
(define-syntax @func{let}
  (syntax-rules ()
    ((let ((name val) ...) body1 body2 ...)
     ((lambda (name ...) body1 body2 ...)
      val ...))
    ((let tag ((name val) ...) body1 body2 ...)
     ((letrec ((tag (lambda (name ...)
                      body1 body2 ...)))
        tag)
      val ...))))
@end example

@c page
@node baselib
@chapter Base library


@ignore
In the original document this chapter is the "base.tex" file.
@end ignore

This chapter describes Scheme's @rsixlibrary{base} library, which
exports many of the procedure and syntax bindings that are traditionally
associated with Scheme.


@menu
* baselib types::               Base types.
* baselib definitions::         Definitions.
* baselib bodies::              Bodies.
* baselib expressions::         Expressions.
* baselib predicates::          Equivalence and procedure predicates.
* baselib math::                Mathematics.
* baselib booleans::            Booleans.
* baselib lists::               Pairs and lists.
* baselib symbols::             Symbols.
* baselib characters::          Characters.
* baselib strings::             Strings.
* baselib vectors::             Vectors.
* baselib errors::              Errors and violations.
* baselib control::             Control features.
* baselib iteration::           Iteration.
* baselib quasiquotation::      Quasiquotation.
* baselib syntax binding::      Binding constructs for syntactic keywords.
* baselib transformers::        Macro transformers.
* baselib tail call::           Tail calls and tail contexts.
@end menu


@c page
@node baselib types
@section Base types


No object satisfies more than one of the following predicates:

@example
boolean?          pair?
symbol?           number?
char?             string?
vector?           procedure?
null?
@end example

These predicates define the base types @emph{boolean}, @emph{pair},
@emph{symbol}, @emph{number}, @emph{char} (or @emph{character}),
@emph{string}, @emph{vector}, and @emph{procedure}.  Moreover, the empty
list is a special object of its own type.

Note that, although there is a separate boolean type, any Scheme value
can be used as a boolean value for the purpose of a conditional test.


@c page
@node baselib definitions
@section Definitions


Definitions may appear within a @meta{top-level body}, at the top of a
@meta{library body}, or at the top of a @meta{body}.

A @meta{definition} may be a variable definition or keyword definition.
Macro uses that expand into definitions or groups of definitions
(packaged in a @func{begin}, @func{let-syntax}, or @func{letrec-syntax}
form) may also appear wherever other definitions may appear.


@menu
* baselib definitions variables::  Variables definitions.
* baselib definitions syntax::     Syntax definitions.
@end menu


@c page
@node baselib definitions variables
@subsection Variables definitions


The @func{define} form described in this section is a @meta{definition}
used to create variable bindings and may appear anywhere other
definitions may appear.


@deffn Syntax define @meta{variable} @hyper{expression}
@deffnx Syntax define @meta{variable}
@deffnx Syntax define @meta{variable} @hyper{formals} @hyper{body}
@deffnx Syntax define (@meta{variable} . @hyper{formal}) @hyper{body}
The first from of @func{define} binds @meta{variable} to a new location
before assigning the value of @meta{expression} to it.

@example
(define add3
  (lambda (x) (+ x 3)))
(add3 3)                            @result{}  6
(define first car)
(first '(1 2))                      @result{}  1
@end example

The continuation of @meta{expression} should not be invoked more than
once.

@strong{Implementation responsibilities:} Implementations should detect
that the continuation of @meta{expression} is invoked more than once.
If the implementation detects this, it must raise an exception with
condition type @code{&assertion}.

The second form of @func{define} is equivalent to:

@example
(define @meta{variable} @hyper{unspecified})
@end example

@noindent
where @meta{unspecified} is a side--effect--free expression returning an
unspecified value.

In the third form of @func{define}, @meta{formals} must be either a
sequence of zero or more variables, or a sequence of one or more
variables followed by a dot @code{.} and another variable (as in a
lambda expression).  This form is equivalent to:

@example
(define @meta{variable}
  (lambda (@meta{formals}) @hyper{body}))
@end example

In the fourth form of @func{define}, @meta{formal} must be a single
variable.  This form is equivalent to:

@example
(define @meta{variable}
  (lambda @meta{formal} @hyper{body}))
@end example
@end deffn

@c page
@node baselib definitions syntax
@subsection Syntax definitions


The @code{define-syntax} form described in this section is a
@meta{definition} used to create keyword bindings and may appear
anywhere other definitions may appear.


@deffn Syntax define-syntax @meta{keyword} @hyper{expression}
Binds @meta{keyword} to the value of @meta{expression}, which must
evaluate, at macro--expansion time, to a transformer.  Macro
transformers can be created using the @func{syntax-rules} and
@func{identifier-syntax} forms.

Keyword bindings established by @func{define-syntax} are visible
throughout the body in which they appear, except where shadowed by other
bindings, and nowhere else, just like variable bindings established by
@func{define}.  All bindings established by a set of definitions,
whether keyword or variable definitions, are visible within the
definitions themselves.

@strong{Implementation responsibilities:} The implementation should
detect if the value of @meta{expression} cannot possibly be a
transformer.

Example:

@example
(let ()
  (define even?
    (lambda (x)
      (or (= x 0) (odd? (- x 1)))))
  (define-syntax odd?
    (syntax-rules ()
      ((odd?  x) (not (even? x)))))
  (even? 10))
@result{} #t
@end example

An implication of the left--to--right processing order is that one
definition can affect whether a subsequent form is also a definition.

Example:

@example
(let ()
  (define-syntax bind-to-zero
    (syntax-rules ()
      ((bind-to-zero id) (define id 0))))
  (bind-to-zero x)
  x)
@result{} 0
@end example

The behavior is unaffected by any binding for @func{bind-to-zero} that
might appear outside of the @func{let} expression.
@end deffn


@c page
@node baselib bodies
@section Bodies


The @meta{body} of a @func{lambda}, @func{let}, @func{let*},
@func{let-values}, @func{let*-values}, @func{letrec}, or @func{letrec*}
expression, or that of a definition with a body consists of zero or more
definitions followed by one or more expressions.

@example
@meta{definition} @dots{} @hyperi{expression} @hyperii{expression} @dots{}
@end example

Each identifier defined by a definition is local to the @meta{body}.
That is, the identifier is bound, and the region of the binding is the
entire @meta{body}.

Example:

@example
(let ((x 5))
  (define foo (lambda (y) (bar x y)))
  (define bar (lambda (a b) (+ (* a b) a)))
  (foo (+ x 3)))
@result{}  45
@end example

When @func{begin}, @func{let-syntax}, or @func{letrec-syntax} forms
occur in a body prior to the first expression, they are spliced into the
body.  Some or all of the body, including portions wrapped in
@func{begin}, @func{let-syntax}, or @func{letrec-syntax} forms, may be
specified by a macro use.

An expanded @meta{body} containing variable definitions can always be
converted into an equivalent @func{letrec*} expression.  For example,
the @func{let} expression in the above example is equivalent to

@example
(let ((x 5))
  (letrec* ((foo (lambda (y) (bar x y)))
            (bar (lambda (a b) (+ (* a b) a))))
    (foo (+ x 3))))
@end example


@c page
@node baselib expressions
@section Expressions


The entries in this section describe the expressions of the
@rsixlibrary{base} library, which may occur in the position of the
@meta{expression} syntactic variable in addition to the primitive
expression types.


@menu
* baselib expressions quotation::       Quotation.
* baselib expressions procedures::      Procedures.
* baselib expressions conditionals::    Conditionals.
* baselib expressions assignments::     Assignments.
* baselib expressions derived cond::    Derived conditionals.
* baselib expressions binding::         Binding constructs.
* baselib expressions sequencing::      Sequencing.
@end menu

@c page
@node baselib expressions quotation
@subsection Quotation


@deffn Syntax quote @meta{datum}
@meta{Datum} should be a syntactic datum.  @code{(quote @meta{datum})}
evaluates to the datum value represented by @meta{datum}.  This notation
is used to include constants.

@example
(quote a)               @result{} a
(quote #(a b c))        @result{} #(a b c)
(quote (+ 1 2))         @result{} (+ 1 2)
@end example

@code{(quote @meta{datum})} may be abbreviated as @code{'@meta{datum}}:

@example
'"abc"               @result{} "abc"
'145932              @result{} 145932
'a                   @result{} a
'#(a b c)            @result{} #(a b c)
'()                  @result{} ()
'(+ 1 2)             @result{} (+ 1 2)
'(quote a)           @result{} (quote a)
''a                  @result{} (quote a)
@end example

Constants are immutable.

@quotation
@emph{Note:} Different constants that are the value of a @func{quote}
expression may share the same locations.
@end quotation
@end deffn


@c page
@node baselib expressions procedures
@subsection Procedures


@deffn Syntax lambda @meta{formals} @hyper{body}
@meta{formals} must be a formal parameter list as described below, and
@meta{body} must be as described in @ref{baselib bodies}.

A @func{lambda} evaluates to a procedure.  The environment in effect
when the @func{lambda} is evaluated is remembered as part of the
procedure.  When the procedure is later called with some arguments, the
environment in which the @func{lambda} was evaluated is extended by
binding the variables in the parameter list to fresh locations, and the
resulting argument values are stored in those locations.  Then, the
expressions in the body of the @func{lambda} (which may contain
definitions and thus represent a @func{letrec*} form) are evaluated
sequentially in the extended environment.  The results of the last
expression in the body are returned as the results of the procedure
call.

@example
(lambda (x) (+ x x))      @result{} a procedure
((lambda (x) (+ x x)) 4)  @result{} 8

((lambda (x)
   (define (p y)
     (+ y 1))
   (+ (p x) x))
 5) @result{} 11

(define reverse-subtract
  (lambda (x y) (- y x)))
(reverse-subtract 7 10)         @result{} 3

(define add4
  (let ((x 4))
    (lambda (y) (+ x y))))
(add4 6)                        @result{} 10
@end example

@meta{formals} must have one of the following forms:

@table @code
@item (@hyperi{variable} @dots{})
The procedure takes a fixed number of arguments; when the procedure is
called, the arguments are stored in the bindings of the corresponding
variables.

@item @meta{variable}
The procedure takes any number of arguments; when the procedure is
called, the sequence of arguments is converted into a newly allocated
list, and the list is stored in the binding of the @meta{variable}.

@item (@hyperi{variable} @dots{} @meta{variableN} . @meta{variableN+1})
If a period @code{.} precedes the last variable, then the procedure
takes @var{n} or more arguments, where @var{n} is the number of
parameters before the period (there must be at least one).  The value
stored in the binding of the last variable is a newly allocated list of
the arguments left over after all the other arguments have been matched
up against the other parameters.

@example
((lambda x x) 3 4 5 6)          @result{} (3 4 5 6)
((lambda (x y . z) z)
 3 4 5 6)                       @result{} (5 6)
@end example
@end table

Any @meta{variable} must not appear more than once in @meta{formals}.
@end deffn



@c page
@node baselib expressions conditionals
@subsection Conditionals


@deffn Syntax if @meta{test} @meta{consequent} @meta{alternate}
@deffnx Syntax if @meta{test} @meta{consequent}
@meta{test}, @meta{consequent}, and @meta{alternate} must be
expressions.

An @func{if} expression is evaluated as follows: first, @meta{test} is
evaluated.  If it yields a true value, then @meta{consequent} is
evaluated and its values are returned.  Otherwise @meta{alternate} is
evaluated and its values are returned.  If @meta{test} yields @false{}
and no @meta{alternate} is specified, then the result of the expression
is unspecified.

@example
(if (> 3 2) 'yes 'no)           @result{} yes
(if (> 2 3) 'yes 'no)           @result{} no
(if (> 3 2)
    (- 3 2)
    (+ 3 2))                    @result{} 1
(if #f #f)                      @result{} unspecified
@end example

The @meta{consequent} and @meta{alternate} expressions are in tail
context if the @func{if} expression itself is.
@end deffn

@c page
@node baselib expressions assignments
@subsection Assignments


@deffn Syntax set! @meta{variable} @meta{expression}
@meta{expression} is evaluated, and the resulting value is stored in the
location to which @meta{variable} is bound.  @meta{variable} must be
bound either in some region expression or at the top level.  The result
of the @func{set!} expression is unspecified.

@example
(let ((x 2))
  (+ x 1)
  (set! x 4)
  (+ x 1)) @result{} 5
@end example

It is a syntax violation if @meta{variable} refers to an immutable
binding.

@quotation
@emph{Note:} The identifier @func{set!} is exported with level @emph{1}
as well.
@end quotation
@end deffn

@c page
@node baselib expressions derived cond
@subsection Derived conditionals


@deffn Syntax cond @hyperi{cond clause} @hyperii{cond clause} @dots{}
@deffnx {Auxiliary Syntax} {=>}
@deffnx {Auxiliary Syntax} else
Each @meta{cond clause} must be of the form:

@example
(@meta{test} @hyperi{expression} @dots{})
@end example

@noindent
where @meta{test} is an expression.  Alternatively, a @meta{cond clause}
may be of the form:

@example
(@meta{test} => @meta{expression})
@end example

The last @meta{cond clause} may be an ``@code{else} clause'', which has
the form:

@example
(else @hyperi{expression} @hyperii{expression} @dots{})
@end example

A @func{cond} expression is evaluated by evaluating the @meta{test}
expressions of successive @meta{cond clause}s in order until one of them
evaluates to a true value.  When a @meta{test} evaluates to a true
value, then the remaining @meta{expression}s in its @meta{cond clause}
are evaluated in order, and the results of the last @meta{expression} in
the @meta{cond clause} are returned as the results of the entire
@func{cond} expression.

If the selected @meta{cond clause} contains only the @meta{test} and no
@meta{expression}s, then the value of the @meta{test} is returned as the
result.  If the selected @meta{cond clause} uses the @code{=>} alternate
form, then the @meta{expression} is evaluated.  Its value must be a
procedure.  This procedure should accept one argument; it is called on
the value of the @meta{test} and the values returned by this procedure
are returned by the @func{cond} expression.

If all @meta{test}s evaluate to @false{}, and there is no @code{else}
clause, then the conditional expression returns unspecified values; if
there is an @code{else} clause, then its @meta{expression}s are
evaluated, and the values of the last one are returned.

@example
(cond ((> 3 2) 'greater)
      ((< 3 2) 'less))          @result{} greater

(cond ((> 3 3) 'greater)
      ((< 3 3) 'less)
      (else 'equal))            @result{} equal

(cond ('(1 2 3) => cadr)
      (else #f))                @result{} 2
@end example

For a @meta{cond clause} of one of the following forms:

@example
(@meta{test} @hyperi{expression} @dots{})
(else @hyperi{expression} @hyperii{expression} @dots{})
@end example

@noindent
the last @meta{expression} is in tail context if the @func{cond} form
itself is.  For a @meta{cond clause} of the form:

@example
(@meta{test} => @meta{expression})
@end example

@noindent
the (implied) call to the procedure that results from the evaluation of
@meta{expression} is in a tail context if the @func{cond} form itself
is.
@end deffn


@deffn Syntax case @meta{key} @hyperi{case clause} @hyperii{case clause} @dots{}
@meta{key} must be an expression.  Each @meta{case clause} must have one
of the following forms:

@example
((@hyperi{datum} @dots{}) @hyperi{expression} @hyperii{expression} @dots{})
(else @hyperi{expression} @hyperii{expression} @dots{})
@end example

The second form, which specifies an ``@code{else} clause'', may only
appear as the last @meta{case clause}.  Each @meta{datum} is an external
representation of some object.  The data represented by the
@meta{datum}s need not be distinct.

A @func{case} expression is evaluated as follows.

@enumerate
@item
@meta{key} is evaluated and its result is compared using @func{eqv?}
against the data represented by the @meta{datum}s of each @meta{case
clause} in turn, proceeding in order from left to right through the set
of clauses.

@item
If the result of evaluating @meta{key} is equivalent to a datum of a
@meta{case clause}, the corresponding @meta{expression}s are evaluated
from left to right and the results of the last expression in the
@meta{case clause} are returned as the results of the @func{case}
expression.  Otherwise, the comparison process continues.

@item
If the result of evaluating @meta{key} is different from every datum in
each set, then if there is an @code{else} clause its expressions are
evaluated and the results of the last are the results of the @func{case}
expression; otherwise the @func{case} expression returns unspecified
values.
@end enumerate

@example
(case (* 2 3)
  ((2 3 5 7) 'prime)
  ((1 4 6 8 9) 'composite))     @result{} composite
(case (car '(c d))
  ((a) 'a)
  ((b) 'b))                     @result{} unspecified
(case (car '(c d))
  ((a e i o u) 'vowel)
  ((w y) 'semivowel)
  (else 'consonant))            @result{} consonant
@end example

The last @meta{expression} of a @meta{case clause} is in tail context if
the @func{case} expression itself is.
@end deffn


@deffn Syntax and @hyperi{test} @dots{}
The @meta{test}s must be expressions.

If there are no @meta{test}s, @true{} is returned.  Otherwise, the
@meta{test} expressions are evaluated from left to right until a
@meta{test} returns @false{} or the last @meta{test} is reached.  In the
former case, the @func{and} expression returns @false{} without
evaluating the remaining expressions.  In the latter case, the last
expression is evaluated and its values are returned.

@example
(and (= 2 2) (> 2 1))           @result{}  #t
(and (= 2 2) (< 2 1))           @result{}  #f
(and 1 2 'c '(f g))             @result{}  (f g)
(and)                           @result{}  #t
@end example

The @func{and} keyword could be defined in terms of @func{if} using
@func{syntax-rules} as follows:

@example
(define-syntax and
  (syntax-rules ()
    ((and) #t)
    ((and test) test)
    ((and test1 test2 ...)
     (if test1 (and test2 ...) #t))))
@end example

The last @meta{test} expression is in tail context if the @func{and}
expression itself is.
@end deffn


@deffn Syntax or @hyperi{test} @dots{}
The @meta{test}s must be expressions.

If there are no @meta{test}s, @false{} is returned.  Otherwise, the
@meta{test} expressions are evaluated from left to right until a
@meta{test} returns a true value @var{val} or the last @meta{test} is
reached.  In the former case, the @func{or} expression returns @var{val}
without evaluating the remaining expressions.  In the latter case, the
last expression is evaluated and its values are returned.

@example
(or (= 2 2) (> 2 1))            @result{} #t
(or (= 2 2) (< 2 1))            @result{} #t
(or #f #f #f)                   @result{} #f
(or '(b c) (/ 3 0))             @result{} (b c)
@end example

The @func{or} keyword could be defined in terms of @func{if} using
@func{syntax-rules} as follows:

@example
(define-syntax or
  (syntax-rules ()
    ((or) #f)
    ((or test) test)
    ((or test1 test2 ...)
     (let ((x test1))
       (if x x (or test2 ...))))))
@end example

The last @meta{test} expression is in tail context if the @func{or}
expression itself is.
@end deffn

@c page
@node baselib expressions binding
@subsection Binding constructs


The binding constructs described in this section create local bindings
for variables that are visible only in a delimited region.  The syntax
of the constructs @func{let}, @func{let*}, @func{letrec}, and
@func{letrec*} is identical, but they differ in the regions they
establish for their variable bindings and in the order in which the
values for the bindings are computed.

@itemize
@item
In a @func{let} expression, the initial values are computed before any
of the variables become bound; in a @func{let*} expression, the bindings
and evaluations are performed sequentially.

@item
In a @func{letrec} or @func{letrec*} expression, all the bindings are in
effect while their initial values are being computed, thus allowing
mutually recursive definitions.

@item
In a @func{letrec} expression, the initial values are computed before
being assigned to the variables; in a @func{letrec*}, the evaluations
and assignments are performed sequentially.
@end itemize

In addition, the binding constructs @func{let-values} and
@func{let*-values} generalize @func{let} and @func{let*} to allow
multiple variables to be bound to the results of expressions that
evaluate to multiple values.  They are analogous to @func{let} and
@func{let*} in the way they establish regions: in a @func{let-values}
expression, the initial values are computed before any of the variables
become bound; in a @func{let*-values} expression, the bindings are
performed sequentially.

@deffn Syntax let @meta{bindings} @meta{body}
@meta{bindings} must have the form:

@example
((@hyperi{variable} @metai{init}) @dots{})
@end example

@noindent
where each @meta{init} is an expression, and @meta{body} is as described
in @ref{baselib bodies}.  Any variable must not appear more than once in
the @meta{variable}s.

The @meta{init}s are evaluated in the current environment (in some
unspecified order), the @meta{variable}s are bound to fresh locations
holding the results, the @meta{body} is evaluated in the extended
environment, and the values of the last expression of @meta{body} are
returned.  Each binding of a @meta{variable} has @meta{body} as its
region.

@example
(let ((x 2) (y 3))
  (* x y))                      @result{} 6

(let ((x 2) (y 3))
  (let ((x 7)
        (z (+ x y)))
    (* z x)))                   @result{} 35
@end example

See also named @func{let}.
@end deffn


@deffn Syntax {let*} @meta{bindings} @meta{body}
@meta{bindings} must have the form:

@example
((@hyperi{variable} @metai{init}) @dots{})
@end example

@noindent
where each @meta{init} is an expression, and @meta{body} is as described
in @ref{baselib bodies}.

The @func{let*} form is similar to @func{let}, but the @meta{init}s are
evaluated and bindings created sequentially from left to right, with the
region of each binding including the bindings to its right as well as
@meta{body}.  Thus the second @meta{init} is evaluated in an environment
in which the first binding is visible and initialized, and so on.

@example
(let ((x 2) (y 3))
  (let* ((x 7)
         (z (+ x y)))
    (* z x)))             @result{} 70
@end example

@quotation
@emph{Note:} While the variables bound by a @func{let} expression must
be distinct, the variables bound by a @func{let*} expression need not be
distinct.
@end quotation
@end deffn


@deffn Syntax letrec @meta{bindings} @meta{body}
@meta{bindings} must have the form:

@example
((@hyperi{variable} @metai{init}) @dots{})
@end example

@noindent
where each @meta{init} is an expression, and @meta{body} is as described
in @ref{baselib bodies}.  Any variable must not appear more than once in
the @meta{variable}s.

The @meta{variable}s are bound to fresh locations, the @meta{init}s are
evaluated in the resulting environment (in some unspecified order), each
@meta{variable} is assigned to the result of the corresponding
@meta{init}, the @meta{body} is evaluated in the resulting environment,
and the values of the last expression in @meta{body} are returned.  Each
binding of a @meta{variable} has the entire @func{letrec} expression as
its region, making it possible to define mutually recursive procedures.

@example
(letrec ((even?
          (lambda (n)
            (if (zero? n)
                \schtrue
                (odd? (- n 1)))))
         (odd?
          (lambda (n)
            (if (zero? n)
                \schfalse
                (even? (- n 1))))))
  (even? 88))
@result{} #t
@end example

It should be possible to evaluate each @meta{init} without assigning or
referring to the value of any @meta{variable}.  In the most common uses
of @func{letrec}, all the @meta{init}s are @func{lambda}s and the
restriction is satisfied automatically.  Another restriction is that the
continuation of each @meta{init} should not be invoked more than once.

@strong{Implementation responsibilities:} Implementations must detect
references to a @meta{variable} during the evaluation of the @meta{init}
expressions (using one particular evaluation order and order of
evaluating the @meta{init} expressions).  If an implementation detects
such a violation of the restriction, it must raise an exception with
condition type @code{&assertion}.  Implementations may or may not detect
that the continuation of each @meta{init} is invoked more than once.
However, if the implementation detects this, it must raise an exception
with condition type @code{&assertion}.
@end deffn

@deffn Syntax {letrec*} @meta{bindings} @meta{body}
@meta{bindings} must have the form:

@example
((@hyperi{variable} @metai{init}) @dots{})
@end example

@noindent
where each @meta{init} is an expression, and @meta{body} is as described
in @ref{baselib bodies}.  Any variable must not appear more than once in
the @meta{variable}s.

The @meta{variable}s are bound to fresh locations, each @meta{variable}
is assigned in left--to--right order to the result of evaluating the
corresponding @meta{init}, the @meta{body} is evaluated in the resulting
environment, and the values of the last expression in @meta{body} are
returned.  Despite the left--to--right evaluation and assignment order,
each binding of a @meta{variable} has the entire @func{letrec*}
expression as its region, making it possible to define mutually
recursive procedures.

@example
(letrec* ((p
           (lambda (x)
             (+ 1 (q (- x 1)))))
          (q
           (lambda (y)
             (if (zero? y)
                 0
                 (+ 1 (p (- y 1))))))
          (x (p 5))
          (y x))
  y)
@result{}  5
@end example

It must be possible to evaluate each @meta{init} without assigning or
referring to the value of the corresponding @meta{variable} or the
@meta{variable} of any of the bindings that follow it in
@meta{bindings}.  Another restriction is that the continuation of each
@meta{init} should not be invoked more than once.

@strong{Implementation responsibilities:} Implementations must, during
the evaluation of an @meta{init} expression, detect references to the
value of the corresponding @meta{variable} or the @meta{variable} of any
of the bindings that follow it in @meta{bindings}.  If an implementation
detects such a violation of the restriction, it must raise an exception
with condition type @code{&assertion}.  Implementations may or may not
detect that the continuation of each @meta{init} is invoked more than
once.  However, if the implementation detects this, it must raise an
exception with condition type @code{&assertion}.
@end deffn


@deffn Syntax let-values @meta{mv-bindings} @meta{body}
@meta{mv-bindings} must have the form:

@example
((@hyperi{formals} @metai{init}) @dots{})
@end example

@noindent
where each @meta{init} is an expression, and @meta{body} is as described
in @ref{baselib bodies}.  Any variable must not appear more than once in
the set of @meta{formals}.

The @meta{init}s are evaluated in the current environment (in some
unspecified order), and the variables occurring in the @meta{formals}
are bound to fresh locations containing the values returned by the
@meta{init}s, where the @meta{formals} are matched to the return values
in the same way that the @meta{formals} in a @func{lambda} are matched
to the arguments in a procedure call.  Then, the @meta{body} is
evaluated in the extended environment, and the values of the last
expression of @meta{body} are returned.  Each binding of a variable has
@meta{body} as its region.  If the @meta{formals} do not match, an
exception with condition type @code{&assertion} is raised.

@example
(let-values (((a b) (values 1 2))
             ((c d) (values 3 4)))
  (list a b c d))
@result{} (1 2 3 4)

(let-values (((a b . c) (values 1 2 3 4)))
  (list a b c))
@result{} (1 2 (3 4))

(let ((a 'a) (b 'b) (x 'x) (y 'y))
  (let-values (((a b) (values x y))
               ((x y) (values a b)))
    (list a b x y)))
@result{} (x y a b)
@end example
@end deffn


@deffn Syntax {let*-values} @meta{mv-bindings} @meta{body}
@meta{mv-bindings} must have the form:

@example
((@hyperi{formals} @metai{init}) @dots{})
@end example

@noindent
where each @meta{init} is an expression, and @meta{body} is as described
in @ref{baselib bodies}.  In each @meta{formals}, any variable must not
appear more than once.

The @func{let*-values} form is similar to @func{let-values}, but the
@meta{init}s are evaluated and bindings created sequentially from left
to right, with the region including the bindings to its right as well as
@meta{body}.  Thus the second @meta{init} is evaluated in an environment
in which the bindings of the first @meta{formals} is visible and
initialized, and so on.

@example
(let ((a 'a) (b 'b) (x 'x) (y 'y))
  (let*-values (((a b) (values x y))
                ((x y) (values a b)))
    (list a b x y)))
@result{} (x y x y)
@end example

@quotation
@emph{Note:} While all of the variables bound by a @func{let-values}
expression must be distinct, the variables bound by different
@meta{formals} of a @func{let*-values} expression need not be distinct.
@end quotation
@end deffn


@c page
@node baselib expressions sequencing
@subsection Sequencing


@deffn Syntax begin @meta{form} @dots{}
@deffnx Syntax begin @meta{expression} @meta{expression} @dots{}
The @meta{begin} keyword has two different roles, depending on its
context:

@itemize
@item
It may appear as a form in a @meta{body}, @meta{library body}, or
@meta{top-level body}, or directly nested in a @func{begin} form that
appears in a body.  In this case, the @func{begin} form must have the
shape specified in the first header line.  This use of @func{begin} acts
as a @emph{splicing} form: the forms inside the @meta{body} are spliced
into the surrounding body, as if the @func{begin} wrapper were not
actually present.

A @func{begin} form in a @meta{body} or @meta{library body} must be
non--empty if it appears after the first @meta{expression} within the
body.

@item
It may appear as an ordinary expression and must have the shape
specified in the second header line.  In this case, the
@meta{expression}s are evaluated sequentially from left to right, and
the values of the last @meta{expression} are returned.  This expression
type is used to sequence side effects such as assignments or input and
output.
@end itemize

@example
(define x 0)

(begin (set! x 5)
       (+ x 1))
@result{}  6

(begin (display "4 plus 1 equals ")
       (display (+ 4 1)))
@result{} unspecified
@print{} 4 plus 1 equals 5
@end example
@end deffn

@c page
@node baselib predicates
@section Equivalence and procedure predicates


A @emph{predicate} is a procedure that always returns a boolean value
(@true{} or @false{}).  An @emph{equivalence predicate} is the
computational analogue of a mathematical equivalence relation (it is
symmetric, reflexive, and transitive).  Of the equivalence predicates
described in this section, @func{eq?} is the finest or most
discriminating, and @func{equal?} is the coarsest.  The @func{eqv?}
predicate is slightly less discriminating than @func{eq?}.


@deffn Procedure {eqv?} @vari{obj} @varii{obj}
The @func{eqv?} procedure defines a useful equivalence relation on
objects.  Briefly, it returns @true{} if @vari{obj} and @varii{obj}
should normally be regarded as the same object and @false{} otherwise.
This relation is left slightly open to interpretation, but the following
partial specification of @func{eqv?} must hold for all implementations.

The @func{eqv?} procedure returns @true{} if one of the following holds:

@itemize
@item
@vari{obj} and @varii{obj} are both booleans and are the same according
to the @func{boolean=?} procedure.

@item
@vari{obj} and @varii{obj} are both symbols and are the same according
to the @func{symbol=?} procedure.

@item
@vari{obj} and @varii{obj} are both exact number objects and are
numerically equal (see @code{=}).

@item
@vari{obj} and @varii{obj} are both inexact number objects, are
numerically equal (see @code{=}), and yield the same results (in the
sense of @func{eqv?}) when passed as arguments to any other procedure
that can be defined as a finite composition of Scheme's standard
arithmetic procedures.

@item
@vari{obj} and @varii{obj} are both characters and are the same
character according to the @code{char=?} procedure.

@item
Both @vari{obj} and @varii{obj} are the empty list.

@item
@vari{obj} and @varii{obj} are objects such as pairs, vectors,
bytevectors (library chapter ``Bytevectors''), strings, hashtables,
records (library chapter ``Records''), ports (library section ``Port
I/O''), or hashtables (library chapter ``Hash tables'') that refer to
the same locations in the store.

@item
@vari{obj} and @varii{obj} are record--type descriptors that are
specified to be @func{eqv?} in library section ``Procedural layer''.
@end itemize

The @func{eqv?} procedure returns @false{} if one of the following
holds:

@itemize
@item
@vari{obj} and @varii{obj} are of different types.

@item
@vari{obj} and @varii{obj} are booleans for which the @func{boolean=?}
procedure returns @false{}.

@item
@vari{Obj} and @varii{obj} are symbols for which the @func{symbol=?}
procedure returns @false{}.

@item
One of @vari{obj} and @varii{obj} is an exact number object but the
other is an inexact number object.

@item
@vari{obj} and @varii{obj} are rational number objects for which the
@code{=} procedure returns @false{}.

@item
@vari{Obj} and @varii{obj} yield different results (in the sense of
@func{eqv?}) when passed as arguments to any other procedure that can be
defined as a finite composition of Scheme's standard arithmetic
procedures.

@item
@vari{obj} and @varii{obj} are characters for which the @func{char=?}
procedure returns @false{}.

@item
One of @vari{obj} and @varii{obj} is the empty list, but the other is
not.

@item
@vari{obj} and @varii{obj} are objects such as pairs, vectors,
bytevectors (library chapter ``Bytevectors''), strings, records (library
chapter ``Records''), ports (library section ``Port I/O''), or
hashtables (library chapter ``Hashtables'') that refer to distinct
locations.

@item
@vari{obj} and @varii{obj} are pairs, vectors, strings, or records, or
hashtables, where the applying the same accessor (i.e. @func{car},
@func{cdr}, @func{vector-ref}, @func{string-ref}, or record accessors)
to both yields results for which @func{eqv?} returns @false{}.

@item
@vari{obj} and @varii{obj} are procedures that would behave differently
(return different values or have different side effects) for some
arguments.
@end itemize

@quotation
@emph{Note:} The @func{eqv?} procedure returning @true{} when @vari{obj}
and @varii{obj} are number objects does not imply that @code{=} would
also return @true{} when called with @vari{obj} and @varii{obj} as
arguments.
@end quotation

@example
(eqv? 'a 'a)                     @result{} #t
(eqv? 'a 'b)                     @result{} #f
(eqv? 2 2)                       @result{} #t
(eqv? '() '())                   @result{} #t
(eqv? 100000000 100000000)       @result{} #t
(eqv? (cons 1 2) (cons 1 2))     @result{} #f
(eqv? (lambda () 1)
      (lambda () 2))             @result{} #f
(eqv? #f 'nil)                   @result{} #f
@end example

The following examples illustrate cases in which the above rules do not
fully specify the behavior of @func{eqv?}.  All that can be said about
such cases is that the value returned by @func{eqv?} must be a boolean.

@example
(let ((p (lambda (x) x)))
  (eqv? p p))                   @result{} unspecified

(eqv? "" "")                    @result{} unspecified

(eqv? '#() '#())                @result{} unspecified

(eqv? (lambda (x) x)
      (lambda (x) x))           @result{} unspecified

(eqv? (lambda (x) x)
      (lambda (y) y))           @result{} unspecified

(eqv? +nan.0 +nan.0)            @result{} unspecified
@end example

The next set of examples shows the use of @func{eqv?} with procedures
that have local state.  Calls to @func{gen-counter} must return a
distinct procedure every time, since each procedure has its own internal
counter.  Calls to @func{gen-loser} return procedures that behave
equivalently when called.  However, @func{eqv?} may not detect this
equivalence.

@example
(define gen-counter
  (lambda ()
    (let ((n 0))
      (lambda () (set! n (+ n 1)) n))))
(let ((g (gen-counter)))
  (eqv? g g))           @result{}  unspecified
(eqv? (gen-counter) (gen-counter))
                        @result{}  #f
(define gen-loser
  (lambda ()
    (let ((n 0))
      (lambda () (set! n (+ n 1)) 27))))
(let ((g (gen-loser)))
  (eqv? g g))           @result{}  unspecified
(eqv? (gen-loser) (gen-loser))
                        @result{}  unspecified

(letrec ((f (lambda () (if (eqv? f g) 'both 'f)))
         (g (lambda () (if (eqv? f g) 'both 'g))))
  (eqv? f g)) @result{} unspecified

(letrec ((f (lambda () (if (eqv? f g) 'f 'both)))
         (g (lambda () (if (eqv? f g) 'g 'both))))
  (eqv? f g)) @result{} #f
@end example

Implementations may share structure between constants where appropriate.
Furthermore, a constant may be copied at any time by the implementation
so as to exist simultaneously in different sets of locations.  Thus the
value of @func{eqv?} on constants is sometimes
implementation--dependent.

@example
(eqv? '(a) '(a))                 @result{} unspecified
(eqv? "a" "a")                   @result{} unspecified
(eqv? '(b) (cdr '(a b)))         @result{} unspecified
(let ((x '(a)))
  (eqv? x x))                    @result{} #t
@end example
@end deffn


@deffn Procedure eq? @vari{obj} @varii{obj}
The @func{eq?} predicate is similar to @func{eqv?} except that in some
cases it is capable of discerning distinctions finer than those
detectable by @func{eqv?}.

The @func{eq?} and @func{eqv?} predicates are guaranteed to have the
same behavior on symbols, booleans, the empty list, pairs, procedures,
non--empty strings, bytevectors, vectors, and records.  The behavior of
@func{eq?} on number objects and characters is
implementation--dependent, but it always returns either @true{} or
@false{}, and returns @true{} only when @func{eqv?} would also return
@true{}.  The @func{eq?} predicate may also behave differently from
@func{eqv?} on empty vectors, empty bytevectors, and empty strings.

@example
(eq? 'a 'a)                     @result{} #t
(eq? '(a) '(a))                 @result{} unspecified
(eq? (list 'a) (list 'a))       @result{} #f
(eq? "a" "a")                   @result{} unspecified
(eq? "" "")                     @result{} unspecified
(eq? '() '())                   @result{} #t
(eq? 2 2)                       @result{} unspecified
(eq? #\A #\A)                   @result{} unspecified
(eq? car car)                   @result{} #t
(let ((n (+ 2 3)))
  (eq? n n))                    @result{} unspecified
(let ((x '(a)))
  (eq? x x))                    @result{} #t
(let ((x '#()))
  (eq? x x))                    @result{} unspecified
(let ((p (lambda (x) x)))
  (eq? p p))                    @result{} unspecified
@end example
@end deffn


@deffn Procedure {equal?} @vari{obj} @varii{obj}
The @func{equal?}  predicate returns @true{} if and only if the
(possibly infinite) unfoldings of its arguments into regular trees are
equal as ordered trees.

The @func{equal?} predicate treats pairs and vectors as nodes with
outgoing edges, uses @func{string=?} to compare strings, uses
@func{bytevector=?} to compare bytevectors (@ref{stdlib bytevector}),
and uses @func{eqv?} to compare other nodes.

@example
(equal? 'a 'a)                  @result{}  #t

(equal? '(a) '(a))              @result{}  #t

(equal? '(a (b) c)
        '(a (b) c))             @result{}  #t

(equal? "abc" "abc")            @result{}  #t

(equal? 2 2)                    @result{}  #t

(equal? (make-vector 5 'a)
        (make-vector 5 'a))     @result{}  #t

(equal? '#vu8(1 2 3 4 5)
        (u8-list->bytevector
         '(1 2 3 4 5))          @result{}  #t

(equal? (lambda (x) x)
        (lambda (y) y))         @result{}  unspecified

(let* ((x (list 'a))
       (y (list 'a))
       (z (list x y)))
  (list (equal? z (list y x))
        (equal? z (list x x)))) @result{}  (#t #t)
@end example

@quotation
@emph{Note:} The @func{equal?} procedure must always terminate, even if
its arguments contain cycles.
@end quotation
@end deffn


@c ------------------------------------------------------------

@subsubheading Procedure predicate


@deffn Procedure {procedure?} @var{obj}
Return @true{} if @var{obj} is a procedure, otherwise return @false{}.

@example
(procedure? car)                        @result{} #t
(procedure? 'car)                       @result{} #f
(procedure? (lambda (x) (* x x)))       @result{} #t
(procedure? '(lambda (x) (* x x)))      @result{} #f
@end example
@end deffn


@c page
@node baselib math
@section Mathematics


The procedures described here implement arithmetic that is generic over
the numerical tower.  The generic procedures described in this section
accept both exact and inexact number objects as arguments, performing
coercions and selecting the appropriate operations as determined by the
numeric subtypes of their arguments.


@menu
* baselib math exactness::      Propagation of exactness and inexactness.
* baselib math infinities::     Representability of infinities and NaNs.
* baselib math semantics::      Semantics of common operations.
* baselib math ops::            Numerical operations.
* baselib math string::         Numerical input and output.
@end menu


@c page
@node baselib math exactness
@subsection Propagation of exactness and inexactness


The procedures listed below must return the mathematically correct exact
result provided all their arguments are exact:

@example
+            -            *
max          min          abs
numerator    denominator  gcd
lcm          floor        ceiling
truncate     round        rationalize
real-part    imag-part    make-rectangular
@end example

The procedures listed below must return the correct exact result
provided all their arguments are exact, and no divisors are zero:

@example
/
div          mod           div-and-mod
div0         mod0          div0-and-mod0
@end example

Moreover, the procedure @func{expt} must return the correct exact result
provided its first argument is an exact real number object and its
second argument is an exact integer object.

The general rule is that the generic operations return the correct exact
result when all of their arguments are exact and the result is
mathematically well--defined, but return an inexact result when any
argument is inexact.  Exceptions to this rule include:

@example
sqrt            exp             log
sin             cos             tan
asin            acos            atan
expt            make-polar      magnitude
angle
@end example

@noindent
which may (but are not required to) return inexact results even when
given exact arguments, as indicated in the specification of these
procedures.

One general exception to the rule above is that an implementation may
return an exact result despite inexact arguments if that exact result
would be the correct result for all possible substitutions of exact
arguments for the inexact ones.  An example is @code{(* 1.0 0)} which
may return either @math{0} (exact) or @math{0.0} (inexact).


@c page
@node baselib math infinities
@subsection Representability of infinities and NaNs


The specification of the numerical operations is written as though
infinities and NaNs are representable, and specifies many operations
with respect to these number objects in ways that are consistent with
the @ieee{} 754 standard for binary floating--point arithmetic.

An implementation of Scheme may or may not represent infinities and
NaNs; however, an implementation must raise a continuable exception with
condition type @code{&no-infinities} or @code{&no-nans} (respectively;
@ref{stdlib arithmetics flonum}) whenever it is unable to represent an
infinity or NaN as specified.  In this case, the continuation of the
exception handler is the continuation that otherwise would have received
the infinity or NaN value.  This requirement also applies to conversions
between number objects and external representations, including the
reading of program source code.

@c page
@node baselib math semantics
@subsection Semantics of common operations


Some operations are the semantic basis for several arithmetic
procedures.  The behavior of these operations is described in this
section for later reference.


@menu
* baselib math semantics integer::      Integer division.
* baselib math semantics trascend::     Transcendental functions.
@end menu


@c page
@node baselib math semantics integer
@subsubsection Integer division


Scheme's operations for performing integer division rely on mathematical
operations @math{div}, @math{mod}, @math{div_0}, and @math{mod_0},
that are defined as follows:

@itemize
@item
@math{div}, @math{mod}, @math{div_0}, and @math{mod_0} each accept
two real numbers @math{x_1} and @math{x_2} as operands, where @math{x_2}
must be nonzero.

@item
@math{div} returns an integer, and @math{mod} returns a real.  Their
results are specified by:

@example
x_1 div x_2 = n_d
x_1 mod x_2 = x_m
@end example

@noindent
where:

@example
x_1 = n_d * x_2 + x_m
0  <= x_m < |x_2|
@end example

Examples:

@example
 123 div  10 =  12
 123 mod  10 =   3
 123 div -10 = -12
 123 mod -10 =   3
-123 div  10 = -13
-123 mod  10 =   7
-123 div -10 =  13
-123 mod -10 =   7
@end example

@item
@math{div_0} and @math{mod_0} are like @math{div} and @math{mod}, except
the result of @math{mod_0} lies within a half--open interval centered on
zero.  The results are specified by:

@example
x_1 div_0 x_2 = n_d
x_1 mod_0 x_2 = x_m
@end example

@noindent
where:

@example
x_1 = n_d * x_2 + x_m
-|x_2/2| <= x_m < |x_2/2|
@end example

Examples:

@example
 123 div_0  10 =  12
 123 mod_0  10 =   3
 123 div_0 -10 = -12
 123 mod_0 -10 =   3
-123 div_0  10 = -12
-123 mod_0  10 =  -3
-123 div_0 -10 =  12
-123 mod_0 -10 =  -3
@end example
@end itemize



@c page
@node baselib math semantics trascend
@subsubsection Transcendental functions


In general, the transcendental functions @math{\log}, @math{\sin^(-1)}
(arcsine), @math{\cos^(-1)} (arccosine), and @math{\tan^(-1)} are
multiply defined.  The value of @math{\log z} is defined to be the one
whose imaginary part lies in the range from @math{-\pi} (inclusive if
@math{-0.0} is distinguished, exclusive otherwise) to @math{\pi}
(inclusive).  @math{\log 0} is undefined.

The value of @math{\log z} for non--real @math{z} is defined in terms of
log on real numbers as:

@example
log z = log |z| + (angle z) i
@end example

@noindent
where @math{angle z} is the angle of @math{z = a * e^(i * b)} specified
as:

@example
angle z = b + 2 pi n
@end example

with @math{-\pi <= angle z <= \pi} and @math{angle z = b + 2 pi n} for
some integer @math{n}.

With the one--argument version of @math{\log} defined this way, the
values of the two--argument--version of @math{\log}, @code{sin^(-1) z},
@math{cos^(-1) z}, @math{tan^(-1) z}, and the two--argument version of
@math{tan^(-1)} are according to the following formulae:

@example
log_b z = (log z)/(log b)
sin^(-1) z = -i log (i z + sqrt(1 - z^2))
cos^(-1) z = pi/2 - sin^(-1) z
tan^(-1) z = (log (1 + i z) - log (1 - i z)) / (2 i)
tan^(-1) x y = angle(x + yi)
@end example

The range of @math{tan^(-1) x y} is as in the following table.  The
asterisk (*) indicates that the entry applies to implementations that
distinguish minus zero.

@multitable {*} {@math{y} condition} {@math{x} condition} {range of result @math{r}}
@headitem @tab @math{y} condition @tab @math{x} condition @tab range of result @math{r}
@item @tab y = 0.0 @tab x > 0.0 @tab 0.0
@item * @tab y = +0.0  @tab x > 0.0 @tab +0.0
@item * @tab y = -0.0 @tab x > 0.0 @tab -0.0
@item @tab y > 0.0 @tab x > 0.0 @tab 0.0 < r < pi/2
@item @tab y > 0.0 @tab x = 0.0 @tab pi/2
@item @tab y > 0.0 @tab x < 0.0 @tab pi/2 < r < pi
@item @tab y = 0.0 @tab x < 0 @tab pi
@item * @tab y = +0.0 @tab x < 0.0 @tab pi
@item * @tab y = -0.0 @tab x < 0.0 @tab -pi
@item @tab y < 0.0 @tab x < 0.0 @tab -pi< r < -pi/2
@item @tab y < 0.0 @tab x = 0.0 @tab -pi/2
@item @tab y < 0.0 @tab x > 0.0 @tab -pi/2 < r < 0.0
@item @tab y = 0.0 @tab x = 0.0 @tab undefined
@item * @tab y = +0.0 @tab x = +0.0 @tab +0.0
@item * @tab y = -0.0 @tab x = +0.0 @tab -0.0
@item * @tab y = +0.0 @tab x = -0.0 @tab pi
@item * @tab y = -0.0 @tab x = -0.0 @tab -pi
@item * @tab y = +0.0 @tab x = 0 @tab pi/2
@item * @tab y = -0.0 @tab x = 0 @tab -pi/2
@end multitable

@c page
@node baselib math ops
@subsection Numerical operations


@menu
* baselib math ops type pred::          Numerical type predicates.
* baselib math ops exactness::          Generic conversion.
* baselib math ops value pred::         Value predicates.
* baselib math ops comparison::         Comparison.
* baselib math ops arithmetic::         Arithmetic operations.
* baselib math ops part::               Part separation.
* baselib math ops trascend::           Trascendental functions.
* baselib math ops exponentiation::     Exponentiation functions.
* baselib math ops complex::            Complex numbers functions.
@end menu


@c page
@node baselib math ops type pred
@subsubsection Numerical type predicates


@deffn Procedure {number?} @var{obj}
@deffnx Procedure {complex?} @var{obj}
@deffnx Procedure {real?} @var{obj}
@deffnx Procedure {rational?} @var{obj}
@deffnx Procedure {integer?} @var{obj}
These numerical type predicates can be applied to any kind of argument.
They return @true{} if the object is a number object of the named type,
and @false{} otherwise.  In general, if a type predicate is true of a
number object then all higher type predicates are also true of that
number object.  Consequently, if a type predicate is false of a number
object, then all lower type predicates are also false of that number
object.

If @var{z} is a complex number object, then @code{(real? @var{z})} is
true if and only if @code{(zero? (imag-part @var{z}))} and @code{(exact?
(imag-part @var{z}))} are both true.

If @var{x} is a real number object, then @code{(rational? @var{x})} is
true if and only if there exist exact integer objects @vari{k} and
@varii{k} such that @code{(= @var{x} (/ @vari{k} @varii{k}))} and
@code{(= (numerator @var{x}) @vari{k})} and @code{(= (denominator
@var{x}) @varii{k})} are all true.  Thus infinities and NaNs are not
rational number objects.

If @var{q} is a rational number objects, then @code{(integer? @var{q})}
is true if and only if @code{(= (denominator @var{q}) 1)} is true.  If
@var{q} is not a rational number object, then @code{(integer? @var{q})}
is @false{}.

@example
(complex? 3+4i)                        @result{} #t
(complex? 3)                           @result{} #t
(real? 3)                              @result{} #t
(real? -2.5+0.0i)                      @result{} #f
(real? -2.5+0i)                        @result{} #t
(real? -2.5)                           @result{} #t
(real? #e1e10)                         @result{} #t
(rational? 6/10)                       @result{} #t
(rational? 6/3)                        @result{} #t
(rational? 2)                          @result{} #t
(integer? 3+0i)                        @result{} #t
(integer? 3.0)                         @result{} #t
(integer? 8/4)                         @result{} #t

(number? +nan.0)                       @result{} #t
(complex? +nan.0)                      @result{} #t
(real? +nan.0)                         @result{} #t
(rational? +nan.0)                     @result{} #f
(complex? +inf.0)                      @result{} #t
(real? -inf.0)                         @result{} #t
(rational? -inf.0)                     @result{} #f
(integer? -inf.0)                      @result{} #f
@end example

@quotation
@emph{Note:} Except for @func{number?}, the behavior of these type
predicates on inexact number objects is unreliable, because any
inaccuracy may affect the result.
@end quotation
@end deffn


@deffn Procedure {real-valued?} @var{obj}
@deffnx Procedure {rational-valued?} @var{obj}
@deffnx Procedure {integer-valued?} @var{obj}
These numerical type predicates can be applied to any kind of argument.
The @func{real-valued?} procedure returns @true{} if the object is a
number object and is equal in the sense of @code{=} to some real number
object, or if the object is a NaN, or a complex number object whose real
part is a NaN and whose imaginary part is zero in the sense of
@func{zero?}.  The @func{rational-valued?} and @func{integer-valued?}
procedures return @true{} if the object is a number object and is equal
in the sense of @code{=} to some object of the named type, and otherwise
they return @false{}.

@example
(real-valued? +nan.0)                  @result{} #t
(real-valued? +nan.0+0i)               @result{} #t
(real-valued? -inf.0)                  @result{} #t
(real-valued? 3)                       @result{} #t
(real-valued? -2.5+0.0i)               @result{} #t
(real-valued? -2.5+0i)                 @result{} #t
(real-valued? -2.5)                    @result{} #t
(real-valued? #e1e10)                  @result{} #t

(rational-valued? +nan.0)              @result{} #f
(rational-valued? -inf.0)              @result{} #f
(rational-valued? 6/10)                @result{} #t
(rational-valued? 6/10+0.0i)           @result{} #t
(rational-valued? 6/10+0i)             @result{} #t
(rational-valued? 6/3)                 @result{} #t

(integer-valued? 3+0i)                 @result{} #t
(integer-valued? 3+0.0i)               @result{} #t
(integer-valued? 3.0)                  @result{} #t
(integer-valued? 3.0+0.0i)             @result{} #t
(integer-valued? 8/4)                  @result{} #t
@end example

@quotation
@emph{Note:} These procedures test whether a given number object can be
coerced to the specified type without loss of numerical accuracy.
Specifically, the behavior of these predicates differs from the behavior
of @func{real?}, @func{rational?}, and @func{integer?} on complex number
objects whose imaginary part is inexact zero.
@end quotation

@quotation
@emph{Note:} The behavior of these type predicates on inexact number
objects is unreliable, because any inaccuracy may affect the result.
@end quotation
@end deffn


@deffn Procedure {exact?} @var{z}
@deffnx Procedure {inexact?} @var{z}
These numerical predicates provide tests for the exactness of a
quantity.  For any number object, precisely one of these predicates is
true.

@example
(exact? 5)                   @result{} #t
(inexact? +inf.0)            @result{} #t
@end example
@end deffn



@c page
@node baselib math ops exactness
@subsubsection Exactness conversion


These procedures implement the natural one--to-one correspondence
between exact and inexact integer objects throughout an
implementation--dependent range.  The @func{inexact} and @func{exact}
procedures are idempotent.


@deffn Procedure inexact @var{z}
The @func{inexact} procedure returns an inexact representation of
@var{z}.  If inexact number objects of the appropriate type have bounded
precision, then the value returned is an inexact number object that is
nearest to the argument.  If an exact argument has no reasonably close
inexact equivalent, an exception with condition type
@code{&implementation-violation} may be raised.

@quotation
@emph{Note:} For a real number object whose magnitude is finite but so
large that it has no reasonable finite approximation as an inexact
number, a reasonably close inexact equivalent may be @code{+inf.0} or
@code{-inf.0}.  Similarly, the inexact representation of a complex
number object whose components are finite may have infinite components.
@end quotation
@end deffn


@deffn Procedure exact @var{z}
The @func{exact} procedure returns an exact representation of @var{z}.
The value returned is the exact number object that is numerically
closest to the argument; in most cases, the result of this procedure
should be numerically equal to its argument.  If an inexact argument has
no reasonably close exact equivalent, an exception with condition type
@code{&implementation-violation} may be raised.
@end deffn


@c page
@node baselib math ops value pred
@subsubsection Value predicates


@deffn Procedure = @vari{z} @varii{z} @variii{z} @dots{}
@deffnx Procedure < @vari{x} @varii{x} @variii{x} @dots{}
@deffnx Procedure > @vari{x} @varii{x} @variii{x} @dots{}
@deffnx Procedure <= @vari{x} @varii{x} @variii{x} @dots{}
@deffnx Procedure >= @vari{x} @varii{x} @variii{x} @dots{}
These procedures return @true{} if their arguments are (respectively):
equal, monotonically increasing, monotonically decreasing, monotonically
nondecreasing, or monotonically nonincreasing, and @false{} otherwise.

Examples:

@example
(= +inf.0 +inf.0)               @result{} #t
(= -inf.0 +inf.0)               @result{} #f
(= -inf.0 -inf.0)               @result{} #t
@end example

@noindent
for any real number object @var{x} that is neither infinite nor NaN:

@example
(< -inf.0 @var{x} +inf.0)       @result{} #t
(> +inf.0 @var{x} -inf.0)       @result{} #t
@end example

@noindent
for any number object @var{z}:

@example
(= +nan.0 @var{z})              @result{} #f
@end example

For any real number object @var{x}:

@example
(< +nan.0 @var{x})              @result{} #f
(> +nan.0 @var{x})              @result{} #f
@end example

These predicates must be transitive.

@quotation
@emph{Note:} The traditional implementations of these predicates in
Lisp--like languages are not transitive.
@end quotation

@quotation
@emph{Note:} While it is possible to compare inexact number objects
using these predicates, the results may be unreliable because a small
inaccuracy may affect the result; this is especially true of @code{=}
and @func{zero?}  (below).

When in doubt, consult a numerical analyst.
@end quotation
@end deffn


@deffn Procedure {zero?} @var{z}
@deffnx Procedure {positive?} @var{x}
@deffnx Procedure {negative?} @var{x}
@deffnx Procedure {odd?} @var{n}
@deffnx Procedure {even?} @var{n}
@deffnx Procedure {finite?} @var{x}
@deffnx Procedure {infinite?} @var{x}
@deffnx Procedure {nan?} @var{x}
These numerical predicates test a number object for a particular
property, returning @true{} or @false{}.

@table @func
@item zero?
Tests if the number object is @code{=} to zero.

@item positive?
Tests whether it is greater than zero.

@item negative?
Tests whether it is less than zero.

@item odd?
Tests whether it is odd.

@item even?
Tests whether it is even.

@item finite?
Tests whether it is not an infinity and not a NaN.

@item infinite?
Tests whether it is an infinity.

@item nan?
Tests whether it is a NaN.
@end table

@example
(zero? +0.0)                  @result{} #t
(zero? -0.0)                  @result{} #t
(zero? +nan.0)                @result{} #f
(positive? +inf.0)            @result{} #t
(negative? -inf.0)            @result{} #t
(positive? +nan.0)            @result{} #f
(negative? +nan.0)            @result{} #f
(finite? +inf.0)              @result{} #f
(finite? 5)                   @result{} #t
(finite? 5.0)                 @result{} #t
(infinite? 5.0)               @result{} #f
(infinite? +inf.0)            @result{} #t
@end example

@quotation
@emph{Note:} As with the predicates above, the results may be unreliable
because a small inaccuracy may affect the result.
@end quotation
@end deffn


@c page
@node baselib math ops comparison
@subsubsection Comparison


@deffn Procedure max @vari{x} @varii{x} @dots{}
@deffnx Procedure min @vari{x} @varii{x} @dots{}
These procedures return the maximum or minimum of their arguments.

@example
(max 3 4)               @result{} 4
(max 3.9 4)             @result{} 4.0
@end example

For any real number object @var{x} that is not a NaN:

@example
(max +inf.0 @var{x})    @result{} +inf.0
(min -inf.0 @var{x})    @result{} -inf.0
@end example

@quotation
@emph{Note:} If any argument is inexact, then the result is also inexact
(unless the procedure can prove that the inaccuracy is not large enough
to affect the result, which is possible only in unusual
implementations).  If @func{min} or @func{max} is used to compare number
objects of mixed exactness, and the numerical value of the result cannot
be represented as an inexact number object without loss of accuracy,
then the procedure may raise an exception with condition type
@code{&implementation-restriction}.
@end quotation
@end deffn

@c page
@node baselib math ops arithmetic
@subsubsection Arithmetic operations


@deffn Procedure + @vari{z} @dots{}
@deffnx Procedure * @vari{z} @dots{}
These procedures return the sum or product of their arguments.

@example
(+ 3 4)                 @result{}  7
(+ 3)                   @result{}  3
(+)                     @result{}  0
(+ +inf.0 +inf.0)       @result{}  +inf.0
(+ +inf.0 -inf.0)       @result{}  +nan.0

(* 4)                   @result{}  4
(*)                     @result{}  1
(* 5 +inf.0)            @result{}  +inf.0
(* -5 +inf.0)           @result{}  -inf.0
(* +inf.0 +inf.0)       @result{}  +inf.0
(* +inf.0 -inf.0)       @result{}  -inf.0
(* 0 +inf.0)            @result{}  0 or +nan.0
(* 0 +nan.0)            @result{}  0 or +nan.0
(* 1.0 0)               @result{}  0 or 0.0
@end example

For any real number object @var{x} that is neither infinite nor NaN:

@example
(+ +inf.0 x)            @result{}  +inf.0
(+ -inf.0 x)            @result{}  -inf.0
@end example

For any real number object @var{x}:

@example
(+ +nan.0 x)            @result{}  +nan.0
@end example

For any real number object @var{x} that is not an exact 0:

@example
(* +nan.0 x)            @result{}  +nan.0
@end example

If any of these procedures are applied to mixed non--rational real and
non--real complex arguments, they either raise an exception with
condition type @code{&implementation-restriction} or return an
unspecified number object.

Implementations that distinguish @code{-0.0} should adopt behavior
consistent with the following examples:

@example
(+  0.0 -0.0)           @result{}  0.0
(+ -0.0  0.0)           @result{}  0.0
(+  0.0  0.0)           @result{}  0.0
(+ -0.0 -0.0)           @result{} -0.0
@end example
@end deffn


@deffn Procedure - @var{z}
@deffnx Procedure - @vari{z} @varii{z} @variii{z} @dots{}
With two or more arguments, this procedures returns the difference of
its arguments, associating to the left.  With one argument, however, it
returns the additive inverse of its argument.

@example
(- 3 4)                 @result{} -1
(- 3 4 5)               @result{} -6
(- 3)                   @result{} -3
(- +inf.0 +inf.0)       @result{} +nan.0
@end example

If this procedure is applied to mixed non--rational real and non--real
complex arguments, it either raises an exception with condition type
@code{&implementation-restriction} or returns an unspecified number
object.

Implementations that distinguish @math{-0.0} should adopt behavior
consistent with the following examples:

@example
(-  0.0)                @result{} -0.0
(- -0.0)                @result{}  0.0
(-  0.0 -0.0)           @result{}  0.0
(- -0.0  0.0)           @result{} -0.0
(-  0.0  0.0)           @result{}  0.0
(- -0.0 -0.0)           @result{}  0.0
@end example
@end deffn


@deffn Procedure / @var{z}
@deffnx Procedure / @vari{z} @varii{z} @variii{z} @dots{}
If all of the arguments are exact, then the divisors must all be
nonzero.  With two or more arguments, this procedure returns the
quotient of its arguments, associating to the left.  With one argument,
however, it returns the multiplicative inverse of its argument.

@example
(/ 3 4 5)               @result{} 3/20
(/ 3)                   @result{} 1/3
(/ 0.0)                 @result{} +inf.0
(/ 1.0 0)               @result{} +inf.0
(/ -1 0.0)              @result{} -inf.0
(/ +inf.0)              @result{} 0.0
(/ 0 0)                 @result{} exception &assertion
(/ 3 0)                 @result{} exception &assertion
(/ 0 3.5)               @result{} 0.0
(/ 0 0.0)               @result{} +nan.0
(/ 0.0 0)               @result{} +nan.0
(/ 0.0 0.0)             @result{} +nan.0
@end example

If this procedure is applied to mixed non--rational real and non--real
complex arguments, it either raises an exception with condition type
@code{&implementation-restriction} or returns an unspecified number
object.
@end deffn


@deffn Procedure abs @var{x}
Returns the absolute value of its argument.

@example
(abs -7)                 @result{}  7
(abs -inf.0)             @result{}  +inf.0
@end example
@end deffn


@deffn Procedure div-and-mod @vari{x} @varii{x}
@deffnx Procedure div @vari{x} @varii{x}
@deffnx Procedure mod @vari{x} @varii{x}
@deffnx Procedure div0-and-mod0 @vari{x} @varii{x}
@deffnx Procedure div0 @vari{x} @varii{x}
@deffnx Procedure mod0 @vari{x} @varii{x}
These procedures implement number--theoretic integer division and return
the results of the corresponding mathematical operations specified in
@ref{baselib math semantics integer}.  If @vari{x} and @varii{x} are
exact, @varii{x} must be nonzero. In the cases where the mathematical
requirements in @ref{baselib math semantics integer} cannot be satisfied
by any number object, either an exception is raised with condition type
@code{&implementation-restriction}, or unspecified number objects (one
for for @func{div}, @func{mod}, @func{div0} and @func{mod0}, two for
@func{div-and-mod} and @func{div0-and-mod0}) are returned.

@example
(div x1 x2)        @result{} x1 div x2
(mod x1 x2)        @result{} x1 mod x2

(div-and-mod x1 x2)
@result{} x1 div x2,
   x1 mod x2 ;; two return values

(div0 x1 x2)       @result{} x1 div_0 x2
(mod0 x1 x2)       @result{} x1 mod_0 x2

(div0-and-mod0 x1 x2)
@result{} x1 div_0 x2,
   x1 mod_0 x2 ;; two return values
@end example
@end deffn


@deffn Procedure gcd @vari{n} @dots{}
@deffnx Procedure lcm @vari{n} @dots{}
These procedures return the greatest common divisor or least common
multiple of their arguments.  The result is always non--negative.

@example
(gcd 32 -36)                    @result{} 4
(gcd)                           @result{} 0
(lcm 32 -36)                    @result{} 288
(lcm 32.0 -36)                  @result{} 288.0
(lcm)                           @result{} 1
@end example
@end deffn


@c page
@node baselib math ops part
@subsubsection Part separation


@deffn Procedure numerator @var{q}
@deffnx Procedure denominator @var{q}
These procedures return the numerator or denominator of their argument;
the result is computed as if the argument was represented as a fraction
in lowest terms.  The denominator is always positive.  The denominator
of @code{0} is defined to be @code{1}.

@example
(numerator   (/ 6 4))           @result{} 3
(denominator (/ 6 4))           @result{} 2
(denominator (inexact (/ 6 4))) @result{} 2.0
@end example
@end deffn


@deffn Procedure floor @var{x}
@deffnx Procedure ceiling @var{x}
@deffnx Procedure truncate @var{x}
@deffnx Procedure round @var{x}
These procedures return inexact integer objects for inexact arguments
that are not infinities or NaNs, and exact integer objects for exact
rational arguments.

@table @func
@item floor
Returns the largest integer object not larger than @var{x}.

@item ceiling
Returns the smallest integer object not smaller than @var{x}.

@item truncate
Returns the integer object closest to @var{x} whose absolute value is
not larger than the absolute value of @var{x}.

@item round
Returns the closest integer object to @var{x}, rounding to even when
@var{x} represents a number halfway between two integers (this conforms
to @ieee{} 754 round to nearest mode).
@end table

If the argument to one of these procedures is inexact, then the result
is also inexact.  If an exact value is needed, the result should be
passed to the @func{exact} procedure.

Although infinities and NaNs are not integer objects, these procedures
return an infinity when given an infinity as an argument, and a NaN when
given a NaN.

@example
(floor -4.3)            @result{} -5.0
(ceiling -4.3)          @result{} -4.0
(truncate -4.3)         @result{} -4.0
(round -4.3)            @result{} -4.0

(floor 3.5)             @result{} 3.0
(ceiling 3.5)           @result{} 4.0
(truncate 3.5)          @result{} 3.0
(round 3.5)             @result{} 4.0

(round 7/2)             @result{} 4
(round 7)               @result{} 7

(floor +inf.0)          @result{} +inf.0
(ceiling -inf.0)        @result{} -inf.0
(round +nan.0)          @result{} +nan.0
@end example
@end deffn


@deffn Procedure rationalize @vari{x} @varii{x}
The @func{rationalize} procedure returns a number object representing
the @emph{simplest} rational number differing from @vari{x} by no more
than @varii{x}.

A rational number @math{r_1} is @emph{simpler} than another rational
number @math{r_2} if @math{r_1 = p_1/q_1} and @math{r_2 = p_2/q_2} (in
lowest terms) and @math{|p_1| <= |p_2|} and @math{|q_1| <= |q_2|}.  Thus
@math{3/5} is simpler than @math{4/7}.

Although not all rationals are comparable in this ordering (consider
@math{2/7} and @math{3/5}) any interval contains a rational number that
is simpler than every other rational number in that interval (the
simpler @math{2/5} lies between @math{2/7} and @math{3/5}).

Note that @math{0 = 0/1} is the simplest rational of all.

@example
(rationalize (exact .3) 1/10)   @result{} 1/3
(rationalize .3 1/10)           @result{} #i1/3 ; approximately

(rationalize +inf.0 3)          @result{} +inf.0
(rationalize +inf.0 +inf.0)     @result{} +nan.0
(rationalize 3 +inf.0)          @result{} 0.0
@end example

The first two examples hold only in implementations whose inexact real
number objects have sufficient precision.
@end deffn


@c page
@node baselib math ops trascend
@subsubsection Trascendental functions


@deffn Procedure exp @var{z}
@deffnx Procedure log @var{z}
@deffnx Procedure log @vari{z} @varii{z}
@deffnx Procedure sin @var{z}
@deffnx Procedure cos @var{z}
@deffnx Procedure tan @var{z}
@deffnx Procedure asin @var{z}
@deffnx Procedure acos @var{z}
@deffnx Procedure atan @var{z}
@deffnx Procedure atan @vari{x} @varii{x}
These procedures compute the usual transcendental functions.

The @func{exp} procedure computes the base--@var{e} exponential of
@var{z}.

The @func{log} procedure with a single argument computes the natural
logarithm of @var{z} (@strong{not} the base--10 logarithm); @code{(log
@vari{z} @varii{z})} computes the base--@varii{z} logarithm of @vari{z}.

The @func{asin}, @func{acos}, and @func{atan} procedures compute
arcsine, arccosine, and arctangent, respectively.  The two--argument
variant of @func{atan} computes:

@example
(angle (make-rectangular @varii{x} @vari{x}))
@end example

These procedures may return inexact results even when given exact
arguments.

@example
(exp +inf.0)    @result{} +inf.0
(exp -inf.0)    @result{} 0.0
(log +inf.0)    @result{} +inf.0
(log 0.0)       @result{} -inf.0
(log 0)         @result{} exception &assertion

(log -inf.0)    @result{} +inf.0+3.141592653589793i
                ; approximately

(atan -inf.0)   @result{} -1.5707963267948965
                ; approximately

(atan +inf.0)   @result{} 1.5707963267948965
                ; approximately

(log -1.0+0.0i) @result{} 0.0+3.141592653589793i
                ; approximately

(log -1.0-0.0i) @result{} 0.0-3.141592653589793i
                ; approximately if -0.0 is distinguished
@end example
@end deffn



@c page
@node baselib math ops exponentiation
@subsubsection Exponentiation functions


@deffn Procedure sqrt @var{z}
Return the principal square root of @var{z}.  For rational @var{z}, the
result has either positive real part, or zero real part and
non--negative imaginary part.  With @math{\log} defined as in
@ref{baselib math semantics trascend}, the value of @code{(sqrt
@var{z})} could be expressed as @math{e^((\log z)/2)}.

The @func{sqrt} procedure may return an inexact result even when given
an exact argument.

@example
(sqrt -5)               @result{} 0.0+2.23606797749979i
                        ; approximately
(sqrt +inf.0)           @result{} +inf.0
(sqrt -inf.0)           @result{} +inf.0i
@end example
@end deffn


@deffn Procedure exact-integer-sqrt @var{k}
The @func{exact-integer-sqrt} procedure returns two non--negative exact
integer objects @math{s} and @math{r} where @math{@var{k} = s^2 + r} and
@math{@var{k} < (s+1)^2}.

@example
(exact-integer-sqrt 4)  @result{} 2 0 ; two return values
(exact-integer-sqrt 5)  @result{} 2 1 ; two return values
@end example
@end deffn


@deffn Procedure expt @vari{z} @varii{z}
Return @vari{z} raised to the power @varii{z}.  For non--zero @vari{z},
this is @math{e^(z_2 \log z_1)}.  @math{0.0^z} is @math{1.0} if
@math{@var{z} = 0.0}, and @math{0.0} if @code{(real-part @var{z})} is
positive.  For other cases in which the first argument is zero, either
an exception is raised with condition type
@code{&implementation-restriction}, or an unspecified number object is
returned.

For an exact real number object @vari{z} and an exact integer object
@varii{z}, @code{(expt @vari{z} @varii{z})} must return an exact result.
For all other values of @vari{z} and @varii{z}, @code{(expt @vari{z}
@varii{z})} may return an inexact result, even when both @vari{z} and
@varii{z} are exact.

@example
(expt 5 3)                  @result{} 125
(expt 5 -3)                 @result{} 1/125
(expt 5 0)                  @result{} 1
(expt 0 5)                  @result{} 0
(expt 0 5+.0000312i)        @result{} 0.0
(expt 0 -5)                 @result{} unspecified
(expt 0 -5+.0000312i)       @result{} unspecified
(expt 0 0)                  @result{} 1
(expt 0.0 0.0)              @result{} 1.0
@end example
@end deffn


@c page
@node baselib math ops complex
@subsubsection Complex numbers functions


@deffn Procedure make-rectangular @vari{x} @varii{x}
@deffnx Procedure make-polar @variii{x} @variv{x}
@deffnx Procedure real-part @var{z}
@deffnx Procedure imag-part @var{z}
@deffnx Procedure magnitude @var{z}
@deffnx Procedure angle @var{z}
Suppose @math{a_1}, @math{a_2}, @math{a_3}, and @math{a_4} are real
numbers, and @math{c} is a complex number such that the following holds:

@example
c = a_1 + a_2 i = a_3 e^(i a_4)
@end example

Then, if @vari{x}, @varii{x}, @variii{x}, and @variv{x} are number
objects representing @math{a_1}, @math{a_2}, @math{a_3}, and @math{a_4},
respectively, @code{(make-rectangular @vari{x} @varii{x})} returns
@math{c}, and @code{(make-polar @variii{x} @variv{x})} returns @math{c}.

@example
(make-rectangular 1.1 2.2)    @result{} 1.1+2.2i ; approximately
(make-polar 1.1 2.2)          @result{} 1.1@@2.2 ; approximately
@end example

Conversely, if @math{-\pi <= a_4 <= \pi}, and if @math{z} is a number
object representing @math{c}, then @code{(real-part @var{z})} returns
@math{a_1}, @code{(imag-part @var{z})} returns @math{a_2},
@code{(magnitude @var{z})} returns @math{a_3}, and @code{(angle
@var{z})} returns @math{a_4}.

@example
(real-part 1.1+2.2i)    @result{} 1.1
                        ; approximately
(imag-part 1.1+2.2i)    @result{} 2.2
                        ; approximately
(magnitude 1.1@@2.2)     @result{} 1.1
                        ; approximately
(angle     1.1@@2.2)     @result{} 2.2
                        ; approximately

(angle -1.0)            @result{} 3.141592653589793
                        ; approximately
(angle -1.0+0.0i)       @result{} 3.141592653589793
                        ; approximately
(angle -1.0-0.0i)       @result{} -3.141592653589793
                        ; approximately if -0.0 is distinguished
(angle +inf.0)          @result{} 0.0
(angle -inf.0)          @result{} 3.141592653589793
                        ; approximately
@end example

Moreover, suppose @vari{x}, @varii{x} are such that either @vari{x} or
@varii{x} is an infinity, then

@example
(make-rectangular x1 x2)        @result{} @var{z}
(magnitude z)                   @result{} +inf.0
@end example

The @func{make-polar}, @func{magnitude}, and @func{angle} procedures may
return inexact results even when given exact arguments.

@example
(angle -1)              @result{} 3.141592653589793
                        ; approximately
@end example
@end deffn

@c page
@node baselib math string
@subsection Numerical Input and Output


@deffn Procedure {number->string} @var{z}
@deffnx Procedure {number->string} @var{z} @var{radix}
@deffnx Procedure {number->string} @var{z} @var{radix} @var{precision}
The @func{number->string} procedure takes a number object and a radix
and returns as a string an external representation of the given number
object in the given radix such that:

@example
(let ((number @var{z})
      (radix  @var{radix}))
  (eqv?
    (string->number (number->string number radix) radix)
    number))
@end example

@noindent
is true.  If no possible result makes this expression true, an exception
with condition type @code{&implementation-restriction} is raised.

@var{radix} must be an exact integer object, either @math{2}, @math{8},
@math{10}, or @math{16}.  If omitted, @var{radix} defaults to @math{10}.
If a @var{precision} is specified, then @var{z} must be an inexact
complex number object, @var{precision} must be an exact positive integer
object, and @var{radix} must be @math{10}.

@quotation
@emph{Note:} The error case can occur only when @var{z} is not a complex
number object or is a complex number object with a non--rational real or
imaginary part.
@end quotation

If a @var{precision} is specified, then the representations of the
inexact real components of the result, unless they are infinite or NaN,
specify an explicit @meta{mantissa width} @math{p}, and @math{p} is the
least @math{p >= @var{precision}} for which the above expression is
true.

If @var{z} is inexact, the radix is @math{10}, and the above expression
and condition can be satisfied by a result that contains a decimal
point, then the result contains a decimal point and is expressed using
the minimum number of digits (exclusive of exponent, trailing zeroes,
and mantissa width) needed to make the above expression and condition
true; otherwise the format of the result is unspecified.

The result returned by @func{number->string} never contains an explicit
radix prefix.
@end deffn


@deffn Procedure {string->number} @var{string}
@deffnx Procedure {string->number} @var{string} @var{radix}
Return a number object with maximally precise representation expressed
by the given @var{string}.

@var{radix} must be an exact integer object, either @math{2}, @math{8},
@math{10}, or @math{16}.  If supplied, @var{radix} is a default radix
that may be overridden by an explicit radix prefix in @var{string}
(e.g. @code{#o177}).  If @var{radix} is not supplied, then the default
radix is @math{10}.

If @var{string} is not a syntactically valid notation for a number
object or a notation for a rational number object with a zero
denominator, then @func{string->number} returns @false{}.

@example
(string->number "100")          @result{} 100
(string->number "100" 16)       @result{} 256
(string->number "1e2")          @result{} 100.0
(string->number "0/0")          @result{} #f
(string->number "+inf.0")       @result{} +inf.0
(string->number "-inf.0")       @result{} -inf.0
(string->number "+nan.0")       @result{} +nan.0
@end example

@quotation
@emph{Note:} The @func{string->number} procedure always returns a number
object or @false{}; it never raises an exception.
@end quotation
@end deffn

@c page
@node baselib booleans
@section Booleans


The standard boolean objects for true and false have external
representations @true{} and @false{}.  However, of all objects, only
@false{} counts as false in conditional expressions.

@quotation
@emph{Note:} Programmers accustomed to other dialects of Lisp should be
aware that Scheme distinguishes both @false{} and the empty list from
each other and from the symbol @code{nil}.
@end quotation


@deffn Procedure not @var{obj}
Return @true{} if @var{obj} is @false{}, or @false{} otherwise.

@example
(not #t)         @result{} #f
(not 3)          @result{} #f
(not (list 3))   @result{} #f
(not #f)         @result{} #t
(not '())        @result{} #f
(not (list))     @result{} #f
(not 'nil)       @result{} #f
@end example
@end deffn


@deffn Procedure {boolean?} @var{obj}
Return @true{} if @var{obj} is either @true{} or @false{}, or @false{}
otherwise.

@example
(boolean? #f)   @result{} #t
(boolean? 0)    @result{} #f
(boolean? '())  @result{} #f
@end example
@end deffn


@deffn Procedure {boolean=?} @vari{bool} @varii{bool} @variii{bool} @dots{}
Return @true{} if the booleans are the same.
@end deffn


@c page
@node baselib lists
@section Pairs and lists


A @emph{pair} is a compound structure with two fields called the car and
cdr fields (for historical reasons).  Pairs are created by the procedure
@func{cons}.  The car and cdr fields are accessed by the procedures
@func{car} and @func{cdr}.

Pairs are used primarily to represent lists.  A list can be defined
recursively as either the empty list or a pair whose cdr is a list.
More precisely, the set of lists is defined as the smallest set @var{X}
such that:

@itemize
@item
The empty list is in @var{X}.

@item
If @var{list} is in @var{X}, then any pair whose cdr field contains
@var{list} is also in @var{X}.
@end itemize

The objects in the car fields of successive pairs of a list are the
elements of the list.  For example, a two--element list is a pair whose
car is the first element and whose cdr is a pair whose car is the second
element and whose cdr is the empty list.  The length of a list is the
number of elements, which is the same as the number of pairs.

The empty list is a special object of its own type.  It is not a pair.
It has no elements and its length is zero.

@quotation
@emph{Note:} The above definitions imply that all lists have finite
length and are terminated by the empty list.
@end quotation

A chain of pairs not ending in the empty list is called an
@emph{improper list}.  Note that an improper list is not a list.  The
list and dotted notations can be combined to represent improper lists:

@example
(a b c . d)
@end example

@noindent
is equivalent to

@example
(a . (b . (c . d)))
@end example

Whether a given pair is a list depends upon what is stored in the cdr
field.


@deffn Procedure {pair?} @var{obj}
Return @true{} if @var{obj} is a pair, @false{} otherwise.

@example
(pair? '(a . b))        @result{} #t
(pair? '(a b c))        @result{} #t
(pair? '())             @result{} #f
(pair? '#(a b))         @result{} #f
@end example
@end deffn


@deffn Procedure cons @vari{obj} @varii{obj}
Return a newly allocated pair whose car is @vari{obj} and whose cdr is
@varii{obj}.  The pair is guaranteed to be different (in the sense of
@func{eqv?}) from every existing object.

@example
(cons 'a '())           @result{}  (a)
(cons '(a) '(b c d))    @result{}  ((a) b c d)
(cons "a" '(b c))       @result{}  ("a" b c)
(cons 'a 3)             @result{}  (a . 3)
(cons '(a b) 'c)        @result{}  ((a b) . c)
@end example
@end deffn


@deffn Procedure car @var{pair}
Return the contents of the car field of @var{pair}.

@example
(car '(a b c))          @result{} a
(car '((a) b c d))      @result{} (a)
(car '(1 . 2))          @result{} 1
(car '())               @result{} exception &assertion
@end example
@end deffn


@deffn Procedure cdr @var{pair}
Return the contents of the cdr field of @var{pair}.

@example
(cdr '((a) b c d))      @result{} (b c d)
(cdr '(1 . 2))          @result{} 2
(cdr '())               @result{} exception &assertion
@end example
@end deffn


@deffn Procedure caar @var{pair}
@deffnx Procedure cadr @var{pair}
@deffnx Procedure ...
@deffnx Procedure cdddar @var{pair}
@deffnx Procedure cddddr @var{pair}
These procedures are compositions of @func{car} and @func{cdr}, where
for example @func{caddr} could be defined by

@example
(define caddr (lambda (x) (car (cdr (cdr x)))))
@end example

Arbitrary compositions, up to four deep, are provided.  There are
twenty--eight of these procedures in all.
@end deffn


@deffn Procedure {null?} @var{obj}
Return @true{} if @var{obj} is the empty list, @false{} otherwise.
@end deffn


@deffn Procedure {list?} @var{obj}
Return @true{} if @var{obj} is a list, @false{} otherwise.  By
definition, all lists are chains of pairs that have finite length and
are terminated by the empty list.

@example
(list? '(a b c))     @result{} #t
(list? '())          @result{} #t
(list? '(a . b))     @result{} #f
@end example
@end deffn


@deffn Procedure list @var{obj} @dots{}
Return a newly allocated list of its arguments.

@example
(list 'a (+ 3 4) 'c)    @result{} (a 7 c)
(list)                  @result{} ()
@end example
@end deffn


@deffn Procedure length @var{list}
Return the length of @var{list}.

@example
(length '(a b c))               @result{}  3
(length '(a (b) (c d e)))       @result{}  3
(length '())                    @result{}  0
@end example
@end deffn


@deffn Procedure append
@deffnx Procedure append @var{list} @dots{} @var{obj}
Return a possibly improper list consisting of the elements of the first
@var{list} followed by the elements of the other @var{list}s, with
@var{obj} as the cdr of the final pair.  An improper list results if
@var{obj} is not a list.

@example
(append '(x) '(y))              @result{}  (x y)
(append '(a) '(b c d))          @result{}  (a b c d)
(append '(a (b)) '((c)))        @result{}  (a (b) (c))
(append '(a b) '(c . d))        @result{}  (a b c . d)
(append '() 'a)                 @result{}  a
@end example

If @func{append} constructs a non--empty chain of pairs, it is always
newly allocated.  If no pairs are allocated, @var{obj} is returned.

@quotation
@rnrs{6} mandates that this function requires at least the argument
@var{obj}; it is illegal to call this function with no arguments.
Despite this: The Scheme implementations supported by Nausicaa (Larceny,
Ikarus, Mosh, Ypsilon) allow calling this function with no arguments, in
which case the return value is the empty string.  The @library{nausicaa}
language will ensure this behaviour.  (Fri Jun 5, 2009)
@end quotation
@end deffn


@deffn Procedure reverse @var{list}
Return a newly allocated list consisting of the elements of @var{list}
in reverse order.

@example
(reverse '(a b c))              @result{} (c b a)
(reverse '(a (b c) d (e (f))))  @result{} ((e (f)) d (b c) a)
@end example
@end deffn


@deffn Procedure list-tail @var{list} @var{k}
@var{list} should be a list of size at least @var{k}.  Return the
subchain of pairs of @var{list} obtained by omitting the first @var{k}
elements.

@example
(list-tail '(a b c d) 2)        @result{}  (c d)
@end example

@strong{Implementation responsibilities:} The implementation must check
that @var{list} is a chain of pairs whose length is at least @var{k}.
It should not check that it is a chain of pairs beyond this length.
@end deffn


@deffn Procedure list-ref @var{list} @var{k}
@var{list} must be a list whose length is at least @emph{@var{k} + 1}.
The @func{list-tail} procedure returns the @var{k}th element of
@var{list}.

@example
(list-ref '(a b c d) 2)         @result{} c
@end example

@strong{Implementation responsibilities:} The implementation must check
that @var{list} is a chain of pairs whose length is at least
@emph{@var{k} + 1}.  It should not check that it is a list of pairs
beyond this length.
@end deffn


@deffn Procedure map @var{proc} @vari{list} @varii{list} @dots{}
The @var{list}s should all have the same length.  @var{proc} should
accept as many arguments as there are @var{list}s and return a single
value.  @var{proc} should not mutate any of the @var{list}s.

The @func{map} procedure applies @var{proc} element--wise to the
elements of the @var{list}s and returns a list of the results, in order.
@var{proc} is always called in the same dynamic environment as
@func{map} itself.  The order in which @var{proc} is applied to the
elements of the @var{list}s is unspecified.  If multiple returns occur
from @func{map}, the values returned by earlier returns are not mutated.

@example
(map cadr '((a b) (d e) (g h)))         @result{}  (b e h)

(map (lambda (n) (expt n n))
     '(1 2 3 4 5))                      @result{}  (1 4 27 256 3125)

(map + '(1 2 3) '(4 5 6))               @result{}  (5 7 9)

(let ((count 0))
  (map (lambda (ignored)
         (set! count (+ count 1))
         count)
       '(a b)))                         @result{}  (1 2) or (2 1)
@end example

@strong{Implementation responsibilities:} The implementation should
check that the @var{list}s all have the same length.  The implementation
must check the restrictions on @var{proc} to the extent performed by
applying it as described.  An implementation may check whether
@var{proc} is an appropriate argument before applying it.
@end deffn


@deffn Procedure for-each @var{proc} @vari{list} @varii{list} @dots{}
The @var{list}s should all have the same length.  @var{proc} should
accept as many arguments as there are @var{list}s.  @var{proc} should
not mutate any of the @var{list}s.

The @func{for-each} procedure applies @var{proc} element--wise to the
elements of the @var{list}s for its side effects, in order from the
first elements to the last.  @var{proc} is always called in the same
dynamic environment as @func{for-each} itself.  The return values of
@func{for-each} are unspecified.

@example
(let ((v (make-vector 5)))
  (for-each (lambda (i)
              (vector-set! v i (* i i)))
            '(0 1 2 3 4))
  v)
@result{}  #(0 1 4 9 16)

(for-each (lambda (x) x) '(1 2 3 4))
@result{} unspecified

(for-each even? '())
@result{} unspecified
@end example

@strong{Implementation responsibilities:} The implementation should
check that the @var{list}s all have the same length.  The implementation
must check the restrictions on @var{proc} to the extent performed by
applying it as described.  An implementation may check whether
@var{proc} is an appropriate argument before applying it.

@quotation
@emph{Note:} Implementations of @code{for-each} may or may not
tail--call @var{proc} on the last elements.
@end quotation
@end deffn


@c page
@node baselib symbols
@section Symbols


Symbols are objects whose usefulness rests on the fact that two symbols
are identical (in the sense of @func{eq?}, @func{eqv?} and
@func{equal?}) if and only if their names are spelled the same way.  A
symbol literal is formed using @func{quote}.


@deffn Procedure {symbol?} @var{obj}
Return @true{} if @var{obj} is a symbol, @false{} otherwise.

@example
(symbol? 'foo)          @result{} #t
(symbol? (car '(a b)))  @result{} #t
(symbol? "bar")         @result{} #f
(symbol? 'nil)          @result{} #t
(symbol? '())           @result{} #f
(symbol? #f)            @result{} #f
@end example
@end deffn


@deffn Procedure {symbol->string} @var{symbol}
Return the name of @var{symbol} as an immutable string.

@example
(symbol->string 'flying-fish)                   @result{}  "flying-fish"
(symbol->string 'Martin)                        @result{}  "Martin"
(symbol->string (string->symbol "Malvina"))     @result{}  "Malvina"
@end example
@end deffn


@deffn Procedure {symbol=?} @vari{symbol} @varii{symbol} @variii{symbol} @dots{}
Return @true{} if the symbols are the same, i.e., if their names are
spelled the same.
@end deffn


@deffn Procedure {string->symbol} @var{string}
Return the symbol whose name is @var{string}.

@example
(eq? 'mISSISSIppi 'mississippi)
@result{} #f

(string->symbol "mISSISSIppi")
@result{} the symbol with name "mISSISSIppi"

(eq? 'bitBlt (string->symbol "bitBlt"))
@result{} #t

(eq? 'JollyWog (string->symbol (symbol->string 'JollyWog)))
@result{} #t

(string=? "K. Harper, M.D."
          (symbol->string (string->symbol "K. Harper, M.D.")))
@result{} #t
@end example
@end deffn


@c page
@node baselib characters
@section Characters


@emph{Characters} are objects that represent Unicode scalar values.

@quotation
Unicode defines a standard mapping between sequences of @emph{Unicode
scalar values} (integers in the range @math{0} to @code{#x10FFFF},
excluding the range @code{#xD800} to @code{#xDFFF}) in the latest
version of the standard and human--readable ``characters''.

More precisely, Unicode distinguishes between glyphs, which are printed
for humans to read, and characters, which are abstract entities that map
to glyphs (sometimes in a way that's sensitive to surrounding
characters).  Furthermore, different sequences of scalar values
sometimes correspond to the same character.  The relationships among
scalar, characters, and glyphs are subtle and complex.

Despite this complexity, most things that a literate human would call a
``character'' can be represented by a single Unicode scalar value
(although several sequences of Unicode scalar values may represent that
same character).  For example, Roman letters, Cyrillic letters, Hebrew
consonants, and most Chinese characters fall into this category.

Unicode scalar values exclude the range @code{#xD800} to @code{#xDFFF},
which are part of the range of Unicode @emph{code points}.  However, the
Unicode code points in this range, the so--called @emph{surrogates}, are
an artifact of the @utf{}--16 encoding, and can only appear in specific
Unicode encodings, and even then only in pairs that encode scalar
values.  Consequently, all characters represent code points, but the
surrogate code points do not have representations as characters.
@end quotation


@deffn Procedure {char?} @var{obj}
Return @true{} if @var{obj} is a character, @false{} otherwise.
@end deffn


@deffn Procedure {char->integer} @var{char}
@deffnx Procedure {integer->char} @var{sv}
@var{sv} must be a Unicode scalar value, i.e., a non--negative exact
integer object in @code{[0, #xD7FF] union [#xE000, #x10FFFF]}.

Given a character, @func{char->integer} returns its Unicode scalar value
as an exact integer object.  For a Unicode scalar value @var{sv},
@func{integer->char} returns its associated character.

@example
(integer->char 32)                      @result{} #\space
(char->integer (integer->char 5000))    @result{} 5000
(integer->char #\xD800)                 @result{} exception &assertion
@end example
@end deffn


@deffn Procedure {char=?} @vari{char} @varii{char} @variii{char} @dots{}
@deffnx Procedure {char<?} @vari{char} @varii{char} @variii{char} @dots{}
@deffnx Procedure {char>?} @vari{char} @varii{char} @variii{char} @dots{}
@deffnx Procedure {char<=?} @vari{char} @varii{char} @variii{char} @dots{}
@deffnx Procedure {char>=?} @vari{char} @varii{char} @variii{char} @dots{}
These procedures impose a total ordering on the set of characters
according to their Unicode scalar values.

@example
@c (char<? #\z #\\ss)      @result{} #t
(char<? #\z #\Z)                @result{} #f
@end example
@end deffn


@c page
@node baselib strings
@section Strings


Strings are sequences of characters.  The @emph{length} of a string is
the number of characters that it contains.  This number is fixed when
the string is created.  The @emph{valid indices} of a string are the
integers less than the length of the string.  The first character of a
string has index 0, the second has index 1, and so on.


@deffn Procedure {string?} @var{obj}
Return @true{} if @var{obj} is a string, @false{} otherwise.
@end deffn


@deffn Procedure make-string @var{k}
@deffnx Procedure make-string @var{k} @var{char}
Return a newly allocated string of length @var{k}.  If @var{char} is
given, then all elements of the string are initialized to @var{char},
otherwise the contents of the @var{string} are unspecified.
@end deffn


@deffn Procedure string @var{char} @dots{}
Return a newly allocated string composed of the arguments.
@end deffn


@deffn Procedure string-length @var{string}
Return the number of characters in the given @var{string} as an exact
integer object.
@end deffn


@deffn Procedure string-ref @var{string} @var{k}
@var{k} must be a valid index of @var{string}.  The @func{string-ref}
procedure returns character @var{k} of @var{string} using zero--origin
indexing.

@quotation
@emph{Note:} Implementors should make @func{string-ref} run in constant
time.
@end quotation
@end deffn


@deffn Procedure {string=?} @vari{string} @varii{string} @variii{string} @dots{}
Return @true{} if the strings are the same length and contain the same
characters in the same positions.  Otherwise, the @func{string=?}
procedure returns @false{}.

@example
(string=? "Strause" "Strasse")    @result{} #f
@end example
@end deffn


@deffn Procedure {string<?} @vari{string} @varii{string} @variii{string} @dots{}
@deffnx Procedure {string>?} @vari{string} @varii{string} @variii{string} @dots{}
@deffnx Procedure {string<=?} @vari{string} @varii{string} @variii{string} @dots{}
@deffnx Procedure {string>=?} @vari{string} @varii{string} @variii{string} @dots{}
These procedures are the lexicographic extensions to strings of the
corresponding orderings on characters.  For example, @func{string<?} is
the lexicographic ordering on strings induced by the ordering
@func{char<?} on characters.  If two strings differ in length but are
the same up to the length of the shorter string, the shorter string is
considered to be lexicographically less than the longer string.

@example
(string<? "z" "a")      @result{} #t
(string<? "z" "zz")     @result{} #t
(string<? "z" "Z")      @result{} #f
@end example
@end deffn


@deffn Procedure substring @var{string} @var{start} @var{end}
@var{string} must be a string, and @var{start} and @var{end} must be
exact integer objects satisfying:

@example
0 <= @var{start} <= @var{end} <= (string-length @var{string})
@end example

The @func{substring} procedure returns a newly allocated string formed
from the characters of @var{string} beginning with index @var{start}
(inclusive) and ending with index @var{end} (exclusive).
@end deffn


@deffn Procedure string-append @var{string} @dots{}
Return a newly allocated string whose characters form the concatenation
of the given strings.
@end deffn


@deffn Procedure string->list @var{string}
@deffnx Procedure list->string @var{list}
@var{list} must be a list of characters.

The @func{string->list} procedure returns a newly allocated list of the
characters that make up the given string.

The @func{list->string} procedure returns a newly allocated string
formed from the characters in @var{list}.

The @func{string->list} and @func{list->string} procedures are inverses
so far as @func{equal?} is concerned.
@end deffn


@deffn Procedure string-for-each @var{proc} @vari{string} @varii{string} @dots{}
The @var{string}s must all have the same length.  @var{proc} should
accept as many arguments as there are @var{string}s.

The @func{string-for-each} procedure applies @var{proc} element--wise to
the characters of the @var{string}s for its side effects, in order from
the first characters to the last.  @var{proc} is always called in the
same dynamic environment as @func{string-for-each} itself.  The return
values of @func{string-for-each} are unspecified.

Analogous to @func{for-each}.

@strong{Implementation responsibilities:} The implementation must check
the restrictions on @var{proc} to the extent performed by applying it as
described.  An implementation may check whether @var{proc} is an
appropriate argument before applying it.
@end deffn


@deffn Procedure string-copy @var{string}
Returns a newly allocated copy of the given @var{string}.
@end deffn


@c page
@node baselib vectors
@section Vectors


Vectors are heterogeneous structures whose elements are indexed by
integers.  A vector typically occupies less space than a list of the
same length, and the average time needed to access a randomly chosen
element is typically less for the vector than for the list.

The @emph{length} of a vector is the number of elements that it
contains.  This number is a non--negative integer that is fixed when the
vector is created.  The @emph{valid indices} of a vector are the exact
non--negative integer objects less than the length of the vector.  The
first element in a vector is indexed by zero, and the last element is
indexed by one less than the length of the vector.

Like list constants, vector constants must be quoted:

@example
'#(0 (2 2 2 2) "Anna")     @result{}  #(0 (2 2 2 2) "Anna")
@end example


@deffn Procedure vector? @var{obj}
Return @true{} if @var{obj} is a vector, @false{} otherwise.
@end deffn


@deffn Procedure make-vector @var{k}
@deffnx Procedure make-vector @var{k} @var{fill}
Return a newly allocated vector of @var{k} elements.  If a second
argument is given, then each element is initialized to @var{fill}.
Otherwise the initial contents of each element is unspecified.
@end deffn


@deffn Procedure vector @var{obj} @dots{}
Return a newly allocated vector whose elements contain the given
arguments.  Analogous to @code{list}.

@example
(vector 'a 'b 'c)               @result{}  #(a b c)
@end example
@end deffn


@deffn Procedure vector-length @var{vector}
Return the number of elements in @var{vector} as an exact integer
object.
@end deffn


@deffn Procedure vector-ref @var{vector} @var{k}
@var{k} must be a valid index of @var{vector}.  The @func{vector-ref}
procedure returns the contents of element @var{k} of @var{vector}.

@example
(vector-ref '#(1 1 2 3 5 8 13 21) 5)     @result{}  8
@end example
@end deffn


@deffn Procedure vector-set! @var{vector} @var{k} @var{obj}
@var{k} must be a valid index of @var{vector}.  The @func{vector-set!}
procedure stores @var{obj} in element @var{k} of @var{vector}, and
returns unspecified.

Passing an immutable vector to @func{vector-set!} should cause an
exception with condition type @code{&assertion} to be raised.

@example
(let ((vec (vector 0 '(2 2 2 2) "Anna")))
  (vector-set! vec 1 '("Sue" "Sue"))
  vec)
@result{}  #(0 ("Sue" "Sue") "Anna")

(vector-set! '#(0 1 2) 1 "doe")
@result{}  unspecified
    ;; constant vector
    ;; should raise exception &assertion
@end example
@end deffn


@deffn Procedure vector->list @var{vector}
@deffnx Procedure list->vector @var{list}
The @func{vector->list} procedure returns a newly allocated list of the
objects contained in the elements of @var{vector}.

The @func{list->vector} procedure returns a newly created vector
initialized to the elements of the list @var{list}.

@example
(vector->list '#(dah dah didah))        @result{} (dah dah didah)
(list->vector '(dididit dah))           @result{} #(dididit dah)
@end example
@end deffn


@deffn Procedure {vector-fill!} @var{vector fill}
Store @var{fill} in every element of @var{vector} and returns
unspecified.
@end deffn


@deffn Procedure vector-map @var{proc} @vari{vector} @varii{vector} @dots{}
The @var{vector}s must all have the same length.  @var{proc} should
accept as many arguments as there are @var{vector}s and return a single
value.

The @func{vector-map} procedure applies @var{proc} element--wise to the
elements of the @var{vector}s and returns a vector of the results, in
order.  @var{proc} is always called in the same dynamic environment as
@func{vector-map} itself.  The order in which @var{proc} is applied to
the elements of the @var{vector}s is unspecified.  If multiple returns
occur from @func{vector-map}, the return values returned by earlier
returns are not mutated.

Analogous to @func{map}.

@strong{Implementation responsibilities:} The implementation must check
the restrictions on @var{proc} to the extent performed by applying it as
described.  An implementation may check whether @var{proc} is an
appropriate argument before applying it.
@end deffn


@deffn Procedure vector-for-each @var{proc} @vari{vector} @varii{vector} @dots{}
The @var{vector}s must all have the same length.  @var{proc} should
accept as many arguments as there are @var{vector}s.  The
@code{vector-for-each} procedure applies @var{proc} element--wise to the
elements of the @var{vector}s for its side effects, in order from the
first elements to the last.  @var{proc} is always called in the same
dynamic environment as @func{vector-for-each} itself.  The return values
of @func{vector-for-each} are unspecified.

Analogous to @func{for-each}.

@strong{Implementation responsibilities:} The implementation must check
the restrictions on @var{proc} to the extent performed by applying it as
described.  An implementation may check whether @var{proc} is an
appropriate argument before applying it.
@end deffn


@c page
@node baselib errors
@section Errors and violations


@deffn Procedure error @var{who} @var{message} @vari{irritant} @dots{}
@deffnx Procedure assertion-violation @var{who} @var{message} @vari{irritant} @dots{}
@var{who} must be a string or a symbol or @false{}.  @var{message} must
be a string.  The @var{irritant}s are arbitrary objects.

These procedures raise an exception.  The @func{error} procedure should
be called when an error has occurred, typically caused by something that
has gone wrong in the interaction of the program with the external world
or the user.  The @func{assertion-violation} procedure should be called
when an invalid call to a procedure was made, either passing an invalid
number of arguments, or passing an argument that it is not specified to
handle.

The @var{who} argument should describe the procedure or operation that
detected the exception.  The @var{message} argument should describe the
exceptional situation.  The @var{irritant}s should be the arguments to
the operation that detected the operation.

The condition object provided with the exception has the following
condition types:

@itemize
@item
If @var{who} is not @false{}, the condition has condition type
@code{&who}, with @var{who} as the value of its field.  In that case,
@var{who} should be the name of the procedure or entity that detected
the exception.  If it is @false{}, the condition does not have condition
type @code{&who}.

@item
The condition has condition type @code{&message}, with @var{message} as
the value of its field.

@item
The condition has condition type @code{&irritants}, and its field has as
its value a list of the @var{irritant}s.
@end itemize

Moreover, the condition created by @func{error} has condition type
@code{&error}, and the condition created by @func{assertion-violation}
has condition type @code{&assertion}.

@example
(define (fac n)
  (if (not (integer-valued? n))
      (assertion-violation
       'fac "non-integral argument" n))
  (if (negative? n)
      (assertion-violation
       'fac "negative argument" n))
  (letrec
    ((loop (lambda (n r)
             (if (zero? n)
                 r
                 (loop (- n 1) (* r n))))))
      (loop n 1)))

(fac 5)         @result{} 120
(fac 4.5)       @result{} exception &assertion
(fac -3)        @result{} exception &assertion
@end example
@end deffn


@deffn Syntax assert @meta{expression}
An @func{assert} form is evaluated by evaluating @meta{expression}.  If
@meta{expression} returns a true value, that value is returned from the
@func{assert} expression.  If @meta{expression} returns @false{}, an
exception with condition types @code{&assertion} and @code{&message} is
raised.  The message provided in the condition object is
implementation--dependent.

@quotation
@emph{Note:} Implementations should exploit the fact that @func{assert}
is syntax to provide as much information as possible about the location
of the assertion failure.
@end quotation
@end deffn


@c page
@node baselib control
@section Control features


This chapter describes various primitive procedures which control the
flow of program execution in special ways.


@deffn Procedure apply @var{proc} @vari{arg} @dots{} @var{rest-args}
@var{rest-args} must be a list.  @var{proc} should accept @emph{n}
arguments, where @emph{n} is number of @var{arg}s plus the length of
@var{rest-args}.  The @func{apply} procedure calls @var{proc} with the
elements of the list:

@example
(append (list @vari{arg} @dots{}) @var{rest-args})
@end example

@noindent
as the actual arguments.

If a call to @func{apply} occurs in a tail context, the call to
@var{proc} is also in a tail context.

@example
(apply + (list 3 4))                    @result{}  7

(define compose
  (lambda (f g)
    (lambda args
      (f (apply g args)))))

((compose sqrt *) 12 75)                @result{}  30
@end example
@end deffn


@deffn Procedure call-with-current-continuation @var{proc}
@deffnx Procedure call/cc @var{proc}
@var{proc} should accept one argument.  The procedure @func{call/cc}
(which is the same as the procedure
@func{call-with-current-continuation}) packages the current continuation
as an ``escape procedure'' and passes it as an argument to @var{proc}.

The escape procedure is a Scheme procedure that, if it is later called,
will abandon whatever continuation is in effect at that later time and
will instead reinstate the continuation that was in effect when the
escape procedure was created.

Calling the escape procedure may cause the invocation of @var{before}
and @var{after} procedures installed using @func{dynamic-wind}.

The escape procedure accepts the same number of arguments as the
continuation of the original call to @func{call/cc}.

The escape procedure that is passed to @var{proc} has unlimited extent
just like any other procedure in Scheme.  It may be stored in variables
or data structures and may be called as many times as desired.

If a call to @func{call/cc} occurs in a tail context, the call to
@var{proc} is also in a tail context.

The following examples show only some ways in which @func{call/cc} is
used.  If all real uses were as simple as these examples, there would be
no need for a procedure with the power of @func{call/cc}.

@example
(call-with-current-continuation
  (lambda (exit)
    (for-each (lambda (x)
                (if (negative? x)
                    (exit x)))
              '(54 0 37 -3 245 19))
    #t))
@result{}  -3

(define list-length
  (lambda (obj)
    (call-with-current-continuation
      (lambda (return)
        (letrec ((r
                  (lambda (obj)
                    (cond ((null? obj) 0)
                          ((pair? obj)
                           (+ (r (cdr obj)) 1))
                          (else (return #f))))))
          (r obj))))))

(list-length '(1 2 3 4))                        @result{}  4
(list-length '(a b . c))                        @result{} #f
(call-with-current-continuation procedure?)     @result{} #t
@end example

@quotation
@emph{Note:} Calling an escape procedure reenters the dynamic extent of
the call to @func{call/cc}, and thus restores its dynamic environment.
@end quotation
@end deffn


@deffn Procedure values @var{obj} @dots{}
Delivers all of its arguments to its continuation.  The @func{values}
procedure might be defined as follows:

@example
(define (values . things)
  (call-with-current-continuation
    (lambda (cont) (apply cont things))))
@end example

The continuations of all non--final expressions within a sequence of
expressions, such as in @func{lambda}, @func{begin}, @func{let},
@func{let*}, @func{letrec}, @func{letrec*}, @func{let-values},
@func{let*-values}, @func{case}, and @func{cond} forms, usually take an
arbitrary number of values.

Except for these and the continuations created by
@func{call-with-values}, @func{let-values}, and @func{let*-values},
continuations implicitly accepting a single value, such as the
continuations of @meta{operator} and @meta{operand}s of procedure calls
or the @meta{test} expressions in conditionals, take exactly one value.
The effect of passing an inappropriate number of values to such a
continuation is undefined.
@end deffn


@deffn Procedure call-with-values @var{producer} @var{consumer}
@var{producer} must be a procedure and should accept zero arguments.
@var{consumer} must be a procedure and should accept as many values as
@var{producer} returns.  The @func{call-with-values} procedure calls
@var{producer} with no arguments and a continuation that, when passed
some values, calls the @var{consumer} procedure with those values as
arguments.  The continuation for the call to @var{consumer} is the
continuation of the call to @func{call-with-values}.

@example
(call-with-values
    (lambda () (values 4 5))
  (lambda (a b) b))
@result{}  5

(call-with-values * -)
@result{}  -1
@end example

If a call to @code{call-with-values} occurs in a tail context, the call
to @var{consumer} is also in a tail context.

@strong{Implementation responsibilities:} After @var{producer} returns,
the implementation must check that @var{consumer} accepts as many values
as @var{consumer} has returned.
@end deffn


@deffn Procedure dynamic-wind @var{before} @var{thunk} @var{after}
@var{before}, @var{thunk}, and @var{after} must be procedures, and each
should accept zero arguments.  These procedures may return any number of
values.

The @func{dynamic-wind} procedure calls @var{thunk} without arguments,
returning the results of this call.  Moreover, @func{dynamic-wind} calls
@var{before} without arguments whenever the dynamic extent of the call
to @var{thunk} is entered, and @var{after} without arguments whenever
the dynamic extent of the call to @var{thunk} is exited.  Thus, in the
absence of calls to escape procedures created by @func{call/cc},
@func{dynamic-wind} calls @var{before}, @var{thunk}, and @var{after}, in
that order.

While the calls to @var{before} and @var{after} are not considered to be
within the dynamic extent of the call to @var{thunk}, calls to the
@var{before} and @var{after} procedures of any other calls to
@func{dynamic-wind} that occur within the dynamic extent of the call to
@var{thunk} are considered to be within the dynamic extent of the call
to @var{thunk}.

More precisely, an escape procedure transfers control out of the dynamic
extent of a set of zero or more active @func{dynamic-wind} calls @emph{x
@dots{}} and transfer control into the dynamic extent of a set of zero
or more active @func{dynamic-wind} calls @emph{y @dots{}}.  It leaves
the dynamic extent of the most recent @emph{x} and calls without
arguments the corresponding @var{after} procedure.  If the @var{after}
procedure returns, the escape procedure proceeds to the next most recent
@emph{x}, and so on.  Once each @emph{x} has been handled in this
manner, the escape procedure calls without arguments the @var{before}
procedure corresponding to the least recent @emph{y}.  If the
@var{before} procedure returns, the escape procedure reenters the
dynamic extent of the least recent @emph{y} and proceeds with the next
least recent @emph{y}, and so on.  Once each @emph{y} has been handled
in this manner, control is transferred to the continuation packaged in
the escape procedure.

@strong{Implementation responsibilities:} The implementation must check
the restrictions on @var{thunk} and @var{after} only if they are
actually called.

@example
(let ((path '())
      (c #f))
  (let ((add (lambda (s)
               (set! path (cons s path)))))
    (dynamic-wind
      (lambda () (add 'connect))
      (lambda ()
        (add (call-with-current-continuation
               (lambda (c0)
                 (set! c c0)
                 'talk1))))
      (lambda () (add 'disconnect)))
    (if (< (length path) 4)
        (c 'talk2)
        (reverse path))))
@result{} (connect talk1 disconnect connect talk2 disconnect)

(let ((n 0))
  (call-with-current-continuation
    (lambda (k)
      (dynamic-wind
        (lambda ()
          (set! n (+ n 1))
          (k))
        (lambda ()
          (set! n (+ n 2)))
        (lambda ()
          (set! n (+ n 4))))))
  n)
@result{} 1

(let ((n 0))
  (call-with-current-continuation
    (lambda (k)
      (dynamic-wind
        values
        (lambda ()
          (dynamic-wind
            values
            (lambda ()
              (set! n (+ n 1))
              (k))
            (lambda ()
              (set! n (+ n 2))
              (k))))
        (lambda ()
          (set! n (+ n 4))))))
  n)
@result{} 7
@end example

@quotation
@emph{Note:} Entering a dynamic extent restores its dynamic environment.
@end quotation
@end deffn


@c page
@node baselib iteration
@section Iteration


@cindex Named let


@deffn Syntax let @meta{variable} @meta{bindings} @meta{body}
``Named @func{let}'' is a variant on the syntax of @func{let} that
provides a general looping construct and may also be used to express
recursion.  It has the same syntax and semantics as ordinary @func{let}
except that @meta{variable} is bound within @meta{body} to a procedure
whose parameters are the bound variables and whose body is @meta{body}.
Thus the execution of @meta{body} may be repeated by invoking the
procedure named by @meta{variable}.

@example
(let loop ((numbers '(3 -2 1 6 -5))
           (nonneg '())
           (neg '()))
  (cond ((null? numbers) (list nonneg neg))
        ((>= (car numbers) 0)
         (loop (cdr numbers)
               (cons (car numbers) nonneg)
               neg))
        ((< (car numbers) 0)
         (loop (cdr numbers)
               nonneg
               (cons (car numbers) neg)))))
@result{} ((6 1 3) (-5 -2))
@end example
@end deffn


@c page
@node baselib quasiquotation
@section Quasiquotation


@deffn Syntax quasiquote @meta{qq template}
@deffnx {Auxiliary Syntax} unquote
@deffnx {Auxiliary Syntax} unquote-splicing
``Backquote'' or ``quasiquote'' expressions are useful for constructing
a list or vector structure when some but not all of the desired
structure is known in advance.

@meta{qq template} should be as specified by the grammar at the end of
this entry.

If no @func{unquote} or @func{unquote-splicing} forms appear within the
@meta{qq template}, the result of evaluating @code{(quasiquote @meta{qq
template})} is equivalent to the result of evaluating @code{(quote
@meta{qq template})}.

If an @code{(unquote @meta{expression} @dots{})} form appears inside a
@meta{qq template}, however, the @meta{expression}s are evaluated
(``unquoted'') and their results are inserted into the structure instead
of the @code{unquote} form.

If an @code{(unquote-splicing @meta{expression} @dots{})} form appears
inside a @meta{qq template}, then the @meta{expression}s must evaluate
to lists; the opening and closing parentheses of the lists are then
``stripped away'' and the elements of the lists are inserted in place of
the @func{unquote-splicing} form.

Any @func{unquote-splicing} or multi--operand @func{unquote} form must
appear only within a list or vector @meta{qq template}.

The following abbreviations may be used:

@example
(quasiquote @meta{qq template})       =  `@meta{qq template}
(unquote @meta{expression})           =  ,@meta{expression}
(unquote-splicing @meta{expression})  =  ,@@@meta{expression}
@end example

Examples:

@example
`(list ,(+ 1 2) 4)                      @result{} (list 3 4)

(let ((name 'a))
  `(list ,name ',name))                 @result{} (list a (quote a))

`(a ,(+ 1 2) ,@@(map abs '(4 -5 6)) b)   @result{} (a 3 4 5 6 b)

`((foo ,(- 10 3))
  ,@@(cdr '(c)) . ,(car '(cons)))        @result{} ((foo 7) . cons)

`#(10 5 ,(sqrt 4) ,@@(map sqrt '(16 9)) 8)
                                        @result{} #(10 5 2 4 3 8)

(let ((name 'foo))
  `((unquote name name name)))          @result{} (foo foo foo)

(let ((name '(foo)))
  `((unquote-splicing name name name))) @result{} (foo foo foo)

(let ((q '((append x y) (sqrt 9))))
  ``(foo ,,@@q))
@result{} `(foo (unquote (append x y) (sqrt 9)))

(let ((x '(2 3))
      (y '(4 5)))
  `(foo (unquote (append x y) (sqrt 9))))
@result{} (foo (2 3 4 5) 3)
@end example

Quasiquote forms may be nested.  Substitutions are made only for
unquoted components appearing at the same nesting level as the outermost
@func{quasiquote}.  The nesting level increases by one inside each
successive quasiquotation, and decreases by one inside each unquotation.

@example
`(a `(b ,(+ 1 2) ,(foo ,(+ 1 3) d) e) f)
@result{}  (a `(b ,(+ 1 2) ,(foo 4 d) e) f)

(let ((name1 'x)
      (name2 'y))
  `(a `(b ,,name1 ,',name2 d) e))
@result{}  (a `(b ,x ,'y d) e)
@end example

A @func{quasiquote} expression may return either fresh, mutable objects
or literal structure for any structure that is constructed at run time
during the evaluation of the expression.  Portions that do not need to
be rebuilt are always literal.  Thus:

@example
(let ((a 3))
  `((1 2) ,a ,4 ,'five 6))
@end example

@noindent
may be equivalent to either of the following expressions:

@example
'((1 2) 3 4 five 6)

(let ((a 3))
  (cons '(1 2)
        (cons a (cons 4 (cons 'five '(6))))))
@end example

However, it is not equivalent to this expression:

@example
(let ((a 3))
  (list (list 1 2) a 4 'five 6))
@end example

It is a syntax violation if any of the identifiers @func{quasiquote},
@func{unquote}, or @func{unquote-splicing} appear in positions within a
@meta{qq template} other than as described above.

The following grammar for quasiquote expressions is not context--free.
It is presented as a recipe for generating an infinite number of
production rules.  Imagine a copy of the following rules for @emph{D =
1, 2, 3, @dots{}}.  @emph{D} keeps track of the nesting depth.

@example
@meta{qq template} -> @meta{qq template 1}
@meta{qq template 0} -> @meta{expression}
@meta{quasiquotation @emph{D}} -> (quasiquote @meta{qq template @emph{D}})
@meta{qq template @emph{D}} -> @meta{lexeme datum}
    | @meta{list qq template @emph{D}}
    | @meta{vector qq template @emph{D}}
    | @meta{unquotation @emph{D}}
@meta{list qq template @emph{D}} -> (@arbno{@meta{qq template or splice @emph{D}}})
    | (@atleastone{@meta{qq template or splice @emph{D}}} . @meta{qq template @emph{D}})
    | @meta{quasiquotation @emph{D+1}}
@meta{vector qq template @emph{D}} -> #(@arbno{@meta{qq template or splice @emph{D}}})
@meta{unquotation @emph{D}} -> (unquote @meta{qq template @emph{D-1}})
@meta{qq template or splice @emph{D}} -> @meta{qq template @emph{D}}
    | @meta{splicing unquotation @emph{D}}
@meta{splicing unquotation @emph{D}} ->
      (unquote-splicing @arbno{@meta{qq template @emph{D-1}}})
    | (unquote @arbno{@meta{qq template @emph{D-1}}})
@end example

In @func{quasiquotation}s, a @meta{list qq template @emph{D}} can
sometimes be confused with either an @meta{unquotation @emph{D}} or a
@meta{splicing unquotation @emph{D}}.  The interpretation as an
@meta{unquotation} or @meta{splicing unquotation @emph{D}} takes
precedence.
@end deffn


@c page
@node baselib syntax binding
@section Binding constructs for syntactic keywords


@cindex Binding syntactic keywords
@cindex Syntactic keywords, binding


The @func{let-syntax} and @func{letrec-syntax} forms bind keywords.
Like a @func{begin} form, a @func{let-syntax} or @func{letrec-syntax}
form may appear in a definition context, in which case it is treated as
a definition, and the forms in the body must also be definitions.  A
@func{let-syntax} or @func{letrec-syntax} form may also appear in an
expression context, in which case the forms within their bodies must be
expressions.


@deffn Syntax let-syntax @meta{bindings} @meta{form} @dots{}
@meta{bindings} must have the form:

@example
((@meta{keyword} @meta{expression}) @dots{})
@end example

Each @meta{keyword} is an identifier, and each @meta{expression} is an
expression that evaluates, at macro--expansion time, to a
@emph{transformer}.  Transformers may be created by @func{syntax-rules}
or @func{identifier-syntax} or by one of the other mechanisms described
in library chapter ``@func{syntax-case}''.  It is a syntax violation for
@meta{keyword} to appear more than once in the list of keywords being
bound.

The @meta{form}s are expanded in the syntactic environment obtained by
extending the syntactic environment of the @func{let-syntax} form with
macros whose keywords are the @meta{keyword}s, bound to the specified
transformers.  Each binding of a @meta{keyword} has the @meta{form}s as
its region.

The @meta{form}s of a @func{let-syntax} form are treated, whether in
definition or expression context, as if wrapped in an implicit
@func{begin}.  Thus definitions in the result of expanding the
@meta{form}s have the same region as any definition appearing in place
of the @func{let-syntax} form would have.

@strong{Implementation responsibilities:} The implementation should
detect if the value of @meta{expression} cannot possibly be a
transformer.

@example
(let-syntax ([when (syntax-rules ()
                     ((when test stmt1 stmt2 ...)
                      (if test
                          (begin stmt1
                                 stmt2 ...))))])
  (let ([if #t])
    (when if (set! if 'now))
    if))
@result{}  now

(let ([x 'outer])
  (let-syntax ([m (syntax-rules () ((m) x))])
    (let ([x 'inner])
      (m))))
@result{}  outer

(let ()
  (let-syntax ([def (syntax-rules ()
                      [(def stuff ...) (define stuff ...)])])
    (def foo 42))
  foo)
@result{} 42

(let ()
  (let-syntax ())
  5)
@result{} 5
@end example
@end deffn


@deffn Syntax letrec-syntax @meta{bindings} @meta{form} @dots{}
Same as for @func{let-syntax}.

The @meta{form}s are expanded in the syntactic environment obtained by
extending the syntactic environment of the @func{letrec-syntax} form
with macros whose keywords are the @meta{keyword}s, bound to the
specified transformers.  Each binding of a @meta{keyword} has the
@meta{bindings} as well as the @meta{form}s within its region, so the
transformers can transcribe forms into uses of the macros introduced by
the @func{letrec-syntax} form.

The @meta{form}s of a @func{letrec-syntax} form are treated, whether in
definition or expression context, as if wrapped in an implicit
@func{begin}.  Thus definitions in the result of expanding the
@meta{form}s have the same region as any definition appearing in place
of the @func{letrec-syntax} form would have.

@strong{Implementation responsibilities:} The implementation should
detect if the value of @meta{expression} cannot possibly be a
transformer.

@example
(letrec-syntax
    ([my-or (syntax-rules ()
              [(my-or)    #f]
              [(my-or e)  e]
              [(my-or e1 e2 ...)  (let ([temp e1])
                                    (if temp
                                        temp
                                     (my-or e2 ...)))])])
  (let ([x      #f]
        [y      7]
        [temp   8]
        [let    odd?]
        [if     even?])
    (my-or x (let temp) (if y) y)))
@result{} 7
@end example

The following example highlights how @func{let-syntax} and
@func{letrec-syntax} differ.

@example
(let ([f (lambda (x) (+ x 1))])
  (let-syntax ([f (syntax-rules ()
                    [(f x) x])]
               [g (syntax-rules ()
                    [(g x) (f x)])])
    (list (f 1) (g 1))))
@result{} (1 2)

(let ([f (lambda (x) (+ x 1))])
  (letrec-syntax ([f (syntax-rules ()
                       [(f x) x])]
                  [g (syntax-rules ()
                       [(g x) (f x)])])
    (list (f 1) (g 1))))
@result{} (1 1)
@end example

The two expressions are identical except that the @func{let-syntax} form
in the first expression is a @func{letrec-syntax} form in the second.
In the first expression, the @code{f} occurring in @code{g} refers to
the @func{let}-bound variable @code{f}, whereas in the second it refers
to the keyword @code{f} whose binding is established by the
@func{letrec-syntax} form.
@end deffn


@c page
@node baselib transformers
@section Macro transformers


@cindex Macro transformers
@cindex Transformers, macro


@xref{examples macros}, for examples on macro writing.


@deffn {Syntax @code{(expand)}} syntax-rules (@meta{literal} @dots{}) @meta{syntax rule} @dots{}
@deffnx {Auxiliary Syntax @code{(expand)}} _
@deffnx {Auxiliary Syntax @code{(expand)}} ...
Each @meta{literal} must be an identifier.  Each @meta{syntax rule} must
have the following form:

@example
(@meta{srpattern} @meta{template})
@end example

An @meta{srpattern} is a restricted form of @meta{pattern}, namely, a
nonempty @meta{pattern} in one of four parenthesized forms below whose
first subform is an identifier or an underscore @code{_}.  A
@meta{pattern} is an identifier, constant, or one of the following.

@example
(@meta{pattern} @dots{})
(@meta{pattern} @meta{pattern} @dots{} . @meta{pattern})
(@meta{pattern} @dots{} @meta{pattern} @meta{ellipsis} @meta{pattern} @dots{})
(@meta{pattern} @dots{} @meta{pattern} @meta{ellipsis} @meta{pattern} @dots{} . @meta{pattern})
#(@meta{pattern} @dots{})
#(@meta{pattern} @dots{} @meta{pattern} @meta{ellipsis} @meta{pattern} @dots{})
@end example

An @meta{ellipsis} is the identifier @code{...} (three periods).

A @meta{template} is a pattern variable, an identifier that is not a
pattern variable, a pattern datum, or one of the following.

@example
(@meta{subtemplate} @dots{})
(@meta{subtemplate} @dots{} . @meta{template})
#(@meta{subtemplate} @dots{})
@end example

A @meta{subtemplate} is a @meta{template} followed by zero or more
ellipses.

An instance of @func{syntax-rules} evaluates, at macro--expansion time,
to a new macro transformer by specifying a sequence of hygienic rewrite
rules.  A use of a macro whose keyword is associated with a transformer
specified by @func{syntax-rules} is matched against the patterns
contained in the @meta{syntax rule}s, beginning with the leftmost
@meta{syntax rule}.  When a match is found, the macro use is transcribed
hygienically according to the template.  It is a syntax violation when
no match is found.

An identifier appearing within a @meta{pattern} may be an underscore
@code{_}, a literal identifier listed in the list of literals
@code{(@meta{literal} @dots{})}, or an ellipsis @code{...}.  All other
identifiers appearing within a @meta{pattern} are @emph{pattern
variables}.  It is a syntax violation if an ellipsis or underscore
appears in @code{(@meta{literal} @dots{})}.

While the first subform of @meta{srpattern} may be an identifier, the
identifier is not involved in the matching and is not considered a
pattern variable or literal identifier.

Pattern variables match arbitrary input subforms and are used to refer
to elements of the input.  It is a syntax violation if the same pattern
variable appears more than once in a @meta{pattern}.

Underscores, @code{_}, also match arbitrary input subforms but are not
pattern variables and so cannot be used to refer to those elements.
Multiple underscores may appear in a @meta{pattern}.

A @meta{literal} identifier matches an input subform if and only if the
input subform is an identifier and either both its occurrence in the
input expression and its occurrence in the list of literals have the
same lexical binding, or the two identifiers have the same name and both
have no lexical binding.

A subpattern followed by an ellipsis can match zero or more elements of
the input.

More formally, an input form @math{F} matches a pattern @math{P} if and
only if one of the following holds:

@itemize
@item
@math{P} is an underscore (@code{_}).

@item
@math{P} is a pattern variable.

@item
@math{P} is a literal identifier and @math{F} is an identifier such that
both @math{P} and @math{F} would refer to the same binding if both were
to appear in the output of the macro outside of any bindings inserted
into the output of the macro.  (If neither of two like--named
identifiers refers to any binding, i.e., both are undefined, they are
considered to refer to the same binding.)

@item
@math{P} is of the form:

@example
(P_1 @dots{} P_n)
@end example

@noindent
and @math{F} is a list of @math{n} elements that match @math{P_1}
through @math{P_n}.

@item
@math{P} is of the form:

@example
(P_1 @dots{} P_n . P_x)
@end example

@noindent
and @math{F} is a list or improper list of @math{n} or more elements
whose first @math{n} elements match @math{P_1} through @math{P_n} and
whose @math{n}-th cdr matches @math{P_x}.

@item
@math{P} is of the form:

@example
(P_1 @dots{} P_k P_e @meta{ellipsis} P_(m+1) @dots{} P_n)
@end example

@noindent
where @meta{ellipsis} is the identifier @code{...}  and @math{F} is a
list of @math{n} elements whose first @math{k} elements match @math{P_1}
through @math{P_k}, whose next @math{m-k} elements each match
@math{P_e}, and whose remaining @math{n-m} elements match @math{P_(m+1)}
through @math{P_n}.

@item
@math{P} is of the form:

@example
(P_1 @dots{} P_k P_e @meta{ellipsis} P_(m+1) @dots{} P_n . P_x)
@end example

@noindent
where @meta{ellipsis} is the identifier @code{...} and @math{F} is a
list or improper list of @math{n} elements whose first @math{k} elements
match @math{P_1} through @math{P_k}, whose next @math{m-k} elements each
match @math{P_e}, whose next @math{n-m} elements match @math{P_(m+1)}
through @math{P_n}, and whose @math{n}th and final cdr matches
@math{P_x}.

@item
@math{P} is of the form:

@example
#(P_1 @dots{} P_n)
@end example

@noindent
and @math{F} is a vector of @math{n} elements that match @math{P_1}
through @math{P_n}.

@item
@math{P} is of the form:

@example
#(P_1 @dots{} P_k P_e @meta{ellipsis} P_(m+1) @dots{} P_n)
@end example

@noindent
where @meta{ellipsis} is the identifier @code{...} and @math{F} is a
vector of @math{n} or more elements whose first @math{k} elements match
@math{P_1} through @math{P_k}, whose next @math{m-k} elements each match
@math{P_e}, and whose remaining @math{n-m} elements match @math{P_(m+1)}
through @math{P_n}.

@item
@math{P} is a pattern datum (any nonlist, nonvector, nonsymbol datum)
and @math{F} is equal to @math{P} in the sense of the @func{equal?}
procedure.
@end itemize

When a macro use is transcribed according to the template of the
matching @meta{syntax rule}, pattern variables that occur in the
template are replaced by the subforms they match in the input.

Pattern data and identifiers that are not pattern variables or ellipses
are copied into the output.  A subtemplate followed by an ellipsis
expands into zero or more occurrences of the subtemplate.  Pattern
variables that occur in subpatterns followed by one or more ellipses may
occur only in subtemplates that are followed by (at least) as many
ellipses.  These pattern variables are replaced in the output by the
input subforms to which they are bound, distributed as specified.  If a
pattern variable is followed by more ellipses in the subtemplate than in
the associated subpattern, the input form is replicated as necessary.
The subtemplate must contain at least one pattern variable from a
subpattern followed by an ellipsis, and for at least one such pattern
variable, the subtemplate must be followed by exactly as many ellipses
as the subpattern in which the pattern variable appears.  (Otherwise,
the expander would not be able to determine how many times the subform
should be repeated in the output.)  It is a syntax violation if the
constraints of this paragraph are not met.

A template of the form @code{(@meta{ellipsis} @meta{template})} is
identical to @meta{template}, except that ellipses within the template
have no special meaning.  That is, any ellipses contained within
@meta{template} are treated as ordinary identifiers.  In particular, the
template @code{(... ...)} produces a single ellipsis, @code{...}.  This
allows syntactic abstractions to expand into forms containing ellipses.

@example
(define-syntax be-like-begin
  (syntax-rules ()
    [(be-like-begin name)
     (define-syntax name
       (syntax-rules ()
         ((name expr (... ...))
          (begin expr (... ...)))))]))

(be-like-begin sequence)
(sequence 1 2 3 4) => 4
@end example

As an example for hygienic use of auxiliary identifier, if @func{let}
and @func{cond} are defined as in @ref{baselib expressions binding,let}
and @ref{lang derived,cond} then they are hygienic (as required) and the
following is not an error.

@example
(let ((=> #f))
  (cond (#t => 'ok)))
@result{} ok
@end example

The macro transformer for @func{cond} recognizes @code{=>} as a local
variable, and hence an expression, and not as the identifier @code{=>},
which the macro transformer treats as a syntactic keyword.  Thus the
example expands into:

@example
(let ((=> #f))
  (if #t (begin => 'ok)))
@end example

@noindent
instead of:

@example
(let ((=> #f))
  (let ((temp #t))
    (if temp ('ok temp))))
@end example

@noindent
which would result in an assertion violation.
@end deffn


@deffn {Syntax @code{(expand)}} identifier-syntax @meta{template}
@deffnx {Syntax @code{(expand)}} identifier-syntax (@hyperi{id} @metai{template}) ((set! @hyperii{id} @meta{pattern}) @hyperii{template})
@deffnx {Auxiliary Syntax @code{(expand)}} set!
The @meta{id}s must be identifiers.  The @meta{template}s must be as for
@func{syntax-rules}.

When a keyword is bound to a transformer produced by the first form of
@func{identifier-syntax}, references to the keyword within the scope of
the binding are replaced by @meta{template}.

@example
(define p (cons 4 5))
(define-syntax p.car (identifier-syntax (car p)))
p.car           @result{} 4
(set! p.car 15) @result{} exception &syntax
@end example

The second, more general, form of @func{identifier-syntax} permits the
transformer to determine what happens when @code{set!} is used.  In this
case, uses of the identifier by itself are replaced by
@hyperi{template}, and uses of @code{set!} with the identifier are
replaced by @hyperii{template}.

@example
(define p (cons 4 5))
(define-syntax p.car
  (identifier-syntax
    [_ (car p)]
    [(set! _ e) (set-car! p e)]))
(set! p.car 15)
p.car           @result{} 15
p               @result{} (15 . 5)
@end example
@end deffn



@c page
@node baselib tail call
@section Tail calls and tail contexts


@cindex Tail call
@cindex Tail contexts


A @emph{tail call} is a procedure call that occurs in a @emph{tail
context}.  Tail contexts are defined inductively.  Note that a tail
context is always determined with respect to a particular lambda
expression.

@itemize
@item
The last expression within the body of a lambda expression, shown as
@meta{tail expression} below, occurs in a tail context.

@example
(lambda @meta{formals}
  @arbno{@meta{definition}}
  @arbno{@meta{expression}} @meta{tail expression})
@end example

@item
If one of the following expressions is in a tail context, then the
subexpressions shown as @meta{tail expression} are in a tail context.
These were derived from specifications of the syntax of the forms
described in this chapter by replacing some occurrences of
@meta{expression} with @meta{tail expression}.  Only those rules that
contain tail contexts are shown here.

@example
(if @meta{expression} @meta{tail expression} @meta{tail expression})
(if @meta{expression} @meta{tail expression})

(cond @atleastone{@meta{cond clause}})
(cond @arbno{@meta{cond clause}} (else @meta{tail sequence}))

(case @meta{expression}
  @atleastone{@meta{case clause}})
(case @meta{expression}
  @arbno{@meta{case clause}}
  (else @meta{tail sequence}))

(and @arbno{@meta{expression}} @meta{tail expression})
(or @arbno{@meta{expression}} @meta{tail expression})

(let @meta{bindings} @meta{tail body})
(let @meta{variable} @meta{bindings} @meta{tail body})
(let* @meta{bindings} @meta{tail body})
(letrec* @meta{bindings} @meta{tail body})
(letrec @meta{bindings} @meta{tail body})
(let-values @meta{mv-bindings} @meta{tail body})
(let*-values @meta{mv-bindings} @meta{tail body})

(let-syntax @meta{bindings} @meta{tail body})
(letrec-syntax @meta{bindings} @meta{tail body})

(begin @meta{tail sequence})
@end example

A @meta{cond clause} is:

@example
(@meta{test} @meta{tail sequence})
@end example

@noindent
a @meta{case clause} is:

@example
((@arbno{@meta{datum}}) @meta{tail sequence})
@end example

@noindent
a @meta{tail body} is:

@example
@arbno{@meta{definition}} @meta{tail sequence}
@end example

@noindent
and a @meta{tail sequence} is:

@example
@arbno{@meta{expression}} @meta{tail expression}
@end example

@item
If a @func{cond} expression is in a tail context, and has a clause of
the form @code{(@hyperi{expression} => @hyperii{expression})} then the
(implied) call to the procedure that results from the evaluation of
@hyperii{expression} is in a tail context.  @hyperii{expression} itself
is not in a tail context.
@end itemize

Certain built--in procedures must also perform tail calls.  The first
argument passed to @func{apply} and to @func{call/cc}, and the second
argument passed to @func{call-with-values}, must be called via a tail
call.

In the following example the only tail call is the call to @func{f}.
None of the calls to @func{g} or @func{h} are tail calls.  The reference
to @code{x} is in a tail context, but it is not a call and thus is not a
tail call.

@example
(lambda ()
  (if (g)
      (let ((x (h)))
        x)
      (and (g) (f))))
@end example

@quotation
@emph{Note:} Implementations may recognize that some non--tail calls,
such as the call to @func{h} above, can be evaluated as though they were
tail calls.  In the example above, the @func{let} expression could be
compiled as a tail call to @func{h}.  (The possibility of @func{h}
returning an unexpected number of values can be ignored, because in that
case the effect of the @func{let} is explicitly unspecified and
implementation--dependent.)
@end quotation


@c page
@node stdlib
@chapter Standard libraries


@menu
* stdlib unicode::              Unicode.
* stdlib bytevector::           Bytevectors.
* stdlib list::                 List utilities.
* stdlib sorting::              Sorting.
* stdlib control::              Control structures.
* stdlib records::              Records.
* stdlib exceptions::           Exceptions and conditions.
* stdlib io::                   Input/output.
* stdlib files::                File system.
* stdlib programlib::           Command line access and exit values.
* stdlib arithmetics::          Arithmetics.
* stdlib syntax-case::          Syntax-case.
* stdlib hashtable::            Hashtables.
* stdlib enum::                 Enumerations.
* stdlib complib::              Composite library.
* stdlib eval::                 Evaluation.
* stdlib mutable pairs::        Mutable pairs.
* stdlib mutable strings::      Mutable strings.
* stdlib rfive compat::         @rnrs{5} compatibility.
@end menu


@c page
@node stdlib unicode
@section Unicode


The procedures exported by the @rsixlibrary{unicode} library provide
access to some aspects of the Unicode semantics for characters and
strings: category information, case--independent comparisons, case
mappings, and normalization.

Some of the procedures that operate on characters or strings ignore the
difference between upper case and lower case.  These procedures have
@code{-ci} (for ``case insensitive'') embedded in their names.


@menu
* stdlib unicode characters::   Characters.
* stdlib unicode strings::      Strings.
@end menu


@c page
@node stdlib unicode characters
@subsection Characters


@deffn Procedure char-upcase @var{char}
@deffnx Procedure char-downcase @var{char}
@deffnx Procedure char-titlecase @var{char}
@deffnx Procedure char-foldcase @var{char}
These procedures take a character argument and return a character
result.

If the argument is an upper--case or title--case character, and if there
is a single character that is its lower--case form, then
@func{char-downcase} returns that character.

If the argument is a lower--case or title--case character, and there is
a single character that is its upper--case form, then @func{char-upcase}
returns that character.

If the argument is a lower--case or upper--case character, and there is
a single character that is its title--case form, then
@func{char-titlecase} returns that character.

If the argument is not a title--case character and there is no single
character that is its title--case form, then @func{char-titlecase}
returns the upper--case form of the argument.

Finally, if the character has a case--folded character, then
@func{char-foldcase} returns that character.  Otherwise the character
returned is the same as the argument.

For Turkic characters @code{#\x130} and @code{#\x131},
@func{char-foldcase} behaves as the identity function; otherwise
@func{char-foldcase} is the same as @func{char-downcase} composed with
@func{char-upcase}.

@example
(char-upcase #\i)               @result{} #\I
(char-downcase #\i)             @result{} #\i
(char-titlecase #\i)            @result{} #\I
(char-foldcase #\i)             @result{} #\i
@end example

@quotation
@emph{Note:} @func{char-titlecase} does not always return a title--case
character.
@end quotation

@quotation
@emph{Note:} These procedures are consistent with Unicode's
locale--independent mappings from scalar values to scalar values for
upcase, downcase, titlecase, and case--folding operations.  These
mappings can be extracted from @file{UnicodeData.txt} and
@file{CaseFolding.txt} from the Unicode Consortium, ignoring Turkic
mappings in the latter.

Note that these character--based procedures are an incomplete
approximation to case conversion, even ignoring the user's locale.  In
general, case mappings require the context of a string, both in
arguments and in result.  The @func{string-upcase},
@func{string-downcase}, @func{string-titlecase}, and
@func{string-foldcase} procedures (@ref{stdlib unicode strings} perform
more general case conversion.
@end quotation
@end deffn


@deffn Procedure char-ci=? @vari{char} @varii{char} @variii{char} @dots{}
@deffnx Procedure char-ci<? @vari{char} @varii{char} @variii{char} @dots{}
@deffnx Procedure char-ci>? @vari{char} @varii{char} @variii{char} @dots{}
@deffnx Procedure char-ci<=? @vari{char} @varii{char} @variii{char} @dots{}
@deffnx Procedure char-ci>=? @vari{char} @varii{char} @variii{char} @dots{}
These procedures are similar to @func{char=?}, etc., but operate on the
case--folded versions of the characters.

@example
(char-ci<? #\z #\Z)             @result{} #f
(char-ci=? #\z #\Z)             @result{} #f
@end example
@end deffn


@deffn Procedure char-alphabetic? @var{char}
@deffnx Procedure char-numeric? @var{char}
@deffnx Procedure char-whitespace? @var{char}
@deffnx Procedure char-upper-case? @var{char}
@deffnx Procedure char-lower-case? @var{char}
@deffnx Procedure char-title-case? @var{char}
These procedures return @true{} if their arguments are alphabetic,
numeric, whitespace, upper--case, lower--case, or title--case
characters, respectively; otherwise they return @false{}.

A character is alphabetic if it has the Unicode ``Alphabetic'' property.
A character is numeric if it has the Unicode ``Numeric'' property.  A
character is whitespace if has the Unicode ``White_Space'' property.  A
character is upper case if it has the Unicode ``Uppercase'' property,
lower case if it has the ``Lowercase'' property, and title case if it is
in the Lt general category.

@example
(char-alphabetic? #\a)          @result{} #t
(char-numeric? #\1)             @result{} #t
(char-whitespace? #\space)      @result{} #t
(char-whitespace? #\x00A0)      @result{} #t
(char-lower-case? #\x00AA)      @result{} #t
(char-title-case? #\I)          @result{} #f
(char-title-case? #\x01C5)      @result{} #t
@end example
@end deffn


@deffn Procedure char-general-category @var{char}
Return a symbol representing the Unicode general category of
@var{char}, one of @code{Lu}, @code{Ll}, @code{Lt}, @code{Lm},
@code{Lo}, @code{Mn}, @code{Mc}, @code{Me}, @code{Nd}, @code{Nl},
@code{No}, @code{Ps}, @code{Pe}, @code{Pi}, @code{Pf}, @code{Pd},
@code{Pc}, @code{Po}, @code{Sc}, @code{Sm}, @code{Sk}, @code{So},
@code{Zs}, @code{Zp}, @code{Zl}, @code{Cc}, @code{Cf}, @code{Cs},
@code{Co}, or @code{Cn}.

@example
(char-general-category #\a)             @result{} Ll
(char-general-category #\space)         @result{} Zs
(char-general-category #\x10FFFF)       @result{} Cn
@end example
@end deffn


@c page
@node stdlib unicode strings
@subsection Strings


@deffn Procedure string-upcase @var{string}
@deffnx Procedure string-downcase @var{string}
@deffnx Procedure string-titlecase @var{string}
@deffnx Procedure string-foldcase @var{string}
These procedures take a string argument and return a string result.
They are defined in terms of Unicode's locale--independent case mappings
from Unicode scalar--value sequences to scalar--value sequences.  In
particular, the length of the result string can be different from the
length of the input string.  When the specified result is equal in the
sense of @func{string=?} to the argument, these procedures may return
the argument instead of a newly allocated string.

The @func{string-upcase} procedure converts a string to upper case;
@func{string-downcase} converts a string to lower case.  The
@func{string-foldcase} procedure converts the string to its case--folded
counterpart, using the full case--folding mapping, but without the
special mappings for Turkic languages.  The @func{string-titlecase}
procedure converts the first cased character of each word, and downcases
all other cased characters.

@example
(string-upcase "Hi")                    @result{} "HI"
(string-downcase "Hi")                  @result{} "hi"
(string-foldcase "Hi")                  @result{} "hi"

(string-titlecase "kNock KNoCK")        @result{} "Knock Knock"
(string-titlecase "who's there?")       @result{} "Who's There?"
(string-titlecase "r6rs")               @result{} "R6rs"
(string-titlecase "R6RS")               @result{} "R6rs"
@end example

@quotation
@emph{Note:} The case mappings needed for implementing these procedures
can be extracted from @file{UnicodeData.txt}, @file{SpecialCasing.txt},
@file{WordBreakProperty.txt} (the ``MidLetter'' property partly defines
case--ignorable characters), and @file{CaseFolding.txt} from the Unicode
Consortium.

Since these procedures are locale--independent, they may not be
appropriate for some locales.
@end quotation

@quotation
@emph{Note:} Word breaking, as needed for the correct casing of the
upper case greek sigma and for @func{string-titlecase}, is specified in
Unicode Standard Annex #29.
@end quotation
@end deffn


@deffn Procedure string-ci=? @vari{string} @varii{string} @variii{string} @dots{}
@deffnx Procedure string-ci<? @vari{string} @varii{string} @variii{string} @dots{}
@deffnx Procedure string-ci>? @vari{string} @varii{string} @variii{string} @dots{}
@deffnx Procedure string-ci<=? @vari{string} @varii{string} @variii{string} @dots{}
@deffnx Procedure string-ci>=? @vari{string} @varii{string} @variii{string} @dots{}
These procedures are similar to @func{string=?}, etc., but operate on
the case--folded versions of the strings.

@example
(string-ci<? "z" "Z")                   @result{} #f
(string-ci=? "z" "Z")                   @result{} #t
@end example
@end deffn


@deffn Procedure string-normalize-nfd @var{string}
@deffnx Procedure string-normalize-nfkd @var{string}
@deffnx Procedure string-normalize-nfc @var{string}
@deffnx Procedure string-normalize-nfkc @var{string}
These procedures take a string argument and return a string result,
which is the input string normalized to Unicode normalization form D,
KD, C, or KC, respectively.  When the specified result is equal in the
sense of @func{string=?} to the argument, these procedures may return
the argument instead of a newly allocated string.

@example
(string-normalize-nfd "\xE9;")          @result{} "\x65;\x301;"
(string-normalize-nfc "\xE9;")          @result{} "\xE9;"
(string-normalize-nfd "\x65;\x301;")    @result{} "\x65;\x301;"
(string-normalize-nfc "\x65;\x301;")    @result{} "\xE9;"
@end example
@end deffn


@c page
@node stdlib bytevector
@section Bytevectors


Many applications deal with blocks of binary data by accessing them in
various ways---extracting signed or unsigned numbers of various sizes.
Therefore, the @rsixlibrary{bytevectors} library provides a single type
for blocks of binary data with multiple ways to access that data. It
deals with integers and floating--point representations in various sizes
with specified endianness.

Bytevectors are objects of a disjoint type.  Conceptually, a bytevector
represents a sequence of 8-bit bytes.  The description of bytevectors
uses the term @emph{byte} for an exact integer object in the interval
@math{(-128, @dots{}, 127)} and the term @emph{octet} for an exact
integer object in the interval @math{(0, @dots{}, 255)}.  A byte
corresponds to its two's complement representation as an octet.

The length of a bytevector is the number of bytes it contains. This
number is fixed.  A valid index into a bytevector is an exact,
non--negative integer object less than the length of the bytevector.
The first byte of a bytevector has index 0; the last byte has an index
one less than the length of the bytevector.

Generally, the access procedures come in different flavors according to
the size of the represented integer and the endianness of the
representation.  The procedures also distinguish signed and unsigned
representations.  The signed representations all use two's complement.

Like string literals, literals representing bytevectors do not need to
be quoted:

@example
#vu8(12 23 123)         @result{} #vu8(12 23 123)
@end example


@menu
* stdlib bytevector endianness::        Endianness.
* stdlib bytevector general::           General operations.
* stdlib bytevector bytes and octets::  Operations on bytes and octets.
* stdlib bytevector integers::          Operations on integers of
                                        arbitrary size.
* stdlib bytevector integers 16::       Operations on 16-bit integers.
* stdlib bytevector integers 32::       Operations on 32-bit integers.
* stdlib bytevector integers 64::       Operations on 64-bit integers.
* stdlib bytevector flonum::            Operations on @ieee{} 754
                                        representations.
* stdlib bytevector strings::           Operations on strings.
@end menu


@c page
@node stdlib bytevector endianness
@subsection Endianness


Many operations described in this chapter accept an @emph{endianness}
argument.  Endianness describes the encoding of exact integer objects as
several contiguous bytes in a bytevector.  For this purpose, the binary
representation of the integer object is split into consecutive bytes.
The little--endian encoding places the least significant byte of an
integer first, with the other bytes following in increasing order of
significance.  The big--endian encoding places the most significant byte
of an integer first, with the other bytes following in decreasing order
of significance.

This terminology also applies to @ieee{} 754 numbers: @ieee{} 754
describes how to represent a floating--point number as an exact integer
object, and endianness describes how the bytes of such an integer are
laid out in a bytevector.

@quotation
@emph{Note:} Little-- and big--endianness are only the most common kinds
of endianness.  Some architectures distinguish between the endianness at
different levels of a binary representation.
@end quotation


@c page
@node stdlib bytevector general
@subsection General operations


@deffn Syntax endianness @meta{endianness symbol}
The name of @meta{endianness symbol} must be a symbol describing an
endianness.  An implementation must support at least the symbols
@code{big} and @code{little}, but may support other endianness symbols.

@code{(endianness @meta{endianness symbol})} evaluates to the symbol
named @meta{endianness symbol}.  Whenever one of the procedures
operating on bytevectors accepts an endianness as an argument, that
argument must be one of these symbols.  It is a syntax violation for
@meta{endianness symbol} to be anything other than an endianness symbol
supported by the implementation.

@quotation
@emph{Note:} Implementors should use widely accepted designations for
endianness symbols other than @code{big} and @code{little}.
@end quotation

@quotation
@emph{Note:} Only the name of @meta{endianness symbol} is significant.
@end quotation
@end deffn


@deffn Procedure native-endianness
Return the endianness symbol associated implementation's preferred
endianness (usually that of the underlying machine architecture).  This
may be any @meta{endianness symbol}, including a symbol other than
@code{big} and @code{little}.
@end deffn


@deffn Procedure bytevector? @var{obj}
Return @true{} if @var{obj} is a bytevector, @false{} otherwise.
@end deffn


@deffn Procedure make-bytevector @var{k}
@deffnx Procedure make-bytevector @var{k} @var{fill}
Return a newly allocated bytevector of @var{k} bytes.  If the @var{fill}
argument is missing, the initial contents of the returned bytevector are
unspecified.  If the @var{fill} argument is present, it must be an exact
integer object in the interval @math{(-128, @dots{} 255)} that specifies
the initial value for the bytes of the bytevector: If @var{fill} is
positive, it is interpreted as an octet; if it is negative, it is
interpreted as a byte.
@end deffn


@deffn Procedure bytevector-length @var{bytevector}
Return, as an exact integer object, the number of bytes in
@var{bytevector}.
@end deffn


@deffn Procedure {bytevector=?} @vari{bytevector} @varii{bytevector}
Return @true{} if @vari{bytevector} and @varii{bytevector} are equal;
that is, if they have the same length and equal bytes at all valid
indices.  It returns @false{} otherwise.
@end deffn


@deffn Procedure {bytevector-fill!} @var{bytevector} @var{fill}
The @var{fill} argument is as in the description of the
@func{make-bytevector} procedure.  The @func{bytevector-fill!}
procedure stores @var{fill} in every element of @var{bytevector} and
returns unspecified values.  Analogous to @func{vector-fill!}.
@end deffn


@deffn Procedure {bytevector-copy!} @var{source} @var{source-start} @var{target} @var{target-start} @var{k}
@var{source} and @var{target} must be bytevectors.  @var{source-start},
@var{target-start}, and @var{k} must be non--negative exact integer
objects that satisfy:

@example
0 <= @var{source-start} <= @var{source-start} + @var{k} <= l_@var{source}
0 <= @var{target-start} <= @var{target-start} + @var{k} <= l_@var{target}
@end example

@noindent
where @emph{l_@var{source}} is the length of @var{source} and
@emph{l_@var{target}} is the length of @var{target}.

The @func{bytevector-copy!} procedure copies the bytes from @var{source}
at indices:

@example
@var{source-start}, @dots{}, @var{source-start} + @var{k} - 1
@end example

@noindent
to consecutive indices in @var{target} starting at @var{target-index}.

This must work even if the memory regions for the source and the target
overlap, i.e., the bytes at the target location after the copy must be
equal to the bytes at the source location before the copy.

This returns unspecified values.

@example
(let ((b (u8-list->bytevector '(1 2 3 4 5 6 7 8))))
  (bytevector-copy! b 0 b 3 4)
  (bytevector->u8-list b))
@result{} (1 2 3 1 2 3 4 8)
@end example
@end deffn


@deffn Procedure bytevector-copy @var{bytevector}
Return a newly allocated copy of @var{bytevector}.
@end deffn


@c page
@node stdlib bytevector bytes and octets
@subsection Operations on bytes and octets


@deffn Procedure bytevector-u8-ref @var{bytevector} @var{k}
@deffnx Procedure bytevector-s8-ref @var{bytevector} @var{k}
@var{k} must be a valid index of @var{bytevector}.

The @func{bytevector-u8-ref} procedure returns the byte at index @var{k}
of @var{bytevector}, as an octet.

The @func{bytevector-s8-ref} procedure returns the byte at index @var{k}
of @var{bytevector}, as a (signed) byte.

@example
(let ((b1 (make-bytevector 16 -127))
      (b2 (make-bytevector 16 255)))
  (list
    (bytevector-s8-ref b1 0)
    (bytevector-u8-ref b1 0)
    (bytevector-s8-ref b2 0)
    (bytevector-u8-ref b2 0)))
@result{} (-127 129 -1 255)
@end example
@end deffn


@deffn Procedure {bytevector-u8-set!} @var{bytevector} @var{k} @var{octet}
@deffnx Procedure {bytevector-s8-set!} @var{bytevector} @var{k} @var{byte}
@var{k} must be a valid index of @var{bytevector}.

The @func{bytevector-u8-set!} procedure stores @var{octet} in element
@var{k} of @var{bytevector}.

The @func{bytevector-s8-set!} procedure stores the two's--complement
representation of @var{byte} in element @var{k} of @var{bytevector}.

Both procedures return unspecified values.

@example
(let ((b (make-bytevector 16 -127)))

  (bytevector-s8-set! b 0 -126)
  (bytevector-u8-set! b 1 246)

  (list
    (bytevector-s8-ref b 0)
    (bytevector-u8-ref b 0)
    (bytevector-s8-ref b 1)
    (bytevector-u8-ref b 1)))
@result{} (-126 130 -10 246)
@end example
@end deffn


@deffn Procedure {bytevector->u8-list} @var{bytevector}
@deffnx Procedure {u8-list->bytevector} @var{list}
@var{list} must be a list of octets.

The @func{bytevector->u8-list} procedure returns a newly allocated list
of the octets of @var{bytevector} in the same order.

The @func{u8-list->bytevector} procedure returns a newly allocated
bytevector whose elements are the elements of list @var{list}, in the
same order.  It is analogous to @func{list->vector}.
@end deffn


@c page
@node stdlib bytevector integers
@subsection Operations on integers of arbitrary size


@deffn Procedure bytevector-uint-ref @var{bytevector} @var{k} @var{endianness} @var{size}
@deffnx Procedure bytevector-sint-ref @var{bytevector} @var{k} @var{endianness} @var{size}
@deffnx Procedure {bytevector-uint-set!} @var{bytevector} @var{k} @var{n} @var{endianness} @var{size}
@deffnx Procedure {bytevector-sint-set!} @var{bytevector} @var{k} @var{n} @var{endianness} @var{size}
@var{size} must be a positive exact integer object.  @var{k}, @dots{},
@var{k}+@var{size}-1 must be valid indices of @var{bytevector}.

The @func{bytevector-uint-ref} procedure retrieves the exact integer
object corresponding to the unsigned representation of size @var{size}
and specified by @var{endianness} at indices @math{@var{k}, @dots{},
@var{k} + @var{size} - 1}.

The @func{bytevector-sint-ref} procedure retrieves the exact integer
object corresponding to the two's--complement representation of size
@var{size} and specified by @var{endianness} at indices @var{k},
@dots{}, @var{k}+@var{size}-1.

For @func{bytevector-uint-set!}, @var{n} must be an exact integer object
in the interval @code{(0, @dots{}, 256^(size-1))}.

The @func{bytevector-uint-set!} procedure stores the unsigned
representation of size @var{size} and specified by @var{endianness} into
@var{bytevector} at indices @math{@var{k}, @dots{}, @var{k} + @var{size}
- 1}.

For @func{bytevector-sint-set!}, @var{n} must be an exact integer object
in the interval @math{(-(256^size)/2, @dots{}, (256^(size))/2-1)}.
@func{bytevector-sint-set!} stores the two's--complement representation
of size @var{size} and specified by @var{endianness} into
@var{bytevector} at indices @math{@var{k}, @dots{}, @var{k} + @var{size}
- 1}.

The @code{...-set!} procedures return unspecified values.

@example
(define b (make-bytevector 16 -127))

(bytevector-uint-set! b 0 (- (expt 2 128) 3) (endianness little) 16)

(bytevector-uint-ref b 0 (endianness little) 16)
@result{} #xfffffffffffffffffffffffffffffffd

(bytevector-sint-ref b 0 (endianness little) 16)
@result{} -3

(bytevector->u8-list b)
@result{} (253 255 255 255 255 255 255 255
    255 255 255 255 255 255 255 255)

(bytevector-uint-set! b 0 (- (expt 2 128) 3) (endianness big) 16)

(bytevector-uint-ref b 0 (endianness big) 16)
@result{} #xfffffffffffffffffffffffffffffffd

(bytevector-sint-ref b 0 (endianness big) 16)
@result{} -3

(bytevector->u8-list b)
@result{} (255 255 255 255 255 255 255 255
    255 255 255 255 255 255 255 253))
@end example
@end deffn


@deffn Procedure {bytevector->uint-list} @var{bytevector} @var{endianness} @var{size}
@deffnx Procedure {bytevector->sint-list} @var{bytevector} @var{endianness} @var{size}
@deffnx Procedure {uint-list->bytevector} @var{list} @var{endianness} @var{size}
@deffnx Procedure {sint-list->bytevector} @var{list} @var{endianness} @var{size}
@var{size} must be a positive exact integer object.

For @func{uint-list->bytevector}, @var{list} must be a list of exact
integer objects in the interval @math{(0, @dots{}, (256^size)-1)}.

For @func{sint-list->bytevector}, @var{list} must be a list of exact
integer objects in the interval @math{(-(256^size)/2, @dots{},
(256^(size))/2-1)}.

The length of @var{bytevector} must be divisible by @var{size}.

These procedures convert between lists of integer objects and their
consecutive representations according to @var{size} and @var{endianness}
in the @var{bytevector} objects in the same way as
@func{bytevector->u8-list} and @func{u8-list->bytevector} do for
one--byte representations.

@example
(let ((b (u8-list->bytevector '(1 2 3 255 1 2 1 2))))
  (bytevector->sint-list b (endianness little) 2))
@result{} (513 -253 513 513)

(let ((b (u8-list->bytevector '(1 2 3 255 1 2 1 2))))
  (bytevector->uint-list b (endianness little) 2))
@result{} (513 65283 513 513)
@end example
@end deffn

@c page
@node stdlib bytevector integers 16
@subsection Operations on 16--bit integers


@deffn Procedure {bytevector-u16-ref} @var{bytevector} @var{k} @var{endianness}
@deffnx Procedure {bytevector-s16-ref} @var{bytevector} @var{k} @var{endianness}
@deffnx Procedure {bytevector-u16-native-ref} @var{bytevector} @var{k}
@deffnx Procedure {bytevector-s16-native-ref} @var{bytevector} @var{k}
@deffnx Procedure {bytevector-u16-set!} @var{bytevector} @var{k} @var{n} @var{endianness}
@deffnx Procedure {bytevector-s16-set!} @var{bytevector} @var{k} @var{n} @var{endianness}
@deffnx Procedure {bytevector-u16-native-set!} @var{bytevector} @var{k} @var{n}
@deffnx Procedure {bytevector-s16-native-set!} @var{bytevector} @var{k} @var{n}
@var{k} must be a valid index of @var{bytevector}; so must @var{k}+1.

For @func{bytevector-u16-set!} and @func{bytevector-u16-native-set!},
@var{n} must be an exact integer object in the interval @math{0 <= n <=
2^16-1}.

For @func{bytevector-s16-set!} and @func{bytevector-s16-native-set!},
@var{n} must be an exact integer object in the interval @math{-2^15 <= n
<= 2^15-1}.

These procedures retrieve and set two--byte representations of numbers
at indices @var{k} and @math{@var{k}+1} according to the endianness
specified by @var{endianness}.  The procedures with @code{u16} in their
names deal with the unsigned representation; those with @code{s16} in
their names deal with the two's--complement representation.

The procedures with @code{native} in their names employ the native
endianness, and work only at aligned indices: @var{k} must be a multiple
of @math{2}.

The @code{...-set!} procedures return unspecified values.

@example
(define b
  (u8-list->bytevector
    '(255 255 255 255 255 255 255 255
      255 255 255 255 255 255 255 253)))

(bytevector-u16-ref b 14 (endianness little))           @result{} 65023
(bytevector-s16-ref b 14 (endianness little))           @result{} -513
(bytevector-u16-ref b 14 (endianness big))              @result{} 65533
(bytevector-s16-ref b 14 (endianness big))              @result{} -3

(bytevector-u16-set! b 0 12345 (endianness little))
(bytevector-u16-ref b 0 (endianness little))            @result{} 12345

(bytevector-u16-native-set! b 0 12345)
(bytevector-u16-native-ref b 0)                         @result{} 12345

(bytevector-u16-ref b 0 (endianness little))            @result{} unspecified
@end example
@end deffn


@c page
@node stdlib bytevector integers 32
@subsection Operations on 32--bit integers


@deffn Procedure {bytevector-u32-ref} @var{bytevector} @var{k} @var{endianness}
@deffnx Procedure {bytevector-s32-ref} @var{bytevector} @var{k} @var{endianness}
@deffnx Procedure {bytevector-u32-native-ref} @var{bytevector} @var{k}
@deffnx Procedure {bytevector-s32-native-ref} @var{bytevector} @var{k}
@deffnx Procedure {bytevector-u32-set!} @var{bytevector} @var{k} @var{n} @var{endianness}
@deffnx Procedure {bytevector-s32-set!} @var{bytevector} @var{k} @var{n} @var{endianness}
@deffnx Procedure {bytevector-u32-native-set!} @var{bytevector} @var{k} @var{n}
@deffnx Procedure {bytevector-s32-native-set!} @var{bytevector} @var{k} @var{n}
@var{k}, @dots{}, @var{k}+3 must be valid indices of @var{bytevector}.

For @func{bytevector-u32-set!} and @func{bytevector-u32-native-set!},
@var{n} must be an exact integer object in the interval @math{0 <= n <=
2^32-1}.

For @func{bytevector-s32-set!} and @func{bytevector-s32-native-set!},
@var{n} must be an exact integer object in the interval @math{-2^31 <= n
<= 2^32-1}.

These retrieve and set four--byte representations of numbers at indices
@var{k}, @dots{}, @var{k}+3, according to the endianness specified by
@var{endianness}.  The procedures with @code{u32} in their names deal
with the unsigned representation; those with @code{s32} with the
two's--complement representation.

The procedures with @code{native} in their names employ the native
endianness, and work only at aligned indices: @var{k} must be a multiple
of @math{4}.

The @code{...-set!} procedures return unspecified values.

@example
(define b
  (u8-list->bytevector
    '(255 255 255 255 255 255 255 255
      255 255 255 255 255 255 255 253)))

(bytevector-u32-ref b 12 (endianness little))   @result{} 4261412863
(bytevector-s32-ref b 12 (endianness little))   @result{} -33554433
(bytevector-u32-ref b 12 (endianness big))      @result{} 4294967293
(bytevector-s32-ref b 12 (endianness big))      @result{} -3
@end example
@end deffn

@c page
@node stdlib bytevector integers 64
@subsection Operations on 64--bit integers


@deffn Procedure {bytevector-u64-ref} @var{bytevector} @var{k} @var{endianness}
@deffnx Procedure {bytevector-s64-ref} @var{bytevector} @var{k} @var{endianness}
@deffnx Procedure {bytevector-u64-native-ref} @var{bytevector} @var{k}
@deffnx Procedure {bytevector-s64-native-ref} @var{bytevector} @var{k}
@deffnx Procedure {bytevector-u64-set!} @var{bytevector} @var{k} @var{n} @var{endianness}
@deffnx Procedure {bytevector-s64-set!} @var{bytevector} @var{k} @var{n} @var{endianness}
@deffnx Procedure {bytevector-u64-native-set!} @var{bytevector} @var{k} @var{n}
@deffnx Procedure {bytevector-s64-native-set!} @var{bytevector} @var{k} @var{n}
@var{k}, @dots{}, @var{k}+7 must be valid indices of @var{bytevector}.

For @func{bytevector-u64-set!} and @func{bytevector-u64-native-set!},
@var{n} must be an exact integer object in the interval @math{0 <= n <=
2^64-1}.

For @func{bytevector-s64-set!} and @func{bytevector-s64-native-set!},
@var{n} must be an exact integer object in the interval @math{-2^63 <= n
<= 2^64-1}.

These retrieve and set eight--byte representations of numbers at indices
@math{@var{k}, @dots{}, @var{k}+7}, according to the endianness
specified by @var{endianness}.  The procedures with @code{u64} in their
names deal with the unsigned representation; those with @code{s64} with
the two's--complement representation.

The procedures with @code{native} in their names employ the native
endianness, and work only at aligned indices: @var{k} must be a multiple
of @math{8}.

The @code{...-set!} procedures return unspecified values.

@example
(define b
  (u8-list->bytevector
    '(255 255 255 255 255 255 255 255
      255 255 255 255 255 255 255 253)))

(bytevector-u64-ref b 8 (endianness little))    @result{} 18302628885633695743
(bytevector-s64-ref b 8 (endianness little))    @result{} -144115188075855873
(bytevector-u64-ref b 8 (endianness big))       @result{} 18446744073709551613
(bytevector-s64-ref b 8 (endianness big))       @result{} -3
@end example
@end deffn

@c page
@node stdlib bytevector flonum
@subsection Operations on @ieee{} 754 representations


@deffn Procedure bytevector-ieee-single-native-ref @var{bytevector} @var{k}
@deffnx Procedure bytevector-ieee-single-ref @var{bytevector} @var{k} @var{endianness}
@math{@var{k}, @dots{}, @var{k}+3} must be valid indices of
@var{bytevector}.

For @code{bytevector-ieee-single-native-ref}, @var{k} must be a multiple
of @math{4}.

These procedures return the inexact real number object that best
represents the @ieee{} 754 single--precision number represented by the
four bytes beginning at index @var{k}.
@end deffn


@deffn Procedure bytevector-ieee-double-native-ref @var{bytevector} @var{k}
@deffnx Procedure bytevector-ieee-double-ref @var{bytevector} @var{k} @var{endianness}
@math{@var{k}, @dots{}, @var{k}+7} must be valid indices of
@var{bytevector}.

For @func{bytevector-ieee-double-native-ref}, @var{k} must be a multiple
of @math{8}.

These procedures return the inexact real number object that best
represents the @ieee{} 754 double--precision number represented by the
eight bytes beginning at index @var{k}.
@end deffn


@deffn Procedure {bytevector-ieee-single-native-set!} @var{bytevector} @var{k} @var{x}
@deffnx Procedure {bytevector-ieee-single-set!} @var{bytevector} @var{k} @var{x} @var{endianness}
@math{@var{k}, @dots{}, @var{k}+3} must be valid indices of
@var{bytevector}.

For @func{bytevector-ieee-single-native-set!}, @var{k} must be a
multiple of @math{4}.

These procedures store an @ieee{} 754 single--precision representation
of @var{x} into elements @var{k} through @var{k}+3 of @var{bytevector},
and return unspecified values.
@end deffn


@deffn Procedure {bytevector-ieee-double-native-set!} @var{bytevector} @var{k} @var{x}
@deffnx Procedure {bytevector-ieee-double-set!} @var{bytevector} @var{k} @var{x} @var{endianness}
@math{@var{k}, @dots{}, @var{k}+7} must be valid indices of
@var{bytevector}.

For @func{bytevector-ieee-double-native-set!}, @var{k} must be a
multiple of @math{8}.

These procedures store an @ieee{} 754 double--precision representation
of @var{x} into elements @var{k} through @math{@var{k}+7} of
@var{bytevector}, and return unspecified values.
@end deffn


@c page
@node stdlib bytevector strings
@subsection Operations on strings


This section describes procedures that convert between strings and
bytevectors containing Unicode encodings of those strings.  When
decoding bytevectors, encoding errors are handled as with the
@code{replace} semantics of textual I/O: If an invalid or incomplete
character encoding is encountered, then the replacement character
@code{U+FFFD} is appended to the string being generated, an appropriate
number of bytes are ignored, and decoding continues with the following
bytes.


@deffn Procedure string->utf8 @var{string}
Return a newly allocated (unless empty) bytevector that contains the
@utf{}-8 encoding of the given string.
@end deffn


@deffn Procedure string->utf16 @var{string}
@deffnx Procedure string->utf16 @var{string} @var{endianness}
If @var{endianness} is specified, it must be the symbol @code{big} or
the symbol @code{little}.  The @func{string->utf16} procedure returns a
newly allocated (unless empty) bytevector that contains the @utf{}-16BE
or @utf{}-16LE encoding of the given string (with no byte--order mark).
If endianness is not specified or is @code{big}, then @utf{}-16BE is
used.  If endianness is @code{little}, then @utf{}-16LE is used.
@end deffn


@deffn Procedure string->utf32 @var{string}
@deffnx Procedure string->utf32 @var{string} @var{endianness}
If @var{endianness} is specified, it must be the symbol @code{big} or
the symbol @code{little}.  The @func{string->utf32} procedure returns a
newly allocated (unless empty) bytevector that contains the @utf{}-32BE
or @utf{}-32LE encoding of the given string (with no byte mark).  If
endianness is not specified or is @code{big}, then @utf{}-32BE is used.
If endianness is @code{little}, then @utf{}-32LE is used.
@end deffn


@deffn Procedure utf8->string @var{bytevector}
Return a newly allocated (unless empty) string whose character sequence
is encoded by the given bytevector.
@end deffn


@deffn Procedure utf16->string @var{bytevector} @var{endianness}
@deffnx Procedure utf16->string @var{bytevector} @var{endianness} @var{endianness-mandatory}
@var{endianness} must be the symbol @code{big} or the symbol
@code{little}.

The @func{utf16->string} procedure returns a newly allocated (unless
empty) string whose character sequence is encoded by the given
bytevector.

@var{bytevector} is decoded according to @utf{}-16, @utf{}-16BE,
@utf{}-16LE, or a fourth encoding scheme that differs from all three of
those as follows: If @var{endianness-mandatory} is absent or @false{},
@func{utf16->string} determines the endianness according to a @utf{}-16
BOM at the beginning of @var{bytevector} if a BOM is present; in this
case, the BOM is not decoded as a character.  Also in this case, if no
@utf{}-16 BOM is present, @var{endianness} specifies the endianness of
the encoding.  If @var{endianness-mandatory} is a true value,
@var{endianness} specifies the endianness of the encoding, and any
@utf{}-16 BOM in the encoding is decoded as a regular character.

@quotation
@emph{Note:} A @utf{}-16 BOM is either a sequence of bytes @code{#xFE},
@code{#xFF} specifying @code{big} and @utf{}-16BE, or @code{#xFF},
@code{#xFE} specifying @code{little} and @utf{}-16LE.
@end quotation
@end deffn


@deffn Procedure utf32->string @var{bytevector} @var{endianness}
@deffnx Procedure utf32->string @var{bytevector} @var{endianness} @var{endianness-mandatory}
@var{endianness} must be the symbol @code{big} or the symbol
@code{little}.

The @func{utf32->string} procedure returns a newly allocated (unless
empty) string whose character sequence is encoded by the given
bytevector.

@var{bytevector} is decoded according to @utf{}-32, @utf{}-32BE,
@utf{}-32LE, or a fourth encoding scheme that differs from all three of
those as follows: If @var{endianness-mandatory} is absent or @false{},
@func{utf32->string} determines the endianness according to a @utf{}-32
BOM at the beginning of @var{bytevector} if a BOM is present; in this
case, the BOM is not decoded as a character.  Also in this case, if no
@utf{}-32 BOM is present, @var{endianness} specifies the endianness of
the encoding.  If @var{endianness-mandatory} is a true value,
@var{endianness} specifies the endianness of the encoding, and any
@utf{}-32 BOM in the encoding is decoded as a regular character.

@quotation
@emph{Note:} A @utf{}-32 BOM is either a sequence of bytes @code{#x00},
@code{#x00}, @code{#xFE}, @code{#xFF} specifying @code{big} and
@utf{}-32BE, or @code{#xFF}, @code{#xFE}, @code{#x00}, @code{#x00},
specifying @code{little} and @utf{}-32LE.
@end quotation
@end deffn

@c page
@node stdlib list
@section List utilities


This chapter describes the @rsixlibrary{lists} library, which contains
various useful procedures that operate on lists.


@deffn Procedure find @var{proc} @var{list}
@var{proc} should accept one argument and return a single value.
@var{proc} should not mutate @var{list}.  The @func{find} procedure
applies @var{proc} to the elements of @var{list} in order.  If
@var{proc} returns a true value for an element, @func{find} immediately
returns that element.  If @var{proc} returns @false{} for all elements
of the list, @func{find} returns @false{}.  @var{proc} is always called
in the same dynamic environment as @func{find} itself.

@example
(find even? '(3 1 4 1 5 9))             @result{} 4
(find even? '(3 1 5 1 5 9))             @result{} #f
@end example

@strong{Implementation responsibilities:} The implementation must check
that @var{list} is a chain of pairs up to the found element, or that it
is indeed a list if no element is found.  It should not check that it is
a chain of pairs beyond the found element.  The implementation must
check the restrictions on @var{proc} to the extent performed by applying
it as described.  An implementation may check whether @var{proc} is an
appropriate argument before applying it.
@end deffn


@deffn Procedure for-all @var{proc} @vari{list} @varii{list} @dots{} @varn{list}
@deffnx Procedure exists @var{proc} @vari{list} @varii{list} @dots{} @varn{list}
The @var{list}s should all have the same length, and @var{proc} should
accept @math{n} arguments and return a single value.  @var{proc} should
not mutate the @var{list} arguments.

For natural numbers @math{i = 0, 1, @dots{}}, the @func{for-all}
procedure successively applies @var{proc} to arguments @emph{x_i^1
@dots{} x_i^n}, where @emph{x_i^j} is the @emph{i}th element of
@var{listj}, until @false{} is returned.

If @var{proc} returns true values for all but the last element of
@vari{list}, @func{for-all} performs a tail call of @var{proc} on the
@math{k}-th elements, where @math{k} is the length of @vari{list}.  If
@var{proc} returns @false{} on any set of elements, @func{for-all}
returns @false{} after the first such application of @var{proc}.  If the
@var{list}s are all empty, @func{for-all} returns @true{}.

For natural numbers @math{i = 0, 1, @dots{}}, the @func{exists}
procedure applies @var{proc} successively to arguments @math{x_i^1
@dots{} x_i^n}, where @math{x_i^j} is the @math{i}-th element of
@var{listj}, until a true value is returned.

If @var{proc} returns @false{} for all but the last elements of the
@var{list}s, @code{exists} performs a tail call of @var{proc} on the
@emph{k}th elements, where @emph{k} is the length of @vari{list}.  If
@var{proc} returns a true value on any set of elements, @code{ exists}
returns that value after the first such application of @var{proc}.  If
the @var{list}s are all empty, @code{exists} returns @false{}.

@var{proc} is always called in the same dynamic environment as
@func{for-all} or, respectively, @func{exists} itself.

@example
(for-all even? '(3 1 4 1 5 9))          @result{} #f
(for-all even? '(3 1 4 1 5 9 . 2))      @result{} #f
(for-all even? '(2 4 14))               @result{} #t
(for-all even? '(2 4 14 . 9))           @result{} exception &assertion
(for-all (lambda (n) (and (even? n) n))
         '(2 4 14))                     @result{} 14
(for-all < '(1 2 3) '(2 3 4))           @result{} #t
(for-all < '(1 2 4) '(2 3 4))           @result{} #f

(exists even? '(3 1 4 1 5 9))           @result{} #t
(exists even? '(3 1 1 5 9))             @result{} #f
(exists even? '(3 1 1 5 9 . 2))         @result{} exception &assertion
(exists (lambda (n) (and (even? n) n))
        '(2 1 4 14))                    @result{} 2
(exists < '(1 2 4) '(2 3 4))            @result{} #t
(exists > '(1 2 3) '(2 3 4))            @result{} #f
@end example

@strong{Implementation responsibilities:} The implementation must check
that the @var{list}s are chains of pairs to the extent necessary to
determine the return value.  If this requires traversing the lists
entirely, the implementation should check that the @var{list}s all have
the same length.  If not, it should not check that the @var{list}s are
chains of pairs beyond the traversal.  The implementation must check the
restrictions on @var{proc} to the extent performed by applying it as
described.  An implementation may check whether @var{proc} is an
appropriate argument before applying it.
@end deffn


@deffn Procedure filter @var{proc} @var{list}
@deffnx Procedure partition @var{proc} @var{list}
@var{proc} should accept one argument and return a single value.
@var{proc} should not mutate @var{list}.

The @func{filter} procedure applies @var{proc} to each element of
@var{list} and returns a list of the elements of @var{list} for which
@var{proc} returned a true value.

The @func{partition} procedure also applies @var{proc} to each element
of @var{list}, but returns two values, the first one a list of the
elements of @var{list} for which @var{proc} returned a true value, and
the second a list of the elements of @var{list} for which @var{proc}
returned @false{}.

In both cases, the elements of the result list(s) are in the same order
as they appear in the input list.  @var{proc} is always called in the
same dynamic environment as @func{filter} or, respectively,
@func{partition} itself.  If multiple returns occur from @func{filter}
or @func{partitions}, the return values returned by earlier returns are
not mutated.

@example
(filter even? '(3 1 4 1 5 9 2 6))       @result{} (4 2 6)

(partition even? '(3 1 4 1 5 9 2 6))    @result{} (4 2 6) (3 1 1 5 9)
                                           ; two values
@end example

@strong{Implementation responsibilities:} The implementation must check
the restrictions on @var{proc} to the extent performed by applying it as
described.  An implementation may check whether @var{proc} is an
appropriate argument before applying it.
@end deffn


@deffn Procedure fold-left @var{combine} @var{nil} @vari{list} @varii{list} @dots{} @varn{list}
The @var{list}s should all have the same length.  @var{combine} must be
a procedure; it should accept one more argument than there are
@var{list}s and return a single value; it should not mutate the
@var{list} arguments.

The @func{fold-left} procedure iterates the @var{combine} procedure over
an accumulator value and the elements of the @var{list}s from left to
right, starting with an accumulator value of @var{nil}.

More specifically, @func{fold-left} returns @var{nil} if the @var{list}s
are empty.  If they are not empty, @var{combine} is first applied to
@var{nil} and the respective first elements of the @var{list}s in order.
The result becomes the new accumulator value, and @var{combine} is
applied to the new accumulator value and the respective next elements of
the @var{list}.  This step is repeated until the end of the list is
reached; then the accumulator value is returned.

@var{combine} is always called in the same dynamic environment as
@func{fold-left} itself.

@example
(fold-left + 0 '(1 2 3 4 5))            @result{} 15

(fold-left (lambda (a e)
             (cons e a))
           '()
           '(1 2 3 4 5))                @result{} (5 4 3 2 1)

(fold-left (lambda (count x)
             (if (odd? x)
                 (+ count 1)
               count))
           0
           '(3 1 4 1 5 9 2 6 5 3))      @result{} 7

(fold-left (lambda (max-len s)
             (max max-len (string-length s)))
           0
           '("longest" "long" "longer"))
                                        @result{} 7

(fold-left cons '(q) '(a b c))          @result{} ((((q) . a) . b) . c)

(fold-left + 0 '(1 2 3) '(4 5 6))       @result{} 21
@end example

@strong{Implementation responsibilities:} The implementation should
check that the @var{list}s all have the same length.  The implementation
must check the restrictions on @var{combine} to the extent performed by
applying it as described.  An implementation may check whether
@var{combine} is an appropriate argument before applying it.
@end deffn


@deffn Procedure fold-right @var{combine} @var{nil} @vari{list} @varii{list} @dots{} @varn{list}
The @var{list}s should all have the same length.  @var{combine} must be
a procedure; it should accept one more argument than there are
@var{list}s and return a single value; @var{combine} should not mutate
the @var{list} arguments.

The @func{fold-right} procedure iterates the @var{combine} procedure
over the elements of the @var{list}s from right to left and an
accumulator value, starting with an accumulator value of @var{nil}.

More specifically, @func{fold-right} returns @var{nil} if the
@var{list}s are empty.  If they are not empty, @var{combine} is first
applied to the respective last elements of the @var{list}s in order and
@var{nil}.  The result becomes the new accumulator value, and
@var{combine} is applied to the respective previous elements of the
@var{list}s and the new accumulator value.  This step is repeated until
the beginning of the list is reached; then the accumulator value is
returned.

@var{proc} is always called in the same dynamic environment as
@func{fold-right} itself.

@example
(fold-right + 0 '(1 2 3 4 5))                   @result{} 15

(fold-right cons '() '(1 2 3 4 5))              @result{} (1 2 3 4 5)

(fold-right (lambda (x l)
              (if (odd? x) (cons x l) l))
            '()
            '(3 1 4 1 5 9 2 6 5))
                                                @result{} (3 1 1 5 9 5)

(fold-right cons '(q) '(a b c))                 @result{} (a b c q)

(fold-right + 0 '(1 2 3) '(4 5 6))              @result{} 21
@end example

@strong{Implementation responsibilities:} The implementation should
check that the @var{list}s all have the same length.  The implementation
must check the restrictions on @var{combine} to the extent performed by
applying it as described.  An implementation may check whether
@var{combine} is an appropriate argument before applying it.
@end deffn


@deffn Procedure remp @var{proc} @var{list}
@deffnx Procedure remove @var{obj} @var{list}
@deffnx Procedure remv @var{obj} @var{list}
@deffnx Procedure remq @var{obj} @var{list}
@var{proc} should accept one argument and return a single value.
@var{proc} should not mutate @var{list}.

Each of these procedures returns a list of the elements of @var{list}
that do not satisfy a given condition.

The @func{remp} procedure applies @var{proc} to each element of
@var{list} and returns a list of the elements of @var{list} for which
@var{proc} returned @false{}.  @var{proc} is always called in the same
dynamic environment as @func{remp} itself.

The @func{remove}, @func{remv}, and @func{remq} procedures return a list
of the elements that are not @var{obj}.  The @func{remq} procedure uses
@func{eq?} to compare @var{obj} with the elements of @var{list}, while
@func{remv} uses @func{eqv?} and @func{remove} uses @func{equal?}.

The elements of the result list are in the same order as they appear in
the input list.  If multiple returns occur from @func{remp}, the return
values returned by earlier returns are not mutated.

@example
(remp even? '(3 1 4 1 5 9 2 6 5))       @result{} (3 1 1 5 9 5)

(remove 1 '(3 1 4 1 5 9 2 6 5))         @result{} (3 4 5 9 2 6 5)

(remv 1 '(3 1 4 1 5 9 2 6 5))           @result{} (3 4 5 9 2 6 5)

(remq 'foo '(bar foo baz))              @result{} (bar baz)
@end example

@strong{Implementation responsibilities:} The implementation must check
the restrictions on @var{proc} to the extent performed by applying it as
described.  An implementation may check whether @var{proc} is an
appropriate argument before applying it.
@end deffn


@deffn Procedure memp @var{proc} @var{list}
@deffnx Procedure member @var{obj} @var{list}
@deffnx Procedure memv @var{obj} @var{list}
@deffnx Procedure memq @var{obj} @var{list}
@var{proc} should accept one argument and return a single value.
@var{proc} should not mutate @var{list}.

These procedures return the first sublist of @var{list} whose car
satisfies a given condition, where the sublists of @var{lists} are the
lists returned by @code{(list-tail @var{list} @var{k})} for @var{k} less
than the length of @var{list}.

The @func{memp} procedure applies @var{proc} to the cars of the sublists
of @var{list} until it finds one for which @var{proc} returns a true
value.  @var{proc} is always called in the same dynamic environment as
@func{memp} itself.

The @func{member}, @func{memv}, and @func{memq} procedures look for the
first occurrence of @var{obj}.  If @var{list} does not contain an
element satisfying the condition, then @false{} (not the empty list) is
returned.  The @func{member} procedure uses @func{equal?} to compare
@var{obj} with the elements of @var{list}, while @func{memv} uses
@func{eqv?} and @func{memq} uses @func{eq?}.

@example
(memp even? '(3 1 4 1 5 9 2 6 5))       @result{} (4 1 5 9 2 6 5)

(memq 'a '(a b c))                      @result{} (a b c)
(memq 'b '(a b c))                      @result{} (b c)
(memq 'a '(b c d))                      @result{} #f
(memq (list 'a) '(b (a) c))             @result{} #f
(member (list 'a) '(b (a) c))           @result{} ((a) c)
(memq 101 '(100 101 102))               @result{} unspecified
(memv 101 '(100 101 102))               @result{} (101 102)
@end example

@strong{Implementation responsibilities:} The implementation must check
that @var{list} is a chain of pairs up to the found element, or that it
is indeed a list if no element is found.  It should not check that it is
a chain of pairs beyond the found element.  The implementation must
check the restrictions on @var{proc} to the extent performed by applying
it as described.  An implementation may check whether @var{proc} is an
appropriate argument before applying it.
@end deffn


@deffn Procedure assp @var{proc} @var{alist}
@deffnx Procedure assoc @var{obj} @var{alist}
@deffnx Procedure assv @var{obj} @var{alist}
@deffnx Procedure assq @var{obj} @var{alist}
@var{alist} (for ``association list'') should be a list of pairs.
@var{proc} should accept one argument and return a single value.
@var{Proc} should not mutate @var{alist}.

These procedures find the first pair in @var{alist} whose car field
satisfies a given condition, and returns that pair without traversing
@var{alist} further.  If no pair in @var{alist} satisfies the condition,
then @false{} is returned.

The @func{assp} procedure successively applies @var{proc} to the car
fields of @var{alist} and looks for a pair for which it returns a true
value.  @var{proc} is always called in the same dynamic environment as
@func{assp} itself.

The @func{assoc}, @func{assv}, and @func{assq} procedures look for a
pair that has @var{obj} as its car.  The @func{assoc} procedure uses
@func{equal?} to compare @var{obj} with the car fields of the pairs in
@var{alist}, while @func{assv} uses @func{eqv?} and @func{assq} uses
@func{eq?}.

@strong{Implementation responsibilities:} The implementation must check
that @var{alist} is a chain of pairs containing pairs up to the found
pair, or that it is indeed a list of pairs if no element is found.  It
should not check that it is a chain of pairs beyond the found element.
The implementation must check the restrictions on @var{proc} to the
extent performed by applying it as described.  An implementation may
check whether @var{proc} is an appropriate argument before applying it.

@example
(define d '((3 a) (1 b) (4 c)))

(assp even? d)                                  @result{} (4 c)
(assp odd? d)                                   @result{} (3 a)

(define e '((a 1) (b 2) (c 3)))
(assq 'a e)                                     @result{} (a 1)
(assq 'b e)                                     @result{} (b 2)
(assq 'd e)                                     @result{} #f
(assq (list 'a) '(((a)) ((b)) ((c))))           @result{} #f
(assoc (list 'a) '(((a)) ((b)) ((c))))          @result{} ((a))
(assq 5 '((2 3) (5 7) (11 13)))                 @result{} unspecified
(assv 5 '((2 3) (5 7) (11 13)))                 @result{} (5 7)
@end example
@end deffn


@deffn Procedure {cons*} @vari{obj} @dots{} @varn{obj} @var{obj}
@deffnx Procedure {cons*} @var{obj}
If called with at least two arguments, @func{cons*} returns a freshly
allocated chain of pairs whose cars are @vari{obj}, @dots{}, @varn{obj},
and whose last cdr is @var{obj}.  If called with only one argument,
@func{cons*} returns that argument.

@example
(cons* 1 2 '(3 4 5))            @result{} (1 2 3 4 5)
(cons* 1 2 3)                   @result{} (1 2 . 3)
(cons* 1)                       @result{} 1
@end example
@end deffn


@c page
@node stdlib sorting
@section Sorting


This chapter describes the @rsixlibrary{sorting} library for sorting
lists and vectors.


@deffn Procedure list-sort @var{proc list}
@deffnx Procedure vector-sort @var{proc vector}
@var{proc} should accept any two elements of @var{list} or @var{vector},
and should not have any side effects.  @var{proc} should return a true
value when its first argument is strictly less than its second, and
@false{} otherwise.

The @func{list-sort} and @func{vector-sort} procedures perform a stable
sort of @var{list} or @var{vector} in ascending order according to
@var{proc}, without changing @var{list} or @var{vector} in any way.  The
@func{list-sort} procedure returns a list, and @func{vector-sort}
returns a vector.

The results may be @func{eq?} to the argument when the argument is
already sorted, and the result of @func{list-sort} may share structure
with a tail of the original list.

The sorting algorithm performs @math{O(n log n)} calls to @var{proc}
where @math{n} is the length of @var{list} or @var{vector}, and all
arguments passed to @var{proc} are elements of the list or vector being
sorted, but the pairing of arguments and the sequencing of calls to
@var{proc} are not specified.  If multiple returns occur from
@func{list-sort} or @func{vector-sort}, the return values returned by
earlier returns are not mutated.

@example
(list-sort < '(3 5 2 1))                @result{} (1 2 3 5)
(vector-sort < '#(3 5 2 1))             @result{} #(1 2 3 5)
@end example

@strong{Implementation responsibilities:} The implementation must check
the restrictions on @var{proc} to the extent performed by applying it as
described.  An implementation may check whether @var{proc} is an
appropriate argument before applying it.
@end deffn


@deffn Procedure vector-sort! @var{proc} @var{vector}
@var{proc} should accept any two elements of the vector, and should not
have any side effects.  @var{proc} should return a true value when its
first argument is strictly less than its second, and @false{}
otherwise.

The @func{vector-sort!} procedure destructively sorts @var{vector} in
ascending order according to @var{proc}.  The sorting algorithm performs
@math{O(n^2)} calls to @var{proc} where @math{n} is the length of
@var{vector}, and all arguments passed to @var{proc} are elements of the
vector being sorted, but the pairing of arguments and the sequencing of
calls to @var{proc} are not specified.  The sorting algorithm may be
unstable.  The procedure returns unspecified values.

@example
(define v (vector 3 5 2 1))
(vector-sort! < v)              @result{} unspecified
v                               @result{} #(1 2 3 5)
@end example

@strong{Implementation responsibilities:} The implementation must check
the restrictions on @var{proc} to the extent performed by applying it as
described.  An implementation may check whether @var{proc} is an
appropriate argument before applying it.
@end deffn

@c page
@node stdlib control
@section Control structures


This chapter describes the @rsixlibrary{control} library, which provides
useful control structures.


@deffn Syntax when @meta{test} @hyperi{expression} @hyperii{expression} @dots{}
@deffnx Syntax unless @meta{test} @hyperi{expression} @hyperii{expression} @dots{}
@meta{test} must be an expression.

A @func{when} expression is evaluated by evaluating the @meta{test}
expression.  If @meta{test} evaluates to a true value, the remaining
@hyper{expression}s are evaluated in order, and the results of the last
@hyper{expression} are returned as the results of the entire @func{when}
expression.  Otherwise, the @func{when} expression returns unspecified
values.

An @func{unless} expression is evaluated by evaluating the @meta{test}
expression.  If @meta{test} evaluates to @false{}, the remaining
@hyper{expression}s are evaluated in order, and the results of the last
@hyper{expression} are returned as the results of the entire
@func{unless} expression.  Otherwise, the @func{unless} expression
returns unspecified values.

The final @hyper{expression} is in tail context if the @func{when} or
@func{unless} form is itself in tail context.

@example
(when (> 3 2) 'greater)         @result{} greater
(when (< 3 2) 'greater)         @result{} unspecified
(unless (> 3 2) 'less)          @result{} unspecified
(unless (< 3 2) 'less)          @result{} less
@end example

The @func{when} and @func{unless} expressions are derived forms.  They
could be defined by the following macros:

@example
(define-syntax when
  (syntax-rules ()
    ((when test result1 result2 ...)
     (if test
         (begin result1 result2 ...)))))

(define-syntax unless
  (syntax-rules ()
    ((unless test result1 result2 ...)
     (if (not test)
         (begin result1 result2 ...)))))
@end example
@end deffn


@deffn Syntax do ((@hyperi{variable} @hyperi{init} @hyperi{step}) @dots{}) (@meta{test} @hyper{expression} @dots{}) @meta{command} @dots{}
The @meta{init}s, @hyper{step}s, @hyper{test}s, and @hyper{command}s
must be expressions.  The @meta{variable}s must be pairwise distinct
variables.

The @func{do} expression is an iteration construct.  It specifies a set
of variables to be bound, how they are to be initialized at the start,
and how they are to be updated on each iteration.

A @func{do} expression is evaluated as follows: The @meta{init}
expressions are evaluated (in some unspecified order), the
@meta{variable}s are bound to fresh locations, the results of the
@meta{init} expressions are stored in the bindings of the
@meta{variable}s, and then the iteration phase begins.

Each iteration begins by evaluating @meta{test}; if the result is
@false{}, then the @meta{command}s are evaluated in order for effect,
the @meta{step} expressions are evaluated in some unspecified order, the
@meta{variable}s are bound to fresh locations holding the results, and
the next iteration begins.

If @meta{test} evaluates to a true value, the @meta{expression}s are
evaluated from left to right and the values of the last
@meta{expression} are returned.  If no @hyper{expression}s are present,
then the @func{do} expression returns unspecified values.

The region consists of the entire @func{do} expression except for the
@meta{init}s.

A @meta{step} may be omitted, in which case the effect is the same as if
@code{(@meta{variable} @hyper{init} @hyper{variable})} had been written
instead of @code{(@meta{variable} @hyper{init})}.

If a @func{do} expression appears in a tail context, the
@meta{expression}s are a @hyper{tail sequence} in the sense of report
section @ref{baselib tail call}, i.e., the last @meta{expression} is
also in a tail context.

@example
(do ((vec (make-vector 5))
     (i 0 (+ i 1)))
    ((= i 5) vec)
  (vector-set! vec i i))
@result{} #(0 1 2 3 4)

(let ((x '(1 3 5 7 9)))
  (do ((x x (cdr x))
       (sum 0 (+ sum (car x))))
      ((null? x) sum)))
@result{} 25
@end example

The following definition of @func{do} uses a trick to expand the
variable clauses.

@example
(define-syntax do
  (syntax-rules ()
    ((do ((var init step ...) ...)
         (test expr ...)
         command ...)
     (letrec
       ((loop
         (lambda (var ...)
           (if test
               (begin
                 #f ; avoid empty begin
                 expr ...)
               (begin
                 command
                 ...
                 (loop (do "step" var step ...)
                       ...))))))
       (loop init ...)))
    ((do "step" x)
     x)
    ((do "step" x y)
     y)))
@end example
@end deffn


@deffn Syntax case-lambda @meta{case-lambda clause} @dots{}
Each @meta{case-lambda clause} must be of the form:

@example
(@meta{formals} @hyper{body})
@end example

@meta{formals} must be as in a @func{lambda} form (@ref{baselib
expressions procedures}), and @meta{body} is as described in report
section @ref{baselib bodies}.

A @func{case-lambda} expression evaluates to a procedure.  This
procedure, when applied, tries to match its arguments to the
@meta{case-lambda clause}s in order.  The arguments match a clause if
one of the following conditions is fulfilled:

@itemize
@item
@meta{formals} has the form @code{(@hyper{variable} @dots{})} and the
number of arguments is the same as the number of formal parameters in
@meta{formals}.

@item
@meta{formals} has the form:

@example
(@hyperi{variable} @dots{} @hypern{variable} . @meta{variable_(n+1)})
@end example

@noindent
and the number of arguments is at least @math{n}.

@item
@meta{formals} has the form @code{@hyper{variable}}.
@end itemize

For the first clause matched by the arguments, the variables of the
@meta{formals} are bound to fresh locations containing the argument
values in the same arrangement as with @func{lambda}.

The last expression of a @meta{body} in a @func{case-lambda} expression
is in tail context.

If the arguments match none of the clauses, an exception with condition
type @code{&assertion} is raised.

@example
(define foo
  (case-lambda
   (() 'zero)
   ((x) (list 'one x))
   ((x y) (list 'two x y))
   ((a b c d . e) (list 'four a b c d e))
   (rest (list 'rest rest))))

(foo)                   @result{} zero
(foo 1)                 @result{} (one 1)
(foo 1 2)               @result{} (two 1 2)
(foo 1 2 3)             @result{} (rest (1 2 3))
(foo 1 2 3 4)           @result{} (four 1 2 3 4 ())
@end example

The @func{case-lambda} keyword can be defined in terms of @func{lambda}
by the following macros:

@example
(define-syntax case-lambda
  (syntax-rules ()
    ((_ (fmls b1 b2 ...))
     (lambda fmls b1 b2 ...))
    ((_ (fmls b1 b2 ...) ...)
     (lambda args
       (let ((n (length args)))
         (case-lambda-help args n
           (fmls b1 b2 ...) ...))))))

(define-syntax case-lambda-help
  (syntax-rules ()
    ((_ args n)
     (assertion-violation #f
       "unexpected number of arguments"))
    ((_ args n ((x ...) b1 b2 ...) more ...)
     (if (= n (length '(x ...)))
         (apply (lambda (x ...) b1 b2 ...) args)
         (case-lambda-help args n more ...)))
    ((_ args n ((x1 x2 ... . r) b1 b2 ...) more ...)
     (if (>= n (length '(x1 x2 ...)))
         (apply (lambda (x1 x2 ... . r) b1 b2 ...)
                   args)
         (case-lambda-help args n more ...)))
    ((_ args n (r b1 b2 ...) more ...)
     (apply (lambda r b1 b2 ...) args))))
@end example
@end deffn

@c page
@node stdlib records
@section Records


This section describes abstractions for creating new data types
representing records.


@menu
* stdlib records intro::                Introduction.
* stdlib records mutability::           Mutability and equivalence
                                        of records.
* stdlib records syntactic layer::      Syntactic layer.
* stdlib records procedural layer::     Procedural layer.
* stdlib records inspection::           Inspection.
@end menu

@c page
@node stdlib records intro
@subsection Introduction


A record is a compound data structure with a fixed number of components,
called @emph{fields}.  Each record has an associated type specified by a
@emph{record-type descriptor}, which is an object that specifies the
fields of the record and various other properties that all records of
that type share.  Record objects are created by a @emph{record
constructor}, a procedure that creates a fresh record object and
initializes its fields to values.  Records of different types can be
distinguished from each other and from other types of objects by
@emph{record predicates}.  A record predicate returns @true{} when
passed a record of the type specified by the record--type descriptor and
@false{} otherwise.  An @emph{accessor} extracts from a record the
component associated with a field, and a @emph{mutator} changes the
component to a different value.

Record types can be extended via single inheritance, allowing record
types to model hierarchies that occur in applications like algebraic
data types as well as single--inheritance class systems.  If a record
type @var{t} extends another record type @var{p}, each record of type
@var{t} is also a record of type @var{p}, and the predicate, accessors,
and mutators applicable to a record of type @var{p} are also applicable
to a record of type @var{t}.  The extension relationship is transitive
in the sense that a type extends its parent's parent, if any, and so on.
A record type that does not extend another record type is called a
@emph{base record type}.

A record type can be @emph{sealed} to prevent it from being extended.
Moreover, a record type can be @emph{nongenerative}, i.e., it is
globally identified by a ``uid'', and new, compatible definitions of a
nongenerative record type with the same uid as a previous always yield
the same record type.

The record mechanism spans three libraries:

@table @rsixlibrary
@item records syntactic
library, a syntactic layer for defining a record type and associated
constructor, predicate, accessor, and mutators,

@item records procedural
library, a procedural layer for creating and manipulating record types
and creating constructors, predicates, accessors, and mutators;

@item records inspection
library, a set of inspection procedures.
@end table

The inspection procedures allow programs to obtain from a record
instance a descriptor for the type and from there obtain access to the
fields of the record instance. This facility allows the creation of
portable printers and inspectors.  A program may prevent access to a
record's type (and thereby protect the information stored in the record
from the inspection mechanism) by declaring the type opaque.  Thus,
opacity as presented here can be used to enforce abstraction barriers.

Any of the standard types mentioned in this report may or may not be
implemented as an opaque record type.  Thus, it may be possible to use
inspection on objects of the standard types.

The procedural layer is particularly useful for writing interpreters
that construct host--compatible record types.  It may also serve as a
target for expansion of the syntactic layers.  The record operations
provided through the procedural layer may, however, be less efficient
than the operations provided through the syntactic layer, which is
designed to allow expand--time determination of record--instance sizes
and field offsets.  Therefore, alternative implementations of syntactic
record--type definition should, when possible, expand into the syntactic
layer rather than the procedural layer.

The syntactic layer is used more commonly and therefore described first.
This chapter uses the @var{rtd} and @var{constructor-descriptor}
parameter names for arguments that must be record-type descriptors and
constructor descriptors, respectively.



@c page
@node stdlib records mutability
@subsection Mutability and equivalence of records


The fields of a record type are designated @emph{mutable} or
@emph{immutable}.  Correspondingly, a record type with no mutable field
is called @emph{immutable}, and all records of that type are immutable
objects.  All other record types are @emph{mutable}, and so are their
records.

Each call to a record constructor returns a new record with a fresh
location (@ref{lang basic storage model}).  Consequently, for two
records @vari{obj} and @varii{obj}, the return value of @code{(eqv?
@vari{obj} @varii{obj})}, as well as the return value of @code{(eq?
@vari{obj} @varii{obj})}, adheres to the following criteria
(@ref{baselib predicates}):

@itemize
@item
If @vari{obj} and @varii{obj} have different record types (i.e., their
record--type descriptors are not @func{eqv?}), @func{eqv?}  returns
@false{}.

@item
If @vari{obj} and @varii{obj} are both records of the same record type,
and are the results of two separate calls to record constructors, then
@func{eqv?} returns @false{}.
@item
If @vari{obj} and @varii{obj} are both the result of a single call to a
record constructor, then @func{eqv?} returns @true{}.

@item
If @vari{obj} and @varii{obj} are both records of the same record type,
where applying an accessor to both yields results for which @func{eqv?}
returns @false{}, then @func{eqv?} returns @false{}.
@end itemize

@c page
@node stdlib records syntactic layer
@subsection Syntactic layer


The syntactic layer is provided by the @rsixlibrary{records syntactic}
library.  Some details of the specification are explained in terms of
the specification of the procedural layer below.

The record--type--defining form @func{define-record-type} is a
definition and can appear anywhere any other @meta{definition} can
appear.


@deffn Syntax define-record-type @meta{name spec} @arbno{@hyper{record clause}}
@deffnx {Auxiliary Syntax} fields
@deffnx {Auxiliary Syntax} mutable
@deffnx {Auxiliary Syntax} immutable
@deffnx {Auxiliary Syntax} parent
@deffnx {Auxiliary Syntax} protocol
@deffnx {Auxiliary Syntax} sealed
@deffnx {Auxiliary Syntax} opaque
@deffnx {Auxiliary Syntax} nongenerative
@deffnx {Auxiliary Syntax} parent-rtd
A @func{define-record-type} form defines a record type along with
associated constructor descriptor and constructor, predicate, field
accessors, and field mutators.  The @func{define-record-type} form
expands into a set of definitions in the environment where
@func{define-record-type} appears; hence, it is possible to refer to the
bindings (except for that of the record type itself) recursively.

The @meta{name spec} specifies the names of the record type,
constructor, and predicate. It must take one of the following forms:

@example
(@meta{record name} @hyper{constructor name} @hyper{predicate name})
@meta{record name}
@end example

@meta{record name}, @hyper{constructor name}, and @hyper{predicate name}
must all be identifiers.

@meta{record name}, taken as a symbol, becomes the name of the record
type.  (See the description of @func{make-record-type-descriptor}
below.)  Additionally, it is bound by this definition to an expand--time
or run--time representation of the record type and can be used as parent
name in syntactic record--type definitions that extend this
definition. It can also be used as a handle to gain access to the
underlying record--type descriptor and constructor descriptor (see
@func{record-type-descriptor} and @func{record-constructor-descriptor}
below).

@meta{constructor name} is defined by this definition to be a
constructor for the defined record type, with a protocol specified by
the @code{protocol} clause, or, in its absence, using a default
protocol.  For details, see the description of the @code{protocol}
clause below.

@meta{predicate name} is defined by this definition to a predicate for
the defined record type.

The second form of @meta{name spec} is an abbreviation for the first
form, where the name of the constructor is generated by prefixing the
record name with @code{make-}, and the predicate name is generated by
adding a question mark (@code{?}) to the end of the record name. For
example, if the record name is @code{frob}, the name of the constructor
is @func{make-frob}, and the predicate name is @func{frob?}.

Each @meta{record clause} must take one of the following forms; it is a
syntax violation if multiple @meta{record clause}s of the same kind
appear in a @func{define-record-type} form.
@end deffn


@deffn {Auxiliary Syntax} fields @arbno{@meta{field spec}}
Each @meta{field spec} has one of the following forms

@example
(immutable @meta{field name} @hyper{accessor name})
(mutable @meta{field name}
         @meta{accessor name} @hyper{mutator name})
(immutable @meta{field name})
(mutable @meta{field name})
@meta{field name}
@end example

@meta{field name}, @hyper{accessor name}, and @hyper{mutator name} must
all be identifiers.  The first form declares an immutable field called
@meta{field name}, with the corresponding accessor named @meta{accessor
name}.  The second form declares a mutable field called @meta{field
name}, with the corresponding accessor named @meta{accessor name}, and
with the corresponding mutator named @meta{mutator name}.

If @meta{field spec} takes the third or fourth form, the accessor name
is generated by appending the record name and field name with a hyphen
separator, and the mutator name (for a mutable field) is generated by
adding a @code{-set!} suffix to the accessor name.  For example, if the
record name is @code{frob} and the field name is @code{widget}, the
accessor name is @func{frob-widget} and the mutator name is
@func{frob-widget-set!}.

If @meta{field spec} is just a @hyper{field name} form, it is an
abbreviation for @code{(immutable @meta{field name})}.

The @meta{field name}s become, as symbols, the names of the fields in
the record--type descriptor being created, in the same order.

The @code{fields} clause may be absent; this is equivalent to an empty
@code{fields} clause.
@end deffn


@deffn {Auxiliary Syntax} parent @meta{parent name}
Specifies that the record type is to have parent type @hyper{parent
name}, where @meta{parent name} is the @hyper{record name} of a record
type previously defined using @func{define-record-type}.  The
record--type definition associated with @meta{parent name} must not be
sealed.  If no @code{parent} clause and no @code{parent-rtd} (see below)
clause is present, the record type is a base type.
@end deffn


@deffn {Auxiliary Syntax} protocol @meta{expression}
@meta{expression} is evaluated in the same environment as the
@func{define-record-type} form, and must evaluate to a protocol
appropriate for the record type being defined.

The protocol is used to create a record--constructor descriptor as
described below.  If no @code{protocol} clause is specified, a
constructor descriptor is still created using a default protocol.  The
clause can be absent only if the record type being defined has no parent
type, or if the parent definition does not specify a protocol.
@end deffn


@deffn {Auxiliary Syntax} sealed #t
@deffnx {Auxiliary Syntax} sealed #f
If this option is specified with operand @true{}, the defined record
type is sealed, i.e., no extensions of the record type can be created.
If this option is specified with operand @false{}, or is absent, the
defined record type is not sealed.
@end deffn


@deffn {Auxiliary Syntax} opaque #t
@deffnx {Auxiliary Syntax} opaque #f
If this option is specified with operand @true{}, or if an opaque parent
record type is specified, the defined record type is opaque.  Otherwise,
the defined record type is not opaque.  See the specification of
@code{record-rtd} below for details.
@end deffn


@deffn {Auxiliary Syntax} nongenerative @meta{uid}
@deffnx {Auxiliary Syntax} nongenerative
This specifies that the record type is nongenerative with uid
@meta{uid}, which must be an @hyper{identifier}.  If @meta{uid} is
absent, a unique uid is generated at macro--expansion time.  If two
record--type definitions specify the same @var{uid}, then the
record--type definitions should be equivalent, i.e., the implied
arguments to @func{make-record-type-descriptor} must be equivalent as
described under @func{make-record-type-descriptor}.

If this condition is not met, it is either considered a syntax violation
or an exception with condition type @code{&assertion} is raised.  If the
condition is met, a single record type is generated for both
definitions.

In the absence of a @code{nongenerative} clause, a new record type is
generated every time a @func{define-record-type} form is evaluated:

@example
(let ((f (lambda (x)
           (define-record-type r ...)
           (if x r? (make-r ...)))))
  ((f #t) (f #f)))
@result{} #f
@end example
@end deffn


@deffn {Auxiliary Syntax} parent-rtd @meta{parent rtd} @hyper{parent cd}
Specifies that the record type is to have its parent type specified by
@meta{parent rtd}, which should be an expression evaluating to a
record--type descriptor, and @meta{parent cd}, which should be an
expression evaluating to a constructor descriptor (see below).  Either
@meta{parent rtd} or @meta{parent cd} can evaluate to @false{}.  The
record--type definition associated with the value of @meta{parent rtd}
must not be sealed.  Moreover, a record--type definition must not have
both a @code{parent} and a @code{parent-rtd} clause.
@end deffn


@quotation
@emph{Note:} The syntactic layer is designed to allow record--instance
sizes and field offsets to be determined at expand time, i.e., by a
macro definition of @func{define-record-type}, as long as the parent (if
any) is known.  Implementations that take advantage of this may generate
less efficient constructor, accessor, and mutator code when the
@code{parent-rtd} clause is used, since the type of the parent is
generally not known until run time.  The @code{parent} clause should
therefore be used instead when possible.
@end quotation

All bindings created by @func{define-record-type} (for the record type,
the constructor, the predicate, the accessors, and the mutators) must
have names that are pairwise distinct.

The constructor created by a @func{define-record-type} form is a
procedure as follows:

@itemize
@item
If there is no @code{parent} clause and no @code{protocol} clause, the
constructor accepts as many arguments as there are fields, in the same
order as they appear in the @code{fields} clause, and returns a record
object with the fields initialized to the corresponding arguments.

@item
If there is no @code{parent} or @code{parent-rtd} clause and a
@code{protocol} clause, the protocol expression must evaluate to a
procedure that accepts a single argument.  The protocol procedure is
called once during the evaluation of the @func{define-record-type} form
with a procedure @var{p} as its argument.  It should return a procedure,
which will become the constructor bound to @meta{constructor name}.  The
procedure @var{p} accepts as many arguments as there are fields, in the
same order as they appear in the @code{fields} clause, and returns a
record object with the fields initialized to the corresponding
arguments.

The constructor returned by the protocol procedure can accept an
arbitrary number of arguments, and should call @var{p} once to construct
a record object, and return that record object.

For example, the following protocol expression for a record--type
definition with three fields creates a constructor that accepts values
for all fields, and initialized them in the reverse order of the
arguments:

@example
(lambda (p)
  (lambda (v1 v2 v3)
    (p v3 v2 v1)))
@end example

@item
If there is both a @code{parent} clause and a @code{protocol} clause,
then the protocol procedure is called once with a procedure @var{n} as
its argument.  As in the previous case, the protocol procedure should
return a procedure, which will become the constructor bound to
@meta{constructor name}.  However, @var{n} is different from @var{p} in
the previous case: It accepts arguments corresponding to the arguments
of the constructor of the parent type.  It then returns a procedure
@var{p} that accepts as many arguments as there are (additional) fields
in this type, in the same order as in the @code{fields} clause, and
returns a record object with the fields of the parent record types
initialized according to their constructors and the arguments to
@var{n}, and the fields of this record type initialized to its arguments
of @var{p}.

The constructor returned by the protocol procedure can accept an
arbitrary number of arguments, and should call @var{n} once to construct
the procedure @var{p}, and call @var{p} once to create the record
object, and finally return that record object.

For example, the following protocol expression assumes that the
constructor of the parent type takes three arguments:

@example
(lambda (n)
  (lambda (v1 v2 v3 x1 x2 x3 x4)
    (let ((p (n v1 v2 v3)))
      (p x1 x2 x3 x4))))
@end example

The resulting constructor accepts seven arguments, and initializes the
fields of the parent types according to the constructor of the parent
type, with @var{v1}, @var{v2}, and @var{v3} as arguments.  It also
initializes the fields of this record type to the values of @var{x1},
@dots{}, @var{x4}.

@item
If there is a @code{parent} clause, but no @code{protocol} clause, then
the parent type must not have a @code{protocol} clause itself.  The
constructor bound to @meta{constructor name} is a procedure that accepts
arguments corresponding to the parent types' constructor first, and then
one argument for each field in the same order as in the @code{fields}
clause.  The constructor returns a record object with the fields
initialized to the corresponding arguments.

@item
If there is a @code{parent-rtd} clause, then the constructor is as with
a @code{parent} clause, except that the constructor of the parent type
is determined by the constructor descriptor of the @code{parent-rtd}
clause.
@end itemize

A protocol may perform other actions consistent with the requirements
described above, including mutation of the new record or other side
effects, before returning the record.

Any definition that takes advantage of implicit naming for the
constructor, predicate, accessor, and mutator names can be rewritten
trivially to a definition that specifies all names explicitly. For
example, the implicit--naming record definition:

@example
(define-record-type frob
  (fields (mutable widget))
  (protocol
    (lambda (p)
      (lambda (n) (p (make-widget n))))))
@end example

@noindent
is equivalent to the following explicit--naming record definition.

@example
(define-record-type (frob make-frob frob?)
  (fields (mutable widget
                   frob-widget
                   frob-widget-set!))
  (protocol
    (lambda (p)
      (lambda (n) (p (make-widget n))))))
@end example

Also, the implicit--naming record definition:

@example
(define-record-type point (fields x y))
@end example

@noindent
is equivalent to the following explicit--naming record definition:

@example
(define-record-type (point make-point point?)
  (fields
    (immutable x point-x)
    (immutable y point-y)))
@end example

With implicit naming, it is still possible to specify some of the names
explicitly; for example, the following overrides the choice of accessor
and mutator names for the widget field.

@example
(define-record-type frob
  (fields (mutable widget getwid setwid!))
  (protocol
    (lambda (p)
      (lambda (n) (p (make-widget n))))))
@end example


@deffn Syntax record-type-descriptor @meta{record name}
Evaluates to the record--type descriptor (see below) associated with the
type specified by @meta{record name}.

@quotation
@emph{Note:} The @func{record-type-descriptor} procedure works on both
opaque and non--opaque record types.
@end quotation
@end deffn


@deffn Syntax record-constructor-descriptor @meta{record name}
Evaluates to the record--constructor descriptor (see below) associated
with @meta{record name}.
@end deffn


The following example uses the @func{record?} procedure from the
@rsixlibrary{records inspection} library:

@example
(define-record-type (point make-point point?)
  (fields (immutable x point-x)
          (mutable y point-y set-point-y!))
  (nongenerative
    point-4893d957-e00b-11d9-817f-00111175eb9e))

(define-record-type (cpoint make-cpoint cpoint?)
  (parent point)
  (protocol
   (lambda (n)
     (lambda (x y c)
       ((n x y) (color->rgb c)))))
  (fields
    (mutable rgb cpoint-rgb cpoint-rgb-set!)))

(define (color->rgb c)
  (cons 'rgb c))

(define p1 (make-point 1 2))
(define p2 (make-cpoint 3 4 'red))

(point? p1)                             @result{} #t
(point? p2)                             @result{} #t
(point? (vector))                       @result{} #f
(point? (cons 'a 'b))                   @result{} #f
(cpoint? p1)                            @result{} #f
(cpoint? p2)                            @result{} #f
(point-x p1)                            @result{} 1
(point-y p1)                            @result{} 2
(point-x p2)                            @result{} 3
(point-y p2)                            @result{} 4
(cpoint-rgb p2)                         @result{} (rgb . red)

(set-point-y! p1 17)                    @result{} unspecified
(point-y p1)                            @result{} 17

(record-rtd p1)
@result{} (record-type-descriptor point)

(define-record-type (ex1 make-ex1 ex1?)
  (protocol (lambda (p) (lambda a (p a))))
  (fields (immutable f ex1-f)))

(define ex1-i1 (make-ex1 1 2 3))
(ex1-f ex1-i1)                          @result{} (1 2 3)

(define-record-type (ex2 make-ex2 ex2?)
  (protocol
    (lambda (p) (lambda (a . b) (p a b))))
  (fields (immutable a ex2-a)
          (immutable b ex2-b)))

(define ex2-i1 (make-ex2 1 2 3))
(ex2-a ex2-i1)                          @result{} 1
(ex2-b ex2-i1)                          @result{} (2 3)

(define-record-type (unit-vector
                     make-unit-vector
                     unit-vector?)
  (protocol
   (lambda (p)
     (lambda (x y z)
       (let ((length
               (sqrt (+ (* x x)
                        (* y y)
                        (* z z)))))
         (p (/ x length)
            (/ y length)
            (/ z length))))))
  (fields (immutable x unit-vector-x)
          (immutable y unit-vector-y)
          (immutable z unit-vector-z)))

(define *ex3-instance* #f)

(define-record-type ex3
  (parent cpoint)
  (protocol
   (lambda (n)
     (lambda (x y t)
       (let ((r ((n x y 'red) t)))
         (set! *ex3-instance* r)
         r))))
  (fields
   (mutable thickness))
  (sealed #t) (opaque #t))

(define ex3-i1 (make-ex3 1 2 17))
(ex3? ex3-i1)                           @result{} #t
(cpoint-rgb ex3-i1) @result{} (rgb . red)
(ex3-thickness ex3-i1)                  @result{} 17
(ex3-thickness-set! ex3-i1 18)          @result{} unspecified
(ex3-thickness ex3-i1)                  @result{} 18
*ex3-instance*                          @result{} ex3-i1

(record? ex3-i1)                        @result{} #f
@end example


@c page
@node stdlib records procedural layer
@subsection Procedural layer


The procedural layer is provided by the @rsixlibrary{records procedural}
library.


@deffn Procedure make-record-type-descriptor @var{name} @var{parent} @var{uid} @var{sealed?} @var{opaque?} @var{fields}
Return a @emph{record-type descriptor}, or @emph{rtd}, representing a
record type distinct from all built--in types and other record types.

The @var{name} argument must be a symbol.  It names the record type, and
is intended purely for informational purposes and may be used for
printing by the underlying Scheme system.

The @var{parent} argument must be either @false{} or an rtd. If it is an
rtd, the returned record type, @var{t}, extends the record type @var{p}
represented by @var{parent}.  An exception with condition type
@code{&assertion} is raised if @var{parent} is sealed (see below).

The @var{uid} argument must be either @false{} or a symbol.  If
@var{uid} is a symbol, the record--creation operation is
@emph{nongenerative} i.e., a new record type is created only if no
previous call to @func{make-record-type-descriptor} was made with the
@var{uid}.  If @var{uid} is @false{}, the record--creation operation is
@emph{generative}, i.e., a new record type is created even if a previous
call to @func{make-record-type-descriptor} was made with the same
arguments.

If @func{make-record-type-descriptor} is called twice with the same
@var{uid} symbol, the parent arguments in the two calls must be
@func{eqv?}, the @var{fields} arguments @func{equal?}, the @var{sealed?}
arguments boolean--equivalent (both @false{} or both true), and the
@var{opaque?} arguments boolean--equivalent.  If these conditions are
not met, an exception with condition type @code{&assertion} is raised
when the second call occurs.  If they are met, the second call returns,
without creating a new record type, the same record--type descriptor (in
the sense of @func{eqv?}) as the first call.

@quotation
@emph{Note:} Users are encouraged to use symbol names constructed using
the @acronym{UUID} namespace (for example, using the record--type name
as a prefix) for the uid argument.
@end quotation

The @var{sealed?} flag must be a boolean.  If true, the returned record
type is sealed, i.e., it cannot be extended.

The @var{opaque?} flag must be a boolean.  If true, the record type is
opaque.  If passed an instance of the record type, @func{record?}
returns @false{}.  Moreover, if @code{record-rtd} (see ``Inspection''
below) is called with an instance of the record type, an exception with
condition type @code{&assertion} is raised.  The record type is also
opaque if an opaque parent is supplied.  If @var{opaque?} is @false{}
and an opaque parent is not supplied, the record is not opaque.

The @var{fields} argument must be a vector of field specifiers. Each
field specifier must be a list of the form @code{(mutable @var{name})}
or a list of the form @code{(immutable @var{name})}.  Each name must be
a symbol and names the corresponding field of the record type; the names
need not be distinct.  A field identified as mutable may be modified,
whereas, when a program attempts to obtain a mutator for a field
identified as immutable, an exception with condition type
@code{&assertion} is raised.  Where field order is relevant, e.g., for
record construction and field access, the fields are considered to be
ordered as specified, although no particular order is required for the
actual representation of a record instance.

The specified fields are added to the parent fields, if any, to
determine the complete set of fields of the returned record type.  If
@var{fields} is modified after @func{make-record-type-descriptor} has
been called, the effect on the returned rtd is unspecified.

A generative record--type descriptor created by a call to
@func{make-record-type-descriptor} is not @func{eqv?} to any
record--type descriptor (generative or nongenerative) created by another
call to @func{make-record-type-descriptor}.  A generative record--type
descriptor is @func{eqv?}  only to itself, i.e., @code{(eqv? @vari{rtd}
@varii{rtd})} if, and only if, @code{(eq? @vari{rtd} @varii{rtd})}.
Also, two nongenerative record--type descriptors are @func{eqv?} if, and
only if, they were created by calls to
@func{make-record-type-descriptor} with the same uid arguments.
@end deffn


@deffn Procedure record-type-descriptor? @var{obj}
Return @true{} if the argument is a record--type descriptor, @false{}
otherwise.
@end deffn


@deffn Procedure make-record-constructor-descriptor @var{rtd} @var{parent-constructor-descriptor} @var{protocol}
Return a @emph{record-constructor descriptor} (or @emph{constructor
descriptor} for short) that specifies a @emph{record constructor} (or
@emph{constructor} for short), that can be used to construct record
values of the type specified by @var{rtd}, and which can be obtained via
@func{record-constructor}.  A constructor descriptor can also be used to
create other constructor descriptors for subtypes of its own record
type.  @var{rtd} must be a record--type descriptor.  @var{protocol} must
be a procedure or @false{}.  If it is @false{}, a default @var{protocol}
procedure is supplied.

If @var{protocol} is a procedure, it is handled analogously to the
protocol expression in a @func{define-record-type} form.

If @var{rtd} is a base record type @var{parent-constructor-descriptor}
must be @false{}.  In this case, @var{protocol} is called by
@func{record-constructor} with a single argument @var{p}.  @var{p} is a
procedure that expects one argument for every field of @var{rtd} and
returns a record with the fields of @var{rtd} initialized to these
arguments.  The procedure returned by @var{protocol} should call @var{p}
once with the number of arguments @var{p} expects and return the
resulting record as shown in the simple example below:

@example
(lambda (p)
  (lambda (v1 v2 v3)
    (p v1 v2 v3)))
@end example

Here, the call to @code{p} returns a record whose fields are initialized
with the values of @code{v1}, @code{v2}, and @code{v3}.  The expression
above is equivalent to @code{(lambda (p) p)}.  Note that the procedure
returned by @var{protocol} is otherwise unconstrained; specifically, it
can take any number of arguments.

If @var{rtd} is an extension of another record type @var{parent-rtd} and
@var{protocol} is a procedure, @var{parent-constructor-descriptor} must
be a constructor descriptor of @var{parent-rtd} or @false{}.  If
@var{parent-constructor-descriptor} is a constructor descriptor,
@var{protocol} it is called by @code{record-constructor} with a single
argument @var{n}, which is a procedure that accepts the same number of
arguments as the constructor of @var{parent-constructor-descriptor} and
returns a procedure @var{p} that, when called, constructs the record
itself.  The @var{p} procedure expects one argument for every field of
@var{rtd} (not including parent fields) and returns a record with the
fields of @var{rtd} initialized to these arguments, and the fields of
@var{parent-rtd} and its parents initialized as specified by
@var{parent-constructor-descriptor}.

The procedure returned by @var{protocol} should call @var{n} once with
the number of arguments @var{n} expects, call the procedure @var{p} it
returns once with the number of arguments @var{p} expects and return the
resulting record.  A simple @var{protocol} in this case might be written
as follows:

@example
(lambda (n)
  (lambda (v1 v2 v3 x1 x2 x3 x4)
    (let ((p (n v1 v2 v3)))
      (p x1 x2 x3 x4))))
@end example

This passes arguments @var{v1}, @var{v2}, @var{v3} to @var{n} for
@var{parent-constructor-descriptor} and calls @func{p} with @var{x1},
@dots{}, @var{x4} to initialize the fields of @var{rtd} itself.

Thus, the constructor descriptors for a record type form a sequence of
protocols parallel to the sequence of record--type parents.  Each
constructor descriptor in the chain determines the field values for the
associated record type.  Child record constructors need not know the
number or contents of parent fields, only the number of arguments
accepted by the parent constructor.

@var{protocol} may be @false{}, specifying a default constructor that
accepts one argument for each field of @var{rtd} (including the fields
of its parent type, if any).  Specifically, if @var{rtd} is a base type,
the default @var{protocol} procedure behaves as if it were @code{(lambda
(p) p)}.  If @var{rtd} is an extension of another type, then
@var{parent-constructor-descriptor} must be either @false{} or itself
specify a default constructor, and the default @var{protocol} procedure
behaves as if it were:

@example
(lambda (n)
  (lambda (@vari{v} @dots{} @varj{v} @vari{x} @dots{} @vark{x})
    (let ((p (n @vari{v} @dots{} @varj{v})))
      (p @vari{x} @dots{} @vark{x}))))
@end example

The resulting constructor accepts one argument for each of the record
type's complete set of fields (including those of the parent record
type, the parent's parent record type, etc.) and returns a record with
the fields initialized to those arguments, with the field values for the
parent coming before those of the extension in the argument list.  (In
the example, @emph{j} is the complete number of fields of the parent
type, and $k$ is the number of fields of @var{rtd} itself.)

If @var{rtd} is an extension of another record type and
@var{parent-constructor-descriptor} is @false{},
@var{parent-constructor-descriptor} is treated as if it were a
constructor descriptor for the parent rtd of @var{rtd} with a default
protocol.

@strong{Implementation responsibilities:} If @var{protocol} is a
procedure, the implementation must check the restrictions on it to the
extent performed by applying it as described when the constructor is
called.  An implementation may check whether @var{protocol} is an
appropriate argument before applying it.

@example
(define rtd1
  (make-record-type-descriptor
   'rtd1 #f #f #f #f
   '#((immutable x1) (immutable x2))))

(define rtd2
  (make-record-type-descriptor
   'rtd2 rtd1 #f #f #f
   '#((immutable x3) (immutable x4))))

(define rtd3
  (make-record-type-descriptor
   'rtd3 rtd2 #f #f #f
   '#((immutable x5) (immutable x6))))

(define protocol1
  (lambda (p)
    (lambda (a b c)
      (p (+ a b) (+ b c)))))

(define protocol2
  (lambda (n)
    (lambda (a b c d e f)
      (let ((p (n a b c)))
        (p (+ d e) (+ e f))))))

(define protocol3
  (lambda (n)
    (lambda (a b c d e f g h i)
      (let ((p (n a b c d e f)))
        (p (+ g h) (+ h i))))))

(define cd1
  (make-record-constructor-descriptor
    rtd1 #f protocol1))

(define cd2
  (make-record-constructor-descriptor
    rtd2 cd1 protocol2))

(define cd3
  (make-record-constructor-descriptor
    rtd3 cd2 protocol3))

(define make-rtd1 (record-constructor cd1))

(define make-rtd2 (record-constructor cd2))

(define make-rtd3 (record-constructor cd3))

(make-rtd3 1 2 3 4 5 6 7 8 9)
=> <record with fields initialized to 3, 5, 9, 11, 15, 17>
@end example
@end deffn


@deffn Procedure record-constructor @var{constructor-descriptor}
Call the @var{protocol} of @var{constructor-descriptor} (as described
for @func{make-record-constructor-descriptor}) and returns the resulting
constructor @var{constructor} for records of the record type associated
with @var{constructor-descriptor}.
@end deffn


@deffn Procedure record-predicate @var{rtd}
Return a procedure that, given an object @var{obj}, returns @true{} if
@var{obj} is a record of the type represented by @var{rtd}, and @false{}
otherwise.
@end deffn


@deffn Procedure record-accessor @var{rtd} @var{k}
@var{k} must be a valid field index of @var{rtd}.  The
@func{record-accessor} procedure returns a one--argument procedure whose
argument must be a record of the type represented by @var{rtd}.  This
procedure returns the value of the selected field of that record.

The field selected corresponds to the @var{k}th element (0--based) of
the @var{fields} argument to the invocation of
@func{make-record-type-descriptor} that created @var{rtd}.  Note that
@var{k} cannot be used to specify a field of any type @var{rtd} extends.
@end deffn


@deffn Procedure record-mutator @var{rtd} @var{k}
@var{k} must be a valid field index of @var{rtd}.  The
@func{record-mutator} procedure returns a two--argument procedure whose
arguments must be a record record @var{r} of the type represented by
@var{rtd} and an object @var{obj}.  This procedure stores @var{obj}
within the field of @var{r} specified by @var{k}.  The @var{k} argument
is as in @func{record-accessor}.  If @var{k} specifies an immutable
field, an exception with condition type @code{&assertion} is raised.
The mutator returns unspecified values.
@end deffn


@example
(define :point
  (make-record-type-descriptor
    'point #f
    #f #f #f
    '#((mutable x) (mutable y))))

(define :point-cd
  (make-record-constructor-descriptor :point #f #f))

(define make-point (record-constructor :point-cd))

(define point? (record-predicate :point))
(define point-x (record-accessor :point 0))
(define point-y (record-accessor :point 1))
(define point-x-set! (record-mutator :point 0))
(define point-y-set! (record-mutator :point 1))

(define p1 (make-point 1 2))
(point? p1)                             @result{} #t
(point-x p1)                            @result{} 1
(point-y p1)                            @result{} 2
(point-x-set! p1 5)                     @result{} unspecified
(point-x p1)                            @result{} 5

(define :point2
  (make-record-type-descriptor
    'point2 :point
    #f #f # f '#((mutable x) (mutable y))))

(define make-point2
  (record-constructor
    (make-record-constructor-descriptor :point2
      #f #f)))
(define point2? (record-predicate :point2))
(define point2-xx (record-accessor :point2 0))
(define point2-yy (record-accessor :point2 1))

(define p2 (make-point2 1 2 3 4))
(point? p2)                             @result{} #t
(point-x p2)                            @result{} 1
(point-y p2)                            @result{} 2
(point2-xx p2)                          @result{} 3
(point2-yy p2)                          @result{} 4

(define :point-cd/abs
  (make-record-constructor-descriptor
   :point #f
   (lambda (new)
     (lambda (x y)
       (new (abs x) (abs y))))))

(define make-point/abs
  (record-constructor :point-cd/abs))

(point-x (make-point/abs -1 -2))        @result{} 1
(point-y (make-point/abs -1 -2))        @result{} 2

(define :cpoint
  (make-record-type-descriptor
   'cpoint :point
   #f #f #f
   '#((mutable rgb))))

(define make-cpoint
  (record-constructor
   (make-record-constructor-descriptor
    :cpoint :point-cd
    (lambda (p)
      (lambda (x y c)
	((p x y) (color->rgb c)))))))

(define make-cpoint/abs
  (record-constructor
   (make-record-constructor-descriptor
    :cpoint :point-cd/abs
    (lambda (p)
      (lambda (x y c)
	((p x y) (color->rgb c)))))))

(define cpoint-rgb
  (record-accessor :cpoint 0))

(define (color->rgb c)
  (cons 'rgb c))

(cpoint-rgb (make-cpoint -1 -3 'red))   @result{} (rgb . red)
(point-x (make-cpoint -1 -3 'red))      @result{} -1
(point-x (make-cpoint/abs -1 -3 'red))  @result{} 1
@end example


@c page
@node stdlib records inspection
@subsection Inspection


The @rsixlibrary{records inspection} library provides procedures for
inspecting records and their record--type descriptors.  These procedures
are designed to allow the writing of portable printers and inspectors.

On the one hand, @func{record?} and @func{record-rtd} treat records of
opaque record types as if they were not records.  On the other hand, the
inspection procedures that operate on record--type descriptors
themselves are not affected by opacity.  In other words, opacity
controls whether a program can obtain an rtd from a record.  If the
program has access to the original rtd via
@func{make-record-type-descriptor} or @func{record-type-descriptor}, it
can still make use of the inspection procedures.


@deffn Procedure record? @var{obj}
Return @true{} if @var{obj} is a record, and its record type is not
opaque, and return @false{} otherwise.
@end deffn


@deffn Procedure record-rtd @var{record}
Return the rtd representing the type of @var{record} if the type is not
opaque.  The rtd of the most precise type is returned; that is, the type
@var{t} such that @var{record} is of type @var{t} but not of any type
that extends @var{t}.  If the type is opaque, an exception is raised
with condition type @code{&assertion}.
@end deffn


@deffn Procedure record-type-name @var{rtd}
Return the name of the record--type descriptor @var{rtd}.
@end deffn


@deffn Procedure record-type-parent @var{rtd}
Return the parent of the record--type descriptor @var{rtd}, or @false{}
if it has none.
@end deffn


@deffn Procedure record-type-uid @var{rtd}
Return the uid of the record--type descriptor rtd, or @false{} if it has
none.  (An implementation may assign a generated uid to a record type
even if the type is generative, so the return of a uid does not
necessarily imply that the type is nongenerative.)
@end deffn


@deffn Procedure record-type-generative? @var{rtd}
Return @true{} if @var{rtd} is generative, and @false{} if not.
@end deffn


@deffn Procedure record-type-sealed? @var{rtd}
Return @true{} if the record-type descriptor is sealed, and @false{} if
not.
@end deffn


@deffn Procedure record-type-opaque? @var{rtd}
Return @true{} if the the record-type descriptor is opaque, and @false{}
if not.
@end deffn


@deffn Procedure record-type-field-names @var{rtd}
Return a vector of symbols naming the fields of the type represented by
@var{rtd} (not including the fields of parent types) where the fields
are ordered as described under @func{make-record-type-descriptor}.  The
returned vector may be immutable.  If the returned vector is modified,
the effect on @var{rtd} is unspecified.
@end deffn


@deffn Procedure record-field-mutable? @var{rtd} @var{k}
Returns @true{} if the field specified by @var{k} of the type
represented by @var{rtd} is mutable, and @false{} if not.  @var{k} is as
in @func{record-accessor}.
@end deffn

@c page
@node stdlib exceptions
@section Exceptions and conditions


Scheme allows programs to deal with exceptional situations using two
cooperating facilities: The exception system for raising and handling
exceptional situations, and the condition system for describing these
situations.

The exception system allows the program, when it detects an exceptional
situation, to pass control to an exception handler, and to dynamically
establish such exception handlers.  Exception handlers are always
invoked with an object describing the exceptional situation.  Scheme's
condition system provides a standardized taxonomy of such descriptive
objects, as well as a facility for extending the taxonomy.


@menu
* stdlib exceptions exceptions::        Exceptions.
* stdlib exceptions conditions::        Conditions.
@end menu



@c page
@node stdlib exceptions exceptions
@subsection Exceptions


@cindex Catching exceptions


This section describes Scheme's exception--handling and
exception--raising constructs provided by the @rsixlibrary{exceptions}
library.

Exception handlers are one--argument procedures that determine the
action the program takes when an exceptional situation is signalled.
The system implicitly maintains a current exception handler.

The program raises an exception by invoking the current exception
handler, passing it an object encapsulating information about the
exception.  Any procedure accepting one argument may serve as an
exception handler and any object may be used to represent an exception.

The system maintains the current exception handler as part of the
dynamic environment of the program; @ref{lang basic dynamic extent}.

When a program begins its execution, the current exception handler is
expected to handle all @code{&serious} conditions by interrupting
execution, reporting that an exception has been raised, and displaying
information about the condition object that was provided.  The handler
may then exit, or may provide a choice of other options.  Moreover, the
exception handler is expected to return when passed any other
non--@code{&serious} condition.  Interpretation of these expectations
necessarily depends upon the nature of the system in which programs are
executed, but the intent is that users perceive the raising of an
exception as a controlled escape from the situation that raised the
exception, not as a crash.


@deffn Procedure with-exception-handler @var{handler} @var{thunk}
@var{handler} must be a procedure and should accept one argument.
@var{thunk} must be a procedure that accepts zero arguments.  The
@func{with-exception-handler} procedure returns the results of invoking
@var{thunk}.  @var{handler} is installed as the current exception
handler for the dynamic extent (as determined by @func{dynamic-wind}) of
the invocation of @var{thunk}.

@strong{Implementation responsibilities:} The implementation must check
the restrictions on @var{handler} to the extent performed by applying it
as described when it is called as a result of a call to @func{raise} or
@func{raise-continuable}.  An implementation may check whether
@var{handler} is an appropriate argument before applying it.
@end deffn


@deffn Syntax guard (@meta{variable} @hyperi{cond clause} @hyperii{cond clause} @dots{}) @meta{body}
@deffnx {Auxiliary Syntax} {=>}
@deffnx {Auxiliary Syntax} else
Each @meta{cond clause} is as in the specification of @func{cond}.  (See
report section @ref{baselib expressions derived cond}.)  @code{=>} and
@code{else} are the same as in the @rsixlibrary{base} library.

Evaluating a @func{guard} form evaluates @meta{body} with an exception
handler that binds the raised object to @meta{variable} and within the
scope of that binding evaluates the clauses as if they were the clauses
of a @func{cond} expression.  That implicit @func{cond} expression is
evaluated with the continuation and dynamic environment of the
@func{guard} form.  If every @meta{cond clause}'s @hyper{test} evaluates
to @false{} and there is no @code{else} clause, then
@func{raise-continuable} is re--invoked on the raised object within the
dynamic environment of the original call to @func{raise} except that the
current exception handler is that of the @func{guard} expression.

The final expression in a @meta{cond} clause is in a tail context if the
@func{guard} expression itself is.
@end deffn


@deffn Procedure raise @var{obj}
Raise a non--continuable exception by invoking the current exception
handler on @var{obj}.  The handler is called with a continuation whose
dynamic environment is that of the call to @func{raise}, except that the
current exception handler is the one that was in place when the handler
being called was installed.  When the handler returns, a
non--continuable exception with condition type @code{&non-continuable}
is raised in the same dynamic environment as the handler.
@end deffn


@deffn Procedure raise-continuable @var{obj}
Raise a @emph{continuable exception} by invoking the current exception
handler on @var{obj}. The handler is called with a continuation that is
equivalent to the continuation of the call to @func{raise-continuable},
with these two exceptions:

@enumerate
@item
the current exception handler is the one that was in place when the
handler being called was installed, and

@item
if the handler being called returns, then it will again become the
current exception handler.
@end enumerate

If the handler returns, the values it returns become the values returned
by the call to @code{raise-continuable}.
@end deffn

@example
(guard (con
         ((error? con)
          (if (message-condition? con)
              (display (condition-message con))
              (display "an error has occurred"))
          'error)
         ((violation? con)
          (if (message-condition? con)
              (display (condition-message con))
              (display "the program has a bug"))
          'violation))
  (raise
    (condition
      (make-error)
      (make-message-condition "I am an error"))))
   ;; prints: I am an error
@result{} error

(guard (con
         ((error? con)
          (if (message-condition? con)
              (display (condition-message con))
              (display "an error has occurred"))
          'error))
  (raise
    (condition
      (make-violation)
      (make-message-condition "I am an error"))))
@result{} exception &violation

(guard (con
         ((error? con)
          (display "error opening file")
          #f))
  (call-with-input-file "foo.scm" read))
   ;; prints: error opening file
@result{} #f

(with-exception-handler
  (lambda (con)
    (cond
      ((not (warning? con))
       (raise con))
      ((message-condition? con)
       (display (condition-message con)))
      (else
       (display "a warning has been issued")))
    42)
  (lambda ()
    (+ (raise-continuable
         (condition
           (make-warning)
           (make-message-condition
             "should be a number")))
       23)))
   ;; prints: should be a number
@result{} 65
@end example


@c page
@node stdlib exceptions conditions
@subsection Conditions


This section describes Scheme's @rsixlibrary{conditions} library for
creating and inspecting condition types and values.  A condition value
encapsulates information about an exceptional situation.  Scheme also
defines a number of basic condition types.

Scheme conditions provides two mechanisms to enable communication about
an exceptional situation: subtyping among condition types allows
handling code to determine the general nature of an exception even
though it does not anticipate its exact nature, and compound conditions
allow an exceptional situation to be described in multiple ways.


@menu
* stdlib exceptions conditions objects::        Condition objects.
* stdlib exceptions conditions types::          Standard condition types.
@end menu


@c page
@node stdlib exceptions conditions objects
@subsubsection Condition objects


Conceptually, there are two different kinds of condition objects:
@emph{simple conditions} and @emph{compound conditions}.  An object that
is either a simple condition or a compound condition is simply a
@emph{condition}.  Compound conditions form a type disjoint from the
base types described in report section @ref{baselib types}.  A simple
condition describes a single aspect of an exceptional situation.  A
compound condition represents multiple aspects of an exceptional
situation as a list of simple conditions, its @emph{components}.  Most
of the operations described in this section treat a simple condition
identically to a compound condition with itself as its own sole
component.  For a subtype @var{t} of @code{&condition}, a
@emph{condition of type @var{t}} is either a record of type @var{t} or a
compound condition containing a component of type @var{t}.


@tindex &condition
@deffn {Condition Type} {&condition}
Simple conditions are records of subtypes of the @code{&condition}
record type.  The @code{&condition} type has no fields and is neither
sealed nor opaque.
@end deffn


@deffn Procedure condition @vari{condition} @dots{}
The @func{condition} procedure returns a condition object with the
components of the @var{condition}s as its components, in the same order,
i.e., with the components of @vari{condition} appearing first in the
same order as in @vari{condition}, then with the components of
@varii{condition}, and so on.  The returned condition is compound if the
total number of components is zero or greater than one.  Otherwise, it
may be compound or simple.
@end deffn


@deffn Procedure simple-conditions @var{condition}
The @func{simple-conditions} procedure returns a list of the components
of @var{condition}, in the same order as they appeared in the
construction of @var{condition}.  The returned list is immutable.  If
the returned list is modified, the effect on @var{condition} is
unspecified.

@quotation
@emph{Note:} Because @func{condition} decomposes its arguments into
simple conditions, @func{simple-conditions} always returns a
``flattened'' list of simple conditions.
@end quotation
@end deffn


@deffn Procedure {condition?} @var{obj}
Return @true{} if @var{obj} is a (simple or compound) condition,
otherwise returns @false{}.
@end deffn


@deffn Procedure condition-predicate @var{rtd}
@var{rtd} must be a record--type descriptor of a subtype of
@code{&condition}.  The @func{condition-predicate} procedure returns a
procedure that takes one argument.  This procedure returns @true{} if
its argument is a condition of the condition type represented by
@var{rtd}, i.e., if it is either a simple condition of that record type
(or one of its subtypes) or a compound conditition with such a simple
condition as one of its components, and @false{} otherwise.
@end deffn


@deffn Procedure condition-accessor @var{rtd} @var{proc}
@var{rtd} must be a record--type descriptor of a subtype of
@code{&condition}.  @var{proc} should accept one argument, a record of
the record type of @var{rtd}.  The @func{condition-accessor} procedure
returns a procedure that accepts a single argument, which must be a
condition of the type represented by @var{rtd}.  This procedure extracts
the first component of the condition of the type represented by
@var{rtd}, and returns the result of applying @var{proc} to that
component.
@end deffn

@example
(define-record-type (&cond1 make-cond1 real-cond1?)
  (parent &condition)
  (fields
   (immutable x real-cond1-x)))

(define cond1?
  (condition-predicate
    (record-type-descriptor &cond1)))
(define cond1-x
  (condition-accessor
    (record-type-descriptor &cond1)
    real-cond1-x))

(define foo (make-cond1 'foo))

(condition? foo)                        @result{} #t
(cond1? foo)                            @result{} #t
(cond1-x foo)                           @result{} foo

(define-record-type (&cond2 make-cond2 real-cond2?)
  (parent &condition)
  (fields
   (immutable y real-cond2-y)))

(define cond2?
  (condition-predicate
    (record-type-descriptor &cond2)))
(define cond2-y
  (condition-accessor
     (record-type-descriptor &cond2)
     real-cond2-y))

(define bar (make-cond2 'bar))

(condition? (condition foo bar))        @result{} #t
(cond1? (condition foo bar))            @result{} #t
(cond2? (condition foo bar))            @result{} #t
(cond1? (condition foo))                @result{} #t
(real-cond1? (condition foo))           @result{} unspecified
(real-cond1? (condition foo bar))       @result{} #f
(cond1-x (condition foo bar))           @result{} foo
(cond2-y (condition foo bar))           @result{} bar

(equal? (simple-conditions (condition foo bar))
        (list foo bar))                 @result{} #t

(equal? (simple-conditions
          (condition foo (condition bar)))
        (list foo bar))                 @result{} #t
@end example


@deffn Syntax define-condition-type @meta{condition-type} @meta{supertype} @meta{constructor} @hyper{predicate} @hyperi{field-spec} @dots{}
@meta{condition-type}, @hyper{supertypes}, @meta{constructor}, and
@hyper{predicate} must all be identifiers.  Each @meta{field-spec} must
be of the form

@example
(@meta{field} @hyper{accessor})
@end example

@noindent
where both @meta{field} and @hyper{accessor} must be identifiers.

The @func{define-condition-type} form expands into a record--type
definition for a record type @meta{condition-type}.  The record type
will be non--opaque, non--sealed, and its fields will be immutable.  It
will have @meta{supertype} has its parent type.  The remaining
identifiers will be bound as follows:

@itemize
@item
@meta{constructor} is bound to a default constructor for the type: It
accepts one argument for each of the record type's complete set of
fields (including parent types, with the fields of the parent coming
before those of the extension in the arguments) and returns a condition
object initialized to those arguments.

@item
@meta{predicate} is bound to a predicate that identifies conditions of
type @meta{condition-type} or any of its subtypes.

@item
Each @meta{accessor} is bound to a procedure that extracts the
corresponding field from a condition of type @meta{condition-type}.
@end itemize
@end deffn

@example
(define-condition-type &c &condition
  make-c c?
  (x c-x))

(define-condition-type &c1 &c
  make-c1 c1?
  (a c1-a))

(define-condition-type &c2 &c
  make-c2 c2?
  (b c2-b))
@end example

@example
(define v1 (make-c1 "V1" "a1"))

(c? v1)        @result{} #t
(c1? v1)       @result{} #t
(c2? v1)       @result{} #f
(c-x v1)       @result{} "V1"
(c1-a v1)      @result{} "a1"
@end example

@example
(define v2 (make-c2 "V2" "b2"))

(c? v2)        @result{} #t
(c1? v2)       @result{} #f
(c2? v2)       @result{} #t
(c-x v2)       @result{} "V2"
(c2-b v2)      @result{} "b2"
@end example

@example
(define v3 (condition
             (make-c1 "V3/1" "a3")
             (make-c2 "V3/2" "b3")))

(c? v3)        @result{} #t
(c1? v3)       @result{} #t
(c2? v3)       @result{} #t
(c-x v3)       @result{} "V3/1"
(c1-a v3)      @result{} "a3"
(c2-b v3)      @result{} "b3"
@end example

@example
(define v4 (condition v1 v2))

(c? v4)        @result{} #t
(c1? v4)       @result{} #t
(c2? v4)       @result{} #t
(c-x v4)       @result{} "V1"
(c1-a v4)      @result{} "a1"
(c2-b v4)      @result{} "b2"
@end example

@example
(define v5 (condition v2 v3))

(c? v5)        @result{} #t
(c1? v5)       @result{} #t
(c2? v5)       @result{} #t
(c-x v5)       @result{} "V2"
(c1-a v5)      @result{} "a3"
(c2-b v5)      @result{} "b2"
@end example


@c page
@node stdlib exceptions conditions types
@subsubsection Standard condition types


Hierarchy of standard condition types:

@example
&condition
   |
   +-----> &warning
   |
   +-----> &message
   |       &irritants
   |       &who
   |
   +-----> &serious
              |
              +-----> &error
              |
              +-----> &violation
                         |
                         +-----> &assertion
                         |
                         +-----> &non-continuable
                         |
                         +-----> &implementation-restriction
                         |
                         +-----> &lexical
                         |
                         +-----> &syntax
                         |
                          -----> &undefined
@end example


@tindex &message
@deffn {Condition Type} {&message}
@deffnx Procedure make-message-condition @var{message}
@deffnx Procedure message-condition? @var{obj}
@deffnx Procedure condition-message @var{condition}
This condition type could be defined by:

@example
(define-condition-type &message &condition
  make-message-condition message-condition?
  (message condition-message))
@end example

It carries a message further describing the nature of the condition to
humans.
@end deffn


@tindex &warning
@deffn {Condition Type} {&warning}
@deffnx Procedure make-warning
@deffnx Procedure warning? @var{obj}
This condition type could be defined by:

@example
(define-condition-type &warning &condition
  make-warning warning?)
@end example

This type describes conditions that do not, in principle, prohibit
immediate continued execution of the program, but may interfere with the
program's execution later.
@end deffn


@tindex &serious
@deffn {Condition Type} {&serious}
@deffnx Procedure make-serious-condition
@deffnx Procedure serious-condition? @var{obj}
This condition type could be defined by:

@example
(define-condition-type &serious &condition
  make-serious-condition serious-condition?)
@end example

This type describes conditions serious enough that they cannot safely be
ignored.  This condition type is primarily intended as a supertype of
other condition types.
@end deffn


@tindex &error
@deffn {Condition Type} {&error}
@deffnx Procedure make-error
@deffnx Procedure error? @var{obj}
This condition type could be defined by:

@example
(define-condition-type &error &serious
  make-error error?)
@end example

This type describes errors, typically caused by something that has gone
wrong in the interaction of the program with the external world or the
user.
@end deffn


@tindex &violation
@deffn {Condition Type} {&violation}
@deffnx Procedure make-violation
@deffnx Procedure violation? @var{obj}
This condition type could be defined by:

@example
(define-condition-type &violation &serious
  make-violation violation?)
@end example

This type describes violations of the language standard or a library
standard, typically caused by a programming error.
@end deffn


@tindex &assertion
@deffn {Condition Type} {&assertion}
@deffnx Procedure make-assertion-violation
@deffnx Procedure assertion-violation? @var{obj}
This condition type could be defined by:

@example
(define-condition-type &assertion &violation
  make-assertion-violation assertion-violation?)
@end example

This type describes an invalid call to a procedure, either passing an
invalid number of arguments, or passing an argument of the wrong type.
@end deffn


@tindex &irritants
@deffn {Condition Type} {&irritants}
@deffnx Procedure make-irritants-condition @var{irritants}
@deffnx Procedure irritants-condition? @var{obj}
@deffnx Procedure condition-irritants @var{condition}
This condition type could be defined by:

@example
(define-condition-type &irritants &condition
  make-irritants-condition irritants-condition?
  (irritants condition-irritants))
@end example

@var{irritants} should be a list of objects.  This condition provides
additional information about a condition, typically the argument list of
a procedure that detected an exception.  Conditions of this type are
created by the @func{error} and @func{assertion-violation} procedures of
report section @ref{baselib errors}.
@end deffn


@tindex &who
@deffn {Condition Type} {&who}
@deffnx Procedure make-who-condition @var{who}
@deffnx Procedure who-condition? @var{obj}
@deffnx Procedure condition-who @var{condition}
This condition type could be defined by:

@example
(define-condition-type &who &condition
  make-who-condition who-condition?
  (who condition-who))
@end example

@var{who} should be a symbol or string identifying the entity reporting
the exception.  Conditions of this type are created by the @func{error}
and @func{assertion-violation} procedures (report section @ref{baselib
errors}), and the @func{syntax-violation} procedure (section @ref{lang
basic syntax violations}).
@end deffn


@tindex &non-continuable
@deffn {Condition Type} {&non-continuable}
@deffnx Procedure make-non-continuable-violation
@deffnx Procedure non-continuable-violation? @var{obj}
This condition type could be defined by:

@example
(define-condition-type &non-continuable &violation
  make-non-continuable-violation
  non-continuable-violation?)
@end example

This type indicates that an exception handler invoked via @func{raise}
has returned.
@end deffn


@tindex &implementation-restriction
@deffn {Condition Type} {&implementation-restriction}
@deffnx Procedure make-implementation-restriction-violation
@deffnx Procedure implementation-restriction-violation? @var{obj}
This condition type could be defined by:

@example
(define-condition-type &implementation-restriction
    &violation
  make-implementation-restriction-violation
  implementation-restriction-violation?)
@end example

This type describes a violation of an implementation restriction allowed
by the specification, such as the absence of representations for NaNs
and infinities.
@end deffn


@tindex &lexical
@deffn {Condition Type} {&lexical}
@deffnx Procedure make-lexical-violation
@deffnx Procedure lexical-violation? @var{obj}
This condition type could be defined by:

@example
(define-condition-type &lexical &violation
  make-lexical-violation lexical-violation?)
@end example

This type describes syntax violations at the level of the datum syntax.
@end deffn


@tindex &syntax
@deffn {Condition Type} {&syntax}
@deffnx Procedure make-syntax-violation @var{form} @var{subform}
@deffnx Procedure syntax-violation? @var{obj}
@deffnx Procedure syntax-violation-form @var{condition}
@deffnx Procedure syntax-violation-subform @var{condition}
This condition type could be defined by:

@example
(define-condition-type &syntax &violation
  make-syntax-violation syntax-violation?
  (form syntax-violation-form)
  (subform syntax-violation-subform))
@end example

This type describes syntax violations.  @var{form} should be the
erroneous syntax object or a datum representing the code of the
erroneous form.  @var{subform} should be an optional syntax object or
datum within the erroneous form that more precisely locates the
violation.  It can be @false{} to indicate the absence of more precise
information.
@end deffn


@tindex &undefined
@deffn {Condition Type} {&undefined}
@deffnx Procedure make-undefined-violation
@deffnx Procedure undefined-violation? @var{obj}
This condition type could be defined by:

@example
(define-condition-type &undefined &violation
  make-undefined-violation undefined-violation?)
@end example

This type describes unbound identifiers in the program.
@end deffn

@c page
@node stdlib io
@section Input/output


This chapter describes Scheme's libraries for performing input and
output:

@table @rsixlibrary
@item io ports
it is an I/O layer for conventional, imperative buffered input and
output with text and binary data;

@item io simple
it is a convenience library atop the @rsixlibrary{io ports} library for
textual I/O, compatible with the traditional Scheme I/O procedures.
@end table

The section on conditions defines a condition--type hierarchy that is
exported by both the @rsixlibrary{io ports} and @rsixlibrary{io simple}
libraries.


@menu
* stdlib io conditions::        Condition types.
* stdlib io port::              Port input/output.
* stdlib io simple::            Simple input/output.
@end menu


@c page
@node stdlib io conditions
@subsection Condition types


The procedures described in this chapter, when they detect an
exceptional situation that arises from an ``I/O errors'', raise an
exception with condition type @code{&i/o}.

The condition types and corresponding predicates and accessors are
exported by both the @rsixlibrary{io ports} and @rsixlibrary{io simple}
libraries.  They are also exported by the @rsixlibrary{files} library.


@tindex &i/o
@deffn {Condition Type} {&i/o}
@deffnx Procedure {make-i/o-error}
@deffnx Procedure {i/o-error?} @var{obj}
This condition type could be defined by:

@example
(define-condition-type &i/o &error
  make-i/o-error i/o-error?)
@end example

This is a supertype for a set of more specific I/O errors.
@end deffn


@tindex &i/o-read
@deffn {Condition Type} {&i/o-read}
@deffnx Procedure {make-i/o-read-error}
@deffnx Procedure {i/o-read-error?} @var{obj}
This condition type could be defined by:

@example
(define-condition-type &i/o-read &i/o
  make-i/o-read-error i/o-read-error?)
@end example

This condition type describes read errors that occurred during an I/O
operation.
@end deffn


@tindex &i/o-write
@deffn {Condition Type} {&i/o-write}
@deffnx Procedure {make-i/o-write-error}
@deffnx Procedure {i/o-write-error?} @var{obj}
This condition type could be defined by:

@example
(define-condition-type &i/o-write &i/o
  make-i/o-write-error i/o-write-error?)
@end example

This condition type describes write errors that occurred during an I/O
operation.
@end deffn


@tindex &i/o-invalid-position
@deffn {Condition Type} {&i/o-invalid-position}
@deffnx Procedure {make-i/o-invalid-position-error} @var{position}
@deffnx Procedure {i/o-invalid-position-error?} @var{obj}
@deffnx Procedure {i/o-error-position} @var{condition}
This condition type could be defined by:

@example
(define-condition-type &i/o-invalid-position &i/o
  make-i/o-invalid-position-error
  i/o-invalid-position-error?
  (position i/o-error-position))
@end example

This condition type describes attempts to set the file position to an
invalid position.  @var{position} should be the file position that the
program intended to set.  This condition describes a range error, but
not an assertion violation.
@end deffn


@tindex &i/o-filename
@deffn {Condition Type} {&i/o-filename}
@deffnx Procedure {make-i/o-filename-error} @var{filename}
@deffnx Procedure {i/o-filename-error?} @var{obj}
@deffnx Procedure {i/o-error-filename} @var{condition}
This condition type could be defined by:

@example
(define-condition-type &i/o-filename &i/o
  make-i/o-filename-error i/o-filename-error?
  (filename i/o-error-filename))
@end example

This condition type describes an I/O error that occurred during an
operation on a named file.  @var{filename} should be the name of the
file.
@end deffn


@tindex &i/o-file-protection
@deffn {Condition Type} {&i/o-file-protection}
@deffnx Procedure {make-i/o-file-protection-error} @var{filename}
@deffnx Procedure {i/o-file-protection-error?} @var{obj}
This condition type could be defined by:

@example
(define-condition-type &i/o-file-protection
    &i/o-filename
  make-i/o-file-protection-error
  i/o-file-protection-error?)
@end example

A condition of this type specifies that an operation tried to operate on
a named file with insufficient access rights.
@end deffn


@tindex &i/o-file-is-read-only
@deffn {Condition Type} {&i/o-file-is-read-only}
@deffnx Procedure {make-i/o-file-is-read-only-error} @var{filename}
@deffnx Procedure {i/o-file-is-read-only-error?} @var{obj}
This condition type could be defined by:

@example
(define-condition-type &i/o-file-is-read-only
    &i/o-file-protection
  make-i/o-file-is-read-only-error
  i/o-file-is-read-only-error?)
@end example

A condition of this type specifies that an operation tried to operate on
a named read--only file under the assumption that it is writeable.
@end deffn


@tindex &i/o-file-already-exists
@deffn {Condition Type} {&i/o-file-already-exists}
@deffnx Procedure {make-i/o-file-already-exists-error} @var{filename}
@deffnx Procedure {i/o-file-already-exists-error?} @var{obj}
This condition type could be defined by:

@example
(define-condition-type &i/o-file-already-exists
    &i/o-filename
  make-i/o-file-already-exists-error
  i/o-file-already-exists-error?)
@end example

A condition of this type specifies that an operation tried to operate on
an existing named file under the assumption that it did not exist.
@end deffn


@tindex &i/o-file-does-not-exist
@deffn {Condition Type} {&i/o-file-does-not-exist}
@deffnx Procedure {make-i/o-file-does-not-exist-error} @var{filename}
@deffnx Procedure {i/o-file-does-not-exist-error?} @var{obj}
This condition type could be defined by:

@example
(define-condition-type &i/o-file-does-not-exist
    &i/o-filename
  make-i/o-file-does-not-exist-error
  i/o-file-does-not-exist-error?)
@end example

A condition of this type specifies that an operation tried to operate on
an non--existent named file under the assumption that it existed.
@end deffn


@tindex &i/o-port
@deffn {Condition Type} {&i/o-port}
@deffnx Procedure {make-i/o-port-error} @var{port}
@deffnx Procedure {i/o-port-error?} @var{obj}
@deffnx Procedure {i/o-error-port} @var{condition}
This condition type could be defined by:

@example
(define-condition-type &i/o-port &i/o
  make-i/o-port-error i/o-port-error?
  (port i/o-error-port))
@end example

This condition type specifies the port with which an I/O error is
associated.  @var{port} should be the port.  Conditions raised by
procedures accepting a port as an argument should include an
@code{&i/o-port-error} condition.
@end deffn


@c page
@node stdlib io port
@subsection Port input/output


The @rsixlibrary{io ports} library defines an I/O layer for
conventional, imperative buffered input and output.  A @emph{port}
represents a buffered access object for a data sink or source or both
simultaneously.  The library allows ports to be created from arbitrary
data sources and sinks.

The @rsixlibrary{io ports} library distinguishes between @emph{input
ports} and @emph{output ports}.  An input port is a source for data,
whereas an output port is a sink for data.  A port may be both an input
port and an output port; such a port typically provides simultaneous
read and write access to a file or other data.

The @rsixlibrary{io ports} library also distinguishes between
@emph{binary ports}, which are sources or sinks for uninterpreted bytes,
and @emph{textual ports}, which are sources or sinks for characters and
strings.

This section uses @var{input-port}, @var{output-port},
@var{binary-port}, @var{textual-port}, @var{binary-input-port},
@var{textual-input-port}, @var{binary-output-port},
@var{textual-output-port}, and @var{port} as names for arguments that
must be input ports (or combined input/output ports), output ports (or
combined input/output ports), binary ports, textual ports, binary input
ports, textual input ports, binary output ports, textual output ports,
or any kind of port, respectively.


@menu
* stdlib io port file names::           File names.
* stdlib io port file options::         File options.
* stdlib io port buffer modes::         Buffer modes.
* stdlib io port transcoders::          Transcoders.
* stdlib io port eof object::           End of file object.
* stdlib io port io ports::             Input and output ports.
* stdlib io port input ports::          Input ports.
* stdlib io port binary input::         Binary input.
* stdlib io port textual input::        Textual input.
* stdlib io port output ports::         Output ports.
* stdlib io port binary output::        Binary output.
* stdlib io port textual output::       Textual output.
* stdlib io port input output ports::   Input/output ports.
@end menu

@c page
@node stdlib io port file names
@subsubsection File names


Some of the procedures described in this chapter accept a file name as
an argument.  Valid values for such a file name include strings that
name a file using the native notation of filesystem paths on an
implementation's underlying operating system, and may include
implementation--dependent values as well.

A @var{filename} parameter name means that the corresponding argument
must be a file name.


@c page
@node stdlib io port file options
@subsubsection File options


When opening a file, the various procedures in this library accept a
@code{file-options} object that encapsulates flags to specify how the
file is to be opened.  A @code{file-options} object is an enum--set over
the symbols constituting valid file options.  A @var{file-options}
parameter name means that the corresponding argument must be a
file--options object.


@deffn Syntax file-options @meta{file-options symbol} @dots{}
Each @meta{file-options symbol} must be a symbol.  The
@code{file-options} syntax returns a file--options object that
encapsulates the specified options.

When supplied to an operation that opens a file for output, the
file--options object returned by @code{(file-options)} (without
arguments) specifies that the file is created if it does not exist and
an exception with condition type @code{&i/o-file-already-exists} is
raised if it does exist.  The following standard options can be included
to modify the default behavior.

@table @code
@item no-create
If the file does not already exist, it is not created; instead, an
exception with condition type @code{&i/o-file-does-not-exist} is raised.
If the file already exists, the exception with condition type
@code{&i/o-file-already-exists} is not raised and the file is truncated
to zero length.

@item no-fail
If the file already exists, the exception with condition type
@code{&i/o-file-already-exists} is not raised, even if @code{no-create}
is not included, and the file is truncated to zero length.

@item no-truncate
If the file already exists and the exception with condition type
@code{&i/o-file-already-exists} has been inhibited by inclusion of
@code{no-create} or @code{no-fail}, the file is not truncated, but the
port's current position is still set to the beginning of the file.
@end table

These options have no effect when a file is opened only for input.
Symbols other than those listed above may be used as @meta{file-options
symbol}s; they have implementation--specific meaning, if any.

@quotation
@emph{Note:} Only the name of @meta{file-options symbol} is significant.
@end quotation
@end deffn


@c page
@node stdlib io port buffer modes
@subsubsection Buffer modes


Each port has an associated buffer mode.  For an output port, the buffer
mode defines when an output operation flushes the buffer associated with
the output port.  For an input port, the buffer mode defines how much
data will be read to satisfy read operations.  The possible buffer modes
are the symbols:

@table @code
@item none
for no buffering,

@item line
for flushing upon line endings and reading up to line endings, or other
implementation--dependent behavior,

@item block
for arbitrary buffering.
@end table

This section uses the parameter name @var{buffer-mode} for arguments
that must be buffer-mode symbols.

If two ports are connected to the same mutable source, both ports are
unbuffered, and reading a byte or character from that shared source via
one of the two ports would change the bytes or characters seen via the
other port, a lookahead operation on one port will render the peeked
byte or character inaccessible via the other port, while a subsequent
read operation on the peeked port will see the peeked byte or character
even though the port is otherwise unbuffered.

In other words, the semantics of buffering is defined in terms of side
effects on shared mutable sources, and a lookahead operation has the
same side effect on the shared source as a read operation.


@deffn Syntax buffer-mode @meta{buffer-mode symbol}
@meta{buffer-mode symbol} must be a symbol whose name is one of
@code{none}, @code{line}, and @code{block}.  The result is the
corresponding symbol, and specifies the associated buffer mode.

@quotation
@emph{Note:} Only the name of @meta{buffer-mode symbol} is significant.
@end quotation
@end deffn


@deffn Procedure {buffer-mode?} @var{obj}
Return @true{} if the argument is a valid buffer--mode symbol, and
returns @false{} otherwise.
@end deffn

@c page
@node stdlib io port transcoders
@subsubsection Transcoders


Several different Unicode encoding schemes describe standard ways to
encode characters and strings as byte sequences and to decode those
sequences.  Within this document, a @emph{codec} is an immutable Scheme
object that represents a Unicode or similar encoding scheme.

An @emph{end--of--line style} is a symbol that, if it is not
@code{none}, describes how a textual port transcodes representations of
line endings.

A @emph{transcoder} is an immutable Scheme object that combines a codec
with an end--of--line style and a method for handling decoding errors.
Each transcoder represents some specific bidirectional (but not
necessarily lossless), possibly stateful translation between byte
sequences and Unicode characters and strings.  Every transcoder can
operate in the input direction (bytes to characters) or in the output
direction (characters to bytes).  A @var{transcoder} parameter name
means that the corresponding argument must be a transcoder.

A @emph{binary port} is a port that supports binary I/O, does not have
an associated transcoder and does not support textual I/O.  A
@emph{textual port} is a port that supports textual I/O, and does not
support binary I/O.  A textual port may or may not have an associated
transcoder.


@deffn Procedure latin-1-codec
@deffnx Procedure utf-8-codec
@deffnx Procedure utf-16-codec
These are predefined codecs for the @iso{} 8859-1, @utf{}-8, and
@utf{}-16 encoding schemes.

A call to any of these procedures returns a value that is equal in the
sense of @func{eqv?} to the result of any other call to the same
procedure.
@end deffn


@deffn Syntax eol-style @meta{eol-style symbol}
@meta{eol-style symbol} should be a symbol whose name is one of
@code{lf}, @code{cr}, @code{crlf}, @code{nel}, @code{crnel}, @code{ls},
and @code{none}.

The form evaluates to the corresponding symbol.  If the name of
@meta{eol-style symbol} is not one of these symbols, the effect and
result are implementation--dependent; in particular, the result may be
an eol--style symbol acceptable as an @var{eol-style} argument to
@code{make-transcoder}.  Otherwise, an exception is raised.

All eol--style symbols except @code{none} describe a specific
line--ending encoding:

@example
lf              @meta{linefeed}
cr              @meta{carriage return}
crlf            @meta{carriage return} @meta{linefeed}
nel             @meta{next line}
crnel           @meta{carriage return} @meta{next line}
ls              @meta{line separator}
@end example

For a textual port with a transcoder, and whose transcoder has an
eol--style symbol @code{none}, no conversion occurs.  For a textual
input port, any eol--style symbol other than @code{none} means that all
of the above line-ending encodings are recognized and are translated
into a single linefeed.  For a textual output port, @code{none} and
@code{lf} are equivalent.  Linefeed characters are encoded according to
the specified eol-style symbol, and all other characters that
participate in possible line endings are encoded as is.

@quotation
@emph{Note:} Only the name of @meta{eol-style symbol} is significant.
@end quotation
@end deffn


@deffn Procedure native--eol--style
Return the default end--of--line style of the underlying platform, e.g.,
@code{lf} on Unix and @code{crlf} on Windows.
@end deffn


@tindex i/o-decoding
@deffn {Condition Type} {i/o-decoding}
@deffnx Procedure {make-i/o-decoding-error} @var{port}
@deffnx Procedure {i/o-decoding-error?} @var{obj}
This condition type could be defined by:

@example
(define-condition-type &i/o-decoding &i/o-port
  make-i/o-decoding-error i/o-decoding-error?)
@end example

An exception with this type is raised when one of the operations for
textual input from a port encounters a sequence of bytes that cannot be
translated into a character or string by the input direction of the
port's transcoder.

When such an exception is raised, the port's position is past the
invalid encoding.
@end deffn


@tindex i/o-encoding
@deffn {Condition Type} {i/o-encoding}
@deffnx Procedure {make-i/o-encoding-error} @var{pobj} @var{cobj}
@deffnx Procedure {i/o-encoding-error?} @var{obj}
@deffnx Procedure {i/o-encoding-error-char} @var{condition}
This condition type could be defined by:

@example
(define-condition-type &i/o-encoding &i/o-port
  make-i/o-encoding-error i/o-encoding-error?
  (char i/o-encoding-error-char))
@end example

An exception with this type is raised when one of the operations for
textual output to a port encounters a character that cannot be
translated into bytes by the output direction of the port's transcoder.
@var{cobj} is the character that could not be encoded.
@end deffn


@deffn Syntax error-handling-mode @meta{error-handling-mode symbol}
@meta{error-handling-mode symbol} should be a symbol whose name is one
of @code{ignore}, @code{raise}, and @code{replace}.

The form evaluates to the corresponding symbol.  If
@meta{error-handling-mode symbol} is not one of these identifiers,
effect and result are implementation--dependent: The result may be an
error--handling--mode symbol acceptable as a @var{handling-mode}
argument to @func{make-transcoder}.  If it is not acceptable as a
@var{handling-mode} argument to @func{make-transcoder}, an exception is
raised.

@quotation
@emph{Note:} Only the name of @meta{error-handling-style symbol} is
significant.
@end quotation

The error--handling mode of a transcoder specifies the behavior of
textual I/O operations in the presence of encoding or decoding errors.

If a textual input operation encounters an invalid or incomplete
character encoding, and the error--handling mode is @code{ignore}, an
appropriate number of bytes of the invalid encoding are ignored and
decoding continues with the following bytes.  If the error--handling
mode is @code{replace}, the replacement character @code{U+FFFD} is
injected into the data stream, an appropriate number of bytes are
ignored, and decoding continues with the following bytes.  If the
error--handling mode is @code{raise}, an exception with condition type
@code{&i/o-decoding} is raised.

If a textual output operation encounters a character it cannot encode,
and the error--handling mode is @code{ignore}, the character is ignored
and encoding continues with the next character.  If the error--handling
mode is @code{replace}, a codec--specific replacement character is
emitted by the transcoder, and encoding continues with the next
character.  The replacement character is @code{U+FFFD} for transcoders
whose codec is one of the Unicode encodings, but is the @code{?}
character for the Latin--1 encoding.  If the error--handling mode is
@code{raise}, an exception with condition type @code{&i/o-encoding} is
raised.
@end deffn


@deffn Procedure make-transcoder @var{codec}
@deffnx Procedure make-transcoder @var{codec} @var{eol-style}
@deffnx Procedure make-transcoder @var{codec} @var{eol-style} @var{handling-mode}
@var{codec} must be a codec; @var{eol-style}, if present, an eol--style
symbol; and @var{handling-mode}, if present, an error--handling--mode
symbol.

@var{eol-style} may be omitted, in which case it defaults to the native
end--of--line style of the underlying platform.  @var{handling-mode} may
be omitted, in which case it defaults to @code{replace}.  The result is
a transcoder with the behavior specified by its arguments.
@end deffn


@deffn Procedure native-transcoder
Return an implementation--dependent transcoder that represents a
possibly locale-dependent ``native'' transcoding.
@end deffn


@deffn Procedure transcoder-codec @var{transcoder}
@deffnx Procedure transcoder-eol-style @var{transcoder}
@deffnx Procedure transcoder-error-handling-mode @var{transcoder}
These are accessors for transcoder objects; when applied to a transcoder
returned by @func{make-transcoder}, they return the @var{codec},
@var{eol-style}, and @var{handling-mode} arguments, respectively.
@end deffn


@deffn Procedure {bytevector->string} @var{bytevector} @var{transcoder}
Return the string that results from transcoding the @var{bytevector}
according to the input direction of the transcoder.
@end deffn


@deffn Procedure {string->bytevector} @var{string} @var{transcoder}
Return the bytevector that results from transcoding the @var{string}
according to the output direction of the transcoder.
@end deffn


@c page
@node stdlib io port eof object
@subsubsection End--of--file object


The end--of--file object is returned by various I/O procedures when they
reach end of file.


@deffn Procedure eof-object
Return the end--of--file object.

@example
(eqv? (eof-object) (eof-object))        @result{} #t
(eq? (eof-object) (eof-object))         @result{} #t
@end example
@end deffn

@quotation
@emph{Note:} The end--of--file object is not a datum value, and thus has
no external representation.
@end quotation


@deffn Procedure {eof-object?} @var{obj}
Return @true{} if @var{obj} is the end--of--file object, @false{}
otherwise.
@end deffn


@c page
@node stdlib io port io ports
@subsubsection Input and output ports


The operations described in this section are common to input and output
ports, both binary and textual.  A port may also have an associated
@emph{position} that specifies a particular place within its data sink
or source, and may also provide operations for inspecting and setting
that place.


@deffn Procedure {port?} @var{obj}
Return @true{} if the argument is a port, @false{} otherwise.
@end deffn


@deffn Procedure port-transcoder @var{port}
Return the transcoder associated with @var{port} if @var{port} is
textual and has an associated transcoder, and returns @false{} if
@var{port} is binary or does not have an associated transcoder.
@end deffn


@deffn Procedure {textual-port?} @var{port}
@deffnx Procedure {binary-port?} @var{port}
The @func{textual-port?} procedure returns @true{} if @var{port} is
textual, and returns @false{} otherwise.

The @func{binary-port?}  procedure returns @true{} if @var{port} is
binary, and returns @false{} otherwise.
@end deffn


@deffn Procedure {transcoded-port} @var{binary-port} @var{transcoder}
The @func{transcoded-port} procedure returns a new textual port with the
specified @var{transcoder}.  Otherwise the new textual port's state is
largely the same as that of @var{binary-port}.

If @var{binary-port} is an input port, the new textual port will be an
input port and will transcode the bytes that have not yet been read from
@var{binary-port}.  If @var{binary-port} is an output port, the new
textual port will be an output port and will transcode output characters
into bytes that are written to the byte sink represented by
@var{binary-port}.

As a side effect, however, @func{transcoded-port} closes
@var{binary-port} in a special way that allows the new textual port to
continue to use the byte source or sink represented by
@var{binary-port}, even though @var{binary-port} itself is closed and
cannot be used by the input and output operations described in this
chapter.
@end deffn


@deffn Procedure {port-has-port-position?} @var{port}
@deffnx Procedure port-position @var{port}
The @func{port-has-port-position?} procedure returns @true{} if the port
supports the @func{port-position} operation, and @false{} otherwise.

For a binary port, the @func{port-position} procedure returns the index
of the position at which the next byte would be read from or written to
the port as an exact non--negative integer object.  For a textual port,
@func{port-position} returns a value of some implementation--dependent
type representing the port's position; this value may be useful only as
the @var{pos} argument to @func{set-port-position!}, if the latter is
supported on the port (see below).

If the port does not support the operation, @func{port-position} raises
an exception with condition type @code{&assertion}.

@quotation
@emph{Note:} For a textual port, the port position may or may not be an
integer object.  If it is an integer object, the integer object does not
necessarily correspond to a byte or character position.
@end quotation
@end deffn


@deffn Procedure {port-has-set-port-position!?} @var{port}
@deffnx Procedure {set-port-position!} @var{port} @var{pos}
If @var{port} is a binary port, @var{pos} should be a non--negative
exact integer object.  If @var{port} is a textual port, @var{pos} should
be the return value of a call to @func{port-position} on @var{port}.

The @func{port-has-set-port-position!?} procedure returns @true{} if the
port supports the @func{set-port-position!} operation, and @false{}
otherwise.

The @func{set-port-position!} procedure raises an exception with
condition type @code{&assertion} if the port does not support the
operation, and an exception with condition type
@code{&i/o-invalid-position} if @var{pos} is not in the range of valid
positions of @var{port}.  Otherwise, it sets the current position of the
port to @var{pos}.  If @var{port} is an output port,
@func{set-port-position!} first flushes @var{port}.

If @var{port} is a binary output port and the current position is set
beyond the current end of the data in the underlying data sink, the
object is not extended until new data is written at that position.  The
contents of any intervening positions are unspecified.  Binary ports
created by @func{open-file-output-port} and
@func{open-file-input/output-port} can always be extended in this manner
within the limits of the underlying operating system.  In other cases,
attempts to set the port beyond the current end of data in the
underlying object may result in an exception with condition type
@code{&i/o-invalid-position}.
@end deffn


@deffn Procedure close-port @var{port}
Closes the port, rendering the port incapable of delivering or accepting
data. If @var{port} is an output port, it is flushed before being
closed.  This has no effect if the port has already been closed.  A
closed port is still a port.  The @func{close-port} procedure returns
unspecified values.
@end deffn


@deffn Procedure call-with-port @var{port} @var{proc}
@var{proc} must accept one argument.  The @func{call-with-port}
procedure calls @var{proc} with @var{port} as an argument.  If
@var{proc} returns, @var{port} is closed automatically and the values
returned by @var{proc} are returned.  If @var{proc} does not return,
@var{port} is not closed automatically, except perhaps when it is
possible to prove that @var{port} will never again be used for an input
or output operation.
@end deffn


@c page
@node stdlib io port input ports
@subsubsection Input ports


An input port allows the reading of an infinite sequence of bytes or
characters punctuated by end--of--file objects.  An input port connected
to a finite data source ends in an infinite sequence of end--of--file
objects.

It is unspecified whether a character encoding consisting of several
bytes may have an end of file between the bytes.  If, for example,
@func{get-char} raises an @code{&i/o-decoding} exception because the
character encoding at the port's position is incomplete up to the next
end of file, a subsequent call to @func{get-char} may successfully
decode a character if bytes completing the encoding are available after
the end of file.


@deffn Procedure {input-port?} @var{obj}
Return @true{} if the argument is an input port (or a combined input and
output port), and returns @false{} otherwise.
@end deffn


@deffn Procedure {port-eof?} @var{input-port}
Return @true{} if the @func{lookahead-u8} procedure (if @var{input-port}
is a binary port) or the @func{lookahead-char} procedure (if
@var{input-port} is a textual port) would return the end--of--file
object, and @false{} otherwise.  The operation may block indefinitely if
no data is available but the port cannot be determined to be at end of
file.
@end deffn


@deffn Procedure {open-file-input-port} @var{filename}
@deffnx Procedure {open-file-input-port} @var{filename} @var{file-options}
@deffnx Procedure {open-file-input-port} @var{filename} @var{file-options} @var{buffer-mode}
@deffnx Procedure {open-file-input-port} @var{filename} @var{file-options} @var{buffer-mode} @var{maybe-transcoder}
@var{maybe-transcoder} must be either a transcoder or @false{}.

The @func{open-file-input-port} procedure returns an input port for the
named file.  The @var{file-options} and @var{maybe-transcoder} arguments
are optional.

The @var{file-options} argument, which may determine various aspects of
the returned port, defaults to the value of @code{(file-options)}.

The @var{buffer-mode} argument, if supplied, must be one of the symbols
that name a buffer mode.  The @var{buffer-mode} argument defaults to
@code{block}.

If @var{maybe-transcoder} is a transcoder, it becomes the transcoder
associated with the returned port.

If @var{maybe-transcoder} is @false{} or absent, the port will be a
binary port and will support the @func{port-position} and
@func{set-port-position!} operations.  Otherwise the port will be a
textual port, and whether it supports the @func{port-position} and
@func{set-port-position!} operations is implementation-dependent (and
possibly transcoder-dependent).
@end deffn


@deffn Procedure {open-bytevector-input-port} @var{bytevector}
@deffnx Procedure {open-bytevector-input-port} @var{bytevector} @var{maybe-transcoder}
@var{maybe-transcoder} must be either a transcoder or @false{}.

The @func{open-bytevector-input-port} procedure returns an input port
whose bytes are drawn from @var{bytevector}.  If @var{transcoder} is
specified, it becomes the transcoder associated with the returned port.

If @var{maybe-transcoder} is @false{} or absent, the port will be a
binary port and will support the @func{port-position} and
@func{set-port-position!} operations.  Otherwise the port will be a
textual port, and whether it supports the @func{port-position} and
@func{set-port-position!} operations will be implementation--dependent
(and possibly transcoder--dependent).

If @var{bytevector} is modified after @code{open-bytevector-input-port}
has been called, the effect on the returned port is unspecified.
@end deffn


@deffn Procedure {open-string-input-port} @var{string}
Return a textual input port whose characters are drawn from
@var{string}.  The port may or may not have an associated transcoder; if
it does, the transcoder is implementation--dependent.  The port should
support the @func{port-position} and @func{set-port-position!}
operations.

If @var{string} is modified after @func{open-string-input-port} has been
called, the effect on the returned port is unspecified.
@end deffn


@deffn Procedure {standard-input-port}
Return a fresh binary input port connected to standard input.  Whether
the port supports the @func{port-position} and @func{set-port-position!}
operations is implementation--dependent.
@end deffn


@deffn Procedure {current-input-port}
This returns a default textual port for input.  Normally, this default
port is associated with standard input, but can be dynamically
re--assigned using the @func{with-input-from-file} procedure from the
@rsixlibrary{io simple} library.  The port may or may not have an
associated transcoder; if it does, the transcoder is
implementation--dependent.
@end deffn


@deffn Procedure make-custom-binary-input-port @var{id} @var{read!} @var{get-position} @var{set-position!} @var{close}
Return a newly created binary input port whose byte source is an
arbitrary algorithm represented by the @func{read!} procedure.  @var{id}
must be a string naming the new port, provided for informational
purposes only.  @var{read!} must be a procedure and should behave as
specified below; it will be called by operations that perform binary
input.

Each of the remaining arguments may be @false{}; if any of those
arguments is not @false{}, it must be a procedure and should behave as
specified below.

@table @code
@item (@var{read!} @var{bytevector} @var{start} @var{count})
@var{start} will be a non--negative exact integer object, @var{count}
will be a positive exact integer object, and @var{bytevector} will be a
bytevector whose length is at least @var{start}+@var{count}.

The @var{read!} procedure should obtain up to @var{count} bytes from the
byte source, and should write those bytes into @var{bytevector} starting
at index @var{start}.  The @var{read!} procedure should return an exact
integer object.  This integer object should represent the number of
bytes that it has read.  To indicate an end of file, the @var{read!}
procedure should write no bytes and return 0.

@item (@var{get-position})
The @var{get-position} procedure (if supplied) should return an exact
integer object representing the current position of the input port.  If
not supplied, the custom port will not support the @func{port-position}
operation.

@item (@var{set-position!} @var{pos})
@var{pos} will be a non--negative exact integer object.  The
@var{set-position!} procedure (if supplied) should set the position of
the input port to @var{pos}.  If not supplied, the custom port will not
support the @func{set-port-position!} operation.

@item (@var{close})
The @var{close} procedure (if supplied) should perform any actions that
are necessary when the input port is closed.
@end table

@strong{Implementation responsibilities:} The implementation must check
the return values of @var{read!} and @var{get-position} only when it
actually calls them as part of an I/O operation requested by the
program.  The implementation is not required to check that these
procedures otherwise behave as described.  If they do not, however, the
behavior of the resulting port is unspecified.
@end deffn


@deffn Procedure make-custom-textual-input-port @var{id} @var{read!} @var{get-position} @var{set-position!} @var{close}
Return a newly created textual input port whose character source is an
arbitrary algorithm represented by the @var{read!} procedure.  @var{id}
must be a string naming the new port, provided for informational
purposes only.  @var{read!} must be a procedure and should behave as
specified below; it will be called by operations that perform textual
input.

Each of the remaining arguments may be @false{}; if any of those
arguments is not @false{}, it must be a procedure and should behave as
specified below.

@table @code
@item (@var{read!} @var{string} @var{start} @var{count})
@var{start} will be a non--negative exact integer object, @var{count}
will be a positive exact integer object, and @var{string} will be a
string whose length is at least @var{start}+@var{count}.

The @var{read!} procedure should obtain up to @var{count} characters
from the character source, and should write those characters into
@var{string} starting at index @var{start}.  The @var{read!} procedure
should return an exact integer object representing the number of
characters that it has written.  To indicate an end of file, the
@var{read!}  procedure should write no bytes and return 0.

@item (@var{get-position})
The @var{get-position} procedure (if supplied) should return a single
value.  The return value should represent the current position of the
input port.  If not supplied, the custom port will not support the
@func{port-position} operation.

@item (@var{set-position!} @var{pos})
The @var{set-position!} procedure (if supplied) should set the position
of the input port to @var{pos} if @var{pos} is the return value of a
call to @var{get-position}.  If not supplied, the custom port will not
support the @func{set-port-position!} operation.

@item (@var{close})
The @var{close} procedure (if supplied) should perform any actions that
are necessary when the input port is closed.
@end table

The port may or may not have an an associated transcoder; if it does,
the transcoder is implementation--dependent.

@strong{Implementation responsibilities:} The implementation must check
the return values of @var{read!} and @var{get-position} only when it
actually calls them as part of an I/O operation requested by the
program.  The implementation is not required to check that these
procedures otherwise behave as described.  If they do not, however, the
behavior of the resulting port is unspecified.
@end deffn

@c page
@node stdlib io port binary input
@subsubsection Binary input


@deffn Procedure get-u8 @var{binary-input-port}
Read from @var{binary-input-port}, blocking as necessary, until a byte
is available from @var{binary-input-port} or until an end of file is
reached.  If a byte becomes available, @code{get-u8} returns the byte as
an octet and updates @var{binary-input-port} to point just past that
byte. If no input byte is seen before an end of file is reached, the
end--of--file object is returned.
@end deffn


@deffn Procedure lookahead-u8 @var{binary-input-port}
The @func{lookahead-u8} procedure is like @code{get-u8}, but it does not
update @var{binary-input-port} to point past the byte.
@end deffn


@deffn Procedure get-bytevector-n @var{binary-input-port} @var{count}
@var{count} must be an exact, non--negative integer object representing
the number of bytes to be read.

The @func{get-bytevector-n} procedure reads from
@var{binary-input-port}, blocking as necessary, until @var{count} bytes
are available from @var{binary-input-port} or until an end of file is
reached.

If @var{count} bytes are available before an end of file,
@func{get-bytevector-n} returns a bytevector of size @var{count}.

If fewer bytes are available before an end of file,
@func{get-bytevector-n} returns a bytevector containing those bytes. In
either case, the input port is updated to point just past the bytes
read.

If an end of file is reached before any bytes are available,
@func{get-bytevector-n} returns the end--of--file object.
@end deffn


@deffn Procedure {get-bytevector-n!} @var{binary-input-port} @var{bytevector} @var{start} @var{count}
@var{count} must be an exact, non--negative integer object, representing
the number of bytes to be read. @var{bytevector} must be a bytevector
with at least @var{start}+@var{count} elements.

The @func{get-bytevector-n!} procedure reads from
@var{binary-input-port}, blocking as necessary, until @var{count} bytes
are available from @var{binary-input-port} or until an end of file is
reached.

If @var{count} bytes are available before an end of file, they are
written into @var{bytevector} starting at index @var{start}, and the
result is @var{count}.

If fewer bytes are available before the next end of file, the available
bytes are written into @var{bytevector} starting at index @var{start},
and the result is a number object representing the number of bytes
actually read.

In either case, the input port is updated to point just past the bytes
read. If an end of file is reached before any bytes are available,
@func{get-bytevector-n!} returns the end--of--file object.
@end deffn


@deffn Procedure get-bytevector-some @var{binary-input-port}
Read from @var{binary-input-port}, blocking as necessary, until bytes
are available from @var{binary-input-port} or until an end of file is
reached.  If bytes become available, @func{get-bytevector-some} returns
a freshly allocated bytevector containing the initial available bytes
(at least one), and it updates @var{binary-input-port} to point just
past these bytes.  If no input bytes are seen before an end of file is
reached, the end--of--file object is returned.
@end deffn


@deffn Procedure get-bytevector-all @var{binary-input-port}
Attempts to read all bytes until the next end of file, blocking as
necessary.  If one or more bytes are read, @func{get-bytevector-all}
returns a bytevector containing all bytes up to the next end of file.
Otherwise, @func{get-bytevector-all} returns the end--of--file object.
The operation may block indefinitely waiting to see if more bytes will
become available, even if some bytes are already available.
@end deffn


@c page
@node stdlib io port textual input
@subsubsection Textual input


@deffn Procedure get-char @var{textual-input-port}
Read from @var{textual-input-port}, blocking as necessary, until a
complete character is available from @var{textual-input-port}, or until
an end of file is reached.

If a complete character is available before the next end of file,
@func{get-char} returns that character and updates the input port to
point past the character. If an end of file is reached before any
character is read, @func{get-char} returns the end--of--file object.
@end deffn


@deffn Procedure lookahead-char @var{textual-input-port}
The @func{lookahead-char} procedure is like @func{get-char}, but it does
not update @var{textual-input-port} to point past the character.

@quotation
@emph{Note:} With some of the standard transcoders described in this
document, up to four bytes of lookahead are needed.  Non--standard
transcoders may need even more lookahead.
@end quotation
@end deffn


@deffn Procedure get-string-n @var{textual-input-port} @var{count}
@var{count} must be an exact, non--negative integer object, representing
the number of characters to be read.

The @func{get-string-n} procedure reads from @var{textual-input-port},
blocking as necessary, until @var{count} characters are available, or
until an end of file is reached.

If @var{count} characters are available before end of file,
@func{get-string-n} returns a string consisting of those @var{count}
characters.

If fewer characters are available before an end of file, but one or more
characters can be read, @func{get-string-n} returns a string containing
those characters.

In either case, the input port is updated to point just past the
characters read. If no characters can be read before an end of file, the
end-of-file object is returned.
@end deffn


@deffn Procedure {get-string-n!} @var{textual-input-port} @var{string} @var{start} @var{count}
@var{start} and @var{count} must be exact, non--negative integer
objects, with @var{count} representing the number of characters to be
read.  @var{string} must be a string with at least
@var{start}+@var{count} characters.

The @func{get-string-n!} procedure reads from @var{textual-input-port}
in the same manner as @func{get-string-n}.

If @var{count} characters are available before an end of file, they are
written into @var{string} starting at index @var{start}, and @var{count}
is returned.

If fewer characters are available before an end of file, but one or more
can be read, those characters are written into @var{string} starting at
index @var{start} and the number of characters actually read is returned
as an exact integer object.

If no characters can be read before an end of file, the end--of--file
object is returned.
@end deffn


@deffn Procedure get-string-all @var{textual-input-port}
Read from @var{textual-input-port} until an end of file, decoding
characters in the same manner as @func{get-string-n} and
@func{get-string-n!}.

If characters are available before the end of file, a string containing
all the characters decoded from that data are returned.  If no character
precedes the end of file, the end--of--file object is returned.
@end deffn


@deffn Procedure get-line @var{textual-input-port}
Read from @var{textual-input-port} up to and including the linefeed
character or end of file, decoding characters in the same manner as
@func{get-string-n} and @func{get-string-n!}.

If a linefeed character is read, a string containing all of the text up
to (but not including) the linefeed character is returned, and the port
is updated to point just past the linefeed character.

If an end of file is encountered before any linefeed character is read,
but some characters have been read and decoded as characters, a string
containing those characters is returned.

If an end of file is encountered before any characters are read, the
end--of--file object is returned.

@quotation
@emph{Note:} The end--of--line style, if not @code{none}, will cause all
line endings to be read as linefeed characters.
@end quotation
@end deffn


@deffn Procedure get-datum @var{textual-input-port}
Read an external representation from @var{textual-input-port} and
returns the datum it represents.  The @func{get-datum} procedure returns
the next datum that can be parsed from the given
@var{textual-input-port}, updating @var{textual-input-port} to point
exactly past the end of the external representation of the object.

Any @meta{interlexeme space} (@ref{lang lex syntax}) in the input is
first skipped.  If an end of file occurs after the @meta{interlexeme
space}, the end--of--file object is returned.

If a character inconsistent with an external representation is
encountered in the input, an exception with condition types
@code{&lexical} and @code{&i/o-read} is raised.

Also, if the end of file is encountered after the beginning of an
external representation, but the external representation is incomplete
and therefore cannot be parsed, an exception with condition types
@code{&lexical} and @code{&i/o-read} is raised.
@end deffn


@c page
@node stdlib io port output ports
@subsubsection Output ports


An output port is a sink to which bytes or characters are written.  The
written data may control external devices or may produce files and other
objects that may subsequently be opened for input.


@deffn Procedure {output-port?} @var{obj}
Return @true{} if the argument is an output port (or a combined input
and output port), @false{} otherwise.
@end deffn


@deffn Procedure {flush-output-port} @var{output-port}
Flushe any buffered output from the buffer of @var{output-port} to the
underlying file, device, or object.  The @func{flush-output-port}
procedure returns unspecified values.
@end deffn


@deffn Procedure {output-port-buffer-mode} @var{output-port}
Return the symbol that represents the buffer mode of @var{output-port}.
@end deffn


@deffn Procedure open-file-output-port @var{filename}
@deffnx Procedure open-file-output-port @var{filename} @var{file-options}
@deffnx Procedure open-file-output-port @var{filename} @var{file-options} @var{buffer-mode}
@deffnx Procedure open-file-output-port @var{filename} @var{file-options} @var{buffer-mode} @var{maybe-transcoder}
@var{maybe-transcoder} must be either a transcoder or @false{}.

The @func{open-file-output-port} procedure returns an output port for
the named file.

The @var{file-options} argument, which may determine various aspects of
the returned port, defaults to the value of @code{(file-options)}.

The @var{buffer-mode} argument, if supplied, must be one of the symbols
that name a buffer mode.  The @var{buffer-mode} argument defaults to
@code{block}.

If @var{maybe-transcoder} is a transcoder, it becomes the transcoder
associated with the port.

If @var{maybe-transcoder} is @false{} or absent, the port will be a
binary port and will support the @func{port-position} and
@func{set-port-position!}  operations.  Otherwise the port will be a
textual port, and whether it supports the @func{port-position} and
@func{set-port-position!} operations is implementation--dependent (and
possibly transcoder--dependent).
@end deffn


@deffn Procedure open-bytevector-output-port
@deffnx Procedure open-bytevector-output-port @var{maybe-transcoder}
@var{maybe-transcoder} must be either a transcoder or @false{}.

The @func{open-bytevector-output-port} procedure returns two values: an
output port and an extraction procedure.  The output port accumulates
the bytes written to it for later extraction by the procedure.

If @var{maybe-transcoder} is a transcoder, it becomes the transcoder
associated with the port.  If @var{maybe-transcoder} is @false{} or
absent, the port will be a binary port and will support the
@func{port-position} and @func{set-port-position!}  operations.
Otherwise the port will be a textual port, and whether it supports the
@code{port-position} and @func{set-port-position!} operations is
implementation--dependent (and possibly transcoder--dependent).

The extraction procedure takes no arguments.  When called, it returns a
bytevector consisting of all the port's accumulated bytes (regardless of
the port's current position), removes the accumulated bytes from the
port, and resets the port's position.
@end deffn


@deffn Procedure call-with-bytevector-output-port @var{proc}
@deffnx Procedure call-with-bytevector-output-port @var{proc} @var{maybe-transcoder}
@var{proc} must accept one argument.  @var{maybe-transcoder} must be
either a transcoder or @false{}.

The @func{call-with-bytevector-output-port} procedure creates an output
port that accumulates the bytes written to it and calls @var{proc} with
that output port as an argument.

Whenever @var{proc} returns, a bytevector consisting of all of the
port's accumulated bytes (regardless of the port's current position) is
returned and the port is closed.

The transcoder associated with the output port is determined as for a
call to @func{open-bytevector-output-port}.
@end deffn


@deffn Procedure open-string-output-port
Return two values: a textual output port and an extraction procedure.
The output port accumulates the characters written to it for later
extraction by the procedure.

The port may or may not have an associated transcoder; if it does, the
transcoder is implementation-dependent.  The port should support the
@func{port-position} and @func{set-port-position!} operations.

The extraction procedure takes no arguments.  When called, it returns a
string consisting of all of the port's accumulated characters
(regardless of the current position), removes the accumulated characters
from the port, and resets the port's position.
@end deffn


@deffn Procedure call-with-string-output-port @var{proc}
@var{proc} must accept one argument.

The @func{call-with-string-output-port} procedure creates a textual
output port that accumulates the characters written to it and calls
@var{proc} with that output port as an argument.

Whenever @var{proc} returns, a string consisting of all of the port's
accumulated characters (regardless of the port's current position) is
returned and the port is closed.

The port may or may not have an associated transcoder; if it does, the
transcoder is implementation-dependent.  The port should support the
@func{port-position} and @func{set-port-position!} operations.
@end deffn


@deffn Procedure standard-output-port
@deffnx Procedure standard-error-port
Return a fresh binary output port connected to the standard output or
standard error respectively.  Whether the port supports the
@func{port-position} and @func{set-port-position!} operations is
implementation--dependent.
@end deffn


@deffn Procedure current-output-port
@deffnx Procedure current-error-port
These return default textual ports for regular output and error output.
Normally, these default ports are associated with standard output, and
standard error, respectively.

The return value of @func{current-output-port} can be dynamically
re--assigned using the @func{with-output-to-file} procedure from the
@rsixlibrary{io simple} library.  A port returned by one of these
procedures may or may not have an associated transcoder; if it does, the
transcoder is implementation--dependent.
@end deffn


@deffn Procedure make-custom-binary-output-port @var{id} @var{write!} @var{get-position} @var{set-position!} @var{close}
Return a newly created binary output port whose byte sink is an
arbitrary algorithm represented by the @var{write!} procedure.  @var{id}
must be a string naming the new port, provided for informational
purposes only.  @var{write!} must be a procedure and should behave as
specified below; it will be called by operations that perform binary
output.

Each of the remaining arguments may be @false{}; if any of those
arguments is not @false{}, it must be a procedure and should behave as
specified in the description of @func{make-custom-binary-input-port}.

@table @code
@item (@var{write!} @var{bytevector} @var{start} @var{count})
@var{start} and @var{count} will be non-negative exact integer objects,
and @var{bytevector} will be a bytevector whose length is at least
@var{start}+@var{count}.

The @var{write!} procedure should write up to @var{count} bytes from
@var{bytevector} starting at index @var{start} to the byte sink.  In any
case, the @var{write!} procedure should return the number of bytes that
it wrote, as an exact integer object.
@end table

@strong{Implementation responsibilities:} The implementation must check
the return values of @var{write!} only when it actually calls
@var{write!} as part of an I/O operation requested by the program.  The
implementation is not required to check that @var{write!} otherwise
behaves as described.  If it does not, however, the behavior of the
resulting port is unspecified.
@end deffn


@deffn Procedure make-custom-textual-output-port @var{id} @var{write!} @var{get-position} @var{set-position!} @var{close}
Return a newly created textual output port whose byte sink is an
arbitrary algorithm represented by the @var{write!} procedure.  @var{Id}
must be a string naming the new port, provided for informational
purposes only.  @var{write!} must be a procedure and should behave as
specified below; it will be called by operations that perform textual
output.

Each of the remaining arguments may be @false{}; if any of those
arguments is not @false{}, it must be a procedure and should behave as
specified in the description of @func{make-custom-textual-input-port}.

@table @code
@item (@var{write!} @var{string} @var{start} @var{count})
@var{start} and @var{count} will be non--negative exact integer objects,
and @var{string} will be a string whose length is at least
@var{start}+@var{count}.

The @var{write!} procedure should write up to @var{count} characters
from @var{string} starting at index @var{start} to the character sink.
In any case, the @var{write!} procedure should return the number of
characters that it wrote, as an exact integer object.
@end table

The port may or may not have an associated transcoder; if it does, the
transcoder is implementation--dependent.

@strong{Implementation responsibilities:} The implementation must check
the return values of @var{write!} only when it actually calls
@var{write!} as part of an I/O operation requested by the program.  The
implementation is not required to check that @var{write!} otherwise
behaves as described.  If it does not, however, the behavior of the
resulting port is unspecified.
@end deffn


@c page
@node stdlib io port binary output
@subsubsection Binary output


@deffn Procedure put-u8 @var{binary-output-port} @var{octet}
Write @var{octet} to the output port and returns unspecified values.
@end deffn


@deffn Procedure put-bytevector @var{binary-output-port} @var{bytevector}
@deffnx Procedure put-bytevector @var{binary-output-port} @var{bytevector} @var{start}
@deffnx Procedure put-bytevector @var{binary-output-port} @var{bytevector} @var{start} @var{count}
@var{start} and @var{count} must be non--negative exact integer objects
that default to 0 and:

@example
(- (bytevector-length @var{bytevector}) @var{start})
@end example

@noindent
respectively.  @var{bytevector} must have a length of at least
@var{start}+@var{count}.  The @func{put-bytevector} procedure writes the
@var{count} bytes of the bytevector @var{bytevector} starting at index
@var{start} to the output port.  The @func{put-bytevector} procedure
returns unspecified values.
@end deffn


@c page
@node stdlib io port textual output
@subsubsection Textual output


@deffn Procedure put-char @var{textual-output-port} @var{char}
Write @var{char} to the port. The @func{put-char} procedure returns
unspecified values.
@end deffn


@deffn Procedure put-string @var{textual-output-port} @var{string}
@deffnx Procedure put-string @var{textual-output-port} @var{string} @var{start}
@deffnx Procedure put-string @var{textual-output-port} @var{string} @var{start} @var{count}
@var{start} and @var{count} must be non--negative exact integer objects.
@var{String} must have a length of at least @var{start}+@var{count}.

@var{start} defaults to 0.  @var{count} defaults to:

@example
(- (string-length @var{string}) @var{start})
@end example

The @func{put-string} procedure writes the @var{count} characters of
@var{string} starting at index @var{start} to the port.  The
@func{put-string} procedure returns unspecified values.
@end deffn


@deffn Procedure put-datum @var{textual-output-port} @var{datum}
@var{datum} should be a datum value.

The @func{put-datum} procedure writes an external representation of
@var{datum} to @var{textual-output-port}.  The specific external
representation is implementation--dependent.  However, whenever
possible, an implementation should produce a representation for which
@func{get-datum}, when reading the representation, will return an object
equal (in the sense of @func{equal?}) to @var{datum}.

@quotation
@emph{Note:} Not all datums may allow producing an external
representation for which @func{get-datum} will produce an object that is
equal to the original.  Specifically, NaNs contained in @var{datum} may
make this impossible.
@end quotation

@quotation
@emph{Note:} The @func{put-datum} procedure merely writes the external
representation, but no trailing delimiter.  If @func{put-datum} is used
to write several subsequent external representations to an output port,
care should be taken to delimit them properly so they can be read back
in by subsequent calls to @func{get-datum}.
@end quotation
@end deffn


@c page
@node stdlib io port input output ports
@subsubsection Input/output ports


@deffn Procedure {open-file-input/output-port} @var{filename}
@deffnx Procedure {open-file-input/output-port} @var{filename} @var{file-options}
@deffnx Procedure {open-file-input/output-port} @var{filename} @var{file-options} @var{buffer-mode}
@deffnx Procedure {open-file-input/output-port} @var{filename} @var{file-options} @var{buffer-mode} @var{transcoder}
Return a single port that is both an input port and an output port for
the named file.  The optional arguments default as described in the
specification of @func{open-file-output-port}.  If the input/output port
supports @func{port-position} and/or @func{set-port-position!}, the same
port position is used for both input and output.
@end deffn


@deffn Procedure {make-custom-binary-input/output-port} @var{id} @var{read!} @var{write!} @var{get-position} @var{set-position!} @var{close}
Return a newly created binary input/output port whose byte source and
sink are arbitrary algorithms represented by the @var{read!} and
@var{write!}  procedures.

@var{id} must be a string naming the new port, provided for
informational purposes only.

@var{read!} and @var{write!} must be procedures, and should behave as
specified for the @func{make-custom-binary-input-port} and
@func{make-custom-binary-output-port} procedures.

Each of the remaining arguments may be @false{}; if any of those
arguments is not @false{}, it must be a procedure and should behave as
specified in the description of @func{make-custom-binary-input-port}.
@end deffn


@deffn Procedure {make-custom-textual-input/output-port} @var{id} @var{read!} @var{write!} @var{get-position} @var{set-position!} @var{close}
Return a newly created textual input/output port whose textual source
and sink are arbitrary algorithms represented by the @var{read!} and
@var{write!}  procedures.

@var{id} must be a string naming the new port, provided for
informational purposes only.

@var{read!} and @var{write!} must be procedures, and should behave as
specified for the @func{make-custom-textual-input-port} and
@func{make-custom-textual-output-port} procedures.

Each of the remaining arguments may be @false{}; if any of those
arguments is not @false{}, it must be a procedure and should behave as
specified in the description of @func{make-custom-textual-input-port}.
@end deffn


@c page
@node stdlib io simple
@subsection Simple input/output


This section describes the @rsixlibrary{io simple} library, which
provides a somewhat more convenient interface for performing textual I/O
on ports.  This library implements most of the I/O procedures of the
previous revision of this report.

The ports created by the procedures of this library are textual ports
associated implementation--dependent transcoders.


@deffn Procedure {eof-object}
@deffnx Procedure {eof-object?} @var{obj}
These are the same as @func{eof-object} and @func{eof-object?} from the
@rsixlibrary{ports} library.
@end deffn


@deffn Procedure call-with-input-file @var{filename} @var{proc}
@deffnx Procedure call-with-output-file @var{filename} @var{proc}
@var{proc} should accept one argument.

These procedures open the file named by @var{filename} for input or for
output, with no specified file options, and call @var{proc} with the
obtained port as an argument.

If @var{proc} returns, the port is closed automatically and the values
returned by @var{proc} are returned.

If @var{proc} does not return, the port is not closed automatically,
unless it is possible to prove that the port will never again be used
for an I/O operation.
@end deffn


@deffn Procedure input-port? @var{obj}
@deffnx Procedure output-port? @var{obj}
These are the same as the @func{input-port?} and @func{output-port?}
procedures in the @rsixlibrary{io ports} library.
@end deffn


@deffn Procedure current-input-port
@deffnx Procedure current-output-port
@deffnx Procedure current-error-port
These are the same as the @func{current-input-port},
@func{current-output-port}, and @func{current-error-port} procedures
from the @rsixlibrary{io ports} library.
@end deffn


@deffn Procedure with-input-from-file @var{filename} @var{thunk}
@deffnx Procedure with-output-to-file @var{filename} @var{thunk}
@var{thunk} must be a procedure and must accept zero arguments.

The file is opened for input or output using empty file options, and
@var{thunk} is called with no arguments.

During the dynamic extent of the call to @var{thunk}, the obtained port
is made the value returned by @func{current-input-port} or
@func{current-output-port} procedures; the previous default values are
reinstated when the dynamic extent is exited.

When @var{thunk} returns, the port is closed automatically.  The values
returned by @var{thunk} are returned.

If an escape procedure is used to escape back into the call to
@var{thunk} after @var{thunk} is returned, the behavior is unspecified.
@end deffn


@deffn Procedure open-input-file @var{filename}
Open @var{filename} for input, with empty file options, and returns the
obtained port.
@end deffn


@deffn Procedure open-output-file @var{filename}
Open @var{filename} for output, with empty file options, and returns the
obtained port.
@end deffn


@deffn Procedure close-input-port @var{input-port}
@deffnx Procedure close-output-port @var{output-port}
Close @var{input-port} or @var{output-port}, respectively.
@end deffn


@deffn Procedure read-char
@deffnx Procedure read-char @var{textual-input-port}
Reads from @var{textual-input-port}, blocking as necessary until a
character is available from @var{textual-input-port}, or the data that
are available cannot be the prefix of any valid encoding, or an end of
file is reached.

If a complete character is available before the next end of file,
@func{read-char} returns that character, and updates the input port to
point past that character.

If an end of file is reached before any data are read, @func{read-char}
returns the end--of--file object.

If @var{textual-input-port} is omitted, it defaults to the value
returned by @func{current-input-port}.
@end deffn


@deffn Procedure peek-char
@deffnx Procedure peek-char @var{textual-input-port}
This is the same as @func{read-char}, but does not consume any data from
the port.
@end deffn


@deffn Procedure read
@deffnx Procedure read @var{textual-input-port}
Read an external representation from @var{textual-input-port} and
returns the datum it represents.

The @func{read} procedure operates in the same way as @func{get-datum}.

If @var{textual-input-port} is omitted, it defaults to the value
returned by @func{current-input-port}.
@end deffn


@deffn Procedure write-char @var{char}
@deffnx Procedure write-char @var{char} @var{textual-output-port}
Write an encoding of the character @var{char} to the
@var{textual-output-port}, and returns unspecified values.

If @var{textual-output-port} is omitted, it defaults to the value
returned by @func{current-output-port}.
@end deffn


@deffn Procedure newline
@deffnx Procedure newline @var{textual-output-port}
This is equivalent to using @func{write-char} to write @code{#\linefeed}
to @var{textual-output-port}.

If @var{textual-output-port} is omitted, it defaults to the value
returned by @func{current-output-port}.
@end deffn


@deffn Procedure display @var{obj}
@deffnx Procedure display @var{obj} @var{textual-output-port}
Write a representation of @var{obj} to the given
@var{textual-output-port}.

Strings that appear in the written representation are not enclosed in
doublequotes, and no characters are escaped within those strings.

Character objects appear in the representation as if written by
@func{write-char} instead of by @code{write}.

The @func{display} procedure returns unspecified values.

The @var{textual-output-port} argument may be omitted, in which case it
defaults to the value returned by @func{current-output-port}.
@end deffn


@deffn Procedure write @var{obj}
@deffnx Procedure write @var{obj} @var{textual-output-port}
Write the external representation of @var{obj} to
@var{textual-output-port}.

The @func{write} procedure operates in the same way as @func{put-datum}.

If @var{textual-output-port} is omitted, it defaults to the value
returned by @func{current-output-port}.
@end deffn


@c page
@node stdlib files
@section File system


This chapter describes the @rsixlibrary{files} library for operations on
the file system.  This library, in addition to the procedures described
here, also exports the I/O condition types described in section
@ref{stdlib io conditions}.


@deffn Procedure {file-exists?} @var{filename}
@var{filename} must be a file name.

The @func{file-exists?} procedure returns @true{} if the named file
exists at the time the procedure is called, @false{} otherwise.
@end deffn

@deffn Procedure delete-file @var{filename}
@var{filename} must be a file name.

The @func{delete-file} procedure deletes the named file if it exists and
can be deleted, and returns unspecified values.  If the file does not
exist or cannot be deleted, an exception with condition type
@code{&i/o-filename} is raised.
@end deffn


@c page
@node stdlib programlib
@section Command line access and exit values


The procedures described in this section are exported by the
@rsixlibrary{programs} library.


@deffn Procedure command-line
Return a nonempty list of strings.  The first element is an
implementation--specific name for the running top--level program.  The
remaining elements are command--line arguments according to the
operating system's conventions.
@end deffn


@deffn Procedure exit
@deffnx Procedure exit @var{obj}
Exit the running program and communicates an exit value to the operating
system.

If no argument is supplied, the @func{exit} procedure should communicate
to the operating system that the program exited normally.

If an argument is supplied, the @func{exit} procedure should translate
the argument into an appropriate exit value for the operating system.
If @var{obj} is @false{}, the exit is assumed to be abnormal.
@end deffn


@c page
@node stdlib arithmetics
@section Arithmetics


This chapter describes Scheme's libraries for more specialized numerical
operations: fixnum and flonum arithmetic, as well as bitwise operations
on exact integer objects.


@menu
* stdlib arithmetics bitwise::          Bitwise operations.
* stdlib arithmetics fixnum::           Fixnums.
* stdlib arithmetics flonum::           Flonums.
* stdlib arithmetics exact bitwise::    Exact bitwise arithmetic.
@end menu


@c page
@node stdlib arithmetics bitwise
@subsection Bitwise operations


A number of procedures operate on the binary two's--complement
representations of exact integer objects: Bit positions within an exact
integer object are counted from the right, i.e. bit 0 is the least
significant bit.  Some procedures allow extracting @emph{bit fields},
i.e., number objects representing subsequences of the binary
representation of an exact integer object.  Bit fields are always
positive, and always defined using a finite number of bits.


@c page
@node stdlib arithmetics fixnum
@subsection Fixnums


Every implementation must define its fixnum range as a closed interval:
@math{[-2^(w-1), 2^(w-1)-1]} such that @emph{w} is a (mathematical)
integer @math{w >= 24}.  Every mathematical integer within an
implementation's fixnum range must correspond to an exact integer object
that is representable within the implementation.  A fixnum is an exact
integer object whose value lies within this fixnum range.

This section describes the @rsixlibrary{arithmetic fixnums} library,
which defines various operations on fixnums.  Fixnum operations perform
integer arithmetic on their fixnum arguments, but raise an exception
with condition type @code{&implementation-restriction} if the result is
not a fixnum.

This section uses @var{fx}, @vari{fx}, @varii{fx}, etc., as names for
arguments that must be fixnums.


@deffn Procedure fixnum? @var{obj}
Return @true{} if @var{obj} is an exact integer object within the fixnum
range, @false{} otherwise.
@end deffn


@deffn Procedure fixnum-width
@deffnx Procedure least-fixnum
@deffnx Procedure greatest-fixnum
These procedures return @math{w}, @math{-2^(w-1)} and @math{2^(w-1)-1}:
the width, minimum and the maximum value of the fixnum range,
respectively.
@end deffn


@deffn Procedure {fx=?} @vari{fx} @varii{fx} @variii{fx} @dots{}
@deffnx Procedure {fx>?} @vari{fx} @varii{fx} @variii{fx} @dots{}
@deffnx Procedure {fx<?} @vari{fx} @varii{fx} @variii{fx} @dots{}
@deffnx Procedure {fx>=?} @vari{fx} @varii{fx} @variii{fx} @dots{}
@deffnx Procedure {fx<=?} @vari{fx} @varii{fx} @variii{fx} @dots{}
These procedures return @true{} if their arguments are (respectively):
equal, monotonically increasing, monotonically decreasing, monotonically
nondecreasing, or monotonically nonincreasing, @false{} otherwise.
@end deffn


@deffn Procedure {fxzero?} @var{fx}
@deffnx Procedure {fxpositive?} @var{fx}
@deffnx Procedure {fxnegative?} @var{fx}
@deffnx Procedure {fxodd?} @var{fx}
@deffnx Procedure {fxeven?} @var{fx}
These numerical predicates test a fixnum for a particular property,
returning @true{} or @false{}.  The five properties tested by these
procedures are: whether the number object is zero, greater than zero,
less than zero, odd, or even.
@end deffn


@deffn Procedure {fxmax} @vari{fx} @varii{fx} @dots{}
@deffnx Procedure {fxmin} @vari{fx} @varii{fx} @dots{}
These procedures return the maximum or minimum of their arguments.
@end deffn


@deffn Procedure {fx+} @vari{fx} @varii{fx}
@deffnx Procedure {fx*} @vari{fx} @varii{fx}
These procedures return the sum or product of their arguments, provided
that sum or product is a fixnum.  An exception with condition type
@code{&implementation-restriction} is raised if that sum or product is
not a fixnum.
@end deffn


@deffn Procedure {fx-} @vari{fx} @varii{fx}
@deffnx Procedure {fx-} @var{fx}
With two arguments, this procedure returns the difference
@math{@vari{fx} - @varii{fx}}, provided that difference is a fixnum.

With one argument, this procedure returns the additive inverse of its
argument, provided that integer object is a fixnum.

An exception with condition type @code{&implementation-restriction} is
raised if the mathematically correct result of this procedure is not a
fixnum.

@example
(fx- (least-fixnum))  @result{} exception &assertion
@end example
@end deffn


@deffn Procedure {fxdiv-and-mod} @vari{fx} @varii{fx}
@deffnx Procedure {fxdiv} @vari{fx} @varii{fx}
@deffnx Procedure {fxmod} @vari{fx} @varii{fx}
@deffnx Procedure {fxdiv0-and-mod0} @vari{fx} @varii{fx}
@deffnx Procedure {fxdiv0} @vari{fx} @varii{fx}
@deffnx Procedure {fxmod0} @vari{fx} @varii{fx}
@varii{fx} must be nonzero.

These procedures implement number--theoretic integer division and return
the results of the corresponding mathematical operations specified in
@ref{baselib math semantics integer,Integer division}

@example
(fxdiv @vari{fx} @varii{fx})         @result{} @vari{fx} div @varii{fx}
(fxmod @vari{fx} @varii{fx})         @result{} @vari{fx} mod @varii{fx}
(fxdiv-and-mod @vari{fx} @varii{fx}) @result{} @vari{fx} div @varii{fx}, @vari{fx} mod @varii{fx}
                                        ; two return values
(fxdiv0 @vari{fx} @varii{fx})        @result{} @vari{fx} div_0 @varii{fx}
(fxmod0 @vari{fx} @varii{fx})        @result{} @vari{fx} mod_0 @varii{fx}
(fxdiv0-and-mod0 @vari{fx} @varii{fx})
@result{} @vari{fx} @vari{fx} div_0 @varii{fx}, @vari{fx} mod_0 @varii{fx}
   ; two return values
@end example
@end deffn


@deffn Procedure {fx+/carry} @vari{fx} @varii{fx} @variii{fx}
Return the two fixnum results of the following computation:

@example
(let* ((s (+ @vari{fx} @varii{fx} @variii{fx}))
       (s0 (mod0 s (expt 2 (fixnum-width))))
       (s1 (div0 s (expt 2 (fixnum-width)))))
  (values s0 s1))
@end example
@end deffn


@deffn Procedure {fx-/carry} @vari{fx} @varii{fx} @variii{fx}
Return the two fixnum results of the following computation:

@example
(let* ((d (- @vari{fx} @varii{fx} @variii{fx}))
       (d0 (mod0 d (expt 2 (fixnum-width))))
       (d1 (div0 d (expt 2 (fixnum-width)))))
  (values d0 d1))
@end example
@end deffn


@deffn Procedure {fx*/carry} @vari{fx} @varii{fx} @variii{fx}
Return the two fixnum results of the following computation:

@example
(let* ((s (+ (* @vari{fx} @varii{fx}) @variii{fx}))
       (s0 (mod0 s (expt 2 (fixnum-width))))
       (s1 (div0 s (expt 2 (fixnum-width)))))
  (values s0 s1))
@end example
@end deffn


@deffn Procedure {fxnot} @var{fx}
Return the unique fixnum that is congruent mod @emph{2^w} to the
one's--complement of @var{fx}.
@end deffn


@deffn Procedure {fxand} @vari{fx} @dots{}
@deffnx Procedure {fxior} @vari{fx} @dots{}
@deffnx Procedure {fxxor} @vari{fx} @dots{}
These procedures return the fixnum that is the bit--wise ``and'',
``inclusive or'', or ``exclusive or'' of the two's complement
representations of their arguments.  If they are passed only one
argument, they return that argument.  If they are passed no arguments,
they return the fixnum (either @emph{-1} or @emph{0}) that acts as
identity for the operation.
@end deffn


@deffn Procedure {fxif} @vari{fx} @varii{fx} @variii{fx}
Return the fixnum that is the bit--wise ``if'' of the two's complement
representations of its arguments, i.e. for each bit, if it is 1 in
@vari{fx}, the corresponding bit in @varii{fx} becomes the value of the
corresponding bit in the result, and if it is 0, the corresponding bit
in @variii{fx} becomes the corresponding bit in the value of the result.
This is the fixnum result of the following computation:

@example
(fxior (fxand @vari{fx} @varii{fx})
       (fxand (fxnot @vari{fx}) @variii{fx}))
@end example
@end deffn


@deffn Procedure {fxbit-count} @var{fx}
If @var{fx} is non--negative, this procedure returns the number of 1
bits in the two's complement representation of @var{fx}.  Otherwise it
returns the result of the following computation:

@example
(fxnot (fxbit-count (fxnot @var{ei})))
@end example
@end deffn


@deffn Procedure {fxlength} @var{fx}
Return the number of bits needed to represent @var{fx} if it is
positive, and the number of bits needed to represent @code{(fxnot
@var{fx})} if it is negative, which is the fixnum result of the
following computation:

@example
(do ((result 0 (+ result 1))
     (bits (if (fxnegative? @var{fx})
               (fxnot @var{fx})
               @var{fx})
           (fxarithmetic-shift-right bits 1)))
    ((fxzero? bits)
     result))
@end example
@end deffn


@deffn Procedure {fxfirst-bit-set} @var{fx}
Return the index of the least significant 1 bit in the two's complement
representation of @var{fx}.  If @var{fx} is 0, then -1 is returned.

@example
(fxfirst-bit-set 0)        @result{}  -1
(fxfirst-bit-set 1)        @result{}  0
(fxfirst-bit-set -4)       @result{}  2
@end example
@end deffn


@deffn Procedure {fxbit-set?} @vari{fx} @varii{fx}
@varii{fx} must be non--negative and less than @code{(fixnum-width)}.

The @func{fxbit-set?} procedure returns @true{} if the @varii{fx}th bit
is 1 in the two's complement representation of @vari{fx}, and @false{}
otherwise.  This is the fixnum result of the following computation:

@example
(not
  (fxzero?
    (fxand @vari{fx}
           (fxarithmetic-shift-left 1 @varii{fx}))))
@end example
@end deffn


@deffn Procedure {fxcopy-bit} @vari{fx} @varii{fx} @variii{fx}
@varii{fx} must be non--negative and less than @code{(fixnum-width)}.
@variii{Fx} must be 0 or 1.

The @func{fxcopy-bit} procedure returns the result of replacing the
@varii{fx}th bit of @vari{fx} by @variii{fx}, which is the result of the
following computation:

@example
(let* ((mask (fxarithmetic-shift-left 1 @varii{fx})))
  (fxif mask
        (fxarithmetic-shift-left @variii{fx} @varii{fx})
        @vari{fx}))
@end example
@end deffn


@deffn Procedure {fxbit-field} @vari{fx} @varii{fx} @variii{fx}
@varii{fx} and @variii{fx} must be non-negative and less than
@code{(fixnum-width)}.  Moreover, @varii{fx} must be less than or equal
to @variii{fx}.

The @code{fxbit-field} procedure returns the number represented by the
bits at the positions from @varii{fx} (inclusive) to @variii{fx}
(exclusive), which is the fixnum result of the following computation:

@example
(let* ((mask (fxnot
              (fxarithmetic-shift-left -1 @variii{fx}))))
  (fxarithmetic-shift-right (fxand @vari{fx} mask)
                            @varii{fx}))
@end example
@end deffn


@deffn Procedure {fxcopy-bit-field} @vari{fx} @varii{fx} @variii{fx} @variv{fx}
@varii{fx} and @variii{fx} must be non-negative and less than
@code{(fixnum-width)}.  Moreover, @varii{fx} must be less than or equal
to @variii{fx}.

The @func{fxcopy-bit-field} procedure returns the result of replacing in
@vari{fx} the bits at positions from @varii{fx} (inclusive) to
@variii{fx} (exclusive) by the bits in @variv{fx} from position 0
(inclusive) to position @variii{fx}-@varii{fx} (exclusive), which is the
fixnum result of the following computation:

@example
(let* ((to    @vari{fx})
       (start @varii{fx})
       (end   @variii{fx})
       (from  @variv{fx})
       (mask1 (fxarithmetic-shift-left -1 start))
       (mask2 (fxnot (fxarithmetic-shift-left -1 end)))
       (mask  (fxand mask1 mask2))
       (mask3 (fxnot (fxarithmetic-shift-left -1 (- end start)))))
  (fxif mask
        (fxarithmetic-shift-left (fxand from mask3)
                                 start)
        to))
@end example
@end deffn


@deffn Procedure {fxarithmetic-shift} @vari{fx} @varii{fx}
The absolute value of @varii{fx} must be less than
@code{(fixnum-width)}.

If:

@example
(floor (* @vari{fx} (expt 2 @varii{fx})))
@end example

@noindent
is a fixnum, then that fixnum is returned.  Otherwise an exception with
condition type @code{&implementation-restriction} is raised.
@end deffn


@deffn Procedure {fxarithmetic-shift-left} @vari{fx} @varii{fx}
@deffnx Procedure {fxarithmetic-shift-right} @vari{fx} @varii{fx}
@varii{fx} must be non--negative, and less than @code{(fixnum-width)}.


The @func{fxarithmetic-shift-left} procedure behaves the same as
@func{fxarithmetic-shift}, and @code{(fxarithmetic-shift-right @vari{fx}
@varii{fx})} behaves the same as @code{(fxarithmetic-shift @vari{fx}
(fx- @varii{fx}))}.
@end deffn


@deffn Procedure {fxrotate-bit-field} @vari{fx} @varii{fx} @variii{fx} @variv{fx}
@varii{fx}, @variii{fx}, and @variv{fx} must be non--negative and less
than @code{(fixnum-width)}.  @varii{fx} must be less than or equal to
@variii{fx}.  @variv{fx} must be less than the difference between
@variii{fx} and @varii{fx}.

The @func{fxrotate-bit-field} procedure returns the result of cyclically
permuting in @vari{fx} the bits at positions from @varii{fx} (inclusive)
to @variii{fx} (exclusive) by @variv{fx} bits towards the more
significant bits, which is the result of the following computation:

@example
(let* ((n     @vari{fx})
       (start @varii{fx})
       (end   @variii{fx})
       (count @variv{fx})
       (width (fx- end start)))
  (if (fxpositive? width)
      (let* ((count (fxmod count width))
             (field0
               (fxbit-field n start end))
             (field1
               (fxarithmetic-shift-left
                 field0 count))
             (field2
               (fxarithmetic-shift-right
                 field0 (fx- width count)))
             (field (fxior field1 field2)))
        (fxcopy-bit-field n start end field))
      n))
@end example
@end deffn


@deffn Procedure {fxreverse-bit-field} @vari{fx} @varii{fx} @variii{fx}
@varii{fx} and @variii{fx} must be non-negative and less than
@code{(fixnum-width)}.  Moreover, @varii{fx} must be less than or equal
to @variii{fx}.

The @code{fxreverse-bit-field} procedure returns the fixnum obtained
from @vari{fx} by reversing the order of the bits at positions from
@varii{fx} (inclusive) to @variii{fx} (exclusive).

@example
(fxreverse-bit-field #b1010010 1 4)       @result{}  88 ; #b1011000
@end example
@end deffn


@c page
@node stdlib arithmetics flonum
@subsection Flonums


This section describes the @rsixlibrary{arithmetic flonums} library.

This section uses @var{fl}, @vari{fl}, @varii{fl}, etc., as parameter
names for arguments that must be flonums, and @var{ifl} as a name for
arguments that must be integer--valued flonums, i.e., flonums for which
the @func{integer-valued?} predicate returns true.


@deffn Procedure {flonum?} @var{obj}
Return @true{} if @var{obj} is a flonum, @false{} otherwise.
@end deffn


@deffn Procedure {real->flonum} @var{x}
Return the best flonum representation of @var{x}.

The value returned is a flonum that is numerically closest to the
argument.

@quotation
@emph{Note:} If flonums are represented in binary floating point, then
implementations should break ties by preferring the floating--point
representation whose least significant bit is zero.
@end quotation
@end deffn


@deffn Procedure {fl=?} @vari{fl} @varii{fl} @variii{fl} @dots{}
@deffnx Procedure {fl<?} @vari{fl} @varii{fl} @variii{fl} @dots{}
@deffnx Procedure {fl<=?} @vari{fl} @varii{fl} @variii{fl} @dots{}
@deffnx Procedure {fl>?} @vari{fl} @varii{fl} @variii{fl} @dots{}
@deffnx Procedure {fl>=?} @vari{fl} @varii{fl} @variii{fl} @dots{}
These procedures return @true{} if their arguments are (respectively):
equal, monotonically increasing, monotonically decreasing, monotonically
nondecreasing, or monotonically nonincreasing, @false{} otherwise.
These predicates must be transitive.

@example
(fl=? +inf.0 +inf.0)            @result{} #t
(fl=? -inf.0 +inf.0)            @result{} #f
(fl=? -inf.0 -inf.0)            @result{} #t
(fl=? 0.0 -0.0)                 @result{} #t
(fl<? 0.0 -0.0)                 @result{} #f
(fl=? +nan.0 fl)                @result{} #f
(fl<? +nan.0 fl)                @result{} #f
@end example
@end deffn


@deffn Procedure {flinteger?} @var{fl}
@deffnx Procedure {flzero?} @var{fl}
@deffnx Procedure {flpositive?} @var{fl}
@deffnx Procedure {flnegative?} @var{fl}
@deffnx Procedure {flodd?} @var{ifl}
@deffnx Procedure {fleven?} @var{ifl}
@deffnx Procedure {flfinite?} @var{fl}
@deffnx Procedure {flinfinite?} @var{fl}
@deffnx Procedure {flnan?} @var{fl}
These numerical predicates test a flonum for a particular property,
returning @true{} or @false{}:

@table @func
@item flinteger?
procedure tests whether the number object is an integer,

@item flzero?
tests whether it is @func{fl=?} to zero,

@item flpositive?
tests whether it is greater than zero,

@item flnegative?
tests whether it is less than zero,

@item flodd?
tests whether it is odd,

@item fleven?
tests whether it is even,

@item flfinite?
tests whether it is not an infinity and not a NaN,

@item flinfinite?
tests whether it is an infinity,

@item flnan?
tests whether it is a NaN.
@end table

@example
(flnegative? -0.0)              @result{} #f
(flfinite?   +inf.0)            @result{} #f
(flfinite?   5.0)               @result{} #t
(flinfinite? 5.0)               @result{} #f
(flinfinite? +inf.0)            @result{} #t
@end example

@quotation
@emph{Note:} @code{(flnegative? -0.0)} must return @false{}, else it
would lose the correspondence with @code{(fl< -0.0 0.0)}, which is
@false{} according to @ieee{} 754.
@end quotation
@end deffn


@deffn Procedure {flmax} @vari{fl} @varii{fl} @dots{}
@deffnx Procedure {flmin} @vari{fl} @varii{fl} @dots{}
These procedures return the maximum or minimum of their arguments.  They
always return a NaN when one or more of the arguments is a NaN.
@end deffn


@deffn Procedure {fl+} @vari{fl} @dots{}
@deffnx Procedure {fl*} @vari{fl} @dots{}
These procedures return the flonum sum or product of their flonum
arguments.  In general, they should return the flonum that best
approximates the mathematical sum or product.  (For implementations that
represent flonums using @ieee{} binary floating point, the meaning of
``best'' is defined by the @ieee{} standards.)

@example
(fl+ +inf.0 -inf.0)             @result{}  +nan.0
(fl+ +nan.0 @var{fl})           @result{}  +nan.0
(fl* +nan.0 @var{fl})           @result{}  +nan.0
@end example
@end deffn


@deffn Procedure {fl-} @vari{fl} @varii{fl} @dots{}
@deffnx Procedure {fl-} @var{fl}
@deffnx Procedure {fl/} @vari{fl} @varii{fl} @dots{}
@deffnx Procedure {fl/} @var{fl}
With two or more arguments, these procedures return the flonum
difference or quotient of their flonum arguments, associating to the
left.

With one argument, however, they return the additive or multiplicative
flonum inverse of their argument.

In general, they should return the flonum that best approximates the
mathematical difference or quotient.  (For implementations that
represent flonums using @ieee{} binary floating point, the meaning of
``best'' is reasonably well--defined by the @ieee{} standards.)

@example
(fl- +inf.0 +inf.0)             @result{}  +nan.0
@end example

For undefined quotients, @func{fl/} behaves as specified by the @ieee{}
standards:

@example
(fl/ 1.0 0.0)                   @result{} +inf.0
(fl/ -1.0 0.0)                  @result{} -inf.0
(fl/ 0.0 0.0)                   @result{} +nan.0
@end example
@end deffn


@deffn Procedure {flabs} @var{fl}
Return the absolute value of @var{fl}.
@end deffn


@deffn Procedure {fldiv-and-mod} @vari{fl} @varii{fl}
@deffnx Procedure {fldiv} @vari{fl} @varii{fl}
@deffnx Procedure {flmod} @vari{fl} @varii{fl}
@deffnx Procedure {fldiv0-and-mod0} @vari{fl} @varii{fl}
@deffnx Procedure {fldiv0} @vari{fl} @varii{fl}
@deffnx Procedure {flmod0} @vari{fl} @varii{fl}
These procedures implement number--theoretic integer division and return
the results of the corresponding mathematical operations specified in
report section @ref{baselib math semantics integer}.

In the cases where the mathematical requirements in @ref{baselib math
semantics} cannot be satisfied by any number object, either an exception
is raised with condition type @code{&implementation-restriction}, or
unspecified flonums (one for @func{fldiv}, @func{flmod}, @func{fldiv0}
and @func{flmod0}, two for @func{fldiv-and-mod} and
@func{fldiv0-and-mod0}) are returned.

@example
(fldiv @vari{fl} @varii{fl})         @result{} @vari{fl} div @varii{fl}
(flmod @vari{fl} @varii{fl})         @result{} @vari{fl} mod @varii{fl}
(fldiv-and-mod @vari{fl} @varii{fl}) @result{} @vari{fl} div @varii{fl}, @vari{fl} mod @varii{fl}
                                        ; two return values
(fldiv0 @vari{fl} @varii{fl})        @result{} @vari{fl} div_0 @varii{fl}
(flmod0 @vari{fl} @varii{fl})        @result{} @vari{fl} mod_0 @varii{fl}
(fldiv0-and-mod0 @vari{fl} @varii{fl})
@result{} @vari{fl} div_0 @varii{fl}, @vari{fl} mod_0 @varii{fl}
   ; two return values
@end example
@end deffn


@deffn Procedure {flnumerator} @var{fl}
@deffnx Procedure {fldenominator} @var{fl}
These procedures return the numerator or denominator of @var{fl} as a
flonum; the result is computed as if @var{fl} was represented as a
fraction in lowest terms.  The denominator is always positive.  The
denominator of @math{0.0} is defined to be @math{1.0}.

@example
(flnumerator +inf.0)           @result{} +inf.0
(flnumerator -inf.0)           @result{} -inf.0
(fldenominator +inf.0)         @result{} 1.0
(fldenominator -inf.0)         @result{} 1.0
(flnumerator 0.75)             @result{} 3.0 ; probably
(fldenominator 0.75)           @result{} 4.0 ; probably
@end example

Implementations should implement following behavior:

@example
(flnumerator -0.0)             @result{} -0.0
@end example
@end deffn


@deffn Procedure {flfloor} @var{fl}
@deffnx Procedure {flceiling} @var{fl}
@deffnx Procedure {fltruncate} @var{fl}
@deffnx Procedure {flround} @var{fl}
These procedures return integral flonums for flonum arguments that are
not infinities or NaNs.

@table @func
@item flfloor
Returns the largest integral flonum not larger than @var{fl}.

@item flceiling
Returns the smallest integral flonum not smaller than @var{fl}.

@item fltruncate
Returns the integral flonum closest to @var{fl} whose absolute value is
not larger than the absolute value of @var{fl}.

@item flround
Returns the closest integral flonum to @var{fl}, rounding to even when
@var{fl} represents a number halfway between two integers.
@end table

Although infinities and NaNs are not integer objects, these procedures
return an infinity when given an infinity as an argument, and a NaN when
given a NaN:

@example
(flfloor +inf.0)                @result{} +inf.0
(flceiling -inf.0)              @result{} -inf.0
(fltruncate +nan.0)             @result{} +nan.0
@end example
@end deffn


@deffn Procedure {flexp} @var{fl}
@deffnx Procedure {fllog} @var{fl}
@deffnx Procedure {fllog} @vari{fl} @varii{fl}
@deffnx Procedure {flsin} @var{fl}
@deffnx Procedure {flcos} @var{fl}
@deffnx Procedure {fltan} @var{fl}
@deffnx Procedure {flasin} @var{fl}
@deffnx Procedure {flacos} @var{fl}
@deffnx Procedure {flatan} @var{fl}
@deffnx Procedure {flatan} @vari{fl} @varii{fl}
These procedures compute the usual transcendental functions.

@table @func
@item flexp
Computes the base-E exponential of @var{fl}.

@item fllog
With a single argument computes the natural logarithm of @var{fl} (not
the base ten logarithm); @code{(fllog @vari{fl} @varii{fl})} computes
the base--@varii{fl} logarithm of @vari{fl}.

@item flasin
@itemx flacos
@item flatan
Compute arcsine, arccosine, and arctangent, respectively.  @code{(flatan
@vari{fl} @varii{fl})} computes the arc tangent of @vari{fl}/@varii{fl}.
@end table

@ref{baselib math ops trascend} for the underlying mathematical
operations.  In the event that these operations do not yield a real
result for the given arguments, the result may be a NaN, or may be some
unspecified flonum.

Implementations that use @ieee{} binary floating--point arithmetic
should follow the relevant standards for these procedures.

@example
(flexp +inf.0)          @result{} +inf.0
(flexp -inf.0)          @result{} 0.0
(fllog +inf.0)          @result{} +inf.0
(fllog 0.0)             @result{} -inf.0
(fllog -0.0)            @result{} unspecified ; if -0.0 is distinguished
(fllog -inf.0)          @result{} +nan.0
(flatan -inf.0)         @result{} -1.5707963267948965  ; approximately
(flatan +inf.0)         @result{} 1.5707963267948965   ; approximately
@end example
@end deffn


@deffn Procedure {flsqrt} @var{fl}
Returns the principal square root of @var{fl}.  For @math{-0.0},
@func{flsqrt} should return @math{-0.0}; for other negative arguments,
the result may be a NaN or some unspecified flonum.

@example
(flsqrt +inf.0)         @result{} +inf.0
(flsqrt -0.0)           @result{} -0.0
@end example
@end deffn


@deffn Procedure {flexpt} @vari{fl} @varii{fl}
Either @vari{fl} should be non--negative, or, if @vari{fl} is negative,
@varii{fl} should be an integer object.

The @func{flexpt} procedure returns @vari{fl} raised to the power
@varii{fl}.  If @vari{fl} is negative and @varii{fl} is not an integer
object, the result may be a NaN, or may be some unspecified flonum.

If @vari{fl} and @varii{fl} are both zero, the result is @math{1.0}.  If
@vari{fl} is zero and @varii{fl} is positive, the result is zero.  If
@vari{fl} is negative, the result may be a NaN, or may be some
unspecified flonum.
@end deffn


@tindex &no-infinities
@deffn {Condition Type} {&no-infinities}
@deffnx Procedure {make-no-infinities-violation}
@deffnx Procedure {no-infinities-violation?} @var{obj}
@deffnx {Condition Type} {&no-nans}
@deffnx Procedure {make-no-nans-violation}
@deffnx Procedure {no-nans-violation?} @var{obj}
These condition types could be defined by the following code:

@example
(define-condition-type &no-infinities
    &implementation-restriction
  make-no-infinities-violation
  no-infinities-violation?)

(define-condition-type &no-nans
    &implementation-restriction
  make-no-nans-violation no-nans-violation?)
@end example

These types describe that a program has executed an arithmetic
operations that is specified to return an infinity or a NaN,
respectively, on a Scheme implementation that is not able to represent
the infinity or NaN.  @ref{baselib math infinities}.
@end deffn


@deffn Procedure {fixnum->flonum} @var{fx}
Return a flonum that is numerically closest to @var{fx}.

@quotation
@emph{Note:} The result of this procedure may not be numerically equal
to @var{fx}, because the fixnum precision may be greater than the flonum
precision.
@end quotation
@end deffn


@c page
@node stdlib arithmetics exact bitwise
@subsection Exact bitwise arithmetic


This section describes the @rsixlibrary{arithmetic bitwise} library.
The exact bitwise arithmetic provides generic operations on exact
integer objects.  This section uses @var{ei}, @vari{ei}, @varii{ei},
etc., as parameter names that must be exact integer objects.


@deffn Procedure bitwise-not @var{ei}
Returns the exact integer object whose two's complement representation
is the one's complement of the two's complement representation of
@var{ei}.
@end deffn


@deffn Procedure bitwise-and @vari{ei} @dots{}
@deffnx Procedure bitwise-ior @vari{ei} @dots{}
@deffnx Procedure bitwise-xor @vari{ei} @dots{}
These procedures return the exact integer object that is the bit--wise
``and'', ``inclusive or'', or ``exclusive or'' of the two's complement
representations of their arguments.  If they are passed only one
argument, they return that argument.  If they are passed no arguments,
they return the integer object (either @math{-1} or @math{0}) that acts
as identity for the operation.
@end deffn


@deffn Procedure bitwise-if @vari{ei} @varii{ei} @variii{ei}
Return the exact integer object that is the bit--wise ``if'' of the
two's complement representations of its arguments, i.e. for each bit, if
it is @math{1} in @vari{ei}, the corresponding bit in @varii{ei} becomes
the value of the corresponding bit in the result, and if it is @math{0},
the corresponding bit in @variii{ei} becomes the corresponding bit in
the value of the result.  This is the result of the following
computation:

@example
(bitwise-ior (bitwise-and @vari{ei} @varii{ei})
             (bitwise-and (bitwise-not @vari{ei}) @variii{ei}))
@end example
@end deffn


@deffn Procedure bitwise-bit-count @var{ei}
If @var{ei} is non--negative, this procedure returns the number of 1
bits in the two's complement representation of @var{ei}.  Otherwise it
returns the result of the following computation:

@example
(bitwise-not (bitwise-bit-count (bitwise-not @var{ei})))
@end example
@end deffn


@deffn Procedure bitwise-length @var{ei}
Return the number of bits needed to represent @var{ei} if it is
positive, and the number of bits needed to represent @code{(bitwise-not
@var{ei})} if it is negative, which is the exact integer object that is
the result of the following computation:

@example
(do ((result 0 (+ result 1))
     (bits (if (negative? @var{ei})
               (bitwise-not @var{ei})
               @var{ei})
           (bitwise-arithmetic-shift bits -1)))
    ((zero? bits)
     result))
@end example
@end deffn


@deffn Procedure bitwise-first-bit-set @var{ei}
Returns the index of the least significant @math{1} bit in the two's
complement representation of @var{ei}.  If @var{ei} is @math{0}, then
@math{-1} is returned.

@example
(bitwise-first-bit-set 0)        @result{} -1
(bitwise-first-bit-set 1)        @result{} 0
(bitwise-first-bit-set -4)       @result{} 2
@end example
@end deffn


@deffn Procedure bitwise-bit-set? @vari{ei} @varii{ei}
@varii{ei} must be non--negative.

The @func{bitwise-bit-set?} procedure returns @true{} if the
@varii{ei}th bit is @math{1} in the two's complement representation of
@vari{ei}, and @false{} otherwise.  This is the result of the following
computation:

@example
(not (zero?
       (bitwise-and
         (bitwise-arithmetic-shift-left 1 @varii{ei})
         @vari{ei})))
@end example
@end deffn


@deffn Procedure bitwise-copy-bit @vari{ei} @varii{ei} @variii{ei}
@varii{ei} must be non--negative, and @variii{ei} must be either
@math{0} or @math{1}.

The @func{bitwise-copy-bit} procedure returns the result of replacing
the @varii{ei}th bit of @vari{ei} by @variii{ei}, which is the result of
the following computation:

@example
(let* ((mask (bitwise-arithmetic-shift-left 1 @varii{ei})))
  (bitwise-if mask
            (bitwise-arithmetic-shift-left @variii{ei} @varii{ei})
            @vari{ei}))
@end example
@end deffn


@deffn Procedure bitwise-bit-field @vari{ei} @varii{ei} @variii{ei}
@varii{ei} and @variii{ei} must be non--negative, and @varii{ei} must be
less than or equal to @variii{ei}.

The @func{bitwise-bit-field} procedure returns the number represented by
the bits at the positions from @varii{ei} (inclusive) to @variii{ei}
(exclusive), which is the result of the following computation:

@example
(let ((mask
       (bitwise-not
        (bitwise-arithmetic-shift-left -1 @variii{ei}))))
  (bitwise-arithmetic-shift-right
    (bitwise-and @vari{ei} mask)
    @varii{ei}))
@end example
@end deffn


@deffn Procedure bitwise-copy-bit-field @vari{ei} @varii{ei} @variii{ei} @variv{ei}
@varii{ei} and @variii{ei} must be non--negative, and @varii{ei} must be
less than or equal to @variii{ei}.

The @func{bitwise-copy-bit-field} procedure returns the result of
replacing in @vari{ei} the bits at positions from @varii{ei} (inclusive)
to @variii{ei} (exclusive) by the bits in @variv{ei} from position 0
(inclusive) to position @math{@variii{ei} - @varii{ei}} (exclusive)
which is the result of the following computation:

@example
(let* ((to    @vari{ei})
       (start @varii{ei})
       (end   @variii{ei})
       (from  @variv{ei})
       (mask1
         (bitwise-arithmetic-shift-left -1 start))
       (mask2
         (bitwise-not
           (bitwise-arithmetic-shift-left -1 end)))
       (mask (bitwise-and mask1 mask2)))
  (bitwise-if mask
              (bitwise-arithmetic-shift-left from
                                             start)
              to))
@end example
@end deffn


@deffn Procedure bitwise-arithmetic-shift @vari{ei} @varii{ei}
Return the result of the following computation:

@example
(floor (* @vari{ei} (expt 2 @varii{ei})))
@end example

Examples:

@example
(bitwise-arithmetic-shift -6 -1)    @result{} -3
(bitwise-arithmetic-shift -5 -1)    @result{} -3
(bitwise-arithmetic-shift -4 -1)    @result{} -2
(bitwise-arithmetic-shift -3 -1)    @result{} -2
(bitwise-arithmetic-shift -2 -1)    @result{} -1
(bitwise-arithmetic-shift -1 -1)    @result{} -1
@end example
@end deffn


@deffn Procedure bitwise-arithmetic-shift-left @vari{ei} @varii{ei}
@deffnx Procedure bitwise-arithmetic-shift-right @vari{ei} @varii{ei}
@varii{ei} must be non--negative.

The @func{bitwise-arithmetic-shift-left} procedure returns the same
result as @func{bitwise-arithmetic-shift}, and:

@example
(bitwise-arithmetic-shift-right @vari{ei} @varii{ei})
@end example

@noindent
returns the same result as:

@example
(bitwise-arithmetic-shift @vari{ei} (- @varii{ei}))
@end example
@end deffn


@deffn Procedure bitwise-rotate-bit-field @vari{ei} @varii{ei} @variii{ei} @variv{ei}
@varii{ei}, @variii{ei}, @variv{ei} must be non--negative, @varii{ei}
must be less than or equal to @variii{ei}, and @variv{ei} must be
non-negative.

The procedure returns the result of cyclically permuting in @vari{ei}
the bits at positions from @varii{ei} (inclusive) to @variii{ei}
(exclusive) by @variv{ei} bits towards the more significant bits, which
is the result of the following computation:

@example
(let* ((n     @vari{ei})
       (start @varii{ei})
       (end   @variii{ei})
       (count @variv{ei})
       (width (- end start)))
  (if (positive? width)
      (let* ((count (mod count width))
             (field0
               (bitwise-bit-field n start end))
             (field1 (bitwise-arithmetic-shift-left
                       field0 count))
             (field2 (bitwise-arithmetic-shift-right
                       field0
                       (- width count)))
             (field (bitwise-ior field1 field2)))
        (bitwise-copy-bit-field n start end field))
      n))
@end example
@end deffn


@deffn Procedure bitwise-reverse-bit-field @vari{ei} @varii{ei} @variii{ei}
@varii{ei} and @variii{ei} must be non--negative, and @varii{ei} must be
less than or equal to @variii{ei}.

The @func{bitwise-reverse-bit-field} procedure returns the result
obtained from @vari{ei} by reversing the order of the bits at positions
from @varii{ei} (inclusive) to @variii{ei} (exclusive).

@example
(bitwise-reverse-bit-field #b1010010 1 4)
@result{}  88 ; #b1011000
@end example
@end deffn


@c page
@node stdlib syntax-case
@section Syntax--case


The @rsixlibrary{syntax-case} library provides support for writing
low--level macros in a high--level style, with automatic syntax
checking, input destructuring, output restructuring, maintenance of
lexical scoping and referential transparency (hygiene), and support for
controlled identifier capture.


@menu
* stdlib syntax-case hygiene::          Hygiene.
* stdlib syntax-case objects::          Syntax objects.
* stdlib syntax-case transformers::     Transformers.
* stdlib syntax-case parsing::          Parsing input and producing
                                        output.
* stdlib syntax-case identifier::       Identifier predicates.
* stdlib syntax-case conversion::       Syntax-object and datum
                                        conversions.
* stdlib syntax-case temporaries::      Generating lists of temporaries.
* stdlib syntax-case derived::          Derived forms and procedures.
* stdlib syntax-case violations::       Syntax violations.

Examples

* stdlib syntax-case define-macro::     Common Lisp style macros.
@end menu


@c page
@node stdlib syntax-case hygiene
@subsection Hygiene


Barendregt's @emph{hygiene condition} for the lambda calculus is an
informal notion that requires the free variables of an expression
@emph{N} that is to be substituted into another expression @emph{M} not
to be captured by bindings in @emph{M} when such capture is not
intended.

Kohlbecker, et al. propose a corresponding @emph{hygiene condition for
macro expansion} that applies in all situations where capturing is not
explicit: ``Generated identifiers that become binding instances in the
completely expanded program must only bind variables that are generated
at the same transcription step''.  In the terminology of this document,
the ``generated identifiers'' are those introduced by a transformer
rather than those present in the form passed to the transformer, and a
``macro transcription step'' corresponds to a single call by the
expander to a transformer.  Also, the hygiene condition applies to all
introduced bindings rather than to introduced variable bindings alone.

This leaves open what happens to an introduced identifier that appears
outside the scope of a binding introduced by the same call.  Such an
identifier refers to the lexical binding in effect where it appears
(within a @code{syntax} @hyper{template}) inside the transformer body or
one of the helpers it calls.  This is essentially the referential
transparency property described by Clinger and Rees.  Thus, the hygiene
condition can be restated as follows:

@quotation
A binding for an identifier introduced into the output of a transformer
call from the expander must capture only references to the identifier
introduced into the output of the same transformer call.

A reference to an identifier introduced into the output of a transformer
refers to the closest enclosing binding for the introduced identifier
or, if it appears outside of any enclosing binding for the introduced
identifier, the closest enclosing lexical binding where the identifier
appears (within a @code{syntax} @hyper{template}) inside the transformer
body or one of the helpers it calls.
@end quotation

Explicit captures are handled via @func{datum->syntax}.

Operationally, the expander can maintain hygiene with the help of
@emph{marks}.  Marks are applied selectively by the expander to the
output of each transformer it invokes, and substitutions are applied to
the portions of each binding form that are supposed to be within the
scope of the bound identifiers.  Marks are used to distinguish
like--named identifiers that are introduced at different times (either
present in the source or introduced into the output of a particular
transformer call), and substitutions are used to map identifiers to
their expand-time values.

Each time the expander encounters a macro use, it applies an
@emph{antimark} to the input form, invokes the associated transformer,
then applies a fresh mark to the output.  Marks and antimarks cancel, so
the portions of the input that appear in the output are effectively left
unmarked, while the portions of the output that are introduced are
marked with the fresh mark.

Each time the expander encounters a binding form it creates a set of
substitutions, each mapping one of the (possibly marked) bound
identifiers to information about the binding.  (For a @func{lambda}
expression, the expander might map each bound identifier to a
representation of the formal parameter in the output of the expander.
For a @func{let-syntax} form, the expander might map each bound
identifier to the associated transformer.)  These substitutions are
applied to the portions of the input form in which the binding is
supposed to be visible.

Marks and substitutions together form a @emph{wrap} that is layered on
the form being processed by the expander and pushed down toward the
leaves as necessary.  A wrapped form is referred to as a @emph{wrapped
syntax object}.  Ultimately, the wrap may rest on a leaf that represents
an identifier, in which case the wrapped syntax object is also referred
to as an @emph{identifier}.  An identifier contains a name along with
the wrap.  (Names are typically represented by symbols.)

When a substitution is created to map an identifier to an expand--time
value, the substitution records the name of the identifier and the set
of marks that have been applied to that identifier, along with the
associated expand--time value.  The expander resolves identifier
references by looking for the latest matching substitution to be applied
to the identifier, i.e., the outermost substitution in the wrap whose
name and marks match the name and marks recorded in the substitution.
The name matches if it is the same name (if using symbols, then by
@func{eq?}), and the marks match if the marks recorded with the
substitution are the same as those that appear @emph{below} the
substitution in the wrap, i.e., those that were applied @emph{before}
the substitution.  Marks applied after a substitution, i.e., appear over
the substitution in the wrap, are not relevant and are ignored.

An algebra that defines how marks and substitutions work more precisely
is given in section 2.4 of Oscar Waddell's PhD thesis.


@c page
@node stdlib syntax-case objects
@subsection Syntax objects


A @emph{syntax object} is a representation of a Scheme form that
contains contextual information about the form in addition to its
structure.  This contextual information is used by the expander to
maintain lexical scoping and may also be used by an implementation to
maintain source--object correlation.

A syntax object may be wrapped.  It may also be unwrapped, fully or
partially, i.e., consist of list and vector structure with wrapped
syntax objects or nonsymbol values at the leaves.  More formally, a
syntax object is:

@itemize
@item
a pair of syntax objects,

@item
a vector of syntax objects,

@item
a nonpair, nonvector, nonsymbol value, or

@item
a wrapped syntax object.
@end itemize

The distinction between the terms ``syntax object'' and ``wrapped syntax
object'' is important.  For example, when invoked by the expander, a
transformer must accept a wrapped syntax object but may return any
syntax object, including an unwrapped syntax object.

Syntax objects representing identifiers are always wrapped and are
distinct from other types of values.  Wrapped syntax objects that are
not identifiers may or may not be distinct from other types of values.

@c page
@node stdlib syntax-case transformers
@subsection Transformers


In @func{define-syntax}, @func{let-syntax}, and @func{letrec-syntax}
forms, a binding for a syntactic keyword is an expression that evaluates
to a @emph{transformer}.

A transformer is a @emph{transformation procedure} or a @emph{variable
transformer}.  A transformation procedure is a procedure that must
accept one argument, a wrapped syntax object representing the input, and
return a syntax object representing the output.  The transformer is
called by the expander whenever a reference to a keyword with which it
has been associated is found.  If the keyword appears in the car of a
list--structured input form, the transformer receives the entire
list--structured form, and its output replaces the entire form.  Except
with variable transformers (see below), if the keyword is found in any
other definition or expression context, the transformer receives a
wrapped syntax object representing just the keyword reference, and its
output replaces just the reference.  Except with variable transformers,
an exception with condition type @code{&syntax} is raised if the keyword
appears on the left--hand side of a @func{set!} expression.


@deffn Procedure make-variable-transformer @var{proc}
@var{proc} should accept one argument, a wrapped syntax object, and
return a syntax object.

The @func{make-variable-transformer} procedure creates a @emph{variable
transformer}.  A variable transformer is like an ordinary transformer
except that, if a keyword associated with a variable transformer appears
on the left--hand side of a @func{set!} expression, an exception is not
raised.  Instead, @var{proc} is called with a wrapped syntax object
representing the entire @func{set!} expression as its argument, and its
return value replaces the entire @func{set!}  expression.

@strong{Implementation responsibilities:} The implementation must check
the restrictions on @var{proc} only to the extent performed by applying
it as described.  An implementation may check whether @var{proc} is an
appropriate argument before applying it.
@end deffn


@c page
@node stdlib syntax-case parsing
@subsection Parsing input and producing output


Transformers can destructure their input with @func{syntax-case} and
rebuild their output with @code{syntax}.


@deffn Syntax syntax-case @hyper{expression} (@hyper{literal} @dots{}) @hyper{syntax-case clause} @dots{}
@deffnx {Auxiliary Syntax} _
@deffnx {Auxiliary Syntax} {...}
Each @hyper{literal} must be an identifier.  Each @hyper{syntax-case
clause} must take one of the following two forms.

@example
(@hyper{pattern} @hyper{output expression})
(@hyper{pattern} @hyper{fender} @hyper{output expression})
@end example

@hyper{fender} and @hyper{output expression} must be
@hyper{expression}s.

A @hyper{pattern} is an identifier, constant, or one of the following.

@example
(@hyper{pattern} @dots{})
(@hyper{pattern} @hyper{pattern} @dots{} . @hyper{pattern})
(@hyper{pattern} @dots{} @hyper{pattern} @hyper{ellipsis} @hyper{pattern} @dots{})
(@hyper{pattern} @dots{} @hyper{pattern} @hyper{ellipsis} @hyper{pattern} @dots{} . @hyper{pattern})
#(@hyper{pattern} @dots{})
#(@hyper{pattern} @dots{} @hyper{pattern} @hyper{ellipsis} @hyper{pattern} @dots{})
@end example

An @hyper{ellipsis} is the identifier ``@code{...}'' (three periods).

An identifier appearing within a @hyper{pattern} may be an underscore
@code{_}, a literal identifier listed in the list of literals
@code{(@hyper{literal} @dots{})}, or an ellipsis @code{...}.  All other
identifiers appearing within a @hyper{pattern} are @emph{pattern
variables}.  It is a syntax violation if an ellipsis or underscore
appears in @code{(@hyper{literal} @dots{})}.

@code{_} and @code{...} are the same as in the @rsixlibrary{base}
library.

Pattern variables match arbitrary input subforms and are used to refer
to elements of the input.  It is a syntax violation if the same pattern
variable appears more than once in a @hyper{pattern}.

Underscores also match arbitrary input subforms but are not pattern
variables and so cannot be used to refer to those elements.  Multiple
underscores may appear in a @hyper{pattern}.

A literal identifier matches an input subform if and only if the input
subform is an identifier and either both its occurrence in the input
expression and its occurrence in the list of literals have the same
lexical binding, or the two identifiers have the same name and both have
no lexical binding.

A subpattern followed by an ellipsis can match zero or more elements of
the input.

More formally, an input form @emph{F} matches a pattern @emph{P} if and
only if one of the following holds:

@itemize
@item
@emph{P} is an underscore (@code{_}).

@item
@emph{P} is a pattern variable.

@item
@emph{P} is a literal identifier and @emph{F} is an equivalent
identifier in the sense of @func{free-identifier=?}.

@item
@emph{P} is of the form:

@example
(P_1 ... P_n)
@end example

@noindent
and @emph{F} is a list of @emph{n} elements that match @emph{P_1}
through @emph{P_n}.

@item
@emph{P} is of the form:

@example
(P_1 ... P_n . P_x)
@end example

@noindent
and @emph{F} is a list or improper list of @emph{n} or more elements
whose first @emph{n} elements match @emph{P_1} through @emph{P_n} and
whose @emph{n}th cdr matches @emph{P_x}.

@item
@emph{P} is of the form:

@example
(P_1 ... P_k P_e @hyper{ellipsis} P_(m+1) ... P_n)
@end example

@noindent
where @hyper{ellipsis} is the identifier @code{...}  and @emph{F} is a
proper list of @emph{n} elements whose first @emph{k} elements match
@emph{P_1} through @emph{P_k}, whose next @emph{m-k} elements each match
@emph{P_e}, and whose remaining @emph{n-m} elements match @emph{P_(m+1)}
through @emph{P_n}.

@item
@emph{P} is of the form:

@example
(P_1 ... P_k P_e @hyper{ellipsis} P_(m+1) ... P_n . P_x)
@end example

@noindent
where @hyper{ellipsis} is the identifier @code{...}  and @emph{F} is a
list or improper list of @emph{n} elements whose first @emph{k} elements
match @emph{P_1} through @emph{P_k}, whose next @emph{m-k} elements each
match @emph{P_e}, whose next @emph{n-m} elements match @emph{P_(m+1)}
through @emph{P_n}, and whose @emph{n}th and final cdr matches
@emph{P_x}.

@item
@emph{P} is of the form:

@example
#(P_1 ... P_n)
@end example

@noindent
and @emph{F} is a vector of @emph{n} elements that match @emph{P_1}
through @emph{P_n}.

@item
@emph{P} is of the form:

@example
#(P_1 ... P_k P_e @hyper{ellipsis} P_(m+1) ... P_n)
@end example

@noindent
where @hyper{ellipsis} is the identifier @code{...}  and @emph{F} is a
vector of @emph{n} or more elements whose first @emph{k} elements match
@emph{P_1} through @emph{P_k}, whose next @emph{m-k} elements each match
@emph{P_e}, and whose remaining @emph{n-m} elements match @emph{P_(m+1)}
through @emph{P_n}.

@item
@emph{P} is a pattern datum (any nonlist, nonvector, nonsymbol datum)
and @emph{F} is equal to @emph{P} in the sense of the @func{equal?}
procedure.
@end itemize

A @func{syntax-case} expression first evaluates @hyper{expression}.  It
then attempts to match the @hyper{pattern} from the first
@hyper{syntax-case clause} against the resulting value, which is
unwrapped as necessary to perform the match.  If the pattern matches the
value and no @hyper{fender} is present, @hyper{output expression} is
evaluated and its value returned as the value of the @func{syntax-case}
expression.  If the pattern does not match the value, @func{syntax-case}
tries the second @hyper{syntax-case clause}, then the third, and so on.
It is a syntax violation if the value does not match any of the
patterns.

If the optional @hyper{fender} is present, it serves as an additional
constraint on acceptance of a clause.  If the @hyper{pattern} of a given
@hyper{syntax-case clause} matches the input value, the corresponding
@hyper{fender} is evaluated.  If @hyper{fender} evaluates to a true
value, the clause is accepted; otherwise, the clause is rejected as if
the pattern had failed to match the value.  Fenders are logically a part
of the matching process, i.e., they specify additional matching
constraints beyond the basic structure of the input.

Pattern variables contained within a clause's @hyper{pattern} are bound
to the corresponding pieces of the input value within the clause's
@hyper{fender} (if present) and @hyper{output expression}.  Pattern
variables can be referenced only within @code{syntax} expressions (see
below).  Pattern variables occupy the same name space as program
variables and keywords.

If the @func{syntax-case} form is in tail context, the @hyper{output
expression}s are also in tail position.
@end deffn


@deffn Syntax syntax @hyper{template}
A @func{syntax} expression is similar to a @func{quote} expression
except that:

@enumerate
@item
the values of pattern variables appearing within @hyper{template} are
inserted into @hyper{template},

@item
contextual information associated both with the input and with the
template is retained in the output to support lexical scoping, and

@item
the value of a @func{syntax} expression is a syntax object.
@end enumerate

@quotation
@emph{Note:} The following sharp--quote expression:

@example
#'@hyper{template}
@end example

@noindent
is equivalent to:

@example
(syntax @hyper{template})
@end example

@noindent
@ref{lang lex datum abbreviations} for the other abbreviations.
@end quotation

A @hyper{template} can be one among: a pattern variable, an identifier
that is not a pattern variable, a pattern datum, or one of the
following.

@example
(@hyper{subtemplate} @dots{})
(@hyper{subtemplate} @dots{} . @hyper{template})
#(@hyper{subtemplate} @dots{})
@end example

A @hyper{subtemplate} is a @hyper{template} followed by zero or more
ellipses.

The value of a @code{syntax} form is a copy of @hyper{template} in which
the pattern variables appearing within the template are replaced with
the input subforms to which they are bound:

@itemize
@item
pattern data and identifiers that are not pattern variables or ellipses
are copied directly into the output;

@item
a subtemplate followed by an ellipsis expands into zero or more
occurrences of the subtemplate;

@item
pattern variables that occur in subpatterns followed by one or more
ellipses may occur only in subtemplates that are followed by (at least)
as many ellipses:

@itemize -
@item
these pattern variables are replaced in the output by the input subforms
to which they are bound, distributed as specified;

@item
if a pattern variable is followed by more ellipses in the subtemplate
than in the associated subpattern, the input form is replicated as
necessary;

@item
the subtemplate must contain at least one pattern variable from a
subpattern followed by an ellipsis, and for at least one such pattern
variable, the subtemplate must be followed by exactly as many ellipses
as the subpattern in which the pattern variable appears (otherwise, the
expander would not be able to determine how many times the subform
should be repeated in the output);
@end itemize
@end itemize

@noindent
it is a syntax violation if the above constraints are not met.

A template of the form @code{(@hyper{ellipsis} @hyper{template})} is
identical to @hyper{template}, except that ellipses within the template
have no special meaning.  That is, any ellipses contained within
@hyper{template} are treated as ordinary identifiers.  In particular,
the template @code{(... ...)} produces a single ellipsis.  This allows
macro uses to expand into forms containing ellipses.

The output produced by @func{syntax} is wrapped or unwrapped according
to the following rules.

@itemize
@item
the copy of @code{(@hyperi{t} .  @hyperii{t})} is a pair if @hyperi{t}
or @hyperii{t} contain any pattern variables,

@item
the copy of @code{(@hyper{t} @hyper{ellipsis})} is a list if @hyper{t}
contains any pattern variables,

@item
the copy of @code{#(@hyperi{t} ... @hypern{t})} is a vector if any of
@hyperi{t}, @dots{}, @hypern{t} contain any pattern variables,

@item
the copy of any portion of @hyper{t} not containing any pattern
variables is a wrapped syntax object.
@end itemize

The input subforms inserted in place of the pattern variables are
wrapped if and only if the corresponding input subforms are wrapped.
@end deffn


The following definitions of @func{or} illustrate @func{syntax-case} and
@func{syntax}.  The second is equivalent to the first but uses the
@code{#'} prefix instead of the full @func{syntax} form.

@example
(define-syntax or
  (lambda (x)
    (syntax-case x ()
      [(_) (syntax #f)]
      [(_ e) (syntax e)]
      [(_ e1 e2 e3 ...)
       (syntax (let ([t e1])
                 (if t t (or e2 e3 ...))))])))

(define-syntax or
  (lambda (x)
    (syntax-case x ()
      [(_) #'#f]
      [(_ e) #'e]
      [(_ e1 e2 e3 ...)
       #'(let ([t e1])
           (if t t (or e2 e3 ...)))])))
@end example

The examples below define @emph{identifier macros}, macro uses
supporting keyword references that do not necessarily appear in the
first position of a list--structured form.  The second example uses
@func{make-variable-transformer} to handle the case where the keyword
appears on the left-hand side of a @func{set!}  expression.

@example
(define p (cons 4 5))
(define-syntax p.car
  (lambda (x)
    (syntax-case x ()
      [(_ . rest) #'((car p) . rest)]
      [_  #'(car p)])))

p.car                   @result{} 4
(set! p.car 15)         @result{} exception &syntax

(define p (cons 4 5))
(define-syntax p.car
  (make-variable-transformer
    (lambda (x)
      (syntax-case x (set!)
        [(set! _ e) #'(set-car! p e)]
        [(_ . rest) #'((car p) . rest)]
        [_  #'(car p)]))))
(set! p.car 15)

p.car                   @result{} 15
p                       @result{} (15 . 5)
@end example

@c page
@node stdlib syntax-case identifier
@subsection Identifier predicates


@deffn Procedure {identifier?} @var{obj}
Return @true{} if @var{obj} is an identifier, i.e., a syntax object
representing an identifier, and @false{} otherwise.

The @func{identifier?} procedure is often used within a fender to verify
that certain subforms of an input form are identifiers, as in the
definition of @code{rec}, which creates self--contained recursive
objects, below.

@example
(define-syntax rec
  (lambda (x)
    (syntax-case x ()
      ((_ x e)
       (identifier? #'x)
       #'(letrec ((x e)) x)))))

(map (rec fact
       (lambda (n)
         (if (= n 0)
             1
             (* n (fact (- n 1))))))
     '(1 2 3 4 5))    @result{} (1 2 6 24 120)

(rec 5 (lambda (x) x)) @result{} exception &syntax
@end example
@end deffn


The procedures @func{bound-identifier=?} and @func{free-identifier=?}
each take two identifier arguments and return @true{} if their arguments
are equivalent and @false{} otherwise.  These predicates are used to
compare identifiers according to their @emph{intended use} as free
references or bound identifiers in a given context.


@deffn Procedure {bound-identifier=?} @vari{id} @varii{id}
@vari{id} and @varii{id} must be identifiers.

The procedure @func{bound-identifier=?} returns @true{} if a binding for
one would capture a reference to the other in the output of the
transformer, assuming that the reference appears within the scope of the
binding, and @false{} otherwise.

In general, two identifiers are @func{bound-identifier=?} only if both
are present in the original program or both are introduced by the same
transformer application (perhaps implicitly, see @func{datum->syntax}).

Operationally, two identifiers are considered equivalent by
@func{bound-identifier=?} if and only if they have the same name and
same marks.

The @func{bound-identifier=?} procedure can be used for detecting
duplicate identifiers in a binding construct or for other preprocessing
of a binding construct that requires detecting instances of the bound
identifiers.
@end deffn


@deffn Procedure {free-identifier=?} @vari{id} @varii{id}
@vari{id} and @varii{id} must be identifiers.

The @func{free-identifier=?} procedure returns @true{} if and only if
the two identifiers would resolve to the same binding if both were to
appear in the output of a transformer outside of any bindings inserted
by the transformer.  (If neither of two like--named identifiers resolves
to a binding, i.e., both are unbound, they are considered to resolve to
the same binding.)

Operationally, two identifiers are considered equivalent by
@func{free-identifier=?} if and only the topmost matching substitution
for each maps to the same binding or the identifiers have the same name
and no matching substitution.

The @func{syntax-case} and @func{syntax-rules} forms internally use
@func{free-identifier=?} to compare identifiers listed in the literals
list against input identifiers.

@example
(let ((fred 17))
  (define-syntax a
    (lambda (x)
      (syntax-case x ()
        ((_ id) #'(b id fred)))))
  (define-syntax b
    (lambda (x)
      (syntax-case x ()
        ((_ id1 id2)
         #`(list
             #,(free-identifier=? #'id1 #'id2)
             #,(bound-identifier=? #'id1 #'id2))))))
  (a fred))
@result{} (#t #f)
@end example

The following definition of unnamed @func{let} uses
@func{bound-identifier=?} to detect duplicate identifiers.

@example
(define-syntax let
  (lambda (x)
    (define unique-ids?
      (lambda (ls)
        (or (null? ls)
            (and (let notmem?
                        ((x (car ls)) (ls (cdr ls)))
                   (or (null? ls)
                       (and (not (bound-identifier=?
                                   x (car ls)))
                            (notmem? x (cdr ls)))))
                 (unique-ids? (cdr ls))))))
    (syntax-case x ()
      ((_ ((i v) ...) e1 e2 ...)
       (unique-ids? #'(i ...))
       #'((lambda (i ...) e1 e2 ...) v ...)))))
@end example

The argument @code{#'(i ...)} to @func{unique-ids?} is guaranteed to be
a list by the rules given in the description of @func{syntax} above.

With this definition of @func{let}:

@example
(let ((a 3) (a 4)) (+ a a))    @result{} exception &syntax
@end example

However,

@example
(let-syntax
  ((dolet (lambda (x)
            (syntax-case x ()
              ((_ b)
               #'(let ((a 3) (b 4)) (+ a b)))))))
  (dolet a))
@result{} 7
@end example

@noindent
since the identifier @code{a} introduced by @func{dolet} and the
identifier @code{a} extracted from the input form are not
@func{bound-identifier=?}.

Rather than including @code{else} in the literals list as before, this
version of @func{case} explicitly tests for @code{else} using
@func{free-identifier=?}.

@example
(define-syntax case
  (lambda (x)
    (syntax-case x ()
      ((_ e0 ((k ...) e1 e2 ...) ...
              (else-key else-e1 else-e2 ...))
       (and (identifier? #'else-key)
            (free-identifier=? #'else-key #'else))
       #'(let ((t e0))
           (cond
             ((memv t '(k ...)) e1 e2 ...)
             ...
             (else else-e1 else-e2 ...))))
      ((_ e0 ((ka ...) e1a e2a ...)
              ((kb ...) e1b e2b ...) ...)
       #'(let ((t e0))
           (cond
             ((memv t '(ka ...)) e1a e2a ...)
             ((memv t '(kb ...)) e1b e2b ...)
             ...))))))
@end example

With either definition of @func{case}, @code{else} is not recognized as
an auxiliary keyword if an enclosing lexical binding for @code{else}
exists.  For example,

@example
(let ((else @false{}))
  (case 0 (else (write "oops"))))    @result{} exception &syntax
@end example

@noindent
since @code{else} is bound lexically and is therefore not the same
@code{else} that appears in the definition of @func{case}.
@end deffn


@c page
@node stdlib syntax-case conversion
@subsection Syntax--object and datum conversions


@deffn Procedure {syntax->datum} @var{syntax-object}
Strip all syntactic information from a syntax object and returns the
corresponding Scheme datum.

Identifiers stripped in this manner are converted to their symbolic
names, which can then be compared with @func{eq?}.  Thus, a predicate
@code{symbolic-identifier=?} might be defined as follows.

@example
(define symbolic-identifier=?
  (lambda (x y)
    (eq? (syntax->datum x)
         (syntax->datum y))))
@end example
@end deffn


@deffn Procedure {datum->syntax} @var{template-id} @var{datum}
@var{template-id} must be a template identifier and @var{datum} should
be a datum value.

The @func{datum->syntax} procedure returns a syntax-object
representation of @var{datum} that contains the same contextual
information as @var{template-id}, with the effect that the syntax object
behaves as if it were introduced into the code when @var{template-id}
was introduced.

The @func{datum->syntax} procedure allows a transformer to ``bend''
lexical scoping rules by creating @emph{implicit identifiers} that
behave as if they were present in the input form, thus permitting the
definition of macros that introduce visible bindings for or references
to identifiers that do not appear explicitly in the input form.  For
example, the following defines a @func{loop} expression that uses this
controlled form of identifier capture to bind the variable @code{break}
to an escape procedure within the loop body.  (The derived
@func{with-syntax} form is like @func{let} but binds pattern variables.)

@example
(define-syntax loop
  (lambda (x)
    (syntax-case x ()
      ((k e ...)
       (with-syntax
           ((break (datum->syntax #'k 'break)))
         #'(call-with-current-continuation
             (lambda (break)
               (let f () e ... (f)))))))))

(let ((n 3) (ls '()))
  (loop
    (if (= n 0) (break ls))
    (set! ls (cons 'a ls))
    (set! n (- n 1))))
=> (a a a)
@end example

Were @code{loop} to be defined as:

@example
(define-syntax loop
  (lambda (x)
    (syntax-case x ()
      ((_ e ...)
       #'(call-with-current-continuation
           (lambda (break)
             (let f () e ... (f))))))))
@end example

@noindent
the variable @code{break} would not be visible in @code{e ...}.

The datum argument @var{datum} may also represent an arbitrary Scheme
form, as demonstrated by the following definition of @func{include}.

@example
(define-syntax include
  (lambda (x)
    (define read-file
      (lambda (fn k)
        (let ((p (open-file-input-port fn)))
          (let f ((x (get-datum p)))
            (if (eof-object? x)
                (begin (close-port p) '())
                (cons (datum->syntax k x)
                      (f (get-datum p))))))))
    (syntax-case x ()
      ((k filename)
       (let ((fn (syntax->datum #'filename)))
         (with-syntax (((exp ...)
                        (read-file fn #'k)))
           #'(begin exp ...)))))))
@end example

@code{(include "filename")} expands into a @func{begin} expression
containing the forms found in the file named by @code{"filename"}.  For
example, if the file @file{flib.ss} contains:

@example
(define f (lambda (x) (g (* x x))))
@end example

@noindent
and the file @file{glib.ss} contains:

@example
(define g (lambda (x) (+ x x)))
@end example

@noindent
the expression:

@example
(let ()
  (include "flib.ss")
  (include "glib.ss")
  (f 5))
@end example

@noindent
evaluates to @math{50}.

The definition of @func{include} uses @func{datum->syntax} to convert
the objects read from the file into syntax objects in the proper lexical
context, so that identifier references and definitions within those
expressions are scoped where the @func{include} form appears.
@end deffn


@c page
@node stdlib syntax-case temporaries
@subsection Generating lists of temporaries


Transformers can introduce a fixed number of identifiers into their
output simply by naming each identifier.  In some cases, however, the
number of identifiers to be introduced depends upon some characteristic
of the input expression.  A straightforward definition of @func{letrec},
for example, requires as many temporary identifiers as there are binding
pairs in the input expression.  The procedure
@func{generate-temporaries} is used to construct lists of temporary
identifiers.


@deffn Procedure generate-temporaries @var{l}
@var{l} must be be a list or syntax object representing a
list--structured form; its contents are not important.

The number of temporaries generated is the number of elements in
@var{l}.  Each temporary is guaranteed to be unique, i.e., different
from all other identifiers.

A definition of @func{letrec} equivalent to the one using
@func{syntax-rules} given in @ref{lang derived} is shown below.

@example
(define-syntax letrec
  (lambda (x)
    (syntax-case x ()
      ((_ ((i e) ...) b1 b2 ...)
       (with-syntax
           (((t ...) (generate-temporaries #'(i ...))))
         #'(let ((i <undefined>) ...)
             (let ((t e) ...)
               (set! i t) ...
               (let () b1 b2 ...))))))))
@end example

This version uses @func{generate-temporaries} instead of recursively
defined helper to generate the necessary temporaries.
@end deffn


@c page
@node stdlib syntax-case derived
@subsection Derived forms and procedures


The forms and procedures described in this section can be defined in
terms of the forms and procedures described in earlier sections of this
chapter.


@deffn Syntax with-syntax ((@hyper{pattern} @hyper{expression}) @dots{}) @hyper{body}
The @func{with-syntax} form is used to bind pattern variables, just as
@func{let} is used to bind variables.  This allows a transformer to
construct its output in separate pieces, then put the pieces together.

Each @hyper{pattern} is identical in form to a @func{syntax-case}
pattern.  The value of each @hyper{expression} is computed and
destructured according to the corresponding @hyper{pattern}, and pattern
variables within the @hyper{pattern} are bound as with
@func{syntax-case} to the corresponding portions of the value within
@hyper{body}.

The @func{with-syntax} form may be defined in terms of
@func{syntax-case} as follows.

@example
(define-syntax with-syntax
  (lambda (x)
    (syntax-case x ()
      ((_ ((p e0) ...) e1 e2 ...)
       (syntax (syntax-case (list e0 ...) ()
                 ((p ...) (let () e1 e2 ...))))))))
@end example

The following definition of @func{cond} demonstrates the use of
@func{with-syntax} to support transformers that employ recursion
internally to construct their output.  It handles all @func{cond} clause
variations and takes care to produce one-armed @func{if} expressions
where appropriate.

@example
(define-syntax cond
  (lambda (x)
    (syntax-case x ()
      ((_ c1 c2 ...)
       (let f ((c1 #'c1) (c2* #'(c2 ...)))
         (syntax-case c2* ()
           (()
            (syntax-case c1 (else =>)
              ((else e1 e2 ...) #'(begin e1 e2 ...))
              ((e0) #'e0)
              ((e0 => e1)
               #'(let ((t e0)) (if t (e1 t))))
              ((e0 e1 e2 ...)
               #'(if e0 (begin e1 e2 ...)))))
           ((c2 c3 ...)
            (with-syntax ((rest (f #'c2 #'(c3 ...))))
              (syntax-case c1 (=>)
                ((e0) #'(let ((t e0)) (if t t rest)))
                ((e0 => e1)
                 #'(let ((t e0)) (if t (e1 t) rest)))
                ((e0 e1 e2 ...)
                 #'(if e0
                        (begin e1 e2 ...)
                        rest)))))))))))
@end example
@end deffn


@deffn Syntax quasisyntax @hyper{template}
@deffnx {Auxiliary Syntax} unsyntax
@deffnx {Auxiliary Syntax} unsyntax-splicing
The @func{quasisyntax} form is similar to @func{syntax}, but it allows
parts of the quoted text to be evaluated, in a manner similar to the
operation of @func{quasiquote} (@ref{baselib quasiquotation}).

Within a @func{quasisyntax} @var{template}, subforms of @func{unsyntax}
and @func{unsyntax-splicing} forms are evaluated, and everything else is
treated as ordinary template material, as with @func{syntax}.

The value of each @func{unsyntax} subform is inserted into the output in
place of the @func{unsyntax} form, while the value of each
@func{unsyntax-splicing} subform is spliced into the surrounding list or
vector structure.  Uses of @func{unsyntax} and @func{unsyntax-splicing}
are valid only within @func{quasisyntax} expressions.

A @func{quasisyntax} expression may be nested, with each
@func{quasisyntax} introducing a new level of syntax quotation and each
@func{unsyntax} or @func{unsyntax-splicing} taking away a level of
quotation.  An expression nested within @emph{n} @func{quasisyntax}
expressions must be within @emph{n} @emph{unsyntax} or
@func{unsyntax-splicing} expressions to be evaluated.

As noted in @ref{lang lex datum abbreviations},
@code{#`@hyper{template}} is equivalent to @code{(quasisyntax
@hyper{template})}, @code{#,@hyper{template}} is equivalent to
@code{(unsyntax @hyper{template})}, and @code{#,@@@hyper{template}} is
equivalent to @code{(unsyntax-splicing @hyper{template})}.

The @func{quasisyntax} keyword can be used in place of
@func{with-syntax} in many cases.  For example, the definition of
@func{case} shown under the description of @func{with-syntax} above can
be rewritten using @func{quasisyntax} as follows.

@example
(define-syntax case
  (lambda (x)
    (syntax-case x ()
      ((_ e c1 c2 ...)
       #`(let ((t e))
           #,(let f ((c1 #'c1) (cmore #'(c2 ...)))
               (if (null? cmore)
                   (syntax-case c1 (else)
                     ((else e1 e2 ...)
                      #'(begin e1 e2 ...))
                     (((k ...) e1 e2 ...)
                      #'(if (memv t '(k ...))
                            (begin e1 e2 ...))))
                   (syntax-case c1 ()
                     (((k ...) e1 e2 ...)
                      #`(if (memv t '(k ...))
                            (begin e1 e2 ...)
                            #,(f (car cmore)
                                  (cdr cmore))))))))))))
@end example

Uses of @func{unsyntax} and @func{unsyntax-splicing} with zero or more
than one subform are valid only in splicing (list or vector) contexts.
@code{(unsyntax @var{template} @dots{})} is equivalent to
@code{(unsyntax @var{template}) ...}, and @code{(unsyntax-splicing
@var{template} ...)} is equivalent to @code{(unsyntax-splicing
@var{template}) ...}.  These forms are primarily useful as intermediate
forms in the output of the @func{quasisyntax} expander.

@quotation
@emph{Note:} Uses of @func{unsyntax} and @func{unsyntax-splicing} with
zero or more than one subform enable certain idioms, such as
@code{#,@@#,@@}, which has the effect of a doubly indirect splicing when
used within a doubly nested and doubly evaluated @func{quasisyntax}
expression, as with the nested @func{quasiquote} examples shown in
section @ref{baselib quasiquotation}.
@end quotation
@end deffn

@quotation
@emph{Note:} Any @func{syntax-rules} form can be expressed with
@func{syntax-case} by making the @func{lambda} expression and
@func{syntax} expressions explicit, and @func{syntax-rules} may be
defined in terms of @func{syntax-case} as follows.

@example
(define-syntax syntax-rules
  (lambda (x)
    (syntax-case x ()
      ((_ (lit ...) ((k . p) t) ...)
       (for-all identifier? #'(lit ... k ...))
       #'(lambda (x)
           (syntax-case x (lit ...)
             ((_ . p) #'t) ...))))))
@end example
@end quotation

@quotation
@emph{Note:} The @func{identifier-syntax} form of the base library
(@ref{baselib transformers}) may be defined in terms of
@func{syntax-case}, @func{syntax}, and @func{make-variable-transformer}
as follows.

@example
(define-syntax identifier-syntax
  (syntax-rules (set!)
    ((_ e)
     (lambda (x)
       (syntax-case x ()
         (id (identifier? #'id) #'e)
         ((_ x (... ...)) #'(e x (... ...))))))
    ((_ (id exp1) ((set! var val) exp2))
     (and (identifier? #'id) (identifier? #'var))
     (make-variable-transformer
       (lambda (x)
         (syntax-case x (set!)
           ((set! var val) #'exp2)
           ((id x (... ...)) #'(exp1 x (... ...)))
           (id (identifier? #'id) #'exp1)))))))
@end example
@end quotation

@c page
@node stdlib syntax-case violations
@subsection Syntax violations


@deffn Procedure syntax-violation @var{who message form}
@deffnx Procedure syntax-violation @var{who message form subform}
@var{who} must be @false{} or a string or a symbol.  @var{message} must
be a string.  @var{form} must be a syntax object or a datum value.
@var{subform} must be a syntax object or a datum value.

The @func{syntax-violation} procedure raises an exception, reporting a
syntax violation.  @var{who} should describe the macro transformer that
detected the exception.  The @var{message} argument should describe the
violation.  @var{form} should be the erroneous source syntax object or a
datum value representing a form. The optional @var{subform} argument
should be a syntax object or datum value representing a form that more
precisely locates the violation.

If @var{who} is @false{}, @func{syntax-violation} attempts to infer an
appropriate value for the condition object (see below) as follows: When
@var{form} is either an identifier or a list-structured syntax object
containing an identifier as its first element, then the inferred value
is the identifier's symbol.  Otherwise, no value for @var{who} is
provided as part of the condition object.

The condition object provided with the exception has the following
condition types:

@itemize
@item
If @var{who} is not @false{} or can be inferred, the condition has
condition type @code{&who}, with @var{who} as the value of its field.
In that case, @var{who} should identify the procedure or entity that
detected the exception.  If it is @false{}, the condition does not have
condition type @code{&who}.

@item
The condition has condition type @code{&message}, with @var{message} as
the value of its field.

@item
The condition has condition type @code{&syntax} with @var{form} and
@var{subform} as the value of its fields.  If @var{subform} is not
provided, the value of the subform field is @false{}.
@end itemize
@end deffn


@c page
@node stdlib syntax-case define-macro
@subsection Common Lisp style macros


@findex lisp-transformer
@findex define-macro
@cindex Common Lisp style macros


Using @func{datum->syntax}, it is even possible to break hygiene
entirely and write macros in the style of old Lisp macros.  The
@func{lisp-transformer} procedure defined below creates a transformer
that converts its input into a datum, calls the programmer's procedure
on this datum, and converts the result back into a syntax object scoped
where the original macro use appeared.

@example
(define lisp-transformer
  (lambda (p)
    (lambda (x)
      (syntax-case x ()
        [(kwd . rest)
         (datum->syntax #'kwd
           (p (syntax->datum x)))]))))
@end example


@c page
@node stdlib hashtable
@section Hashtables


The @rsixlibrary{hashtables} library provides a set of operations on
hashtables.

A @emph{hashtable} is a data structure that associates keys with values.
Any object can be used as a key, provided a @emph{hash function} and a
suitable @emph{equivalence function} is available.

A hash function is a procedure that maps keys to exact integer objects.
It is the programmer's responsibility to ensure that the hash function
is compatible with the equivalence function, which is a procedure that
accepts two keys and returns true if they are equivalent and @false{}
otherwise.

Standard hashtables for arbitrary objects based on the @func{eq?} and
@func{eqv?} predicates (@ref{baselib predicates}) are provided.  Also,
hash functions for arbitrary objects, strings, and symbols are provided.

This section uses the @var{hashtable} parameter name for arguments that
must be hashtables, and the @var{key} parameter name for arguments that
must be hashtable keys.


@menu
* stdlib hashtable constructors::       Constructors.
* stdlib hashtable procedures::         Procedures.
* stdlib hashtable inspection::         Inspection.
* stdlib hashtable hash functions::     Hash functions.
@end menu


@c page
@node stdlib hashtable constructors
@subsection Constructors


@deffn Procedure make-eq-hashtable
@deffnx Procedure make-eq-hashtable @var{k}
Return a newly allocated mutable hashtable that accepts arbitrary
objects as keys, and compares those keys with @func{eq?}.  If an
argument is given, the initial capacity of the hashtable is set to
approximately @var{k} elements.
@end deffn


@deffn Procedure make-eqv-hashtable
@deffnx Procedure make-eqv-hashtable @var{k}
Return a newly allocated mutable hashtable that accepts arbitrary
objects as keys, and compares those keys with @func{eqv?}.  If an
argument is given, the initial capacity of the hashtable is set to
approximately @var{k} elements.
@end deffn


@deffn Procedure make-hashtable @var{hash-function} @var{equiv}
@deffnx Procedure make-hashtable @var{hash-function} @var{equiv} @var{k}
@var{hash-function} and @var{equiv} must be procedures.
@var{hash-function} should accept a key as an argument and should return
a non--negative exact integer object.  @var{equiv} should accept two
keys as arguments and return a single value.  Neither procedure should
mutate the hashtable returned by @func{make-hashtable}.

The @func{make-hashtable} procedure returns a newly allocated mutable
hashtable using @var{hash-function} as the hash function and @var{equiv}
as the equivalence function used to compare keys.  If a third argument
is given, the initial capacity of the hashtable is set to approximately
@var{k} elements.

Both @var{hash-function} and @var{equiv} should behave like pure
functions on the domain of keys.  For example, the @func{string-hash}
and @func{string=?} procedures are permissible only if all keys are
strings and the contents of those strings are never changed so long as
any of them continues to serve as a key in the hashtable.  Furthermore,
any pair of keys for which @var{equiv} returns true should be hashed to
the same exact integer objects by @var{hash-function}.

@strong{Implementation responsibilities:} The implementation must check
the restrictions on @var{hash-function} and @var{equiv} to the extent
performed by applying them as described.

@quotation
@emph{Note:} Hashtables are allowed to cache the results of calling the
hash function and equivalence function, so programs cannot rely on the
hash function being called for every lookup or update.  Furthermore any
hashtable operation may call the hash function more than once.
@end quotation
@end deffn


@c page
@node stdlib hashtable procedures
@subsection Procedures


@deffn Procedure {hashtable?} @var{obj}
Return @true{} if @var{obj} is a hashtable, @false{} otherwise.
@end deffn


@deffn Procedure hashtable-size @var{hashtable}
Return the number of keys contained in @var{hashtable} as an exact
integer object.
@end deffn


@deffn Procedure hashtable-ref @var{hashtable} @var{key} @var{default}
Return the value in @var{hashtable} associated with @var{key}.  If
@var{hashtable} does not contain an association for @var{key},
@var{default} is returned.
@end deffn


@deffn Procedure {hashtable-set!} @var{hashtable} @var{key} @var{obj}
Change @var{hashtable} to associate @var{key} with @var{obj}, adding a
new association or replacing any existing association for @var{key}, and
returns unspecified values.
@end deffn


@deffn Procedure {hashtable-delete!} @var{hashtable} @var{key}
Remove any association for @var{key} within @var{hashtable} and returns
unspecified values.
@end deffn


@deffn Procedure {hashtable-contains?} @var{hashtable} @var{key}
Return @true{} if @var{hashtable} contains an association for @var{key},
@false{} otherwise.
@end deffn


@deffn Procedure {hashtable-update!} @var{hashtable} @var{key} @var{proc} @var{default}
@var{proc} should accept one argument, should return a single value, and
should not mutate @var{hashtable}.

The @func{hashtable-update!} procedure applies @var{proc} to the value
in @var{hashtable} associated with @var{key}, or to @var{default} if
@var{hashtable} does not contain an association for @var{key}.  The
@var{hashtable} is then changed to associate @var{key} with the value
returned by @var{proc}.

The behavior of @func{hashtable-update!} is equivalent to the following
code, but may be implemented more efficiently in cases where the
implementation can avoid multiple lookups of the same key:

@example
(hashtable-set!
  hashtable key
  (proc (hashtable-ref
         hashtable key default)))
@end example
@end deffn


@deffn Procedure hashtable-copy @var{hashtable}
@deffnx Procedure hashtable-copy @var{hashtable} @var{mutable}
Return a copy of @var{hashtable}.  If the @var{mutable} argument is
provided and is true, the returned hashtable is mutable; otherwise it is
immutable.
@end deffn


@deffn Procedure {hashtable-clear!} @var{hashtable}
@deffnx Procedure {hashtable-clear!} @var{hashtable} @var{k}
Remove all associations from @var{hashtable} and returns unspecified
values.

If a second argument is given, the current capacity of the hashtable is
reset to approximately @var{k} elements.
@end deffn


@deffn Procedure hashtable-keys @var{hashtable}
Return a vector of all keys in @var{hashtable}.  The order of the vector
is unspecified.
@end deffn


@deffn Procedure hashtable-entries @var{hashtable}
Return two values, a vector of the keys in @var{hashtable}, and a vector
of the corresponding values.

Example:

@example
(let ((h (make-eqv-hashtable)))
  (hashtable-set! h 1 'one)
  (hashtable-set! h 2 'two)
  (hashtable-set! h 3 'three)
  (hashtable-entries h))
@result{} #(1 2 3) #(one two three) ; two return values
@end example

@noindent
the order of the entries in the result vectors is not known.
@end deffn


@c page
@node stdlib hashtable inspection
@subsection Inspection


@deffn Procedure hashtable-equivalence-function @var{hashtable}
Return the equivalence function used by @var{hashtable} to compare keys.
For hashtables created with @func{make-eq-hashtable} and
@func{make-eqv-hashtable}, returns @func{eq?} and @func{eqv?}
respectively.
@end deffn


@deffn Procedure hashtable-hash-function @var{hashtable}
Return the hash function used by @var{hashtable}.  For hashtables
created by @func{make-eq-hashtable} or @func{make-eqv-hashtable},
@false{} is returned.
@end deffn


@deffn Procedure {hashtable-mutable?} @var{hashtable}
Return @true{} if @var{hashtable} is mutable, otherwise @false{}.
@end deffn


@c page
@node stdlib hashtable hash functions
@subsection Hash functions


The @func{equal-hash}, @func{string-hash}, and @func{string-ci-hash}
procedures of this section are acceptable as the hash functions of a
hashtable only if the keys on which they are called are not mutated
while they remain in use as keys in the hashtable.


@deffn Procedure equal-hash @var{obj}
Return an integer hash value for @var{obj}, based on its structure and
current contents.  This hash function is suitable for use with
@func{equal?} as an equivalence function.

@quotation
@emph{Note:} Like @func{equal?}, the @func{equal-hash} procedure must
always terminate, even if its arguments contain cycles.
@end quotation
@end deffn


@deffn Procedure string-hash @var{string}
Return an integer hash value for @var{string}, based on its current
contents.  This hash function is suitable for use with @func{string=?}
as an equivalence function.
@end deffn


@deffn Procedure string-ci-hash @var{string}
Return an integer hash value for @var{string} based on its current
contents, ignoring case.  This hash function is suitable for use with
@func{string-ci=?} as an equivalence function.
@end deffn


@deffn Procedure symbol-hash @var{symbol}
Return an integer hash value for @var{symbol}.
@end deffn


@c page
@node stdlib enum
@section Enumerations


This chapter describes the @rsixlibrary{enums} library for dealing with
enumerated values and sets of enumerated values.  Enumerated values are
represented by ordinary symbols, while finite sets of enumerated values
form a separate type, known as the @emph{enumeration sets}.  The
enumeration sets are further partitioned into sets that share the same
@emph{universe} and @emph{enumeration type}.  These universes and
enumeration types are created by the @func{make-enumeration} procedure.
Each call to that procedure creates a new enumeration type.

This library interprets each enumeration set with respect to its
specific universe of symbols and enumeration type.  This facilitates
efficient implementation of enumeration sets and enables the complement
operation.

In the descriptions of the following procedures, @var{enum-set} ranges
over the enumeration sets, which are defined as the subsets of the
universes that can be defined using @func{make-enumeration}.


@deffn Procedure make-enumeration @var{symbol-list}
@var{symbol-list} must be a list of symbols.

The @func{make-enumeration} procedure creates a new enumeration type
whose universe consists of those symbols (in canonical order of their
first appearance in the list) and returns that universe as an
enumeration set whose universe is itself and whose enumeration type is
the newly created enumeration type.
@end deffn


@deffn Procedure enum-set-universe @var{enum-set}
Return the set of all symbols that comprise the universe of its
argument, as an enumeration set.
@end deffn


@deffn Procedure enum-set-indexer @var{enum-set}
Return a unary procedure that, given a symbol that is in the universe of
@var{enum-set}, returns its 0--origin index within the canonical
ordering of the symbols in the universe; given a symbol not in the
universe, the unary procedure returns @false{}.

@example
(let* ((e (make-enumeration '(red green blue)))
       (i (enum-set-indexer e)))
  (list (i 'red) (i 'green) (i 'blue) (i 'yellow)))
@result{} (0 1 2 #f)
@end example

The @func{enum-set-indexer} procedure could be defined as follows using
the @func{memq} procedure from the @rsixlibrary{lists} library:

@example
(define (enum-set-indexer set)
  (let* ((symbols (enum-set->list
                    (enum-set-universe set)))
         (cardinality (length symbols)))
    (lambda (x)
      (cond
       ((memq x symbols)
        => (lambda (probe)
             (- cardinality (length probe))))
       (else #f)))))
@end example
@end deffn


@deffn Procedure enum-set-constructor @var{enum-set}
Return a unary procedure that, given a list of symbols that belong to
the universe of @var{enum-set}, returns a subset of that universe that
contains exactly the symbols in the list.  The values in the list must
all belong to the universe.
@end deffn


@deffn Procedure enum-set->list @var{enum-set}
Return a list of the symbols that belong to its argument, in the
canonical order of the universe of @var{enum-set}.

@example
(let* ((e (make-enumeration '(red green blue)))
       (c (enum-set-constructor e)))
  (enum-set->list (c '(blue red))))    @result{} (red blue)
@end example
@end deffn


@deffn Procedure {enum-set-member?} @var{symbol} @var{enum-set}
@deffnx Procedure {enum-set-subset?} @vari{enum-set} @varii{enum-set}
@deffnx Procedure {enum-set=?} @vari{enum-set} @varii{enum-set}
The @func{enum-set-member?} procedure returns @true{} if its first
argument is an element of its second argument, @false{} otherwise.

The @func{enum-set-subset?} procedure returns @true{} if the universe of
@vari{enum-set} is a subset of the universe of @varii{enum-set}
(considered as sets of symbols) and every element of @vari{enum-set} is
a member of @varii{enum-set}.  It returns @false{} otherwise.

The @func{enum-set=?} procedure returns @true{} if @vari{enum-set} is a
subset of @varii{enum-set} and vice versa, as determined by the
@func{enum-set-subset?} procedure.  This implies that the universes of
the two sets are equal as sets of symbols, but does not imply that they
are equal as enumeration types.  Otherwise, @false{} is returned.

@example
(let* ((e (make-enumeration '(red green blue)))
       (c (enum-set-constructor e)))
  (list
   (enum-set-member? 'blue (c '(red blue)))
   (enum-set-member? 'green (c '(red blue)))
   (enum-set-subset? (c '(red blue)) e)
   (enum-set-subset? (c '(red blue)) (c '(blue red)))
   (enum-set-subset? (c '(red blue)) (c '(red)))
   (enum-set=? (c '(red blue)) (c '(blue red)))))
@result{} (#t #f #t #t #f #t)
@end example
@end deffn


@deffn Procedure enum-set-union @vari{enum-set} @varii{enum-set}
@deffnx Procedure enum-set-intersection @vari{enum-set} @varii{enum-set}
@deffnx Procedure enum-set-difference @vari{enum-set} @varii{enum-set}
@vari{enum-set} and @varii{enum-set} must be enumeration sets that have
the same enumeration type.

The @func{enum-set-union} procedure returns the union of @vari{enum-set}
and @varii{enum-set}.  The @func{enum-set-intersection} procedure
returns the intersection of @vari{enum-set} and @varii{enum-set}.  The
@func{enum-set-difference} procedure returns the difference of
@vari{enum-set} and @varii{enum-set}.

@example
(let* ((e (make-enumeration '(red green blue)))
       (c (enum-set-constructor e)))
  (list (enum-set->list
         (enum-set-union (c '(blue)) (c '(red))))
        (enum-set->list
         (enum-set-intersection (c '(red green))
                                (c '(red blue))))
        (enum-set->list
         (enum-set-difference (c '(red green))
                              (c '(red blue))))))
@result{} ((red blue) (red) (green))
@end example
@end deffn


@deffn Procedure enum-set-complement @var{enum-set}
Return @var{enum-set}'s complement with respect to its universe.

@example
(let* ((e (make-enumeration '(red green blue)))
       (c (enum-set-constructor e)))
  (enum-set->list
    (enum-set-complement (c '(red)))))
@result{} (green blue)
@end example
@end deffn


@deffn Procedure enum-set-projection @vari{enum-set} @varii{enum-set}
Project @vari{enum-set} into the universe of @varii{enum-set}, dropping
any elements of @vari{enum-set} that do not belong to the universe of
@varii{enum-set}.  (If @vari{enum-set} is a subset of the universe of
its second, no elements are dropped, and the injection is returned.)

@example
(let ((e1 (make-enumeration
            '(red green blue black)))
      (e2 (make-enumeration
            '(red black white))))
  (enum-set->list
    (enum-set-projection e1 e2))))
@result{} (red black)
@end example
@end deffn


@deffn Syntax define-enumeration @hyper{type-name} (@hyper{symbol} @dots{}) @hyper{constructor-syntax}
The @func{define-enumeration} form defines an enumeration type and
provides two macros for constructing its members and sets of its
members.

A @func{define-enumeration} form is a definition and can appear anywhere
any other @hyper{definition} can appear.

@hyper{type-name} is an identifier that is bound as a syntactic keyword;
@hyper{symbol} @dots{} are the symbols that comprise the universe of the
enumeration (in order).

@code{(@hyper{type-name} @hyper{symbol})} checks at macro-expansion time
whether the name of @hyper{symbol} is in the universe associated with
@hyper{type-name}.  If it is, @code{(@hyper{type-name} @hyper{symbol})}
is equivalent to @code{@hyper{symbol}}.  It is a syntax violation if it
is not.

@hyper{constructor-syntax} is an identifier that is bound to a macro
that, given any finite sequence of the symbols in the universe, possibly
with duplicates, expands into an expression that evaluates to the
enumeration set of those symbols.

@code{(@hyper{constructor-syntax} @hyper{symbol} ...)} checks at
macro-expansion time whether every @hyper{symbol} ... is in the universe
associated with @hyper{type-name}.  It is a syntax violation if one or
more is not.  Otherwise:

@example
(@hyper{constructor-syntax} @hyper{symbol} ...)
@end example

@noindent
is equivalent to:

@example
((enum-set-constructor (@hyper{constructor-syntax}))
 '(@hyper{symbol} ...))
@end example

Example:

@example
(define-enumeration color
  (black white purple maroon)
  color-set)

(color black)                      @result{} black
(color purpel)                     @result{} exception &syntax
(enum-set->list (color-set))       @result{} ()
(enum-set->list
  (color-set maroon white))        @result{} (white maroon)
@end example

@quotation
@emph{Note:} In @code{(@hyper{type-name} @hyper{symbol})} and
@code{(@hyper{constructor-syntax} @hyper{symbol} ...)} forms, only the
names of the @hyper{symbol}s are significant.
@end quotation
@end deffn


@c page
@node stdlib complib
@section Composite library


The @rsixlibrary{} library is a composite of most of the libraries
described in this report.  The only exceptions are:

@table @rsixlibrary
@item eval
@item mutable-pairs
@item mutable-strings
@item r5rs
@end table

The library exports all procedures and syntactic forms provided by the
component libraries.

All of the bindings exported by @rsixlibrary{} are exported for both
@code{run} and @code{expand}; @ref{lang library import export}.


@c page
@node stdlib eval
@section Evaluation


The @rsixlibrary{eval} library allows a program to create Scheme
expressions as data at run time and evaluate them.


@deffn Procedure eval @var{expression} @var{environment}
Evaluate @var{expression} in the specified environment and returns its
value.  @var{expression} must be a syntactically valid Scheme expression
represented as a datum value, and @var{environment} must be an
@emph{environment}, which can be created using the @func{environment}
procedure described below.

If the first argument to @code{eval} is determined not to be a
syntactically correct expression, then @func{eval} must raise an
exception with condition type @code{&syntax}.  Specifically, if the
first argument to @code{eval} is a definition or a splicing @func{begin}
form containing a definition, it must raise an exception with condition
type @code{&syntax}.
@end deffn


@deffn Procedure environment @var{import-spec} @dots{}
@var{import-spec} must be a datum representing an @hyper{import spec}
(@ref{lang library form}).

The @func{environment} procedure returns an environment corresponding to
@var{import-spec}.

The bindings of the environment represented by the specifier are
immutable: If @func{eval} is applied to an expression that is determined
to contain an assignment to one of the variables of the environment,
then @func{eval} must raise an exception with a condition type
@code{&syntax}.
@ignore
The errata corrige says to append the following here:

  for consistency with treatement of similar situations as syntax
  violations in section 7.1 of the base document.

but I see no &syntax in section 7.1 of R6RS document.
@end ignore

@example
(library (foo)
  (export)
  (import (rnrs)
          (rnrs eval))
  (write
    (eval '(let ((x 3)) x)
          (environment '(rnrs)))))      ;; writes 3

(library (foo)
  (export)
  (import (rnrs)
          (rnrs eval))
  (write
    (eval
      '(eval:car (eval:cons 2 4))
      (environment
        '(prefix (only (rnrs) car cdr cons null?)
                 eval:)))))             ;; writes 2
@end example
@end deffn


@c page
@node stdlib mutable pairs
@section Mutable pairs


The procedures provided by the @rsixlibrary{mutable-pairs} library allow
new values to be assigned to the car and cdr fields of previously
allocated pairs.


@deffn Procedure {set-car!} @var{pair} @var{obj}
Store @var{obj} in the car field of @var{pair}.  The @func{set-car!}
procedure returns unspecified values.

@example
(define (f) (list 'not-a-constant-list))
(define (g) '(constant-list))
(set-car! (f) 3)        @result{} unspecified
(set-car! (g) 3)        @result{} unspecified
                        ; should raise exception &assertion
@end example

If an immutable pair is passed to @func{set-car!}, an exception with
condition type @code{&assertion} should be raised.
@end deffn


@deffn Procedure {set-cdr!} @var{pair} @var{obj}
Store @var{obj} in the cdr field of @var{pair}.  The @func{set-cdr!}
procedure returns unspecified values.

If an immutable pair is passed to @func{set-cdr!}, an exception with
condition type @code{&assertion} should be raised.

@example
(let ((x (list 'a 'b 'c 'a))
      (y (list 'a 'b 'c 'a 'b 'c 'a)))
  (set-cdr! (list-tail x 2) x)
  (set-cdr! (list-tail y 5) y)
  (list
   (equal? x x)
   (equal? x y)
   (equal? (list x y 'a) (list y x 'b))))
@result{}  (#t #t #f)
@end example
@end deffn


@c page
@node stdlib mutable strings
@section Mutable strings


The @func{string-set!} procedure provided by the
@rsixlibrary{mutable-strings} library allows mutating the characters of
a string in--place.


@deffn Procedure {string-set!} @var{string} @var{k} @var{char}
@var{k} must be a valid index of @var{string}.

The @func{string-set!} procedure stores @var{char} in element @var{k} of
@var{string} and returns unspecified values.

Passing an immutable string to @func{string-set!} should cause an
exception with condition type @code{&assertion} to be raised.

@example
(define (f) (make-string 3 #\*))
(define (g) "***")
(string-set! (f) 0 #\?)         @result{} unspecified
(string-set! (g) 0 #\?)         @result{} unspecified
                                ; should raise exception &assertion

(string-set! (symbol->string 'immutable)
             0
             #\?)               @result{} unspecified
                                ; should raise exception &assertion
@end example

@quotation
@emph{Note:} Implementors should make @func{string-set!} run in constant
time.
@end quotation
@end deffn


@deffn Procedure {string-fill!} @var{string} @var{char}
Store @var{char} in every element of the given @var{string} and returns
unspecified values.
@end deffn


@c page
@node stdlib rfive compat
@section @rnrs{5} compatibility


The features described in this chapter are exported from the
@rsixlibrary{r5rs} library and provide some functionality of the
preceding revision of this report that was omitted from the main part of
the current report.


@deffn Procedure exact->inexact @var{z}
@deffnx Procedure inexact->exact @var{z}
These are the same as the @code{inexact} and @code{exact} procedures;
@ref{baselib math ops exactness}.
@end deffn


@deffn Procedure quotient @vari{n} @varii{n}
@deffnx Procedure remainder @vari{n} @varii{n}
@deffnx Procedure modulo @vari{n} @varii{n}
These procedures implement number--theoretic (integer) division.
@varii{N} must be non--zero.  All three procedures return integer
objects.  If @vari{n}/@varii{n} is an integer object:

@example
(quotient @vari{n} @varii{n})   @result{} @vari{n}/@varii{n}
(remainder @vari{n} @varii{n})  @result{} 0
(modulo @vari{n} @varii{n})     @result{} 0
@end example

If @vari{n}/@varii{n} is not an integer object:

@example
(quotient @vari{n} @varii{n})   @result{} @var{n_q}
(remainder @vari{n} @varii{n})  @result{} @var{n_r}
(modulo @vari{n} @varii{n})     @result{} @var{n_m}
@end example

@noindent
where @var{n_q} is @vari{n}/@varii{n} rounded towards zero,

@example
0 < |@var{n_r}| < |@varii{n}|
0 < |@var{n_m}| < |@varii{n}|
@end example

@noindent
@var{n_r} and @var{n_m} differ from @vari{n} by a multiple of @varii{n},
@var{n_r} has the same sign as @vari{n}, and @var{n_m} has the same sign
as @varii{n}.

Consequently, for integer objects @vari{n} and @varii{n} with @varii{n}
not equal to 0,

@example
(= @vari{n} (+ (* @varii{n} (quotient @vari{n} @varii{n}))
               (remainder @vari{n} @varii{n})))
@result{} #t
@end example

@noindent
provided all number object involved in that computation are exact.

@example
(modulo 13 4)           @result{}  1
(remainder 13 4)        @result{}  1

(modulo -13 4)          @result{}  3
(remainder -13 4)       @result{}  -1

(modulo 13 -4)          @result{}  -3
(remainder 13 -4)       @result{}  1

(modulo -13 -4)         @result{}  -1
(remainder -13 -4)      @result{}  -1

(remainder -13 -4.0)    @result{}  -1.0
@end example

@quotation
@emph{Note:} These procedures could be defined in terms of @func{div}
and @func{mod} (@ref{baselib math ops arithmetic}) as follows (without
checking of the argument types):

@example
(define (sign n)
  (cond
    ((negative? n) -1)
    ((positive? n) 1)
    (else 0)))

(define (quotient n1 n2)
  (* (sign n1) (sign n2) (div (abs n1) (abs n2))))

(define (remainder n1 n2)
  (* (sign n1) (mod (abs n1) (abs n2))))

(define (modulo n1 n2)
  (* (sign n2) (mod (* (sign n2) n1) (abs n2))))
@end example
@end quotation
@end deffn


@deffn Syntax delay @hyper{expression}
The @func{delay} construct is used together with the procedure
@func{force} to implement @emph{lazy evaluation} or @emph{call by need}.

@code{(delay @hyper{expression})} returns an object called a
@emph{promise} which at some point in the future may be asked (by the
@func{force} procedure) to evaluate @hyper{expression}, and deliver the
resulting value.  The effect of @hyper{expression} returning multiple
values is unspecified.
@end deffn


@deffn Procedure force @var{promise}
@var{promise} must be a promise.

The @func{force} procedure forces the value of @var{promise}.  If no
value has been computed for the promise, then a value is computed and
returned.  The value of the promise is cached (or ``memoized'') so that
if it is forced a second time, the previously computed value is
returned.

@example
(force (delay (+ 1 2)))                 @result{}  3

(let ((p (delay (+ 1 2))))
  (list (force p) (force p)))           @result{}  (3 3)

(define a-stream
  (letrec ((next
            (lambda (n)
              (cons n (delay (next (+ n 1)))))))
    (next 0)))
(define head car)
(define tail
  (lambda (stream) (force (cdr stream))))

(head (tail (tail a-stream)))           @result{}  2
@end example

Promises are mainly intended for programs written in functional style.
The following examples should not be considered to illustrate good
programming style, but they illustrate the property that only one value
is computed for a promise, no matter how many times it is forced.

@example
(define count 0)
(define p
  (delay (begin (set! count (+ count 1))
                (if (> count x)
                    count
                    (force p)))))
(define x 5)
p                     @result{}  a promise
(force p)             @result{}  6
p                     @result{}  a promise, still
(begin (set! x 10)
       (force p))     @result{}  6
@end example

Here is a possible implementation of @func{delay} and @func{force}.
Promises are implemented here as procedures of no arguments, and
@func{force} simply calls its argument:

@example
(define force
  (lambda (object)
    (object)))
@end example

The expression:

@example
(delay @hyper{expression})
@end example

@noindent
has the same meaning as the procedure call:

@example
(make-promise (lambda () @hyper{expression}))
@end example

@noindent
as follows:

@example
(define-syntax delay
  (syntax-rules ()
    ((delay expression)
     (make-promise (lambda () expression)))))
@end example

@noindent
where @func{make-promise} is defined as follows:

@example
(define make-promise
  (lambda (proc)
    (let ((result-ready?  #f)
          (result         #f))
      (lambda ()
        (if result-ready?
            result
            (let ((x (proc)))
              (if result-ready?
                  result
                  (begin (set! result-ready? #t)
                         (set! result x)
                         result))))))))
@end example
@end deffn


@deffn Procedure null-environment @var{n}
@var{n} must be the exact integer object 5.

The @func{null-environment} procedure returns an environment specifier
suitable for use with @func{eval} representing an environment that is
empty except for the (syntactic) bindings for all keywords described in
the previous revision of this report, including bindings for @code{=>},
@code{...}, @code{else}, and @code{_} that are the same as those in the
@rsixlibrary{base} library.
@end deffn


@deffn Procedure scheme-report-environment @var{n}
@var{n} must be the exact integer object 5.

The @func{scheme-report-environment} procedure returns an environment
specifier for an environment that is empty except for the bindings for
the identifiers described in the previous revision of this report,
omitting @func{load}, @func{interaction-environment},
@func{transcript-on}, @func{transcript-off}, and @func{char-ready?}.

The variable bindings have as values the procedures of the same names
described in this report, and the keyword bindings, including @code{=>},
@code{...}, @code{else}, and @code{_} are the same as those described in
this report.
@end deffn


@c page
@node naulib
@chapter The @library{nausicaa} language


This chapter describes the @library{nausicaa} language and its
differences from the @library{rnrs} language.  Most of them are the
revised implementations of @srfi{} specifications that were voted to the
@emph{final} status.


@menu
* naulib features::             Feature--based conditional expansion.
* naulib funcs::                Functions.
* naulib macros::               Syntactic abstractions.
* naulib parameters::           Paremeter objects.
* naulib shared::               External representation for data
                                with shared structure.
* naulib environment::          Accessing environment variables.
* naulib unimplemented::        Unimplemented error condition.
@end menu

@c page
@node naulib features
@section Feature--based conditional expansion


It is desirable that programs which depend on additions to the standard
Scheme language name those additions.  For example the @srfi{}s provide
the specifications of some additions (``features'').  The
@func{cond-expand} syntax described below provides the means to actually
check that features are present in the Scheme system.

A particular functionality may exist in several or even most Scheme
systems but its @api{} may be different (use of a procedure or
special--form, name, number of parameters, etc).  To write code that
will run on several Scheme systems, it is useful to have a common
construct to enable or disable sections of code based on the existence
or absence of a feature in the Scheme system being used.

Features are identified by feature identifiers.  In order for the
semantics of this construct to be well--defined, the feature identifier
must of course refer to a feature which has a well--defined meaning.
There is thus a need for a registry to keep track of the formal
specification associated with each valid feature--identifier.


@deffn Syntax cond-expand @meta{clause} ...
@deffnx Syntax cond-expand @meta{clause} ... (else @meta{body})
Test for the existence of features at macro--expansion time.  Each
@var{clause} has the following formal definition:

@example
<clause>
        -> (<feature requirement> <body>)

<feature requirement>
        -> <feature identifier>
         | (and <feature requirement>*)
         | (or  <feature requirement>*)
         | (not <feature requirement>)

<feature identifier>
        -> one of the symbols described below, or "else"
@end example

@func{cond-expand} either expands into the @meta{body} of one of its
clauses or signals an error during syntactic processing.
@func{cond-expand} expands into the body of the first clause whose
feature requirement is currently satisfied (the @code{else} clause, if
present, is selected if none of the previous clauses is selected).

A @meta{feature requirement} has an obvious interpretation as a logical
formula, where the @meta{feature identifier} symbols have meaning true
if the associated feature is in effect at the location of the
@func{cond-expand} form, and @false{} otherwise.  A feature requirement
is satisfied if its formula is true under this interpretation.

Examples:

@example
(cond-expand
  ((or linux macos)
   ---)
  (windows
   ---))
@end example
@end deffn


What follows is the list of supported features.

@table @code
@item ikarus
@itemx larceny
@itemx mosh
@itemx ypsilon
The name of the underlying Scheme implementation.

@item linux-gnu
@itemx openbsd
@itemx winnt3.5
@itemx sysv4
@itemx darwin
@itemx ...
Platform identifiers selected by the @code{AC_CANONICAL_TARGET} macro of
@gnu{} Autoconf.  @xref{Canonicalizing, Canonicalizing, Getting the
Canonical System Type, autoconf, @gnu{} Autoconf}.

@item r6rs
The @rnrs{6} Scheme language.

@item and-let*
The @func{and-let*} syntax.

@item begin0
The @func{begin0} syntax.

@item char-sets
The @library{char-sets} library.

@item checks
The @library{checks} library.

@item compare
The @library{compare} library.

@item cut
The @func{cut} and @func{cute} syntaxes.

@item loops
The @library{loops} library.

@item format
The @library{format} library.

@item lists
The @library{lists} library.

@item parameters
The parameter objects feature.

@item random
The @library{random} library.

@item recursion
The @func{recursion} syntax.

@item receive
The @func{receive} syntax.

@item sharing
The shared structures reading/writing @srfi{} functions.

@item streams
The @library{streams} library.

@item strings
The @library{strings} library.

@item time
The @library{time} library.

@item vectors
The @library{vectors} library.
@end table


@c page
@node naulib funcs
@section Functions


@defun finite? @var{z}
@defunx infinite? @var{z}
@defunx nan? @var{z}
Wrappers for the functions defined by @rnrs{6} that apply the predicate
to both the real and imaginary parts of @var{z}.

The specifications of the @rnrs{6} functions mandates that the argument
can only be a real number.
@end defun


@defun pretty-print @var{obj}
@defunx pretty-print @var{obj} @var{port}
Print a human readable representation of @var{obj} to @var{port} or the
return value of @func{current-output-port}.
@end defun


@defun {symbol*->string} @var{obj}
Convert the symbol or string @var{obj} to a string.  If @var{obj} is a
string return it, else apply @func{symbol->string} to it and return the
result.
@end defun


@c page
@node naulib macros
@section Syntactic abstractions


@deffn Syntax and-let* @var{formals} . @var{body}
Like an ordinary @func{and}, an @func{and-let*} special form evaluates
the expressions in @var{formals} in order until the first one that
yields @false{}.  Unlike @func{and}, however, a non--@false{} result of
one expression can be bound to a fresh variable and used in the
subsequent expressions.  @func{and-let*} is a cross--breed between
@func{let*} and @func{and}.

@var{formals} can be the empty list, or a list of the following
elements:

@table @code
@item (@var{varname} @var{expression})
evaluate @var{expression}; if the result is @false{}: return @false{}
immediately, else bind the result to @var{varname}; @var{varname} is
available to the rest of the @var{expressions}, and the @code{BODY};

@item @var{expression}
evaluate @var{expression}; if the result is @false{}: return @false{}
immediately;

@item @var{bound-variable}
look up @var{bound-variable}; if the value is @false{}; return @false{}
immediately; @var{bound-varname} is available to the rest of the
@var{expressions}, and the @var{body}.
@end table

If all the @var{expression}s and @var{bound-variable}s in @var{formals}
evaluate to true, the forms in @var{body} are evaluated and the result
of the last one returned.
@end deffn

@c ------------------------------------------------------------

@deffn Syntax begin0 @var{form0} @var{form} ...
Evaluate all the @var{form}s like the standard @func{begin}, but return
the value of @var{form0}.  The implementation comes from the @rnrs{6}
original document, Appendix A ``Formal semantics''.

@example
(define-syntax begin0
  (syntax-rules ()
    ((_ ?form0 ?form ...)
     (call-with-values
         (lambda () ?form0)
       (lambda x
         ?form ...
         (apply values x))))))
@end example

This syntax is called @func{prog1} in Common Lisp and Emacs Lisp.
@end deffn

@c ------------------------------------------------------------

@deffn Syntax receive @var{formals} @var{expression} . @var{body}
The mechanism provided for binding identifiers to the values of a
multiple--valued expression are @func{call-with-values},
@func{let-values}, @func{let*-values}.  @func{receive} is another
concise and readable syntax for creating such bindings.

@var{formals} can have any of 3 forms:

@table @code
@item (variable1 ... variablen)
The environment in which the @func{receive}--expression is evaluated is
extended by binding @var{variable1}, ..., @var{variablen} to fresh
locations.  The @var{expression} is evaluated, and its values are stored
into those locations (it is an error if @var{expression} does not have
exactly @var{n} values).

@item variable
The environment in which the @func{receive}--expression is evaluated is
extended by binding @var{variable} to a fresh location.  The
@var{expression} is evaluated, its values are converted into a newly
allocated list, and the list is stored in the location bound to
@var{variable}.

@item (variable1 ... variablen . variablen+1)
The environment in which the @func{receive}--expression is evaluated is
extended by binding @var{variable1}, ..., @var{variablen+1} to fresh
locations.  The @var{expression} is evaluated.  Its first @var{n} values
are stored into the locations bound to @var{variable1}, ...,
@var{variablen}.  Any remaining values are converted into a newly
allocated list, which is stored into the location bound to
@var{variablen+1} (it is an error if @var{expression} does not have at
least @var{n} values.
@end table

In any case, the expressions in @var{body} are evaluated sequentially in
the extended environment.  The results of the last expression in the
body are the values of the @var{receive}--expression.
@end deffn

@c ------------------------------------------------------------

@deffn Syntax cut @var{slot-or-expr} ...
@deffnx Syntax cute @var{slot-or-expr} ...
When programming in functional style, it is frequently necessary to
specialize some of the parameters of a multi--parameter procedure.  For
example, from the binary operation @func{cons} one might want to obtain
the unary operation @code{(lambda (x) (cons 1 x))}.  This specialization
of parameters is also known as ``partial application'', ``operator
section'' or ``projection''.

The @func{cut} and @func{cute} syntaxes allow to write this sort of
specialization in a simple and compact way.  Examples:

@example
(cut cons (+ a 1) <>)   = (lambda (x2)
                            (cons (+ a 1) x2))

(cut list 1 <> 3 <> 5)  = (lambda (x2 x4)
                            (list 1 x2 3 x4 5))

(cut list)              = (lambda () (list))

(cut list 1 <> 3 <...>) = (lambda (x2 . xs)
                            (apply list 1 x2 3 xs))

(cut <> a b)            = (lambda (f) (f a b))
@end example

@func{cut} specializes some of the parameters of its first argument.
The parameters that are to show up as formal variables of the result are
indicated by the symbol @code{<>}, pronouced as ``slot''.  In addition,
the symbol @code{<...>}, pronounced as ``rest--slot'', matches all
residual arguments of a variable argument procedure.  The first argument
can also be a slot.

@func{cute} evaluates the non--slot expressions at the time the
procedure is specialized, not at the time the specialized procedure is
called.  For example:

@example
(cute cons (+ a 1) <>) = (let ((a1 (+ a 1)))
                           (lambda (x2)
                             (cons a1 x2)))
@end example

@noindent
@func{cute} will evaluate @code{(+ a 1)} once, while the @func{cut}
would evaluate it during every invocation of the resulting procedure.

The formal syntax of a specialized expression is:

@example
<cut-expression> -> (cut  <slot-or-expr> <slot-or-expr>*)
                 |  (cut  <slot-or-expr> <slot-or-expr>* <...>)
                 |  (cute <slot-or-expr> <slot-or-expr>*)
                 |  (cute <slot-or-expr> <slot-or-expr>* <...>)

<slot-or-expr>   -> <>              ; a "slot"
                 |  <expression>    ; a "non-slot expression"
@end example
@end deffn

@c ------------------------------------------------------------

@deffn Syntax recursion @var{formals} @var{body}
Allow the simple and non--imperative construction of self--referential
expressions.  The implementation is:

@example
(define-syntax recursion
  (syntax-rules ()
    ((_ (?name . ?variables) . ?body)
     (letrec ((?name (lambda ?variables . ?body))) ?name))
    ((_ ?name ?expr)
     (letrec ((?name ?expr)) ?name))))
@end example

@func{recursion} generates an inner lambda using @var{formals} and
@var{body}; the first identifier in @var{formals} is meant to be bound
to the inner lambda itself.  The return value is a function that invokes
the inner lambda with the inner lambda and the given arguments.

For example the following:

@example
(define (dummy n)
  (let loop ((n n))
    (if (zero? n)
        1
      (* n (loop (- n 1))))))

(dummy 5)
@end example

@noindent
can be rewritten:

@example
(letrec ((dummy (lambda (loop n)
                  (if (zero? n)
                      1
                    (* n (loop (- n 1)))))))
  (dummy 5))
@result{} 120
@end example

@noindent
which can be written:

@example
((recursion (loop n)
   (if (zero? n)
       1
     (* n (loop (- n 1)))))
 5)
@result{} 120
@end example
@end deffn

@c ------------------------------------------------------------


@c page
@node naulib parameters
@section Paremeter objects


@defun make-parameter @var{init}
@defunx make-parameter @var{init} @var{converter}
Return a new parameter object which is bound to a cell containing the
value returned by @code{(converter init)}.  If @var{converter} is not
specified, the identity function is used instead.

The parameter object is a procedure which accepts zero or one argument.
When it is called with no argument, the content of the cell bound to
this parameter object in the current dynamic environment is returned.
When it is called with one argument, the content of the cell bound to
this parameter object in the current dynamic environment is set to the
result of the call @code{(converter arg)}, where @var{arg} is the
argument passed to the parameter object, and an unspecified value is
returned.

Examples:

@example
(define radix
  (make-parameter 10))

(radix)         @result{} 10
(radix 2)
(radix)         @result{} 2
@end example
@end defun


@deffn Syntax parameterize ((@var{expr1} @var{expr2}) ...) . @var{body}
@deffnx Syntax parameterise ((@var{expr1} @var{expr2}) ...) . @var{body}
The expressions @var{expr1} and @var{expr2} are evaluated in an
unspecified order.  The value of the @var{expr1} expressions must be
parameter objects.  For each @var{expr1} expression and in an
unspecified order, the local dynamic environment is extended with a
binding of the parameter object @var{expr1} to a new cell whose content
is the result of the call @code{(converter val)}, where @var{val} is the
value of @var{expr2} and @var{converter} is the conversion procedure of
the parameter object.

The resulting dynamic environment is then used for the evaluation of the
forms in @var{body}.  The result(s) of the @func{parameterize} form are
the result(s) of the @var{body}.

With reference to the example above:

@example
(radix)
=>  2

(parameterize ((radix 16))
  (radix))
=>  16

(radix)
=>  2

(define (f n)
  (number->string n (radix)))

(f 10)
=>  "1010"

(parameterize ((radix 8))
  (f 10))
=>  "12"
@end example
@end deffn


@c page
@node naulib shared
@section External representation for data with shared structure


@rnrs{6} Scheme provides the procedure @func{write}, which prints
machine--readable representations of lists and other objects.  However,
the printed representation does not preserve information about what
parts of the structure are shared, and in the case of self--referential
objects the behavior of @func{write} itself is undefined; it is
permitted to go into an infinite loop.

For example, it is possible to have a list within which two or more
members are the same string (in the sense of @func{eq?}), but when the
list is written, there is not sufficient information in the
representation to recover the @func{eq?} relationship.  When the list is
read back in, there will be two or more copies of the string which are
@func{eqv?}  but not @func{eq?}.

As an example of the second problem, the results of evaluating:

@example
(define a (cons 'val1 'val2))
(set-cdr! a a)
(write a)
@end example

@noindent
are undefined.

@defun write-with-shared-structure @var{obj}
@defunx write-with-shared-structure @var{obj} @var{port}
@defunx write-with-shared-structure @var{obj} @var{port} @var{optarg}
@defunx write/ss @var{obj}
@defunx write/ss @var{obj} @var{port}
@defunx write/ss @var{obj} @var{port} @var{optarg}
Write a written representation of @var{obj} to the given @var{port}.
Strings that appear in the written representation are enclosed in
doublequotes, and within those strings backslash and doublequote
characters are escaped by backslashes.  Character objects are written
using the @code{#\} notation.

@func{write-with-shared-structure} and @func{write/ss} are bound to the
same function.

Objects which denote locations rather than values, if they appear at
more than one point in the data being written, are preceded by
@code{#N=} the first time they are written and replaced by @code{#N#}
all subsequent times they are written, where @var{N} is a natural number
used to identify that particular object.

If objects which denote locations occur only once in the structure, then
@func{write/ss} produces the same external representation for those
objects as @func{write}.

@func{write/ss} terminates in finite time when writing finite data, and
produces a finite representation when writing finite data.

@func{write/ss} returns an unspecified value.

The @var{port} argument may be omitted, in which case it defaults to the
value returned by @func{current-output-port}.

The @var{optarg} is ignored.

The following example shows a cons cell whose cdr contains itself:

@example
(define a (cons 'val1 'val2))
(set-cdr! a a)
(write/ss a)
@print{} #1=(val1 . #1#)
@end example
@end defun


@defun read-with-shared-structure
@defunx read-with-shared-structure @var{port}
@defunx read/ss
@defunx read/ss @var{port}
Convert the external representations of Scheme objects produced by
@func{write/ss} into scheme objects.  Return the next object parsable
from the given input @var{port}, updating @var{port} to point to the
first character past the end of the external representation of the
object.

@func{read-with-shared-structure} and @func{read/ss} are bound to the
same function.

If an end--of--file is encountered in the input before any characters
are found that can begin an object, then an end--of--file object is
returned.  The port remains open, and further attempts to read it (by
@func{read/ss} or @func{read}) will also return an end--of--file object.

If an end--of--file is encountered after the beginning of an object's
external representation, but the external representation is incomplete
and therefore not parsable, an error is signalled.

The @var{port} argument may be omitted, in which case it defaults to the
value returned by @func{current-input-port}.

It is an error to read from a closed port.
@end defun


@c page
@node naulib environment
@section Accessing environment variables


Most operating systems provide a mechanism for passing auxiliary
parameters implicitly to child processes.  Usually, this mechanism is
called ``the environment'', and is conceptually a map from string names
to string values.  The string names are called environment variables.

Some applications rely on environment variables to modify their behavior
according to local settings.  Also, various established protocols rely
on environment variables as a form of interprocess communication.


@deffn Procedure get-environment-variable @var{name}
Return the value of the named environment variable as a string, or
@false{} if the named environment variable is not found.  The name
argument is expected to be a string.  @func{get-environment-variable}
may use locale--setting information to encode the name and decode the
value of the environment variable.  If the value cannot be decoded, an
exception may be raised.

@example
(get-environment-variable "PATH")
@result{} "/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin"
@end example
@end deffn


@deffn Procedure get-environment-variables
Return names and values of all the environment variables as an alist.
The same decoding considerations as for @func{get-environment-variable}
apply.

@example
(get-environment-variables)
@result{} (("PATH" .
     "/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin")
    ("USERNAME" . "taro"))
@end example
@end deffn


@c page
@node naulib unimplemented
@section Unimplemented error condition


@deftp Condition &unimplemented
The type of condition used to signal unimplemented features.  It is
defined as:

@example
(define-condition-type &unimplemented &error
  make-unimplemented-condition
  unimplemented-condition?)
@end example
@end deftp


@defun raise-unimplemented-error @var{who}
@defunx raise-unimplemented-error @var{who} @var{message}
@defunx raise-unimplemented-error @var{who} @var{message} @var{irritants}
Raise a composite condition embedding a condition of type
@code{&unimplemented}.

@var{who} is used for the @code{&who} component.

@var{message} is used for the @code{&message} component and defaults to
@code{feature not implemented or not available}.

@var{irritants} is used for the @code{&irritants} component.
@end defun


@c page
@node more
@chapter More libraries


@menu
* lists::                       Lists library.
* char-sets::                   Character sets library.
* strings::                     Strings library.
* vectors::                     Vectors library.
* checks::                      Lightweight testing.

Low level libraries

* one-dimension::               One dimensional extended ranges.
@end menu

@c page
@node lists
@section List library


This chapter describes the @library{lists} library: a collection of
functions and macros to manipulate lists.


@menu
* lists intro::                 Introduction.
* lists linear update::         Linear update procedures.
* lists improper::              Improper lists.
* lists cons::                  Constructors.
* lists pred::                  Predicates.
* lists select::                Selectors.
* lists misc::                  Length, append, concatenate,
                                reverse, zip and count.
* lists fold::                  Fold, unfold and map.
* lists filter::                Filtering and partitioning.
* lists search::                Searching.
* lists delete::                Deletion.
* lists alist::                 Association lists.
* lists set::                   Set operations on lists.
@end menu

@c page
@node lists intro
@subsection Introduction


The documentation of this library obeys the following conventions for
procedure formals:

@table @var
@item list
A proper (finite, nil--terminated) list.

@item clist
A proper or circular list.

@item flist
A finite (proper or dotted) list.

@item pair
A pair.

@item x
@itemx y
@itemx d
@itemx a
Any value.

@item object
@itemx value
Any value.

@item n
@itemx i
A natural number (an integer @math{>= 0}).

@item proc
A procedure.

@item pred
A procedure whose return value is treated as a boolean.

@item =
A boolean procedure taking two arguments.
@end table

It is an error to pass a circular or dotted list to a procedure not
defined to accept such an argument.


@c page
@node lists linear update
@subsection Linear update procedures


Many procedures in this library have ``pure'' and ``linear update''
variants.  A ``pure'' procedure has no side--effects, and in particular
does not alter its arguments in any way.  A ``linear update'' procedure
is allowed, but not required, to side--effect its arguments in order to
construct its result.  ``Linear update'' procedures are typically given
names ending with an exclamation point.

So, for example, @code{(append!  list1 list2)} is allowed to construct
its result by simply using @func{set-cdr!} to set the cdr of the last
pair of @var{list1} to point to @var{list2}, and then returning
@var{list1} (unless @var{list1} is the empty list, in which case it
would simply return @var{list2}).  However, @func{append!} may also
elect to perform a pure append operation, this is a legal definition of
append!:

@example
(define append! append)
@end example

This is why we do not call these procedures ``destructive'', because
they aren't required to be destructive.  They are @emph{potentially}
destructive.

What this means is that we may only apply linear--update procedures to
values that we know are ``dead'', values that will never be used again
in our program.  This must be so, since we can't rely on the value
passed to a linear--update procedure after that procedure has been
called.  It might be unchanged; it might be altered.

The ``linear'' in ``linear update'' doesn't mean ``linear time'' or
``linear space'' or any sort of multiple--of--n kind of meaning.  It's a
fancy term that type theorists and pure functional programmers use to
describe systems where we are only allowed to have exactly one
reference to each variable.  This provides a guarantee that the value
bound to a variable is bound to no other variable.  So when we use a
variable in a variable reference, we ``use it up''.  Knowing that no
one else has a pointer to that value means the a system primitive is
free to side--effect its arguments to produce what is, observationally,
a pure--functional result.

In the context of this library, ``linear update'' means we, the
programmers, know there are no other live references to the value passed
to the procedure; after passing the value to one of these procedures,
the value of the old pointer is indeterminate.  Basically, we are
licensing the Scheme implementation to alter the data structure if it
feels like it; we have declared we don't care either way.

We get no help from Scheme in checking that the values we claim are
``linear'' really are.  So we better get it right.  Or play it safe and
use the non--@code{!} procedures; it doesn't do any good to compute
quickly if we get the wrong answer.

Why go to all this trouble to define the notion of ``linear update'' and
use it in a procedure spec, instead of the more common notion of a
``destructive'' operation?

First, note that destructive list--processing procedures are almost
always used in a linear--update fashion.  This is in part required by
the special case of operating upon the empty list, which can't be
side--effected.  This means that destructive operators are not pure
side--effects, they have to return a result.

Second, note that code written using linear--update operators can be
trivially ported to a pure, functional subset of Scheme by simply
providing pure implementations of the linear--update operators.

Finally, requiring destructive side--effects ruins opportunities to
parallelise these operations, and the places where one has taken the
trouble to spell out destructive operations are usually exactly the code
one would want a parallelising compiler to parallelise: the
efficiency--critical kernels of the algorithm.

Linear--update operations are easily parallelised.  Going with a
linear--update spec doesn't close off these valuable alternative
implementation techniques.  This list library is intended as a set of
low--level, basic operators, so we don't want to exclude these possible
implementations.

The linear--update procedures in this library are:

@example
alist-delete!           append!
append-map!             append-reverse!
break!                  concatenate!
delete!                 delete-duplicates!
drop-right!             filter!
lset-adjoin!            lset-diff+intersection!
lset-difference!        lset-xor!
lset-intersection!      lset-union!
map!                    partition!
remove!                 reverse!
span!                   split-at!
take!                   take-while!
@end example



@c page
@node lists improper
@subsection Improper lists


Scheme does not properly have a list type, just as the C language does
not have a string type.  Rather, Scheme has a binary--tuple type, from
which one can build binary trees.  There is an interpretation of Scheme
values that allows one to treat these trees as lists.  Further
complications ensue from the fact that Scheme allows side--effects to
these tuples, raising the possibility of lists of unbounded length, and
trees of unbounded depth (that is, circular data structures).

However, there is a simple view of the world of Scheme values that
considers every value to be a list of some sort.  That is, every value
is either:

@itemize
@item
a ``proper list'' (a finite, nil--terminated list) such as:

@example
(a b c)
()
(32)
@end example

@item
a ``dotted list'' (a finite, non--nil terminated list) such as:

@example
(a b c . d)
(x . y)
42
george
@end example

@item
or a ``circular list'' (an infinite, unterminated list).
@end itemize

Note that the zero--length dotted lists are simply all the non--null,
non--pair values.

This view is captured by the predicates @func{proper-list?},
@func{dotted-list?}, and @func{circular-list?}.  This list library users
should note that dotted lists are not commonly used, and are considered
by many Scheme programmers to be an ugly artifact of Scheme's lack of a
true list type.  However, dotted lists do play a noticeable role in the
syntax of Scheme, in the ``rest'' parameters used by n--ary lambdas:

@example
(lambda (x y . rest) ...)
@end example

Dotted lists are not fully supported by this list library.  Most
procedures are defined only on proper lists, that is: finite,
nil--terminated lists.  The procedures that will also handle circular or
dotted lists are specifically marked.  While this design decision
restricts the domain of possible arguments one can pass to these
procedures, it has the benefit of allowing the procedures to catch the
error cases where programmers inadvertently pass scalar values to a list
procedure by accident, e.g., by switching the arguments to a procedure
call.


@c page
@node lists cons
@subsection Constructors


@cindex List constructors


@menu
* lists cons pair::             Pair constructors.
* lists cons list::             List constructors.
@end menu


@c page
@node lists cons pair
@subsubsection Pair constructors


@defun xcons @var{d} @var{a}
Defined as @code{(lambda (d a) (cons a d))}.  Of utility only as a value
to be conveniently passed to higher--order procedures.

@example
(xcons '(b c) 'a) => (a b c)
@end example

The name stands for ``eXchanged CONS''.
@end defun


@c page
@node lists cons list
@subsubsection List constructors


@defun make-list @var{n} [@var{fill}]
Return an @var{n}--element list, whose elements are all the value
@var{fill}.  If the fill argument is not given, the elements of the list
may be arbitrary values.

@example
(make-list 4 'c)
=> (c c c c)

(make-list 0)
=> ()
@end example
@end defun


@defun list-tabulate @var{n} @var{init-proc}
Return an @var{n}--element list.  Element @var{i} of the list, where
@code{0 <= i < n}, is produced by @code{(init-proc i)}.  No guarantee is
made about the dynamic order in which @var{init-proc} is applied to
these indices.

@example
(list-tabulate 4 values)
=> (0 1 2 3)
@end example
@end defun


@defun list-copy @var{flist}
Copy the spine of the argument.
@end defun


@defun tree-copy @var{list}
Copy the whole tree of the list @var{list}, not only the spine (which is
what @func{list-copy} does).
@end defun


@defun circular-list @var{elt1} @var{elt2} ...
Construct a circular list of the elements.

@example
(circular-list 'z 'q) => (z q z q z q ...)
@end example
@end defun


@defun iota @var{count} [@var{start} @var{step}]
Return a list containing the elements:

@example
(start start+step ... start+(count-1)*step)
@end example

The @var{start} and @var{step} parameters default to @code{0} and
@code{1}, respectively.  This procedure takes its name from the
@acronym{APL} primitive.

@example
(iota 5)
=> (0 1 2 3 4)

(iota 5 0 -0.1)
=> (0 -0.1 -0.2 -0.3 -0.4)
@end example

@quotation
@strong{Nausicaa note:} When @var{count} is negative the return value is
the empty list.
@end quotation
@end defun


@c page
@node lists pred
@subsection Predicates


@cindex List predicates


@menu
* lists pred kind::             Kinds of list.
* lists pred null::             Null predicates.
* lists pred pair::             Pair predicates.
* lists pred cmp::              Comparison.
@end menu

@c page
@node lists pred kind
@subsubsection Kind of list


The predicates @func{proper-list?}, @func{circular-list?}, and
@func{dotted-list?} partition the entire universe of Scheme values.


@defun proper-list? @var{x}
Return true if, and only if, @var{x} is a proper list: a finite,
nil--terminated list.  More carefully: The empty list is a proper list.
A pair whose cdr is a proper list is also a proper list:

@example
<proper-list> ::= ()                        (Empty proper list)
              |   (cons <x> <proper-list>)  (Proper-list pair)
@end example

Note that this definition rules out circular lists.  This function is
required to detect this case and return @false{}.  Nil--terminated lists
are called ``proper'' lists by @rnrs{5} and Common Lisp.  The opposite
of proper is improper.

@rnrs{5} binds this function to the variable @func{list?}.

@example
(not (proper-list? x)) = (or (dotted-list? x)
                             (circular-list? x))
@end example
@end defun


@defun circular-list? @var{x}
Return @true{} if @var{x} is a circular list.  A circular list is a
value such that for every @code{n >= 0}, @code{cdrn(x)} is a pair.

Terminology: The opposite of circular is finite.

@example
(not (circular-list? x)) = (or (proper-list? x)
                               (dotted-list? x))
@end example
@end defun


@defun dotted-list? @var{x}
Return @true{} if @var{x} is a finite, non--nil--terminated list.  That
is, there exists an @code{n >= 0} such that @code{cdrn(x)} is neither a
pair nor @code{()}.  This includes non--pair, non--@code{()} values
(e.g. symbols, numbers), which are considered to be dotted lists of
length 0.

@example
(not (dotted-list? x)) = (or (proper-list? x)
                             (circular-list? x))
@end example
@end defun


@c page
@node lists pred null
@subsubsection Null predicates


@defun null-list? @var{clist}
@var{clist} is a proper or circular list.  This procedure returns
@true{} if the argument is the empty list, and @false{} otherwise.  It
is an error to pass this procedure a value which is not a proper or
circular list.  This procedure is recommended as the termination
condition for list--processing procedures that are not defined on dotted
lists.
@end defun


@c page
@node lists pred pair
@subsubsection Pair predicates


@defun not-pair? @var{x}
Defined as: @code{(lambda (x) (not (pair? x)))}.  Provided as a
procedure as it can be useful as the termination condition for
list--processing procedures that wish to handle all finite lists, both
proper and dotted.
@end defun


@c page
@node lists pred cmp
@subsubsection Comparison


@defun list=? @var{elm=?} @var{list1} ...
Determines list equality, given an element--equality procedure
@var{elm=?}.  Proper list @var{AL} equals proper list @var{BL} if:

@enumerate
@item
They are of the same length.

@item
Their corresponding elements are equal, according to @var{elm=?}.
@end enumerate

If the element--comparison procedure's first argument is from
@var{listi}, then its second argument is from @var{listi+1}, i.e. it is
always called as @code{(elm=? a b)} for @var{a} an element of list
@var{AL}, and @var{b} an element of list @var{BL}.

In the n--ary case, every @var{listi} is compared to @var{listi+1} (as
opposed, for example, to comparing @var{list1} to every @var{listi}, for
@code{i>1}).

If there are no list arguments at all, @func{list=?} simply returns
@true{}.

It is an error to apply @func{list=?} to anything except proper lists.
While implementations may choose to extend it to circular lists, note
that it cannot reasonably be extended to dotted lists, as it provides no
way to specify an equality procedure for comparing the list terminators.

@quotation
@emph{Nausicaa note:} The @value{PACKAGE} implementation does
@strong{not} support comparison of circular lists.
@end quotation

Note that the dynamic order in which the @var{elm=?} procedure is applied
to pairs of elements is not specified.  For example, if @func{list=?} is
applied to three lists, @var{AL}, @var{BL}, and @var{CL}, it may first
completely compare @var{AL} to @var{BL}, then compare @var{BL} to
@var{CL}, or it may compare the first elements of @var{AL} and @var{BL},
then the first elements of @var{BL} and @var{CL}, then the second
elements of @var{AL} and @var{BL}, and so forth.

The equality procedure must be consistent with @func{eq?}.  That is, it
must be the case that:

@example
(eq? x y) @result{} (elm=? x y)
@end example

Note that this implies that two lists which are @func{eq?} are always
@func{list=?}, as well; implementations may exploit this fact to
``short--cut'' the element--by--element comparisons.

Examples:

@example
(list=? eq?)
@result{} #t

(list=? eq? '(a))
@result{} #t

(list=? = '(1 2 3 4)
          '(1 2 3 4))
@result{} #t

(list=? = '(1 2 3 4)
          '(1 9 3 4))
@result{} #f

(list=? = '(1 2 3 4)
          '(1 2 3 4)
          '(1 2 3 4))
@result{} #t

(list=? = '(1 2 3 4)
          '(1 2 3 4 5)
          '(1 2 3 4))
@result{} #f

(list=? = '())
@result{} #t

(list=? = '() '())
@result{} #t

(list=? = '() '() '())
@result{} #t
@end example
@end defun


@c page
@node lists select
@subsection Selectors


@cindex List selectors


@defun first @var{pair}
@defunx second  @var{pair}
@defunx third   @var{pair}
@defunx fourth  @var{pair}
@defunx fifth   @var{pair}
@defunx sixth   @var{pair}
@defunx seventh @var{pair}
@defunx eighth  @var{pair}
@defunx ninth   @var{pair}
@defunx tenth   @var{pair}
Synonyms for @func{car}, @func{cadr}, @func{caddr}, ...

@example
(third '(a b c d e))
=> c
@end example
@end defun


@defun {car+cdr} @var{pair}
The fundamental pair deconstructor:

@example
(lambda (p)
  (values (car p) (cdr p)))
@end example

This can, of course, be implemented more efficiently by a compiler.
@end defun


@defun take @var{x} @var{i}
@defunx drop @var{x} @var{i}
@func{take} returns the first @var{i} elements of list @var{x}.
@func{drop} returns all but the first @var{i} elements of list @var{x}.

@example
(take '(a b c d e) 2)
=> (a b)

(drop '(a b c d e) 2)
=> (c d e)
@end example

@var{x} may be any value: a proper, circular, or dotted list:

@example
(take '(1 2 3 . d) 2)
=> (1 2)

(drop '(1 2 3 . d) 2)
=> (3 . d)

(take '(1 2 3 . d) 3)
=> (1 2 3)

(drop '(1 2 3 . d) 3)
=> d
@end example

For a legal @var{i}, @func{take} and @func{drop} partition the list in a
manner which can be inverted with append:

@example
(append (take x i) (drop x i)) = x
@end example

@func{drop} is exactly equivalent to performing @var{i} @func{cdr}
operations on @var{x}; the returned value shares a common tail with
@var{x}.

If the argument is a list of non--zero length, @func{take} is guaranteed
to return a freshly--allocated list, even in the case where the entire
list is taken, e.g. @code{(take lis (length lis))}.

@quotation
@emph{Nausicaa note:} @func{drop} will return a list which is a portion
of the argument, unless the return value is the empty list.
@end quotation
@end defun


@defun take-right @var{flist} @var{i}
@defunx drop-right @var{flist} @var{i}
@func{take-right} returns the last @var{i} elements of @var{flist}.
@func{drop-right} returns all but the last @var{i} elements of
@var{flist}.

@example
(take-right '(a b c d e) 2)
=> (d e)

(drop-right '(a b c d e) 2)
=> (a b c)
@end example

The returned list may share a common tail with the argument list.

@var{flist} may be any finite list, either proper or dotted:

@example
(take-right '(1 2 3 . d) 2)
=> (2 3 . d)

(drop-right '(1 2 3 . d) 2)
=> (1)

(take-right '(1 2 3 . d) 0)
=> d

(drop-right '(1 2 3 . d) 0)
=> (1 2 3)
@end example

For a legal @var{i}, @func{take-right} and @var{drop-right} partition
the list in a manner which can be inverted with append:

@example
(append (take flist i) (drop flist i)) = flist
@end example

The return value of @func{take-right} is guaranteed to share a common
tail with @var{flist}.

If the argument is a list of non--zero length, @func{drop-right} is
guaranteed to return a freshly--allocated list, even in the case where
nothing is dropped, e.g. @code{(drop-right lis 0)}.
@end defun


@defun take! @var{clist} @var{i}
@defunx drop-right! @var{flist} @var{i}
@func{take!} and @func{drop-right!} are linear--update variants of
@func{take} and @func{drop-right}: the procedure is allowed, but not
required, to alter the argument list to produce the result.

If @var{x} is circular, @func{take!} may return a
shorter--than--expected list:

@example
(take! (circular-list 1 3 5) 8)
=> (1 3)

(take! (circular-list 1 3 5) 8)
=> (1 3 5 1 3 5 1 3)
@end example

@quotation
@emph{Nausicaa note:} When applied to a circular list these functions go
in infinite loop.
@end quotation
@end defun


@defun split-at @var{x} @var{i}
@defunx split-at! @var{x} @var{i}
@func{split-at} splits the list @var{x} at index @var{i}, returning a
list of the first @var{i} elements, and the remaining tail.  It is
equivalent to:

@example
(values (take x i) (drop x i))
@end example

@func{split-at!} is the linear--update variant.  It is allowed, but not
required, to alter the argument list to produce the result.

@example
(split-at '(a b c d e f g h) 3)
=> (a b c)
   (d e f g h)
@end example
@end defun


@defun last @var{pair}
@defunx last-pair @var{pair}
@func{last} returns the last element of the non--empty, finite list
@var{pair}.  @func{last-pair} returns the last pair in the non--empty,
finite list @var{pair}.

@example
(last '(a b c)) => c
(last-pair '(a b c)) => (c)
@end example
@end defun


@c page
@node lists misc
@subsection Length, append, concatenate, reverse, zip and count


@cindex List length
@cindex List appending
@cindex List contatenation
@cindex List reversing
@cindex List zip
@cindex List counting


@defun {length+} @var{clist}
Return the length of the argument, or @false{} if @var{clist} is a
circular list.

The length of a proper list is a non--negative integer @math{n} such
that @func{cdr} applied @math{n} times to the list produces the empty
list.
@end defun


@defun append! @var{list1} ...
Return a list consisting of the elements of @var{list1} followed by the
elements of the other list parameters.  It is allowed, but not required,
to alter cons cells in the argument lists to construct the result list.
The last argument is never altered; the result list shares structure
with this parameter.

@example
(append! '(x) '(y))        =>  (x y)
(append! '(a) '(b c d))    =>  (a b c d)
(append! '(a (b)) '((c)))  =>  (a (b) (c))
(append! '(a b) '(c . d))  =>  (a b c . d)
(append! '() 'a)           =>  a
(append! '(x y))           =>  (x y)
(append!)                  =>  ()
@end example
@end defun


@defun concatenate @var{list-of-lists}
@defunx concatenate! @var{list-of-lists}
These functions append the elements of their argument together.  That
is, @func{concatenate} returns:

@example
(apply append list-of-lists)
@end example

@noindent
or, equivalently:

@example
(reduce-right append '() list-of-lists)
@end example

@func{concatenate!} is the linear--update variant, defined in terms
of @func{append!} instead of @func{append}.

Note that some Scheme implementations do not support passing more than a
certain number (e.g. 64) of arguments to an n--ary procedure.  In these
implementations, the @code{(apply append ...)} idiom would fail when
applied to long lists, but concatenate would continue to function
properly.

As with @func{append} and @func{append!}, the last element of the input
list may be any value at all.
@end defun


@defun reverse! @var{list}
Return a newly allocated list consisting of the elements of @var{list}
in reverse order.  It is permitted, but not required, to alter the
argument's cons cells to produce the reversed list.

@example
(reverse! '(a b c))              =>  (c b a)
(reverse! '(a (b c) d (e (f))))  =>  ((e (f)) d (b c) a)
@end example
@end defun


@defun append-reverse @var{rev-head} @var{tail}
@defunx append-reverse! @var{rev-head} @var{tail}
@func{append-reverse} returns:

@example
(append (reverse rev-head) tail)
@end example

It is provided because it is a common operation, a common
list--processing style calls for this exact operation to transfer values
accumulated in reverse order onto the front of another list, and because
the implementation is significantly more efficient than the simple
composition it replaces.

@quotation
But note that this pattern of iterative computation followed by a
@func{reverse} can frequently be rewritten as a recursion, dispensing
with the @func{reverse} and @func{append-reverse} steps, and shifting
temporary, intermediate storage from the heap to the stack, which is
typically a win for reasons of cache locality and eager storage
reclamation.
@end quotation

@func{append-reverse!} is just the linear--update variant; it is
allowed, but not required, to alter @var{rev-head}'s cons cells to
construct the result.
@end defun


@defun zip @var{clist1} @var{clist2} ...
Defined as:

@example
(lambda lists (apply map list lists))
@end example

If @func{zip} is passed @var{n} lists, it returns a list as long as the
shortest of these lists, each element of which is an @var{n}--element
list comprised of the corresponding elements from the parameter lists.

@example
(zip '(one two three)
     '(1 2 3)
     '(odd even odd even odd even odd even))
  => ((one 1 odd) (two 2 even) (three 3 odd))

(zip '(1 2 3))
  => ((1) (2) (3))
@end example

At least one of the argument lists must be finite:

@example
(zip '(3 1 4 1)
     (circular-list #f #t))
=> ((3 #f)
    (1 #t)
    (4 #f)
    (1 #t))
@end example
@end defun


@defun unzip1 @var{list}
@defunx unzip2 @var{list}
@defunx unzip3 @var{list}
@defunx unzip4 @var{list}
@defunx unzip5 @var{list}
@func{unzip1} takes a list of lists, where every list must contain at
least one element, and returns a list containing the initial element of
each such list.  That is, it returns @code{(map car lists)}.

@func{unzip2} takes a list of lists, where every list must contain at
least two elements, and returns two values: a list of the first
elements, and a list of the second elements.

@func{unzip3} does the same for the first three elements of the lists,
and so forth.

@example
(unzip2 '((1 one) (2 two) (3 three)))
=> (1 2 3)
   (one two three)
@end example
@end defun


@defun count @var{pred} @var{clist1} ...
@var{pred} is a procedure taking as many arguments as there are lists
and returning a single value.  It is applied element--wise to the
elements of the lists, and a count is tallied of the number of elements
that produce a true value.  This count is returned.

@func{count} is ``iterative'' in that it is guaranteed to apply
@var{pred} to the list elements in a left--to--right order.  The
counting stops when the shortest list expires.

@example
(count even? '(3 1 4 1 5 9 2 5 6))                => 3
(count <     '(1 2 4 8) '(2 4 6 8 10 12 14 16))   => 3
@end example

At least one of the argument lists must be finite:

@example
(count < '(3 1 4 1) (circular-list 1 10)) => 2
@end example
@end defun


@c page
@node lists fold
@subsection Fold, unfold and map


@cindex List fold, unfold and map


@menu
* lists fold fold::             Folding.
* lists fold reduce::           Reducing.
* lists fold unfold::           Unfolding.
* lists fold map::              Mapping.
@end menu


@c page
@node lists fold fold
@subsubsection Folding


@defun fold @var{kons} @var{knil} @var{clist1} @var{clist2} ...
The fundamental list iterator.  First, consider the single
list--parameter case: if @code{clist1 = (e1 e2 ... en)}, then this
procedure returns:

@example
(kons en ... (kons e2 (kons e1 knil)) ... )
@end example

@noindent
that is, it obeys the (tail) recursion:

@example
(fold kons knil ell) = (fold kons (kons (car ell) knil)
                             (cdr ell))
(fold kons knil '()) = knil
@end example

Examples:

@example
;; add the elements
(fold + 0 '(1 2 3))
=> 6

;; reverse a list
(fold cons '() '(1 2 3))
=> (3 2 1)

;; append in reverse order
(fold cons '(4 5 6) '(3 2 1))
=> (1 2 3 4 5 6)

;; how many symbols?
(fold (lambda (x count)
        (if (symbol? x)
            (+ count 1)
          count))
      0
      '(a 1 b 2 c 3))
=> 3

;; length of the longest string
(fold (lambda (s len)
        (max len (string-length s)))
      0
      '("ciao" "hello" "salut" "hola"))
=> 5
@end example

If @var{n} list arguments are provided, then the @var{kons} function
must take @var{n+1} parameters: one element from each list, and the
``seed'' or fold state, which is initially @var{knil}.  The fold
operation terminates when the shortest list runs out of values:

@example
(fold cons* '() '(a b c)
                '(1 2 3 4 5))
=> (c 3
    b 2
    a 1)

(fold (lambda (a b c knil)
        (cons (list a b c) knil))
      '()
      '(1 2 3)
      '(10 20 30)
      '(100 200 300))
=> '((3 30 300)
     (2 20 200)
     (1 10 100))
@end example

At least one of the list arguments must be finite.
@end defun

@c ------------------------------------------------------------

@defun fold-right* @var{kons} @var{knil} @var{clist1} @var{clist2} ...
The fundamental list recursion operator.  First, consider the single
list--parameter case.  If @code{clist1 = (e1 e2 ... en)}, then this
procedure returns:

@example
(kons e1 (kons e2 ... (kons en knil)))
@end example

@noindent
that is, it obeys the recursion:

@example
(fold-right* kons knil ell) = (kons (car ell)
                                    (fold-right* kons knil (cdr ell)))
(fold-right* kons knil '()) = knil
@end example

Examples:

@example
;; copy the list
(fold-right* cons '() '(1 2 3))
=> (1 2 3)

;; add elements
(fold-right* + 0 numbers)
=> 45

;; prepend elements
(fold-right* cons '(4 5 6) '(1 2 3))
=> (1 2 3 4 5 6)

;; filter the even numbers
(fold-right* (lambda (x l)
               (if (even? x)
                   (cons x l)
                 l))
             '()
             '(0 1 2 3 4 5 6 7 8 9))
=> (0 2 4 6 8)
@end example

If @var{n} list arguments are provided, then the @var{kons} function
must take @var{n}+1 parameters: one element from each list, and the
``seed'' or fold state, which is initially @var{knil}.  The fold
operation terminates when the shortest list runs out of values:

At least one of the list arguments must be finite.

@example
(fold-right* cons*
             '()
             '(a b c)
             '(1 2 3 4 5))
=> (a 1
    b 2
    c 3)

(fold-right* (lambda (a b c knil)
               (cons (list a b c)
                     knil))
             '()
             '(1 2 3)
             '(10 20 30)
             '(100 200 300))
=> ((1 10 100)
    (2 20 200)
    (3 30 300))
@end example
@end defun

@c ------------------------------------------------------------

@defun pair-fold @var{kons} @var{knil} @var{clist1} @var{clist2} ...
Analogous to @func{fold}, but @var{kons} is applied to successive
sublists of the lists, rather than successive elements; that is,
@var{kons} is applied to the pairs making up the lists, giving this
(tail) recursion:

@example
(pair-fold kons knil ell) = (let ((tail (cdr ell)))
                              (pair-fold kons
                                         (kons ell knil)
                                         tail))
(pair-fold kons knil '()) = knil
@end example

For finite lists, the @var{kons} function may reliably apply
@func{set-cdr!} to the pairs it is given without altering the sequence
of execution.

Examples:

@example
(pair-fold (lambda (elm knil)
             (cons (car elm) knil))
           '(999)
           '(1 2 3))
=> (3 2 1 999)

;;; destructively reverse a list
(pair-fold (lambda (pair tail)
             (set-cdr! pair tail)
             pair)
           '()
           '(0 1 2 3 4 5))
=> (5 4 3 2 1 0)
@end example

At least one of the list arguments must be finite.
@end defun

@c ------------------------------------------------------------

@defun pair-fold-right @var{kons} @var{knil} @var{clist1} @var{clist2} ...
Holds the same relationship with @func{fold-right} that @func{pair-fold}
holds with fold.  Obeys the recursion:

@example
(pair-fold-right kons knil lis) =
    (kons lis (pair-fold-right kons knil (cdr lis)))
(pair-fold-right kons knil '()) = knil
@end example

Example:

@example
(pair-fold-right cons '() '(a b c)) => ((a b c) (b c) (c))
@end example

At least one of the list arguments must be finite.

Examples:

@example
(pair-fold-right (lambda (elm knil)
                   (cons (car elm) knil))
                 '(999)
                 '(1 2 3))
=> (1 2 3 999)

(pair-fold-right (lambda (pair tail)
                   (set-cdr! pair tail)
                   pair)
                 '()
                 '(0 1 2 3 4 5))
=> (0 1 2 3 4 5)

(pair-fold-right (lambda (a b c knil)
                   (cons (list (car a)
                               (car b)
                               (car c))
                         knil))
                 '(999)
                 '(1 2 3)
                 '(10 20 30)
                 '(100 200 300))
=> '((1 10 100)
     (2 20 200)
     (3 30 300)
     999)
@end example
@end defun


@c page
@node lists fold reduce
@subsubsection Reducing


@defun reduce @var{f} @var{ridentity} @var{list}
@func{reduce} is a variant of @func{fold}.  @var{ridentity} should be a
``right identity'' of the procedure @var{f}; that is, for any value
@var{x} acceptable to @var{f}:

@example
(f x ridentity) = x
@end example

@func{reduce} has the following definition:

@example
if list = (), return ridentity;
otherwise, return (fold f (car list) (cdr list)).
@end example

@noindent
in other words, we compute @code{(fold f ridentity list)}.

Note that @var{ridentity} is used only in the empty--list case.

You typically use @func{reduce} when applying @var{f} is expensive and
you'd like to avoid the extra application incurred when fold applies
@var{f} to the head of list and the identity value, redundantly
producing the same value passed in to @var{f}.  For example, if @var{f}
involves searching a file directory or performing a database query, this
can be significant.

In general, however, @func{fold} is useful in many contexts where
@func{reduce} is not (consider the examples given in the @func{fold}
definition: only one of the five folds uses a function with a right
identity; the other four may not be performed with @func{reduce}).

@quotation
@emph{Note:} @acronym{MIT} Scheme and Haskell flip @var{f}'s arguments
order for their @func{reduce} and @func{fold} functions.
@end quotation

Examples:

@example
;; take the max of a list of non-negative integers
(reduce max 0 '(1 2 3 4 5 6))
=> 6

(reduce + 0 '(0 1 2 3 4 5 6 7 8 9))
=> 45
@end example
@end defun


@defun reduce-right @var{f} @var{ridentity} @var{list}
@func{reduce-right} is the @func{fold-right} variant of
@func{reduce}.  It obeys the following definition:

@example
(reduce-right f ridentity '()) = ridentity
(reduce-right f ridentity '(e1)) = (f e1 ridentity) = e1
(reduce-right f ridentity '(e1 e2 ...)) =
    (f e1 (reduce f ridentity (e2 ...)))
@end example

@noindent
in other words, we compute @code{(fold-right f ridentity list)}.

@example
;; append a bunch of lists together
(reduce-right append
              '()
              '((1 2 3)
                (4 5)
                (6 7 8 9)
                (0)))
=> (1 2 3 4 5 6 7 8 9 0)
@end example
@end defun


@c page
@node lists fold unfold
@subsubsection Unfolding


@defun unfold @var{stop?} @var{map-to-elm} @var{seed-step} @var{first-seed} [@var{tail-gen}]
Generate a list from a starting value; return the result.  It is is the
fundamental recursive list constructor, just as @func{fold-right} is the
fundamental recursive list consumer.  It is best described by its basic
recursion:

@example
(unfold stop? map-to-elm seed-step seed tail-gen) =
    (if (stop? seed)
        (tail-gen seed)
      (cons (map-to-elm seed)
            (unfold stop? map-to-elm
                    seed-step (seed-step seed)
                    tail-gen)))
@end example

The arguments are:

@table @var
@item stop?
Determines when to stop: it is applied to the current seed value, and if
the return value is @true{}: unfolding stops.  If it evaluates to
@true{} at the first invocation: the return value of @func{unfold} is
the return value of @var{tail-gen}.

@item map-to-elm
Maps each seed value to the corresponding list element.  It is applied
to the current seed value and must return the value to append to the
result list.

@item seed-step
Maps each seed value to next seed value.

@item first-seed
The ``state'' value for the unfold.  It is the first seed value.

@item tail-gen
Applied to the seed value that caused @func{stop?} to return @true{},
must return the tail of the result list.  Defaults to @code{(lambda (x)
'())}.
@end table

While @func{unfold} may seem a bit abstract to novice functional
programmers, it can be used in a number of ways:

@example
;; List of squares: 1^2 ... 5^2
(unfold (lambda (x) (> x 5))
        (lambda (x) (* x x))
        (lambda (x) (+ x 1))
        1)
=> (1 4 9 16 25)

;; Copy a proper list.
(unfold null-list? car cdr '(1 2 3 4 5))
=> (1 2 3 4 5)

;; Read current input port into a list of values.
(unfold eof-object? values (lambda (x) (read)) (read))

;; Copy a possibly non-proper list:
(unfold not-pair? car cdr '(1 2 3 4 . 5) values)
=> (1 2 3 4 . 5)

;; Append HEAD onto TAIL:
(unfold null-list? car cdr '(1 2 3) (lambda (x) '(4 5 6)))
=> (1 2 3 4 5 6)
@end example

@quotation
@emph{Note:} Interested functional programmers may enjoy noting that
@func{fold-right} and @func{unfold} are in some sense inverses.  That
is, given operations @var{knull?}, @var{kar}, @var{kdr}, @var{kons}, and
@var{knil} satisfying:

@example
(kons (kar x) (kdr x)) = x and (knull? knil) = #t
@end example

@noindent
then:

@example
(fold-right kons knil (unfold knull? kar kdr x)) = x
@end example

@noindent
and:

@example
(unfold knull? kar kdr (fold-right kons knil x)) = x
@end example

This combinator sometimes is called an ``anamorphism''; when an explicit
@var{tail-gen} procedure is supplied, it is called an ``apomorphism''.
@end quotation
@end defun

@c ------------------------------------------------------------

@defun unfold-right @var{stop?} @var{map-to-elm} @var{seed-step} @var{first-seed} [@var{tail}]
Generate a list from a starting value; return the result.  It is the
fundamental iterative list constructor, just as @func{fold} is the
fundamental iterative list consumer.  Construct a list with the
following loop:

@example
(let loop ((seed seed)
           (ell  tail))
  (if (stop? seed)
      ell
    (loop (seed-step seed)
          (cons (map-to-elm seed) ell))))
@end example

Arguments are:

@table @var
@item stop?
Determine when to stop unfolding.

@item map-to-elm
Map each seed value to the corresponding list element.

@item seed-step
Map each seed value to next seed value.

@item first-seed
The ``state'' value for the unfold.

@item tail
List terminator; defaults to @code{'()}.
@end table

While @func{unfold-right} may seem a bit abstract to novice functional
programmers, it can be used in a number of ways:

@example
;; List of squares: 1^2 ... 10^2
(unfold-right zero?
              (lambda (x) (* x x))
              (lambda (x) (- x 1))
              5)
=> (1 4 9 16 25)

;; Reverse a proper list.
(unfold-right null-list? car cdr '(1 2 3 4 5))
=> (5 4 3 2 1)

;; Read current input port into a list of values.
(unfold-right eof-object? values (lambda (x) (read)) (read))

;; Equivalent to: (append-reverse rev-head tail)
(unfold-right null-list? car cdr '(3 2 1) '(4 5 6))
=> (1 2 3 4 5 6)
@end example

@quotation
@emph{Note:} Interested functional programmers may enjoy noting that
@func{fold} and @func{unfold-right} are in some sense inverses.  That
is, given operations @var{knull?}, @var{kar}, @var{kdr}, @var{kons}, and
@var{knil} satisfying:

@example
(kons (kar x) (kdr x)) = x and (knull? knil) = #t
@end example

@noindent
then:

@example
(fold kons knil (unfold-right knull? kar kdr x)) = x
@end example

@noindent
and:

@example
(unfold-right knull? kar kdr (fold kons knil x)) = x.
@end example

This combinator presumably has some pretentious mathematical name;
interested readers are invited to communicate it to the author.
@end quotation
@end defun


@c page
@node lists fold map
@subsubsection Mapping


@defun map* @var{proc} @var{clist1} @var{clist2} ...
@var{proc} is a procedure taking as many arguments as there are list
arguments and returning a single value.  @func{map} applies @var{proc}
element--wise to the elements of the lists and returns a list of the
results, in order.  The dynamic order in which @var{proc} is applied to
the elements of the lists is unspecified.

@example
(map* cadr '((a b)
             (d e)
             (g h)))
=>  (b e h)

(map* (lambda (n)
        (expt n n))
  '(1 2 3 4 5))
=> (1 4 27 256 3125)

(map* + '(1 2 3)
        '(4 5 6))
=> (5 7 9)

(let ((count 0))
  (map*
      (lambda (ignored)
        (set! count (+ count 1))
        count)
    '(a b)))
=> (1 2) or (2 1)
@end example

This procedure extends the @rnrs{6} specification of @func{map} to allow
the arguments to be of unequal length; it terminates when the shortest
list runs out of elements.

At least one of the list arguments must be a finite list:

@example
(map* +
      '(3 1 4 1)
      (circular-list 1 0))
=> (4 1 5 1)
@end example
@end defun


@defun for-each* @var{proc} @var{clist1} @var{clist2} ...
The arguments to @func{for-each*} are like the arguments to @func{map*},
but @func{for-each*} calls @var{proc} for its side effects rather than
for its values.  Unlike @func{map*}, @func{for-each*} is guaranteed to
call @var{proc} on the elements of the lists in order from the first
element(s) to the last, and the value returned by @func{for-each*} is
unspecified.

@example
(let ((v (make-vector 5)))
  (for-each*
       (lambda (i)
         (vector-set! v i (* i i)))
    '(0 1 2 3 4))
  v)
=> #(0 1 4 9 16)
@end example

This procedure extends the @rnrs{6} specification for @func{for-each} to
allow the arguments to be of unequal length; it terminates when the
shortest list runs out.

At least one of the list arguments must be a finite list.
@end defun


@defun append-map @var{f} @var{clist1} @var{clist2} ...
@defunx append-map! @var{f} @var{clist1} @var{clist2} ...
Equivalent to:

@example
(apply append (map f clist1 clist2 ...))
@end example

@noindent
and:

@example
(apply append! (map f clist1 clist2 ...))
@end example

@noindent
so it makes sense to use these functions only when the list returned by
the map process is a list of lists.

Map @var{f} over the elements of the lists, just as in the @func{map}
function.  However, the results of the applications are appended
together to make the final result.  @func{append-map} uses @func{append}
to append the results together; @func{append-map!} uses @func{append!}.

The dynamic order in which the various applications of @var{f} are made
is not specified.

Example:

@example
(append-map!
    (lambda (x)
      (list x (- x)))
  '(1 3 8))
=> (1 -1 3 -3 8 -8)

(append-map! list
             '(1 2 3)
             '(10 20 30))
=> (1 10 2 20 3 30)
@end example

At least one of the list arguments must be a finite list.
@end defun


@defun {map!} @var{f} @var{list1} @var{clist2} ...
Linear--update variant of @func{map}, @func{map!} is allowed, but not
required, to alter the cons cells of @var{list1} to construct the result
list.

The dynamic order in which the various applications of @var{f} are made
is not specified.  In the n--ary case, @var{clist2}, @var{clist3},
... must have at least as many elements as @var{list1}.
@end defun


@defun map-in-order @var{f} @var{clist1} @var{clist2} ...
A variant of the @func{map} procedure that guarantees to apply @var{f}
across the elements of the @var{listi} arguments in a left--to--right
order.  This is useful for mapping procedures that both have side
effects and return useful values.

At least one of the list arguments must be finite.
@end defun


@defun pair-for-each @var{f} @var{clist1} @var{clist2} ...
Like @func{for-each}, but @var{f} is applied to successive sublists of
the argument lists.  That is, @var{f} is applied to the cons cells of
the lists, rather than the lists' elements.  These applications occur in
left--to--right order.

The @var{f} procedure may reliably apply @func{set-cdr!} to the pairs it
is given without altering the sequence of execution.

@example
(pair-for-each
    (lambda (pair)
      (display pair)
      (newline))
  '(a b c))
; prints
;    (a b c)
;    (b c)
;    (c)
@end example

At least one of the list arguments must be a finite list.
@end defun


@defun filter-map @var{f} @var{clist1} @var{clist2} ...
Like @func{map}, but only true values are saved.

@example
(filter-map
    (lambda (x)
      (and (number? x)
           (* x x)))
  '(a 1 b 3 c 7))
=> (1 9 49)
@end example

The dynamic order in which the various applications of @var{f} are made
is not specified.

At least one of the list arguments must be finite.
@end defun


@c page
@node lists filter
@subsection Filtering and partitioning


@cindex List filtering


@defun filter @var{pred} @var{list}
@defunx filter! @var{pred} @var{list}
Return all the elements of @var{list} that satisfy predicate @var{pred}.
The list is not disordered: elements that appear in the result list
occur in the same order as they occur in @var{list}.  The returned list
may share a common tail with @var{list}.  The dynamic order in which the
various applications of @var{pred} are made is not specified.

@example
(filter even? '(0 7 8 8 43 -4)) => (0 8 8 -4)
@end example

@func{filter!} is the linear--update variant of @func{filter}.  It is
allowed, but not required, to alter the cons cells in @var{list} to
construct the result lists.
@end defun


@defun partition @var{pred} @var{list}
@defunx partition! @var{pred} @var{list}
Partition the elements of @var{list} with predicate @var{pred}, and
return two values: the list of in--elements and the list of
out--elements.  The list is not disordered: elements occur in the result
lists in the same order as they occur in @var{list}.  The dynamic order
in which the various applications of @var{pred} are made is not
specified.  One of the returned lists may share a common tail with
@var{list}.

@example
(partition symbol? '(one 2 3 four five 6))
  => (one four five)
     (2 3 6)
@end example

@func{partition!} is the linear--update variant of @func{partition}.
It is allowed, but not required, to alter the cons cells in @var{list}
to construct the result lists.
@end defun


@defun remove* @var{pred} @var{list}
@defunx remove*! @var{pred} @var{list}
Return @var{list} without the elements that satisfy predicate
@var{pred}:

@example
(lambda (pred list)
  (filter (lambda (x)
            (not (pred x)))
          list))
@end example

The list is not disordered: elements that appear in the result list
occur in the same order as they occur in @var{list}.  The returned list
may share a common tail with @var{list}.  The dynamic order in which the
various applications of pred are made is not specified.

@example
(remove* even? '(0 7 8 8 43 -4)) => (7 43)
@end example

@func{remove*!} is the linear--update variant of @func{remove*}.  It is
allowed, but not required, to alter the cons cells in @var{list} to
construct the result lists.
@end defun


@c page
@node lists search
@subsection Searching


@cindex List searching
@cindex Searching in lists


The following procedures all search lists for a leftmost element
satisfying some criteria.  This means they do not always examine the
entire list; thus, there is no efficient way for them to reliably detect
and signal an error when passed a dotted or circular list.  Here are the
general rules describing how these procedures work when applied to
different kinds of lists:

@table @emph
@item proper lists
The standard, canonical behavior happens in this case.

@item dotted lists
It is an error to pass these procedures a dotted list that does not
contain an element satisfying the search criteria.  That is, it is an
error if the procedure has to search all the way to the end of the
dotted list.

However, this @srfi{} does not specify anything at all about the
behavior of these procedures when passed a dotted list containing an
element satisfying the search criteria.  It may finish successfully,
signal an error, or perform some third action.

Different implementations may provide different functionality in this
case; code which is compliant with this @srfi{} may not rely on any
particular behavior.  Future @srfi{}'s may refine @ansrfi{1} to define
specific behavior in this case.

In brief, @ansrfi{1} compliant code may not pass a dotted list argument to
these procedures.

@item circular lists
It is an error to pass these procedures a circular list that does not
contain an element satisfying the search criteria.  Note that the
procedure is not required to detect this case; it may simply diverge.
It is, however, acceptable to search a circular list if the search is
successful; that is, if the list contains an element satisfying the
search criteria.
@end table


@defun find-tail @var{pred} @var{clist}
Return the first pair of @var{clist} whose car satisfies @var{pred}.  If
no pair does, return @false{}.

@func{find-tail} can be viewed as a general--predicate variant of the
@func{member*} function.

Examples:

@example
(find-tail even? '(3 1 37 -8 -5 0 0)) => (-8 -5 0 0)
(find-tail even? '(3 1 37 -5)) => #f

;; MEMBER* X LIS:
(find-tail (lambda (elt) (equal? x elt)) lis)
@end example

In the circular--list case, this procedure ``rotates'' the list.

@func{find-tail} is essentially @func{drop-while}, where the sense of
the predicate is inverted: @func{find-tail} searches until it finds an
element satisfying the predicate; @func{drop-while} searches until it
finds an element that doesn't satisfy the predicate.

@quotation
@emph{Note:} the @func{find} defined by @rnrs{6} has an ambiguity in its
lookup semantics: if @func{find} returns @false{}, we cannot tell (in
general) if it found a @false{} element that satisfied @var{pred}, or if
it did not find any element at all.

In many situations, this ambiguity cannot arise: either the list being
searched is known not to contain any @false{} elements, or the list is
guaranteed to have an element satisfying @var{pred}.

However, in cases where this ambiguity can arise, we should use
@func{find-tail} instead of @func{find}, @func{find-tail} has no such
ambiguity.
@end quotation
@end defun


@defun take-while @var{pred} @var{clist}
@defunx take-while! @var{pred} @var{clist}
Return the longest initial prefix of @var{clist} whose elements all
satisfy the predicate @var{pred}.

@func{take-while!} is the linear--update variant.  It is allowed, but
not required, to alter the argument list to produce the result.

Example:

@example
(take-while even? '(2 18 3 10 22 9)) => (2 18)
@end example
@end defun


@defun drop-while @var{pred} @var{clist}
Drops the longest initial prefix of @var{clist} whose elements all
satisfy the predicate @var{pred}, and returns the rest of the list.

Example:

@example
(drop-while even? '(2 18 3 10 22 9)) => (3 10 22 9)
@end example

The circular--list case may be viewed as ``rotating'' the list.
@end defun


@defun span @var{pred} @var{clist}
@defunx span! @var{pred} @var{list}
@defunx break @var{pred} @var{clist}
@defunx break! @var{pred} @var{list}
@func{span} splits the list into the longest initial prefix whose
elements all satisfy @var{pred}, and the remaining tail.  @func{break}
inverts the sense of the predicate: the tail commences with the first
element of the input list that satisfies the predicate.

In other words: @func{span} finds the intial span of elements satisfying
@var{pred}, and @func{break} breaks the list at the first element
satisfying @var{pred}.

@func{span} is equivalent to:

@example
(values (take-while pred clist)
        (drop-while pred clist))
@end example

@func{span!} and @func{break!} are the linear--update variants.  They
are allowed, but not required, to alter the argument list to produce the
result.

Examples:

@example
(span even? '(2 18 3 10 22 9))
  => (2 18)
     (3 10 22 9)

(break even? '(3 1 4 1 5 9))
  => (3 1)
     (4 1 5 9)
@end example
@end defun


@defun any @var{pred} @var{clist1} @var{clist2} ...
Apply the predicate across the lists, returning true if the predicate
returns true on any application.

If there are n list arguments @var{clist1} ... @var{clistn}, then
@var{pred} must be a procedure taking n arguments and returning a
boolean result.

@func{any} applies @var{pred} to the first elements of the @var{clisti}
parameters.  If this application returns a true value, @func{any}
immediately returns that value.  Otherwise, it iterates, applying pred
to the second elements of the @var{clisti} parameters, then the third,
and so forth.  The iteration stops when a true value is produced or one
of the lists runs out of values; in the latter case, @func{any} returns
@false{}.  The application of @var{pred} to the last element of the
lists is a tail call.

Note the difference between @func{find} and @func{any}: @func{find}
returns the element that satisfied the predicate; @func{any} returns the
true value that the predicate produced.

Like @func{every}, @func{any}'s name does not end with a question mark:
this is to indicate that it does not return a simple boolean (@true{} or
@false{}), but a general value.

Examples:

@example
(any integer? '(a 3 b 2.7))   => #t
(any integer? '(a 3.1 b 2.7)) => #f
(any < '(3 1 4 1 5)
       '(2 7 1 8 2)) => #t
@end example
@end defun


@defun every @var{pred} @var{clist1} @var{clist2} ...
Apply the predicate across the lists, returning true if the predicate
returns true on every application.

If there are n list arguments @var{clist1} ... @var{clistn}, then
@var{pred} must be a procedure taking n arguments and returning a
boolean result.

@func{every} applies @var{pred} to the first elements of the
@var{clisti} parameters.  If this application returns @false{},
@func{every} immediately returns @false{}.  Otherwise, it iterates,
applying @var{pred} to the second elements of the @var{clisti}
parameters, then the third, and so forth.  The iteration stops when a
@false{} value is produced or one of the lists runs out of values.  In
the latter case, @func{every} returns the true value produced by its
final application of @var{pred}.  The application of @var{pred} to the
last element of the lists is a tail call.

If one of the @var{clisti} has no elements, @func{every} simply returns
@true{}.

Like @func{any}, @func{every}'s name does not end with a question mark:
this is to indicate that it does not return a simple boolean (@true{} or
@false{}), but a general value.
@end defun


@defun list-index @var{pred} @var{clist1} @var{clist2} ...
Return the index of the leftmost element that satisfies @var{pred}.

If there are n list arguments, then @var{pred} must be a function taking
n arguments and returning a boolean result.

@func{list-index} applies @var{pred} to the first elements of the
@var{clisti} parameters.  If this application returns true,
@func{list-index} immediately returns zero.  Otherwise, it iterates,
applying @var{pred} to the second elements of the @var{clisti}
parameters, then the third, and so forth.  When it finds a tuple of list
elements that cause @var{pred} to return true, it stops and returns the
zero--based index of that position in the lists.

The iteration stops when one of the lists runs out of values; in this
case, @func{list-index} returns @false{}.

Examples:

@example
(list-index even? '(3 1 4 1 5 9)) => 2
(list-index < '(3 1 4 1 5 9 2 5 6) '(2 7 1 8 2)) => 1
(list-index = '(3 1 4 1 5 9 2 5 6) '(2 7 1 8 2)) => #f
@end example
@end defun


@defun member* @var{x} @var{list}
@defunx member* @var{x} @var{list} @var{=}
Return the first sublist of @var{list} whose car is @var{x}, where the
sublists of list are the non--empty lists returned by @code{(drop list
i)} for @var{i} less than the length of @var{list}.  If @var{x} does not
occur in @var{list}, then @false{} is returned.

Examples:

@example
(member* '(a) '(b (a) c))
=> ((a) c)
@end example

@func{member*} extends the @rnrs{6} definition of @func{member} to allow
the client to pass in an optional equality procedure @var{=} used to
compare keys.  @var{=} defaults to @func{equal?}.

The comparison procedure is used to compare the elements @var{ei} of
list to the key @var{x} in this way:

@example
(= x ei) ; list is (E1 ... En)
@end example

@noindent
that is, the first argument is always @var{x}, and the second argument
is one of the list elements.  Thus one can reliably find the first
element of list that is greater than five with:

@example
(member* 5 list <)
@end example

Note that fully general list searching may be performed with the
@func{find-tail} and @func{find} procedures:

@example
(find-tail even? list) ; Find the first elt with an even key.
@end example
@end defun


@c page
@node lists delete
@subsection Deletion


@cindex List deletion


@defun delete @var{x} @var{list} [@var{=}]
@defunx delete! @var{x} @var{list} [@var{=}]
Use the comparison procedure @var{=} (which defaults to @func{equal?})
to find all elements of list that are equal to @var{x}, and delete them
from @var{list}.  The dynamic order in which the various applications of
@var{=} are made is not specified.

The list is not disordered: elements that appear in the result list
occur in the same order as they occur in the argument list.  The result
may share a common tail with the argument list.

Note that fully general element deletion can be performed with the
@func{remove} and @func{remove!} procedures:

@example
;; Delete all the even elements from LIS:
(remove even? lis)
@end example

The comparison procedure is used in this way: @code{(= x ei)}; that is,
@var{x} is always the first argument, and a list element is always the
second argument.  The comparison procedure will be used to compare each
element of list exactly once; the order in which it is applied to the
various @var{ei} is not specified.  Thus, one can reliably remove all
the numbers greater than 5 from a list with @code{(delete 5 list <)}.

@func{delete!} is the linear--update variant of @func{delete}.  It is
allowed, but not required, to alter the cons cells in its argument list
to construct the result.
@end defun


@defun delete-duplicates @var{list} [@var{=}]
@defunx {delete-duplicates!} @var{list} [@var{=}]
Remove duplicate elements from the list argument.  If there are multiple
equal elements in @var{list}, the result list only contains the first or
leftmost of these elements in the result.  The order of these surviving
elements is the same as in the original list: @func{delete-duplicates}
does not disorder the list (hence it is useful for ``cleaning up''
association lists).

The @var{=} parameter is used to compare the elements of the list; it
defaults to @func{equal?}.  If @var{x} comes before @var{y} in
@var{list}, then the comparison is performed @code{(= x y)}.  The
comparison procedure will be used to compare each pair of elements in
list no more than once; the order in which it is applied to the various
pairs is not specified.

Implementations of @func{delete-duplicates} are allowed to share common
tails between argument and result lists; for example, if the list
argument contains only unique elements, it may simply return exactly
this list.

Be aware that, in general, @func{delete-duplicates} runs in time O(n2)
for n--element lists.  Uniquifying long lists can be accomplished in O(n
lg n) time by sorting the list to bring equal elements together, then
using a linear--time algorithm to remove equal elements.  Alternatively,
one can use algorithms based on element--marking, with linear--time
results.

@func{delete-duplicates!} is the linear--update variant of
@func{delete-duplicates}; it is allowed, but not required, to alter the
cons cells in its argument list to construct the result.

@example
(delete-duplicates '(a b a c a b c z)) => (a b c z)

;; Clean up an alist:
(delete-duplicates '((a . 3) (b . 7) (a . 9) (c . 1))
                   (lambda (x y)
                     (eq? (car x) (car y))))
  => ((a . 3) (b . 7) (c . 1))
@end example
@end defun


@c page
@node lists alist
@subsection Association lists


@cindex List, association lists
@cindex Association lists
@cindex Alists


An ``association list'' (or ``alist'') is a list of pairs.  The car of
each pair contains a key value, and the cdr contains the associated data
value.  They can be used to construct simple look--up tables in Scheme.
Note that association lists are probably inappropriate for
performance--critical use on large data; in these cases, hash tables or
some other alternative should be employed.


@defun assoc* @var{key} @var{alist} [@var{=}]
@defunx assq @var{key} @var{alist}
@defunx assv @var{key} @var{alist}
@var{alist} must be an association list: a list of pairs.  These
procedures find the first pair in @var{alist} whose car field is
@var{key}, and returns that pair.  If no pair in @var{alist} has
@var{key} as its car, then @false{} is returned.

@func{assq} uses @func{eq?} to compare @var{key} with the car fields of
the pairs in @var{alist}, while @func{assv} uses @func{eqv?} and
@func{assoc*} uses @func{equal?}.

Example:

@example
(define e '((a 1) (b 2) (c 3)))
(assq 'a e)                            =>  (a 1)
(assq 'b e)                            =>  (b 2)
(assq 'd e)                            =>  #f
(assq (list 'a) '(((a)) ((b)) ((c))))  =>  #f
(assoc (list 'a) '(((a)) ((b)) ((c)))) =>  ((a))
(assq 5 '((2 3) (5 7) (11 13)))    =>  *unspecified*
(assv 5 '((2 3) (5 7) (11 13)))    =>  (5 7)
@end example

@func{assoc*} allows the client to pass in an optional equality
procedure @var{=} used to compare keys.

The comparison procedure is used to compare the elements @var{ei} of
list to the key parameter in this way:

@example
(= key (car ei)) ; list is (E1 ... En)
@end example

@noindent
that is, the first argument is always @var{key}, and the second argument
is one of the list elements.  Thus one can reliably find the first entry
of @var{alist} whose @var{key} is greater than five with @code{(assoc* 5
alist <)}.

Note that fully general alist searching may be performed with the
@func{find-tail} and @func{find} procedures:

@example
;; Look up the first association in alist with an even key:
(find (lambda (a)
        (even? (car a)))
      alist)
@end example
@end defun


@defun alist-cons @var{key} @var{datum} @var{alist}
Defined as:

@example
(lambda (key datum alist)
  (cons (cons key datum) alist))
@end example

Cons a new alist entry mapping @var{key} to @var{datum} onto
@var{alist}.
@end defun


@defun alist-copy @var{alist}
Make a fresh copy of @var{alist}.  This means copying each pair that
forms an association as well as the spine of the list:

@example
(lambda (a)
  (map (lambda (elt)
         (cons (car elt) (cdr elt)))
       a))
@end example
@end defun


@defun alist-delete @var{key} @var{alist} [@var{=}]
@defunx alist-delete! @var{key} @var{alist} [@var{=}]
Delete all associations from @var{alist} with the given @var{key}, using
the key-comparison procedure @var{=}, which defaults to @func{equal?}.
The dynamic order in which the various applications of @var{=} are made
is not specified.

Return values may share common tails with the @var{alist} argument.  The
alist is not disordered: elements that appear in the result alist occur
in the same order as they occur in @var{alist}.

The comparison procedure is used to compare the element keys @var{ki} of
@var{alist}'s entries to the key parameter in this way: @code{(= key
ki)}.  Thus, one can reliably remove all entries of @var{alist} whose
key is greater than five with @code{(alist-delete 5 alist <)}.

@func{alist-delete!} is the linear--update variant of
@func{alist-delete}; it is allowed, but not required, to alter cons
cells from the alist parameter to construct the result.
@end defun


@c page
@node lists set
@subsection Set operations on lists


@cindex List set operations
@cindex Set operations on lists


These procedures implement operations on sets represented as lists of
elements.  They all take an @var{=} argument used to compare elements of
lists.  This equality procedure is required to be consistent with
@func{eq?}.  That is, it must be the case that:

@example
(eq? x y) => (= x y)
@end example

Note that this implies, in turn, that two lists that are @func{eq?} are
also set--equal by any legal comparison procedure.  This allows for
constant--time determination of set operations on @func{eq?} lists.

Be aware that these procedures typically run in time O(n * m) for n--
and m--element list arguments.  Performance--critical applications
operating upon large sets will probably wish to use other data
structures and algorithms.


@defun lset<=? @var{=} @var{list1} ...
Return true if, and only if, every @var{listi} is a subset of
@var{listi+1}, using @var{=} for the element--equality procedure.  List
@var{AL} is a subset of list @var{BL} if every element in @var{AL} is
equal to some element of @var{BL}.  When performing an element
comparison, the @var{=} procedure's first argument is an element of
@var{AL}, its second argument an element of @var{BL}.

Examples:

@example
(lset<=? eq? '(a) '(a b a) '(a b c c)) => #t

(lset<=? eq?) => #t             ; Trivial cases
(lset<=? eq? '(a)) => #t
@end example

If invoked with all null lists, the return value is @true{}.
@end defun


@defun lset=? @var{=} @var{list1} @var{list2} ...
Return true if, and only if, every @var{listi} is set--equal to
@var{listi+1}, using @var{=} for the element--equality procedure.
``Set--equal'' simply means that @var{listi} is a subset of
@var{listi+1}, and @var{listi+1} is a subset of @var{listi}.  The
@var{=} procedure's first argument is an element of @var{listi}, its
second argument is an element of @var{listi+1}.

Examples:

@example
(lset=? eq? '(b e a) '(a e b) '(e e b a)) => #t

(lset=? eq?) => #t               ; Trivial cases
(lset=? eq? '(a)) => #t
@end example
@end defun


@defun lset-adjoin @var{=} @var{list} @var{elt1} ...
Add the @var{elti} elements not already in the @var{list} parameter to
the result list.  The result shares a common tail with the @var{list}
parameter.  The new elements are added to the front of the list, but no
guarantees are made about their order.  The @var{=} parameter is an
equality procedure used to determine if an elti is already a member of
list.  Its first argument is an element of @var{list}, its second is one
of the @var{elti}.

The @var{list} parameter is always a suffix of the result; even if the
list parameter contains repeated elements, these are not reduced.

Example:

@example
(lset-adjoin eq? '(a b c d c e) 'a 'e 'i 'o 'u)
  => (u o i a b c d c e)
@end example
@end defun


@defun lset-union @var{=} @var{list1} ...
@defunx lset-union! @var{=} @var{list1} ...
Return the union of the lists, using @var{=} for the element--equality
procedure.

The union of lists @var{AL} and @var{BL} is constructed as follows:

@enumerate
@item
If @var{AL} is the empty list, the answer is @var{BL} (or a copy of
@var{BL}).

@item
Otherwise, the result is initialised to be list @var{AL} (or a copy of
@var{AL}).

@item
Proceed through the elements of list @var{BL} in a left--to--right
order.  If @var{b} is such an element of @var{BL}, compare every element
@var{r} of the current result list to @var{b}: @code{(= r b)}.  If all
comparisons fail, @var{b} is consed onto the front of the result.
@end enumerate

However, there is no guarantee that @var{=} will be applied to every
pair of arguments from @var{AL} and @var{BL}.  In particular, if
@var{AL} is @func{eq?} to @var{BL}, the operation may immediately
terminate.

In the n--ary case, the two--argument @func{list-union} operation is
simply folded across the argument lists.

Examples:

@example
(lset-union eq? '(a b c d e) '(a e i o u))
  => (u o i a b c d e)

;; Repeated elements in LIST1 are preserved.
(lset-union eq? '(a a c) '(x a x)) => (x a a c)

;; Trivial cases
(lset-union eq?) => ()
(lset-union eq? '(a b c)) => (a b c)
@end example

@func{lset-union!} is the linear--update variant of @func{lset-union}.
It is allowed, but not required, to use the cons cells in the first list
parameter to construct its answer.  @func{lset-union!} is permitted to
recycle cons cells from any of its list arguments.
@end defun


@defun lset-intersection @var{=} @var{list1} @var{list2} ...
@defunx lset-intersection! @var{=} @var{list1} ...
Return the intersection of the lists, using @var{=} for the
element--equality procedure.

The intersection of lists @var{AL} and @var{BL} is comprised of every
element of @var{AL} that is @var{=} to some element of @var{BL}:
@code{(= a b)}, for @var{a} in @var{AL}, and @var{b} in @var{BL}.  Note
this implies that an element which appears in @var{BL} and multiple
times in list @var{AL} will also appear multiple times in the result.

The order in which elements appear in the result is the same as they
appear in @var{list1}; that is, @func{lset-intersection} essentially
filters @var{list1}, without disarranging element order.  The result may
share a common tail with @var{list1}.

In the n--ary case, the two--argument @func{list-intersection} operation
is simply folded across the argument lists.  However, the dynamic order
in which the applications of @var{=} are made is not specified.  The
procedure may check an element of @var{list1} for membership in every
other list before proceeding to consider the next element of
@var{list1}, or it may completely intersect @var{list1} and @var{list2}
before proceeding to @var{list3}, or it may go about its work in some
third order.

Examples:

@example
(lset-intersection eq? '(a b c d e) '(a e i o u))
  => (a e)

;; Repeated elements in LIST1 are preserved.
(lset-intersection eq? '(a x y a) '(x a x z))
  => '(a x a)

(lset-intersection eq? '(a b c))        ; Trivial case
  => (a b c)
@end example

@func{lset-intersection!} is the linear--update variant of
@func{lset-intersection}.  It is allowed, but not required, to use the
cons cells in the first list parameter to construct its answer.
@end defun


@defun lset-difference @var{=} @var{list1} @var{list2} ...
@defunx lset-difference! @var{=} @var{list1} ...
Return the difference of the lists, using @var{=} for the
element--equality procedure: all the elements of @var{list1} that are
not @var{=} to any element from one of the other @var{listi} parameters.

The @var{=} procedure's first argument is always an element of
@var{list1}; its second is an element of one of the other @var{listi}.
Elements that are repeated multiple times in the @var{list1} parameter
will occur multiple times in the result.

The order in which elements appear in the result is the same as they
appear in @var{list1}; that is, @func{lset-difference} essentially
filters @var{list1}, without disarranging element order.  The result may
share a common tail with @var{list1}.

The dynamic order in which the applications of @var{=} are made is not
specified.  The procedure may check an element of @var{list1} for
membership in every other list before proceeding to consider the next
element of @var{list1}, or it may completely compute the difference of
@var{list1} and @var{list2} before proceeding to @var{list3}, or it may
go about its work in some third order.

@example
(lset-difference eq? '(a b c d e) '(a e i o u))
  => (b c d)

(lset-difference eq? '(a b c))   ; Trivial case
  => (a b c)
@end example

@func{lset-difference!} is the linear--update variant of
@func{lset-difference}.  It is allowed, but not required, to use the
cons cells in the first list parameter to construct its answer.
@end defun


@defun lset-xor @var{=} @var{list1} ...
@defunx lset-xor! @var{=} @var{list1} ...
Return the exclusive--or of the sets, using @var{=} for the
element--equality procedure.  If there are exactly two lists, this is
all the elements that appear in exactly one of the two lists.  The
operation is associative, and thus extends to the n--ary case, in which
the result is a list of the elements that appear in an odd number of the
lists.  The result may share a common tail with any of the @var{listi}
parameters.

More precisely, for two lists @var{AL} and @var{BL}, @var{AL} xor
@var{BL} is a list of:

@itemize
@item
every element @var{a} of @var{AL} such that there is no element @var{b}
of @var{BL} such that @code{(= a b)}, and

@item
every element @var{b} of @var{BL} such that there is no element @var{a}
of @var{AL} such that @code{(= b a)}.
@end itemize

However, an implementation is allowed to assume that @var{=} is
symmetric; that is, that @code{(= a b) => (= b a)}.

This means, for example, that if a comparison @code{(= a b)} produces
true for some @var{a} in @var{AL} and @var{b} in @var{BL}, both @var{a}
and @var{b} may be removed from inclusion in the result.

In the n--ary case, the binary--xor operation is simply folded across
the lists.

Examples:

@example
(lset-xor eq? '(a b c d e) '(a e i o u))
  => (d c b i o u)

;; Trivial cases.
(lset-xor eq?) => ()
(lset-xor eq? '(a b c d e)) => (a b c d e)
@end example

@func{lset-xor!} is the linear--update variant of @func{lset-xor}.  It
is allowed, but not required, to use the cons cells in the first list
parameter to construct its answer.
@end defun


@defun {lset-diff+intersection} @var{=} @var{list1} @var{list2} ...
@defunx {lset-diff+intersection!} @var{=} @var{list1} @var{list2} ...
Return two values: the difference and the intersection of the lists.  It
is equivalent to:

@example
(values (lset-difference = list1 list2 ...)
        (lset-intersection = list1
                             (lset-union = list2 ...)))
@end example

@noindent
but can be implemented more efficiently.

The @var{=} procedure's first argument is an element of @var{list1}; its
second is an element of one of the other @var{listi}.

Either of the answer lists may share a common tail with @var{list1}.
This operation essentially partitions @var{list1}.

@func{lset-diff+intersection!} is the linear--update variant of
@func{lset-diff+intersection}.  It is allowed, but not required, to use
the cons cells in the first list parameter to construct its answer.
@end defun

@c page
@node char-sets
@section Character sets


The @library{char-sets} library handles sets of characters.

A ``character'' is every value for which the application of @func{char?}
returns @true{}.  This library does nothing that can be related to a
specific character encoding, like @ascii{}, @acronym{UCS} or Unicode.
When needed, it relies on @func{string->list} to split a string into its
character components.  @ref{stdlib unicode,Unicode}.

@menu
* char-sets intro::             Introduction.
* char-sets make::              Constructors.
* char-sets pred::              Predicates.
* char-sets inspect::           Inspection.
* char-sets ops::               Operations.
* char-sets std::               Standard char sets.
@end menu

@c page
@node char-sets intro
@subsection Introduction


In the following procedure specifications:

@table @var
@item cs
Is a character set.

@item str
Is a string.

@item ch
Is a character.

@item char-list
Is a list of characters.

@item pred
Is a unary character predicate procedure, returning a boolean value when
applied to a character.

@item obj
May be any value at all.
@end table

A parameter followed by @code{...} means zero--or--more elements.

Unless otherwise noted in the specification of a procedure, procedures
always return character sets that are distinct from the parameter
character sets.

@c page
@node char-sets make
@subsection Constructors


@defun char-set . @var{args}
Build and return a new char-set.

@var{args} may be the empty list or a mixed list of characters and pairs
of items.  Pairs of characters are interpreted as limits--inclusive
ranges of characters.
@end defun


@defun char-set-copy @var{cs}
Return a newly allocated char-set holding a copy of @var{cs}.
@end defun


@defun char-set-add @var{cs} @var{char/range}
@defunx char-set-add! @var{cs} @var{char/range}
Add values to an existing char-set.  @var{char/range} can be a single
character or a pair characters interpreted as a limits--inclusive range
of characters.
@end defun

@c page
@node char-sets pred
@subsection Predicates


@defun char-set? @var{obj}
Return true if @var{obj} is a valid char-set.
@end defun


@defun char-set-empty? @var{cs}
Return true if the char-set is empty.
@end defun


@defun char-set-contains? @var{cs} @var{ch}
Return true if @var{ch} is an element of @var{cs}.
@end defun


@defun char-set=? @var{cs-a} @var{cs-b}
Return true if the arguments represent the same char-set.
@end defun


@defun char-set<? @var{cs-a} @var{cs-b}
Return true if all the elements of @var{cs-a} are strictly less than all
the elements of @var{cs-b}.  Empty sets cannot be ordered, so if an
argument is empty the return value is @false{}.
@end defun


@defun char-set-subset? @var{cs-a} @var{cs-b}
@defunx char-set-strict-subset? @var{cs-a} @var{cs-b}
Return @true{} if @var{cs-b} is a subset or a strict subset of
@var{cs-a}.
@end defun

@c page
@node char-sets inspect
@subsection Inspection


@defun char-set-size @var{cs}
Return the number of characters in @var{cs}.
@end defun

@c page
@node char-sets ops
@subsection Operations


@subsubheading Set operations


@defun char-set-intersection @var{cs-a} @var{cs-b}
Return a new set representing the intersection of the arguments; the
intersection is the set of characters present in both the arguments.

The returned set may share some structure with the arguments.
@end defun


@defun char-set-union @var{cs-a} @var{cs-b}
Return a new set representing the union of the arguments; the union is
the set of characters present in one and/or the other argument.

The returned set may share some structure with the arguments.
@end defun


@defun char-set-difference @var{cs-a} @var{cs-b}
Return a new set representing the difference between the arguments; the
difference is the set of characters present in one or the other
argument (but not both).

The returned set may share some structure with the arguments.
@end defun


@defun char-set-complement @var{cs}
@defunx char-set-complement @var{cs} @var{cs-universe}
Return a new set representing the complement of @var{cs} in
@var{cs-universe}.  The complement is the set of characters present in
@var{cs-universe}, which are not present in @var{cs}.

If omitted, @var{cs-universe} defaults to @code{char-set:full}.

The returned set may share some structure with the arguments.
@end defun

@c ------------------------------------------------------------

@subsubheading List operations


@defun char-set-for-each @var{proc} @var{cs}
Apply @var{proc} to each character in @var{cs}.
@end defun


@defun char-set-every @var{proc} @var{cs}
Apply @var{proc} to each character in the set and return true if all the
return values are true.  The application stops at the first @false{}
return value.
@end defun


@defun char-set-any @var{proc} @var{cs}
Apply @var{proc} to each character in the set and return true if at
least one of the returned values is true.  The application stops at the
first true return value.
@end defun


@defun char-set-fold @var{kons} @var{knil} @var{cs}
Fold @var{kons} over the characters in the set.
@end defun


@defun char-set->list @var{cs}
Return a list holding all the characters in the set.  Beware that for
some character sets the resulting list can be big.
@end defun

@c page
@node char-sets std
@subsection Predefined char sets


Several character sets are predefined for convenience:

@vtable @code
@item char-set:lower-case
Lower-case letters.

@item char-set:upper-case
Upper-case letters.

@item char-set:title-case
Title-case letters.

@item char-set:letter
Letters.

@item char-set:digit
Digits.

@item char-set:letter+digit
Letters and digits.

@item char-set:graphic
Printing characters except spaces.

@item char-set:printing
Printing characters including spaces.

@item char-set:whitespace
Whitespace characters.

@item char-set:iso-control
The ISO control characters.

@item char-set:punctuation
Punctuation characters.

@item char-set:symbol
Symbol characters.

@item char-set:hex-digit
A hexadecimal digit: 0-9, A-F, a-f.

@item char-set:blank
Blank characters -- horizontal whitespace.

@item char-set:ascii
All characters in the @ascii{} set.

@item char-set:empty
Empty set.

@item char-set:full
All characters.
@end vtable

Note that there may be characters in @code{char-set:letter} that are
neither upper or lower case; this might occur in implementations that
use a character type richer than @ascii{}, such as Unicode.  A ``graphic
character'' is one that would put ink on your page.  While the exact
composition of these sets may vary depending upon the character type
provided by the underlying Scheme system, here are the definitions for
some of the sets in an @ascii{} implementation:

@example
char-set:lower-case     a-z
char-set:upper-case     A-Z
char-set:letter         A-Z and a-z
char-set:digit          0123456789
char-set:punctuation    !"#%&'()*,-./:;?@@[\]_@{@}
char-set:symbol         $+<=>^`|~
char-set:whitespace     Space, newline, tab, form feed,
                        vertical tab, carriage return
char-set:blank          Space and tab
char-set:graphic        letter + digit + punctuation + symbol
char-set:printing       graphic + whitespace
char-set:iso-control    ASCII 0-31 and 127
@end example

Note that the existence of the @code{char-set:ascii} set implies that
the underlying character set is required to be at least as rich as
@ascii{} (including @ascii{}'s control characters).

@quotation
@emph{Rationale:} The name choices reflect a shift from the older
``alphabetic/numeric'' terms found in @rnrs{5} and @posix{} to newer,
Unicode--influenced ``letter/digit'' lexemes.
@end quotation


@c ------------------------------------------------------------

@subheading Unicode, Latin-1 and @ascii{} definitions of the standard character sets


In Unicode Scheme implementations, the base character sets are
compatible with Java's Unicode specifications.  For @ascii{} or Latin-1,
we simply restrict the Unicode set specifications to their first 128 or
256 codes, respectively.  Scheme implementations that are not based on
@ascii{}, Latin-1 or Unicode should attempt to preserve the sense or
spirit of these definitions.

The following descriptions frequently make reference to the ``Unicode
character database''.  This is a file, available at:

@center @url{ftp://ftp.unicode.org/Public/UNIDATA/UnicodeData.txt}

Each line contains a description of a Unicode character.  The first
semicolon--delimited field of the line gives the hex value of the
character's code; the second field gives the name of the character, and
the third field gives a two--letter category.  Other fields give simple
1-1 case--mappings for the character and other information; see:

@center @url{ftp://ftp.unicode.org/Public/UNIDATA/UnicodeData.html}

@noindent
for further description of the file's format.  Note in particular the
two--letter category specified in the the third field, which is
referenced frequently in the descriptions below.


@subsubheading char-set:lower-case

For Unicode, we follow Java's specification: a character is lowercase if:

@itemize
@item
it is not in the range [U+2000,U+2FFF];

@item
the Unicode attribute table does not give a lowercase mapping for it;

@item at least one of the following is true:

@itemize
@item
the Unicode attribute table gives a mapping to uppercase for the
character;

@item
the name for the character in the Unicode attribute table contains the
words ``SMALL LETTER'' or ``SMALL LIGATURE''.
@end itemize
@end itemize

The lower--case @ascii{} characters are:

@example
abcdefghijklmnopqrstuvwxyz
@end example

Latin-1 adds another 33 lower--case characters to the @ascii{} set:

@example
00B5    MICRO SIGN
00DF    LATIN SMALL LETTER SHARP S
00E0    LATIN SMALL LETTER A WITH GRAVE
00E1    LATIN SMALL LETTER A WITH ACUTE
00E2    LATIN SMALL LETTER A WITH CIRCUMFLEX
00E3    LATIN SMALL LETTER A WITH TILDE
00E4    LATIN SMALL LETTER A WITH DIAERESIS
00E5    LATIN SMALL LETTER A WITH RING ABOVE
00E6    LATIN SMALL LETTER AE
00E7    LATIN SMALL LETTER C WITH CEDILLA
00E8    LATIN SMALL LETTER E WITH GRAVE
00E9    LATIN SMALL LETTER E WITH ACUTE
00EA    LATIN SMALL LETTER E WITH CIRCUMFLEX
00EB    LATIN SMALL LETTER E WITH DIAERESIS
00EC    LATIN SMALL LETTER I WITH GRAVE
00ED    LATIN SMALL LETTER I WITH ACUTE
00EE    LATIN SMALL LETTER I WITH CIRCUMFLEX
00EF    LATIN SMALL LETTER I WITH DIAERESIS
00F0    LATIN SMALL LETTER ETH
00F1    LATIN SMALL LETTER N WITH TILDE
00F2    LATIN SMALL LETTER O WITH GRAVE
00F3    LATIN SMALL LETTER O WITH ACUTE
00F4    LATIN SMALL LETTER O WITH CIRCUMFLEX
00F5    LATIN SMALL LETTER O WITH TILDE
00F6    LATIN SMALL LETTER O WITH DIAERESIS
00F8    LATIN SMALL LETTER O WITH STROKE
00F9    LATIN SMALL LETTER U WITH GRAVE
00FA    LATIN SMALL LETTER U WITH ACUTE
00FB    LATIN SMALL LETTER U WITH CIRCUMFLEX
00FC    LATIN SMALL LETTER U WITH DIAERESIS
00FD    LATIN SMALL LETTER Y WITH ACUTE
00FE    LATIN SMALL LETTER THORN
00FF    LATIN SMALL LETTER Y WITH DIAERESIS
@end example

Note that three of these have no corresponding Latin-1 upper--case
character:

@example
00B5    MICRO SIGN
00DF    LATIN SMALL LETTER SHARP S
00FF    LATIN SMALL LETTER Y WITH DIAERESIS
@end example

The compatibility micro character uppercases to the non--Latin-1 Greek
capital mu; the German sharp s character uppercases to the pair of
characters ``SS'', and the capital y--with--diaeresis is non--Latin-1.

@c ------------------------------------------------------------

@subsubheading char-set:upper-case

For Unicode, we follow Java's specification: a character is uppercase if

@itemize
@item
it is not in the range @code{[U+2000, U+2FFF]};

@item
the Unicode attribute table does not give an uppercase mapping for it
(this excludes titlecase characters);

@item
at least one of the following is true:

@itemize
@item
the Unicode attribute table gives a mapping to lowercase for the
character;

@item
the name for the character in the Unicode attribute table contains the
words ``CAPITAL LETTER'' or ``CAPITAL LIGATURE''.
@end itemize
@end itemize

The upper--case @ascii{} characters are:

@example
ABCDEFGHIJKLMNOPQRSTUVWXYZ
@end example

Latin-1 adds another 30 upper--case characters to the @ascii{} set:

@example
00C0    LATIN CAPITAL LETTER A WITH GRAVE
00C1    LATIN CAPITAL LETTER A WITH ACUTE
00C2    LATIN CAPITAL LETTER A WITH CIRCUMFLEX
00C3    LATIN CAPITAL LETTER A WITH TILDE
00C4    LATIN CAPITAL LETTER A WITH DIAERESIS
00C5    LATIN CAPITAL LETTER A WITH RING ABOVE
00C6    LATIN CAPITAL LETTER AE
00C7    LATIN CAPITAL LETTER C WITH CEDILLA
00C8    LATIN CAPITAL LETTER E WITH GRAVE
00C9    LATIN CAPITAL LETTER E WITH ACUTE
00CA    LATIN CAPITAL LETTER E WITH CIRCUMFLEX
00CB    LATIN CAPITAL LETTER E WITH DIAERESIS
00CC    LATIN CAPITAL LETTER I WITH GRAVE
00CD    LATIN CAPITAL LETTER I WITH ACUTE
00CE    LATIN CAPITAL LETTER I WITH CIRCUMFLEX
00CF    LATIN CAPITAL LETTER I WITH DIAERESIS
00D0    LATIN CAPITAL LETTER ETH
00D1    LATIN CAPITAL LETTER N WITH TILDE
00D2    LATIN CAPITAL LETTER O WITH GRAVE
00D3    LATIN CAPITAL LETTER O WITH ACUTE
00D4    LATIN CAPITAL LETTER O WITH CIRCUMFLEX
00D5    LATIN CAPITAL LETTER O WITH TILDE
00D6    LATIN CAPITAL LETTER O WITH DIAERESIS
00D8    LATIN CAPITAL LETTER O WITH STROKE
00D9    LATIN CAPITAL LETTER U WITH GRAVE
00DA    LATIN CAPITAL LETTER U WITH ACUTE
00DB    LATIN CAPITAL LETTER U WITH CIRCUMFLEX
00DC    LATIN CAPITAL LETTER U WITH DIAERESIS
00DD    LATIN CAPITAL LETTER Y WITH ACUTE
00DE    LATIN CAPITAL LETTER THORN
@end example

@c ------------------------------------------------------------

@subsubheading char-set:title-case

In Unicode, a character is titlecase if it has the category @code{Lt} in
the character attribute database.  There are very few of these
characters; here is the entire 31-character list as of Unicode 3.0:

@example
01C5    LATIN CAPITAL LETTER D WITH SMALL LETTER Z WITH CARON
01C8    LATIN CAPITAL LETTER L WITH SMALL LETTER J
01CB    LATIN CAPITAL LETTER N WITH SMALL LETTER J
01F2    LATIN CAPITAL LETTER D WITH SMALL LETTER Z
1F88    GREEK CAPITAL LETTER ALPHA WITH PSILI AND PROSGEGRAMMENI
1F89    GREEK CAPITAL LETTER ALPHA WITH DASIA AND PROSGEGRAMMENI
1F8A    GREEK CAPITAL LETTER ALPHA WITH PSILI AND VARIA AND PROSGEGRAMMENI
1F8B    GREEK CAPITAL LETTER ALPHA WITH DASIA AND VARIA AND PROSGEGRAMMENI
1F8C    GREEK CAPITAL LETTER ALPHA WITH PSILI AND OXIA AND PROSGEGRAMMENI
1F8D    GREEK CAPITAL LETTER ALPHA WITH DASIA AND OXIA AND PROSGEGRAMMENI
1F8E    GREEK CAPITAL LETTER ALPHA WITH PSILI AND PERISPOMENI AND PROSGEGRAMMENI
1F8F    GREEK CAPITAL LETTER ALPHA WITH DASIA AND PERISPOMENI AND PROSGEGRAMMENI
1F98    GREEK CAPITAL LETTER ETA WITH PSILI AND PROSGEGRAMMENI
1F99    GREEK CAPITAL LETTER ETA WITH DASIA AND PROSGEGRAMMENI
1F9A    GREEK CAPITAL LETTER ETA WITH PSILI AND VARIA AND PROSGEGRAMMENI
1F9B    GREEK CAPITAL LETTER ETA WITH DASIA AND VARIA AND PROSGEGRAMMENI
1F9C    GREEK CAPITAL LETTER ETA WITH PSILI AND OXIA AND PROSGEGRAMMENI
1F9D    GREEK CAPITAL LETTER ETA WITH DASIA AND OXIA AND PROSGEGRAMMENI
1F9E    GREEK CAPITAL LETTER ETA WITH PSILI AND PERISPOMENI AND PROSGEGRAMMENI
1F9F    GREEK CAPITAL LETTER ETA WITH DASIA AND PERISPOMENI AND PROSGEGRAMMENI
1FA8    GREEK CAPITAL LETTER OMEGA WITH PSILI AND PROSGEGRAMMENI
1FA9    GREEK CAPITAL LETTER OMEGA WITH DASIA AND PROSGEGRAMMENI
1FAA    GREEK CAPITAL LETTER OMEGA WITH PSILI AND VARIA AND PROSGEGRAMMENI
1FAB    GREEK CAPITAL LETTER OMEGA WITH DASIA AND VARIA AND PROSGEGRAMMENI
1FAC    GREEK CAPITAL LETTER OMEGA WITH PSILI AND OXIA AND PROSGEGRAMMENI
1FAD    GREEK CAPITAL LETTER OMEGA WITH DASIA AND OXIA AND PROSGEGRAMMENI
1FAE    GREEK CAPITAL LETTER OMEGA WITH PSILI AND PERISPOMENI AND PROSGEGRAMMENI
1FAF    GREEK CAPITAL LETTER OMEGA WITH DASIA AND PERISPOMENI AND PROSGEGRAMMENI
1FBC    GREEK CAPITAL LETTER ALPHA WITH PROSGEGRAMMENI
1FCC    GREEK CAPITAL LETTER ETA WITH PROSGEGRAMMENI
1FFC    GREEK CAPITAL LETTER OMEGA WITH PROSGEGRAMMENI
@end example

There are no @ascii{} or Latin-1 titlecase characters.

@c ------------------------------------------------------------

@subsubheading char-set:letter

In Unicode, a letter is any character with one of the letter categories
(@code{Lu}, @code{Ll}, @code{Lt}, @code{Lm}, @code{Lo}) in the Unicode
character database.

There are 52 @ascii{} letters:

@example
abcdefghijklmnopqrstuvwxyz
ABCDEFGHIJKLMNOPQRSTUVWXYZ
@end example

There are 117 Latin-1 letters.  These are the 115 characters that are
members of the Latin-1 @code{char-set:lower-case} and
@code{char-set:upper-case} sets, plus:

@example
00AA    FEMININE ORDINAL INDICATOR
00BA    MASCULINE ORDINAL INDICATOR
@end example

These two letters are considered lower--case by Unicode, but not by Java
or @ansrfi{14}.

@c ------------------------------------------------------------

@subsubheading char-set:digit

In Unicode, a character is a digit if it has the category @code{Nd} in
the character attribute database.  In Latin-1 and @ascii{}, the only
such characters are @code{0123456789}.  In Unicode, there are other
digit characters in other code blocks, such as Gujarati digits and
Tibetan digits.

@c ------------------------------------------------------------

@subsubheading char-set:hex-digit

The only hex digits are @code{0123456789abcdefABCDEF}.

@c ------------------------------------------------------------

@subsubheading char-set:letter+digit

The union of @code{char-set:letter} and @code{char-set:digit}.

@c ------------------------------------------------------------

@subsubheading char-set:graphic

A graphic character is one that would put ink on paper.  The @ascii{}
and Latin-1 graphic characters are the members of:

@example
char-set:letter
char-set:digit
char-set:punctuation
char-set:symbol
char-set:printing
@end example

A printing character is one that would occupy space when printed, i.e.,
a graphic character or a space character.  @code{char-set:printing} is
the union of @code{char-set:whitespace} and @code{char-set:graphic}.

@c ------------------------------------------------------------

@subsubheading char-set:whitespace

In Unicode, a whitespace character is either:

@itemize
@item
a character with one of the space, line, or paragraph separator
categories (Zs, Zl or Zp) of the Unicode character database.

@item
@code{U+0009} Horizontal tabulation (\t control-I).

@item
@code{U+000A} Line feed (\n control-J).

@item
@code{U+000B} Vertical tabulation (\v control-K).

@item
@code{U+000C} Form feed (\f control-L).

@item
@code{U+000D} Carriage return (\r control-M).
@end itemize

There are 24 whitespace characters in Unicode 3.0:

@example
0009    HORIZONTAL TABULATION   \t control-I
000A    LINE FEED               \n control-J
000B    VERTICAL TABULATION     \v control-K
000C    FORM FEED               \f control-L
000D    CARRIAGE RETURN         \r control-M
0020    SPACE                   Zs
00A0    NO-BREAK SPACE          Zs
1680    OGHAM SPACE MARK        Zs
2000    EN QUAD                 Zs
2001    EM QUAD                 Zs
2002    EN SPACE                Zs
2003    EM SPACE                Zs
2004    THREE-PER-EM SPACE      Zs
2005    FOUR-PER-EM SPACE       Zs
2006    SIX-PER-EM SPACE        Zs
2007    FIGURE SPACE            Zs
2008    PUNCTUATION SPACE       Zs
2009    THIN SPACE              Zs
200A    HAIR SPACE              Zs
200B    ZERO WIDTH SPACE        Zs
2028    LINE SEPARATOR          Zl
2029    PARAGRAPH SEPARATOR     Zp
202F    NARROW NO-BREAK SPACE   Zs
3000    IDEOGRAPHIC SPACE       Zs
@end example

The @ascii{} whitespace characters are the first six characters in the
above list; line feed, horizontal tabulation, vertical tabulation, form
feed, carriage return, and space.  These are also exactly the characters
recognised by the @posix{} @cfunc{isspace} procedure.  Latin-1 adds the
no-break space.

@c ------------------------------------------------------------

@subsubheading char-set:iso-control

The @iso{} control characters are the Unicode/Latin-1 characters in the
ranges @code{[U+0000, U+001F]} and @code{[U+007F, U+009F]}.

@ascii{} restricts this set to the characters in the range
@code{[U+0000, U+001F]} plus the character @code{U+007F}.

Note that Unicode defines other control characters which do not belong
to this set (hence the qualifying prefix @code{iso-} in the name).

@c ------------------------------------------------------------

@subsubheading char-set:punctuation

In Unicode, a punctuation character is any character that has one of the
punctuation categories in the Unicode character database (@code{Pc},
@code{Pd}, @code{Ps}, @code{Pe}, @code{Pi}, @code{Pf}, or @code{Po}.)

ASCII has 23 punctuation characters:

@example
!"#%&'()*,-./:;?@@[\]_@{@}
@end example

Latin-1 adds six more:

@example
00A1    INVERTED EXCLAMATION MARK
00AB    LEFT-POINTING DOUBLE ANGLE QUOTATION MARK
00AD    SOFT HYPHEN
00B7    MIDDLE DOT
00BB    RIGHT-POINTING DOUBLE ANGLE QUOTATION MARK
00BF    INVERTED QUESTION MARK
@end example

Note that the nine @ascii{} characters @code{$+<=>^`|~} are not
punctuation.  They are ``symbols''.

@c ------------------------------------------------------------

@subsubheading char-set:symbol

In Unicode, a symbol is any character that has one of the symbol
categories in the Unicode character database (@code{Sm}, @code{Sc},
@code{Sk}, or @code{So}).

There are nine @ascii{} symbol characters:

@example
$+<=>^`|~
@end example

Latin-1 adds 18 more:

@example
00A2    CENT SIGN
00A3    POUND SIGN
00A4    CURRENCY SIGN
00A5    YEN SIGN
00A6    BROKEN BAR
00A7    SECTION SIGN
00A8    DIAERESIS
00A9    COPYRIGHT SIGN
00AC    NOT SIGN
00AE    REGISTERED SIGN
00AF    MACRON
00B0    DEGREE SIGN
00B1    PLUS-MINUS SIGN
00B4    ACUTE ACCENT
00B6    PILCROW SIGN
00B8    CEDILLA
00D7    MULTIPLICATION SIGN
00F7    DIVISION SIGN
@end example

@c ------------------------------------------------------------

@subsubheading char-set:blank

Blank chars are horizontal whitespace.  In Unicode, a blank character is
either:

@itemize
@item
a character with the space separator category (@code{Zs}) in the Unicode
character database;

@item
@code{U+0009} Horizontal tabulation (\t control-I).
@end itemize

There are eighteen blank characters in Unicode 3.0:

@example
0009    HORIZONTAL TABULATION   \t control-I
0020    SPACE                   Zs
00A0    NO-BREAK SPACE          Zs
1680    OGHAM SPACE MARK        Zs
2000    EN QUAD                 Zs
2001    EM QUAD                 Zs
2002    EN SPACE                Zs
2003    EM SPACE                Zs
2004    THREE-PER-EM SPACE      Zs
2005    FOUR-PER-EM SPACE       Zs
2006    SIX-PER-EM SPACE        Zs
2007    FIGURE SPACE            Zs
2008    PUNCTUATION SPACE       Zs
2009    THIN SPACE              Zs
200A    HAIR SPACE              Zs
200B    ZERO WIDTH SPACE        Zs
202F    NARROW NO-BREAK SPACE   Zs
3000    IDEOGRAPHIC SPACE       Zs
@end example

The @ascii{} blank characters are the first two characters above;
horizontal tab and space.  Latin-1 adds the no--break space.


@c page
@node strings
@section Strings library


This chapter describes the @library{strings} library, which extends the
string operations available in the base library (@pxref{baselib
strings} for details).

@menu
* strings intro::               Introduction.
* strings convention::          Interface conventions.
* strings views::               Views over strings.
* strings cons::                Constructors.
* strings pred::                Predicates.
* strings compar::              Comparison.
* strings map::                 Mapping functions.
* strings case::                Case mapping.
* strings fold::                Fold, unfold.
* strings select::              Selection.
* strings pad::                 Padding and trimming.
* strings prefix::              Prefixes and suffixes.
* strings search::              Searching.
* strings filter::              Filtering and deleting.
* strings list::                List and string conversion.
* strings replicate::           Replicate and rotate.
* strings mutate::              Mutators.
* strings misc::                Miscellaneous functions.
* strings kmp::                 Knuth--Morris--Pratt searching.
@end menu

@c page
@node strings intro
@subsection Introduction


@subsubheading Library organisation

The functions are split in a high level library, @library{strings}, and
a low level one, @library{strings strings-low}.  The main difference
between the two layers is that the high level library makes heavy usage
of macros to implement the @dfn{string views}, which allow accessing
substrings using a friendly syntax.

All the functions in the low level library are bound to identifiers
starting with @code{%}; for example: @func{string-prefix?} is a high
level macro, @func{%string-prefix?} is its low level function
homologous.

@subsubheading The model for strings

This library relies on the string representation of the underlying
Scheme implementation: Strings are sequences of ``code points'' or
character encodings.  Operations such as comparison or reversal are
always done code point by code point.

It's possible that a legal string might not be a sensible ``text''
sequence; for example, consider a string comprised entirely of
zero--width Unicode accent characters with no preceding base character
to modify: It is a legal string, albeit one that does not make sense
when interpreted as a sequence of natural--language text.  The routines
in @library{strings} do not handle these ``text'' concerns.

@subsubheading Direction of iteration

The library accesses the characters of a string in left--to--right or
right--to--left order depending on the convenience of the algorithm.

@subsubheading Handling side effects

Care must be taken when using functions with side effects.  One reason
for this is that some of the supported Scheme implementations will
collapse equal datum strings in the source code to the same string in
the program.  Mutating such a string in a point of the program, will
make the change visible to other points in the program.  For example:

@example
(import (rnrs)
  (rnrs mutable-strings))

(define str1 "abcd")
(define str2 "abcd")

(string-set! str1 2 #\9)
(write str2)
@end example

@noindent
when executed by Ypsilon and Larceny will print @code{"ab9d"}, while
when executed by Ikarus and Mosh will print @code{"abcd"} (last verified
with revisions checked out on Fri Jun 5, 2009).  To avoid this problem
completely, we can change the program in this way:

@example
(import (rnrs)
  (rnrs mutable-strings))

(define str1 (string-copy "abcd"))
(define str2 (string-copy "abcd"))

(string-set! str1 2 #\9)
(write str2)
@end example

@noindent
of course we may want to wrap into @func{string-copy} only the strings
that are meant to be mutated.

@c page
@node strings convention
@subsection Interface conventions


The following naming convention holds:

@itemize
@item
Procedures whose names end in @code{-ci} are case--insensitive variants.

@item
Procedures whose names end in @code{!} are side--effecting variants.
What values these procedures return is usually not specified.

@item
The order of common arguments is consistent across the different
procedures in the high level library, and consistent across the
different procedures in the low level library.  Arguments to some
functions in the high level library have different order from the one of
the homologous low level functions.

@item
Procedures that have left/right directional variants use the following
convention:

@multitable {right--to--left} {Suffix}
@headitem Direction @tab Suffix
@item left-to-right @tab @meta{none}
@item right-to-left @tab --right
@item both @tab --both
@end multitable
@end itemize

In the procedure specifications:

@itemize
@item
An @var{s} argument is a string view, which is expanded by a macro in a
triplet of values: the string, the index of the first char in the
selected substring, the index of the character past the last in the
selected position.  @ref{strings views} for details.

@item
A @var{str} argument is a string.

@item
A @var{char} argument is a character.

@item
The @var{start} and @var{past} arguments are half--open string indices
specifying a substring within a string argument; when unspecified, they
default to zero and the length of the string, respectively.  When
specified, it must be the case that:

@example
0 <= start <= past <= (string-length s)
@end example

@noindent
for the corresponding argument @var{str}.  They typically restrict a
procedure's action to the indicated substring; when @var{start} equals
@var{past} the selected substring is the empty string.

@item
A @var{pred} argument is a unary character predicate procedure,
returning a true/false value when applied to a character.

@item
A @var{char}/@var{char-set}/@var{pred} argument is a value used to
select/search for a character in a string.  If it is a character, it is
used in an equality test; if it is a character set, it is used in a
membership test; if it is a procedure, it is applied to the characters
as a test predicate.

@item
An @var{i} or @var{k} argument is an exact non--negative integer
specifying an index into a string.

@item
The @var{len} and @var{nchars} arguments are exact non--negative
integers specifying a length of a string or some number of characters.

@item
An @var{obj} argument can be any value.
@end itemize

An argument followed by @code{...} means zero--or--more elements.  So
the procedure with the signature:

@example
sum-squares x ...
@end example

@noindent
takes zero or more arguments (@var{x}, @dots{}), while the procedure
with signature:

@example
spell-check doc dict1 dict2 ...
@end example

@noindent
takes two mandatory arguments (@var{doc} and @var{dict1}) and zero or
more optional arguments (@var{dict2} ...).

If a procedure is said to return ``unspecified'', this means that
nothing at all is known about what the procedure returns.  Such a
procedure is not even required to be consistent from call to call.  It
is simply required to return a value (or values) that may be passed to a
command continuation, e.g. as the value of an expression appearing as a
non--terminal subform of a begin expression.

Comparison is simply done on individual code--points of the string.
Unless explicitly stated, for all the functions: Case--insensitive
comparison is done using the functions:

@example
char-ci=? char-ci<? char-ci>? char-ci<=? char-ci>=?
@end example

@noindent
provided by the underlying implementation, or by case--folding
characters with the operation:

@example
(char-downcase (char-upcase c))
@end example

@noindent
where the two case--mapping operations are assumed to be one--to--one,
locale--insensitive and context--insensitive, and compatible with the
one--to--one case mappings specified by Unicode's @file{UnicodeData.txt}
table:

@center @url{ftp://ftp.unicode.org/Public/UNIDATA/UnicodeData.txt}

@c page
@node strings views
@subsection Views over strings


String views are syntactic sugar to allow substring specification with
minimum overhead.  Many low level string functions act on substrings
specified with triplets of arguments:

@itemize
@item
The full string.

@item
The @dfn{start index}: The index of the first code point included in the
selected substring.

@item
The @dfn{past index}: The index of the code point past the last code
point included in the selected substring.
@end itemize

To select the substring @code{"hello"} from the string @code{"ciao,
hello, salut"} we have to determine the half--open range of code points,
which is @math{[6, 11)}, then apply a function to the arguments:

@example
(%the-function "ciao, hello, salut" 6 11)

;;              0123456789012345678
;;              0         1
@end example

@noindent
while to select the whole string we can do:

@example
(%the-function "ciao, hello, salut" 0 18)
@end example

@noindent
or:

@example
(let ((str "ciao, hello, salut"))
  (%the-function str 0 (string-length str)))
@end example

With the string views implemented by @library{strings}, the low level
function is wrapped by a high level syntax which can be invoked as:

@example
(the-function "ciao, hello, salut")
        ; select the whole string

(the-function ("ciao, hello, salut"))
        ; select the whole string

(the-function ("ciao, hello, salut" 6))
        ; select the substring [6, 18)

(the-function ("ciao, hello, salut" 6 11))
        ; select the substring [6, 11)
@end example

@noindent
additionally the following verbose syntaxes are supported:

@example
(the-function (view "ciao, hello, salut"))
        ; select the whole string

(the-function (view "ciao, hello, salut"
                (start 6)))
        ; select the substring [6, 18)

(the-function (view "ciao, hello, salut"
                (start 6)
                (past 11)))
        ; select the substring [6, 11)

(the-function (view "ciao, hello, salut"
                (past 11)))
        ; select the substring [0, 11)
@end example

@noindent
instead of a triplet of arguments, the string view is a single argument
that can be:

@table @code
@item @meta{string}
@itemx (@meta{string})
@itemx (view @meta{string})
The string itself or an unquoted list holding the string itself,
optionally prefixed by the @code{view} auxiliary syntax: It selects the
whole string.

@item (@meta{string} @meta{start-index})
@itemx (view @meta{string} (start @meta{start-index}))
An unquoted list holding the string and the start index, optionally with
auxiliary syntaxes @code{view} and @code{start}: It selects the
substring from the start index to the end.

@item (view @meta{string} (past @meta{past-index}))
An unquoted list holding the string and the past index, with auxiliary
syntaxes @code{view} and @code{past}: It selects the substring from zero
to the selected past index.

@item (@meta{string} @meta{start-index} @meta{past-index})
@itemx (view @meta{string} (start @meta{start-index}) (past @meta{past-index}))
An unquoted list holding the string, the start index and the past index,
optionally with auxiliary syntaxes @code{view}, @code{start} and
@code{past}: It selects the substring between the start and past
indexes.
@end table

@noindent
@meta{string}, @meta{start-index} and @meta{past-index} can be arbitrary
Scheme expressions.  High level macros accepting two or more substrings
as arguments support string views for all of them.  When we are
concerned with the overhead of string views, we can use the low level
functions directly.

In the following documentation: Low level functions and high level
macros are documented together; only the meaning of arguments to the low
level function are described, the meaning of high level arguments is
obvious.

@c page
@node strings cons
@subsection Constructors


@defun string-concatenate @var{string-list}
Append the elements of @var{string-list} together into a single string.
Guaranteed to return a freshly allocated string.
@end defun


@deffn Function %string-concatenate-reverse @var{string-list} @var{final-string} @var{nchars}
@deffnx Macro string-concatenate-reverse @var{string-list}
@deffnx Macro string-concatenate-reverse @var{string-list} @var{final-string}
@deffnx Macro string-concatenate-reverse @var{string-list} @var{final-string} @var{nchars}
Reverse @var{string-list} then concatenate the elements, which must be
strings.  The first @var{nchars} code points in @var{final-string} are
consed onto the beginning of @var{string-list} before performing the the
reversal and concatenation operations.

@var{final-string} defaults to the empty string and @var{nchars}
defaults to the length of @var{final-string}.

This procedure is useful in the construction of procedures that
accumulate character data into lists of string buffers, and wish to
convert the accumulated data into a single string when done.
@end deffn


@defun string-tabulate @var{integer->char} @var{len}
Construct a string of size @var{len} by applying @var{integer->char} to
each index in the range @math{[0, @var{len})} to produce the
corresponding string element.  The order in which @var{integer->char} is
applied to the indices is not specified.
@end defun

@c page
@node strings pred
@subsection Predicates


@defun string-null? @var{s}
Return @true{} if @var{obj} is the empty string, otherwise return
@false{}.
@end defun


@deffn Function %string-every @var{char/char-set/pred} @var{str} @var{start} @var{past}
@deffnx Function %string-any @var{char/char-set/pred} @var{str} @var{start} @var{past}
@deffnx Macro string-every @var{char/char-set/pred} @var{S}
@deffnx Macro string-any @var{char/char-set/pred} @var{S}
Check to see if the given criteria is true on every/any character in
@var{str}, proceeding from left (index @var{start}) to right (index
@var{past}).

If the selected substring is empty, the return value is @false{}.

If @var{char/char-set/pred} is a character, it is tested for equality
with the elements of @var{s}.

If @var{char/char-set/pred} is a character set, the elements of @var{s}
are tested for membership in the set.

If @var{char/char-set/pred} is a predicate procedure, it is applied to
the elements of @var{s}.  The predicate is ``witness--generating'':

@itemize
@item
If @func{string-any} returns true, the returned true value is the one
produced by the application of the predicate.

@item
If @func{string-every} returns true, the returned true value is the one
produced by the application of the predicate to the last code point in
the substring.
@end itemize

If the predicate is applied to the final element of the selected
substring, that final application is a tail call.

The names of these procedures do not end with a question mark; this is
to indicate that, in the predicate case, they do not return a simple
boolean (@true{} or @false{}), but a general value.
@end deffn

@c page
@node strings compar
@subsection Comparison


@deffn Function %string-compare @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2} @var{proc<} @var{proc=} @var{proc>}
@deffnx Function %string-compare-ci @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2} @var{proc<} @var{proc=} @var{proc>}
@deffnx Macro string-compare @var{S1} @var{S2} @var{proc<} @var{proc=} @var{proc>}
@deffnx Marco string-compare-ci @var{S1} @var{S2} @var{proc<} @var{proc=} @var{proc>}
Determine the @dfn{mismatch index} between the two substrings: the
largest index @math{i} such that for every @math{0 <= j < i},
@math{str1[j] = str2[j]}; that is, @math{i} is the first position that
does not match.

The mismatch index is always an index into @var{str1}; in the case of
equal strings, it is always @var{past1}; the functions observe the
protocol in this redundant case for uniformity.

The characters at the mismatch index are compared using @func{char<=?}
or @func{char-ci<=?}.  @var{proc<}, @var{proc=}, or @var{proc>} are
applied to the mismatch index, depending upon whether the substring of
@var{str1} is less than, equal to, or greater than the substring of
@var{str2}.  The result of the application is returned.

If we just want to have the mismatch index: We can use @func{values} as
value for @var{proc<}, @var{proc=} and @var{proc>}.  Another interesting
option is to use @code{(lambda (mismatch-index) #f)} or @code{(lambda
(mismatch-index) #t)}.

Examples:

@example
(string-compare "abcd" "abcd" values values values)
@result{} 5

(string-compare "abcd" "abcd12" values values values)
@result{} 5
@end example
@end deffn


@deffn Function %string= @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2}
@deffnx Function %string-ci= @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2}
@deffnx Macro string= @var{S1} @var{S2}
@deffnx Macro string-ci= @var{S1} @var{S2}
Compare two substrings: Return true if they are equal according to
@func{char=?}, @false{} otherwise.
@end deffn


@deffn Function %string<> @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2}
@deffnx Function %string-ci<> @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2}
@deffnx Macro string<> @var{S1} @var{S1}
@deffnx Macro string-ci<> @var{S1} @var{S2}
Compare two substrings: Return @false{} if they are equal according to
@func{char=?}, true otherwise.
@end deffn


The following predicates are the lexicographic extensions of the
corresponding character predicates.  A string @var{str1} is
``lexicographically'' less than @var{str2} if @var{str1} would come
first in a dictionary.


@deffn Function %string< @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2}
@deffnx Function %string-ci< @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2}
@deffnx Macro string< @var{S1} @var{S2}
@deffnx Macro string-ci< @var{S1} @var{S2}
Compare two substrings: Return true if the first is lexicographically
less than the second, @false{} otherwise.  If @var{str2} is longer than
@var{str1} but the substrings are equal up to the end of @var{str1}:
Return @true{}.

@example
(string< "abcd" "abcd") @result{} #f
(string< "abc"  "abcd") @result{} #t
(string< "abcd" "abc") @result{} #f
(string< "ABcd" "abcd") @result{} #t
(string< "abcd" "a2cd") @result{} #f

(string-ci< "abcd" "abcd") @result{} #f
(string-ci< "abc"  "abcd") @result{} #t
(string-ci< "abcd" "abc") @result{} #f
(string-ci< "ABcd" "abcd") @result{} #f
(string-ci< "abcd" "a2cd") @result{} #f
@end example
@end deffn


@deffn Function %string<= @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2}
@deffnx Function %string-ci<= @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2}
@deffnx Macro string<= @var{S1} @var{S2}
@deffnx Macro string-ci<= @var{S1} @var{S2}
Compare two substrings: Return true if the first is lexicographically
less than, or equal to, the second; @false{} otherwise.  If @var{str2}
is longer than @var{str1} but the substrings are equal up to the end of
@var{str1}: Return @true{}.

@example
(string<= "abcd" "abcd") @result{} #t
(string<= "abc"  "abcd") @result{} #t
(string<= "abcd" "abc") @result{} #f
(string<= "ABcd" "abcd") @result{} #t
(string<= "abcd" "a2cd") @result{} #f

(string-ci<= "abcd" "abcd") @result{} #t
(string-ci<= "abc"  "abcd") @result{} #t
(string-ci<= "abcd" "abc") @result{} #f
(string-ci<= "ABcd" "abcd") @result{} #t
(string-ci<= "abcd" "a2cd") @result{} #f
@end example
@end deffn


@deffn Function %string> @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2}
@deffnx Function %string-ci> @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2}
@deffnx Macro string> @var{S1} @var{S2}
@deffnx Macro string-ci> @var{S1} @var{S2}
Compare two substrings: Return true if the first is lexicographically
greater than the second, @false{} otherwise.  If @var{str1} is longer
than @var{str2} but the substrings are equal up to the end of
@var{str2}: Return @true{}.

@example
(string> "abcd" "abcd") @result{} #f
(string> "abcd" "abc") @result{} #t
(string> "abc"  "abcd") @result{} #f
(string> "abcd" "ABcd") @result{} #t
(string> "a2cd" "abcd") @result{} #f

(string-ci> "abcd" "abcd") @result{} #f
(string-ci> "abcd" "abc") @result{} #t
(string-ci> "abc"  "abcd") @result{} #f
(string-ci> "abcd" "ABcd") @result{} #f
(string-ci> "a2cd" "abcd") @result{} #f
@end example
@end deffn


@deffn Function %string>= @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2}
@deffnx Function %string-ci>= @var{str1} @var{beg1} @var{past1} @var{str2} @var{beg2} @var{past2}
@deffnx Macro string>= @var{S1} @var{S2}
@deffnx Macro string-ci>= @var{S1} @var{S2}
Compare two substrings: Return true if the first is lexicographically
greater than, or equal to, the second; @false{} otherwise.  If
@var{str1} is longer than @var{str2} but the substrings are equal up to
the end of @var{str2}: Return @true{}.

@example
(string>= "abcd" "abcd") @result{} #t
(string>= "abcd" "abc") @result{} #t
(string>= "abc" "abcd") @result{} #f
(string>= "abcd" "ABcd") @result{} #t
(string>= "a2cd" "abcd") @result{} #f

(string-ci>= "abcd" "abcd") @result{} #t
(string-ci>= "abcd" "abc") @result{} #t
(string-ci>= "abc"  "abcd") @result{} #f
(string-ci>= "abcd" "ABcd") @result{} #t
(string-ci>= "a2cd" "abcd") @result{} #f
@end example
@end deffn

@c page
@node strings map
@subsection Mapping functions


@deffn Function %string-map @var{proc} @var{str} @var{start} @var{past}
@deffnx Macro string-map @var{proc} @var{S}
@var{proc} is a @code{char->char} procedure; it is mapped over the
selected substring of @var{str}.  Return the result string and do not
alter its @var{str} parameter.

The order in which @var{proc} is applied to the elements of @var{str} is
not specified.
@end deffn


@deffn Function %string-map! @var{proc} @var{str} @var{start} @var{past}
@deffnx Macro string-map! @var{proc} @var{S}
@var{proc} is a @code{char->char} procedure; it is mapped over the
selected substring of @var{str}.  Store the mapped code points into the
selected substring of @var{str} itself.

The order in which @var{proc} is applied to the elements of @var{s} is
not specified.
@end deffn


@deffn Function %string-for-each* @var{proc} @var{str} @var{start} @var{past}
@deffnx Macro string-for-each* @var{proc} @var{S}
Apply @var{proc} to each character in the selected substring of
@var{str}.  This function iterates from start to end in increasing
order.
@end deffn


@deffn Function %string-for-each-index @var{proc} @var{str} @var{start} @var{past}
@deffnx Macro string-for-each-index @var{proc} @var{S}
Apply @var{proc} to each index of the selected substring of @var{str},
in order.  This is simply a method of looping over a string that is
guaranteed to be safe and correct.  Example:

@example
(define S "...")

(let* ((len (string-length S))
       (ans (make-string len)))
  (string-for-each-index
      (lambda (i) (string-set! ans (- len i) (string-ref S i)))
      S)
  ans)
@end example
@end deffn

@c page
@node strings case
@subsection Case mapping


@deffn Function %string-titlecase*! @var{str} @var{start} @var{past}
@deffnx Macro string-titlecase* @var{s}
@deffnx Macro string-titlecase*! @var{s}
For every character @math{c} in the selected range of @var{str}, if
@math{c} is preceded by a cased character, it is downcased; otherwise it
is titlecased.

@func{string-titlecase*} returns the result string and does not alter
its s parameter.  @func{string-titlecase!} is the in--place
side--effecting variant.

Examples:

@example
(string-titlecase* "--capitalize tHIS sentence.")
@result{} "--Capitalize This Sentence."

(string-titlecase* "see Spot run. see Nix run.")
@result{} "See Spot Run. See Nix Run."

(string-titlecase* "3com makes routers.")
@result{} "3Com Makes Routers."
@end example

Note that the character preceding @code{str[start]} has no effect on the
titlecase decision for character @code{str[start]}:

@example
(string-titlecase* ("greasy fried chicken" 2))
@result{} "Easy Fried Chicken"
@end example
@end deffn


@deffn Macro string-upcase* @var{s}
@deffnx Macro string-upcase*! @var{s}
@deffnx Macro string-downcase* @var{s}
@deffnx Macro string-downcase*! @var{s}
Raise or lower the case of the alphabetic characters in the string.
There are no low level functions for these macros because they are just
wrappers for @func{%string-map} and @func{%string-map!}.

@func{string-upcase} and @func{string-downcase} return the result string
and do not alter their s parameter.  @func{string-upcase!} and
@func{string-downcase!} are the in--place side--effecting variants.
@end deffn

@c page
@node strings fold
@subsection Fold, unfold and map


@defun string-fold @var{kons} @var{knil} @var{str0} @var{str} ...
@defunx string-fold-right @var{kons} @var{knil} @var{str0} @var{str} ...
The fundamental string iterator.  @var{kons} is iterated left--to--right
over each index in all of the strings, stopping at the end of the
shortest; @var{kons} is applied as:

@example
(@var{kons} @var{idx} @var{state}
  (string-ref @var{str0} @var{idx})
  (string-ref @var{str}  @var{idx})
  )
@end example

@noindent
where @var{state} is the current state value; the current state value
begins with @var{knil}, and becomes whatever @var{kons} returned at the
respective iteration; @var{idx} is the current index.

@func{string-fold-right} is similar to @func{string-fold}, but it
iterates right--to--left.

@quotation
Notice that to allow for an unspecified number of arguments, these folds
hand the state as first argument to @var{kons}, as opposed to the usual
fold arguments.
@end quotation
@end defun

@c ------------------------------------------------------------

@deffn Function %string-fold* @var{kons} @var{knil} @var{str} @var{start} @var{past}
@deffnx Function %string-fold-right* @var{kons} @var{knil} @var{str} @var{start} @var{past}
@deffnx Macro string-fold* @var{kons} @var{knil} @var{S}
@deffnx Macro string-fold-right* @var{kons} @var{knil} @var{S}
Fundamental iterators for substrings.  @var{kons} is iterated over each
character of the selected substring:

@example
(@var{kons}
  (string-ref @var{str} (+ @var{start} @var{idx}))
  @var{state})
@end example

@noindent
where @var{state} is the current state value; the current state value
begins with @var{knil}, and becomes whatever @var{kons} returned at the
respective iteration; @var{idx} is the current index.

The left--fold iterator, @func{%string-fold*}, builds the return value
as:

@example
(@var{kons}
  (string-ref @var{str} (- @var{past} 1))
  (@var{kons}
    (string-ref @var{str} (- @var{past} 2))
    ...
      (@var{kons}
        (string-ref @var{str} (+ @var{start} 2))
        (@var{kons}
           (string-ref @var{str} (+ @var{start} 1))
           (@var{kons}
              (string-ref @var{str} @var{start})
              @var{knil})))))
@end example

The right--fold iterator, @func{%string-fold-right*}, builds the return
value as:

@example
(@var{kons}
  (string-ref @var{str} @var{start}
  (@var{kons}
    (string-ref @var{str} (+ @var{start} 1))
    ...
      (@var{kons}
        (string-ref @var{str} (- @var{past} 3))
        (@var{kons}
           (string-ref @var{str} (- @var{past} 2))
           (@var{kons}
              (string-ref @var{str} (- @var{past} 1))
              @var{knil})))))
@end example

Examples:

@example
;; Convert a string to a list of chars.
(string-fold* cons '() "abcd")
@result{} (#\d #\c #\b #\a))

;; Count the number of upper-case characters in a string.
(string-fold* (lambda (c count)
                (if (char-upper-case? c)
                    (+ count 1)
                  count))
               0
               "ABCdefGHi")
@result{} 5

;; Double every backslash character in S.
(let* ((str "abc\\de\\f\\ghi")
       (ans-len (string-fold
                 (lambda (c sum)
                   (+ sum (if (char=? c #\\) 2 1)))
                 0 str))
       (ans (make-string ans-len)))
  (string-fold*
   (lambda (c i)
     (let ((i (if (char=? c #\\)
                  (begin
                    (string-set! ans i #\\)
                    (+ i 1))
                i)))
        (string-set! ans i c)
        (+ i 1)))
    0 str)
   ans)
@result{} "abc\\\\de\\\\f\\\\ghi"
@end example
@end deffn

@c ------------------------------------------------------------

@defun string-unfold @var{stop?} @var{seed->char} @var{make-seed} @var{first-seed}
@defunx string-unfold @var{stop?} @var{seed->char} @var{make-seed} @var{first-seed} @var{base-string}
@defunx string-unfold @var{stop?} @var{seed->char} @var{make-seed} @var{first-seed} @var{base-string} @var{make-final}
This is a fundamental constructor for strings.  Arguments description
follows.

@table @var
@item make-seed
A map function used to generate a series of ``seed'' values from the
initial seed:

@example
@var{first-seed}
(@var{make-seed} @var{first-seed})            @result{} seed2
(@var{make-seed} seed2)                 @result{} seed3
(@var{make-seed} seed3)                 @result{} seed4
...
@end example

@item stop?
A predicate function telling when to stop generating characters; when it
returns true when applied to one of the seed values.

@item seed->char
Map function mapping each seed value to the corresponding character in
the result string.  These chars are assembled into the string in a
left--to--right order.

@item base
An optional string which is used as initial/leftmost portion of the
constructed string.  Defaults to the empty string.

@item make-final
Optional function applied to the terminal seed value (on which
@var{stop?}  returns true) to produce the final/rightmost portion of the
constructed string.  Defaults to @code{(lambda (x) "")}.
@end table

More precisely, the following (simple, inefficient) definitions hold:

@example
;; iterative
(define (string-unfold stop? seed->char make-seed
                       first-seed base-string make-final)
  (let loop ((seed    first-seed)
             (result  base-string))
    (if (stop? seed)
        (string-append result (make-final seed))
      (loop (make-seed seed)
            (string-append result
                           (string (seed->char seed)))))))

;; recursive
(define (string-unfold stop? seed->char make-seed
                       first-seed base-string make-final)
  (string-append
     base-string
     (let loop ((seed first-seed))
       (if (stop? seed)
           (make-final seed)
         (string-append (string (seed->char seed))
                        (loop (make-seed seed)))))))
@end example

This function is a fairly powerful string constructor; we can use it to
convert a list to a string, read a port into a string, reverse a string,
copy a string, and so forth.  Examples:

@example
(port->string p) = (string-unfold eof-object? values
                                  (lambda (x) (read-char p))
                                  (read-char p))

(list->string lis) = (string-unfold null? car cdr lis)

(string-tabulate f size) = (string-unfold (lambda (i)
                                            (= i size))
                                          f add1 0)
@end example

@noindent
to map @var{seed->char} over a list @var{lis}, producing a string:

@example
(string-unfold null? (compose f car) cdr lis)
@end example

Interested functional programmers may enjoy noting that
@func{string-fold-right} and @func{string-unfold} are in some sense
inverses.  That is, given operations @var{knull?}, @var{kar}, @var{kdr},
@var{kons}, and @var{knil} satisfying:

@example
(kons (kar x) (kdr x)) @result{} x
(knull? knil) @result{} #t
@end example

@noindent
then:

@example
(string-fold-right kons knil
                   (string-unfold knull? kar kdr x))
@result{} x
@end example

@noindent
and:

@example
(string-unfold knull? kar kdr
               (string-fold-right kons knil s))
@result{} s
@end example

The final string constructed does not share storage with either
@var{base-string} or the value produced by @var{make-final}.
@end defun

@c ------------------------------------------------------------

@defun string-unfold-right @var{stop?} @var{seed->char} @var{make-seed} @var{first-seed}
@defunx string-unfold-right @var{stop?} @var{seed->char} @var{make-seed} @var{first-seed} @var{base-string}
@defunx string-unfold-right @var{stop?} @var{seed->char} @var{make-seed} @var{first-seed} @var{base-string} @var{make-final}
This is a fundamental constructor for strings.  The arguments are like
the ones of @func{string-unfold}.  The difference from
@func{string-unfold} is that this function builds the list from right to
left; more precisely, the following (simple, inefficient) definitions
hold:

@example
;; iterative
(define (string-unfold-right
           stop? seed->char make-seed
           first-seed base-string make-final)
  (let lp ((seed    first-seed)
           (result  base))
    (if (stop? seed)
        (string-append (make-final seed) result)
      (loop (make-seed seed)
            (string-append (string (seed->char seed))
                           result)))))

;; recursive
(define (string-unfold-right
           stop? seed->char make-seed
           first-seed base-string make-final)
  (string-append
     (let loop ((seed first-seed))
       (if (stop? seed)
           (make-final seed)
         (string-append (loop (make-seed seed))
                        (string (seed->char seed)))))
     base))
@end example

Interested functional programmers may enjoy noting that
@func{string-fold} and @func{string-unfold-right} are in some sense
inverses.  That is, given operations @var{knull?}, @var{kar}, @var{kdr},
@var{kons}, and @var{knil} satisfying:

@example
(kons (kar x) (kdr x)) @result{} x
(knull? knil) @result{} #t
@end example

@noindent
then:

@example
(string-fold kons knil
             (string-unfold-right knull? kar kdr x))
@result{} x
@end example

@noindent
and:

@example
(string-unfold-right knull? kar kdr
                     (string-fold kons knil s))
@result{} s
@end example

The final string constructed does not share storage with either
@var{base-string} or the value produced by @func{make-final}.
@end defun

@c page
@node strings select
@subsection Selection


@deffn Macro substring* S
Wrapper for @func{substring} accepting a string view as argument.
@end deffn


@deffn Function %string-copy* @var{str} @var{start} @var{past}
@deffnx Macro string-copy* @var{S}
The function @func{%string-copy*} is an alias for @func{substring}; the
macro @func{string-copy*} is an alias for @func{substring*}.

@quotation
These exist for symmetry with the @library{vectors} library; they allow
more confidence in converting a vector function into a string function,
and vice versa, by just replacing the string @code{vector} with the
string @code{string}, and vice versa.
@end quotation
@end deffn


@deffn Function %string-reverse-copy* @var{str} @var{start} @var{past}
@deffnx Macro string-reverse-copy* @var{V}
Like @func{%string-copy*}, but copy the elements in the reverse order
from the selected substring of @var{str}.

@example
(string-reverse-copy* ('#(5 4 3 2 1 0) 1 5))
@result{} #(1 2 3 4)
@end example
@end deffn


@deffn Function %string-copy*! @var{dst-str} @var{dst-start} @var{src-str} @var{src-start} @var{src-past}
@deffnx Macro string-copy*! @var{dst-S} @var{src-S}
Write the selected source substring into the selected destination
substring.  This function is the side--effecting variant of
@func{substring} and @func{substring*}.  This function supports copying
over the same string.

The selected destination substring starts at @var{dst-start} in
@var{dst-str} and may extend until the end of the string.  In the
destination string view @var{dst-S}: If a past index is specified, it is
ignored.
@end deffn


@deffn Function %string-reverse-copy*! @var{dst-str} @var{dst-start} @var{src-str} @var{src-start} @var{src-past}
@deffnx Macro string-reverse-copy*! @var{V-dst} @var{V-src}
Like @func{%string-copy*!}, but this copies the elements in the reverse
order.  This function supports copying over the same string.
@end deffn


@deffn Function %string-take @var{nchars} @var{str} @var{start} @var{past}
@deffnx Function %string-take-right @var{nchars} @var{str} @var{start} @var{past}
@deffnx Macro string-take @var{S} @var{nchars}
@deffnx Macro string-take-right @var{S} @var{nchars}
Return the first or last @var{nchars} of the selected substring.  These
functions always return a newly allocated string.
@end deffn


@deffn Function %string-drop @var{nchars} @var{str} @var{start} @var{past}
@deffnx Function %string-drop-right @var{nchars} @var{str} @var{start} @var{past}
@deffnx Macro string-drop @var{S} @var{nchars}
@deffnx Macro string-drop-right @var{S} @var{nchars}
Drop the first or last @var{nchars} of the selected substring and return
the resulting string.  These functions always return a newly allocated
string.
@end deffn

@c page
@node strings pad
@subsection Padding and trimming


@deffn Function %string-pad @var{requested-len} @var{fill-char} @var{str} @var{start} @var{past}
@deffnx Function %string-pad-right @var{requested-len} @var{fill-char} @var{str} @var{start} @var{past}
@deffnx Macro string-pad @var{S} @var{requested-len}
@deffnx Macro string-pad @var{S} @var{requested-len} @var{fill-char}
@deffnx Macro string-pad-right @var{S} @var{requested-len}
@deffnx Macro string-pad-right @var{S} @var{requested-len} @var{fill-char}
Build a string of length @var{requested-len} comprised of @var{str}
padded on the left or right by as many occurrences of the character
@var{fill-char} as needed.  Always return a newly allocated string.

If @var{str} has more than @var{requested-len} chars, it is truncated on
the left or right to length @var{requested-len}.  For the macros:
@var{fill-char} defaults to @code{#\space}.

Examples:

@example
(string-pad     "325" 5) => "  325"
(string-pad   "71325" 5) => "71325"
(string-pad "8871325" 5) => "71325"
@end example
@end deffn


@deffn Function %string-trim @var{char/char-set/pred} @var{str} @var{start} @var{past}
@deffnx Function %string-trim-right @var{char/char-set/pred} @var{str} @var{start} @var{past}
@deffnx Function %string-trim-both @var{char/char-set/pred} @var{str} @var{start} @var{past}
@deffnx Macro string-trim @var{S} @var{char/char-set/pred}
@deffnx Macro string-trim-right @var{S} @var{char/char-set/pred}
@deffnx Macro string-trim-both @var{S} @var{char/char-set/pred}
Trim @var{str} by skipping over all characters on the left/on the
right/on both sides that satisfy the second parameter
@var{char/char-set/pred}:

@itemize
@item
If it is a character @var{char}, characters equal to @var{char} are
trimmed.

@item
If it is a char set @var{char-set}, characters contained in
@var{char-set} are trimmed.

@item
If it is a predicate @var{pred}, it is a test predicate that is applied
to the characters in @var{s}; a character causing it to return true is
skipped.
@end itemize

Always return a newly allocated string.
@end deffn

@c page
@node strings prefix
@subsection Prefixes and suffixes


@deffn Function %string-prefix-length @var{str1} @var{start1} @var{past1} @var{str2} @var{start2} @var{past2}
@deffnx Function %string-prefix-length-ci @var{str1} @var{start1} @var{past1} @var{str2} @var{start2} @var{past2}
@deffnx Macro string-prefix-length @var{S1} @var{S2}
@deffnx Macro string-prefix-length-ci @var{S1} @var{S2}
Return the length of the longest common prefix of the two substrings.
This is equivalent to the ``mismatch index'' for the strings (modulo the
start index offsets).
@end deffn


@deffn Function %string-suffix-length @var{str1} @var{start1} @var{past1} @var{str2} @var{start2} @var{past2}
@deffnx Function %string-suffix-length-ci @var{str1} @var{start1} @var{past1} @var{str2} @var{start2} @var{past2}
@deffnx Macro string-suffix-length @var{S1} @var{S2}
@deffnx Macro string-suffix-length-ci @var{S1} @var{S2}
Return the length of the longest common suffix of the two substrings.
@end deffn


@deffn Function %string-prefix? @var{str1} @var{start1} @var{past1} @var{str2} @var{start2} @var{past2}
@deffnx Function %string-prefix-ci? @var{str1} @var{start1} @var{past1} @var{str2} @var{start2} @var{past2}
@deffnx Macro string-prefix? @var{S1} @var{S2}
@deffnx Macro string-prefix-ci? @var{S1} @var{S2}
Return @true{} if the selected substring of @var{str1} is a prefix of
the selected substring of @var{str2}, otherwise return @false{}.  Notice
that the empty string is a prefix of every string.
@end deffn


@deffn Function %string-suffix? @var{str1} @var{start1} @var{past1} @var{str2} @var{start2} @var{past2}
@deffnx Function %string-suffix-ci? @var{str1} @var{start1} @var{past1} @var{str2} @var{start2} @var{past2}
@deffnx Macro string-suffix? @var{S1} @var{S2}
@deffnx Macro string-suffix-ci? @var{S1} @var{S2}
Return @true{} if the selected substring of @var{str1} is a suffix of
the selected substring of @var{str2}, otherwise return @false{}.  Notice
that the empty string is @strong{not} a suffix of every string.
@end deffn

@c page
@node strings search
@subsection Searching


@deffn Function %string-index @var{char/char-set/pred} @var{str} @var{start} @var{past}
@deffnx Function %string-index-right @var{char/char-set/pred} @var{str} @var{start} @var{past}
@deffnx Macro string-index @var{S} @var{char/char-set/pred}
@deffnx Macro string-index-right @var{S} @var{char/char-set/pred}
Search through the string from the left or right, returning the index of
the first occurrence of a character which:

@itemize
@item
Equals @var{char/char-set/pred} if it is a character.

@item
Is in @var{char/char-set/pred} if it is a character set.

@item
Satisfies the predicate @var{char/char-set/pred} if it is a procedure.
@end itemize

If no match is found: Return @false{}.
@end deffn


@deffn Function %string-skip @var{char/char-set/pred} @var{str} @var{start} @var{past}
@deffnx Function %string-skip-right @var{char/char-set/pred} @var{str} @var{start} @var{past}
@deffnx Macro string-skip @var{S} @var{char/char-set/pred}
@deffnx Macro string-skip-right @var{S} @var{char/char-set/pred}
Search through the string from the left or right, returning the index of
the first occurrence of a character which:

@itemize
@item
Does not equal @var{char/char-set/pred} if it is a character.

@item
Is not in @var{char/char-set/pred} if it is a character set.

@item
Does not satisfy the predicate @var{char/char-set/pred} if it is a
procedure.
@end itemize

If no match is found: Return @false{}.
@end deffn


@deffn Function %string-count @var{char/char-set/pred} @var{str} @var{start} @var{past}
@deffnx Macro string-count @var{S} @var{char/char-set/pred}
Return a count of the number of characters in @var{str} that satisfy the
@var{char/char-set/pred} argument:

@itemize
@item
If it is a procedure, it is applied to the character as a predicate.

@item
If it is a character set, the character is tested for membership.

@item
If it is a character, it is used in an equality test.
@end itemize
@end deffn


@deffn Function %string-contains @var{str1} @var{start1} @var{past1} @var{str2} @var{start2} @var{past2}
@deffnx Function %string-contains-ci @var{str1} @var{start1} @var{past1} @var{str2} @var{start2} @var{past2}
@deffnx Macro string-contains @var{S1} @var{S2}
@deffnx Macro string-contains-ci @var{S1} @var{S2}
Return true if the selected substring of @var{str1} contains the
selected substring of @var{str2}, else return @false{}.  The return
value is the index in @var{str1} where the substring of @var{str2}
occurs.
@end deffn

@c page
@node strings filter
@subsection Filtering and deleting


@deffn Function %string-filter @var{char/char-set/pred} @var{str} @var{start} @var{past}
@deffnx Macro string-filter @var{S} @var{char/char-set/pred}
Filter the selected substring, retaining only those characters that
satisfy the @var{char/char-set/pred} argument:

@itemize
@item
If it is a procedure, it is applied to the character as a predicate.

@item
If it is a character set, the character is tested for membership.

@item
If it is a character, it is used in an equality test.
@end itemize

Always return a newly allocated string.
@end deffn


@deffn Function %string-delete @var{char/char-set/pred} @var{str} @var{start} @var{past}
@deffnx Macro string-delete @var{S} @var{char/char-set/pred}
Filter the string @var{s}, retaining only those characters that do
@strong{not} satisfy the @var{char/char-set/pred} argument:

@itemize
@item
If it is a procedure, it is applied to the character as a predicate.

@item
If it is a character set, the character is tested for membership.

@item
If it is a character, it is used in an equality test.
@end itemize

Always return a newly allocated string.
@end deffn

@c page
@node strings list
@subsection List and string conversion


@deffn Function %string->list* @var{str} @var{start} @var{past}
@deffnx Macro string->list* @var{S}
Return a newly allocated list of the characters that make up the given
substring.  This is an extension of the @func{string->list} function
that adds support for substrings.
@end deffn


@defun reverse-list->string @var{char-list}
Reverse the given list of characters, then compose a string with the
result.

@example
(reverse-list->string '(#\a #\B #\c))
@result{} "cBa"
@end example
@end defun


@deffn Function %string-tokenize @var{token-set} @var{str} @var{start} @var{past}
@deffnx Function %string-tokenise @var{token-set} @var{str} @var{start} @var{past}
@deffnx Macro string-tokenize @var{S} @var{token-set}
@deffnx Macro string-tokenise @var{S} @var{token-set}
Split the selected substring into a list of strings, where each string
is a maximal, non--empty, contiguous sequence of characters from the
character set @var{token-set}.

@example
(string-tokenize "Help make programs run, run, RUN!"
                 (char-set #\space))
@result{} ("Help" "make" "programs" "run," "run," "RUN!")
@end example

@quotation
This function provides a minimal parsing facility for simple
applications.  More sophisticated parsers that handle quoting and
backslash effects can easily be constructed using regular--expression
systems; be careful not to use @func{string-tokenize} in contexts where
more serious parsing is needed.
@end quotation
@end deffn


@defun string-join @var{string-list}
@defunx string-join @var{string-list} @var{delimiter}
@defunx string-join @var{string-list} @var{delimiter} @var{grammar}
@defunx %string-join @var{string-list} @var{delimiter} @var{grammar}
This procedure is a simple unparser: It pastes strings together using
the @var{delimiter} string.  @var{delimiter} defaults to a single white
space.  @var{grammar} is a symbol that determines how the delimiter is
used, and defaults to @code{infix}.  Supported values for @var{grammar}
are:

@table @code
@item infix
Means an infix or separator grammar: Insert the delimiter between list
elements.  An empty list will produce an empty string.

@quotation
@strong{Note}: Parsing an empty string with an infix grammar is
ambiguous.  Is it an empty list, or a list of one element, the empty
string?
@end quotation

@item strict-infix
Means the same as @code{infix}, but will raise an error if given an
empty list.

@item suffix
Means a suffix or terminator grammar: Insert the delimiter after every
list element.  This grammar has no ambiguities.

@item prefix
Means a prefix grammar: Insert the delimiter before every list element.
This grammar has no ambiguities.
@end table

The delimiter is the string used to delimit elements; it defaults to a
single space.

Examples:

@example
(string-join '("foo" "bar" "baz") ":")
@result{} "foo:bar:baz"

(string-join '("foo" "bar" "baz") ":" 'suffix)
@result{} "foo:bar:baz:"

;; Infix grammar is ambiguous wrt empty list vs. empty string,
(string-join '()   ":") @result{} ""
(string-join '("") ":") @result{} ""

(string-join '("") "," 'strict-infix) @result{} ""
(string-join '("") "," 'suffix) @result{} ","
(string-join '("") "," 'prefix) @result{} ","
@end example
@end defun

@c page
@node strings replicate
@subsection Replicate and rotate


@deffn Function %xsubstring @var{from} @var{to} @var{str} @var{start} @var{end}
@deffnx Macro xsubstring @var{S} @var{from} @var{to}
Extended substring procedure replicating the selected substring ``up and
down'' index space, in both the positive and negative directions.

The call:

@example
(%xsubstring from to "abcdefg" 3 6)
@end example

@noindent
selects the substring @code{"def"} and defines the conceptual
bidirectionally--infinite string:

@example
...  e  f  d  e  f  d  e  f  d  e  f ...
... -5 -4 -3 -2 -1  0 +1 +2 +3 +4 +5 ...
                    ^
@end example

@noindent
that is @code{def} repeated in both directions.  @func{%xsubstring}
returns the substring of this string beginning at index @var{from}, and
ending at @var{to}.

We can use this function to perform a variety of tasks:

@itemize
@item
To replicate a string:

@example
(xsubstring "abc" 0 7) @result{} "abcabca"
@end example

@item
To rotate a string left:

@example
(xsubstring "abcdef" 1  7) @result{} "bcdefa"
(xsubstring "abcdef" 2  8) @result{} "cdefab"
(xsubstring "abcdef" 3  9) @result{} "defabc"
(xsubstring "abcdef" 4 10) @result{} "efabcd"
(xsubstring "abcdef" 5 11) @result{} "fabcde"
@end example

@item
To rotate a string right:

@example
(xsubstring "abcdef" -1 5) @result{} "fabcde"
(xsubstring "abcdef" -2 4) @result{} "efabcd"
(xsubstring "abcdef" -3 3) @result{} "defabc"
(xsubstring "abcdef" -4 2) @result{} "cdefab"
(xsubstring "abcdef" -5 1) @result{} "bcdefa"
@end example
@end itemize

Note that:

@itemize
@item
The @var{from}/@var{to} indices give a half--open range; the characters
from index @var{from} up to, but not including, index @var{to}.

@item
The @var{from}/@var{to} indices are not in terms of the index space for
string @var{str}.  They are in terms of the replicated index space of
the substring defined by @var{str}, @var{start}, and @var{past}.
@end itemize

It is an error if @var{start} equals @var{past}.
@end deffn


@deffn Function xstring-xcopy! @var{from} @var{to} @var{target} @var{target-start} @var{source} @var{source-start} @var{source-past}
@deffnx Macro string-xcopy! @var{target-S} @var{source-S} @var{from} @var{to}
Exactly the same as @func{%xsubstring}, but the extracted text is
written into the selected substring of @var{target}.

This operation is not defined if @code{(eq? target start)}; we cannot
copy a string on top of itself.
@end deffn

@c page
@node strings mutate
@subsection Mutators


@defun string-swap! @var{str} @var{i} @var{j}
Swap the characters in @var{str} at positions @var{i} and @var{j}.
@end defun


@deffn Function %string-fill*! @var{fill-char} @var{str} @var{start} @var{past}
@deffnx Macro string-fill*! @var{S} @var{fill-char}
Fill the selected substring with @var{fill-char}, modifying the original
string.
@end deffn

@c page
@node strings misc
@subsection Miscellaneous functions


@deffn Function %string-replace @var{str1} @var{start1} @var{past1} @var{str2} @var{start2} @var{past2}
@deffnx Macro string-replace @var{S1} @var{S2}
Replace the selected string in @var{str1} with the selected string in
@var{str2}.  Return a newly allocated string.
@end deffn


@deffn Function %string-reverse @var{str} @var{start} @var{past}
@deffnx Function %string-reverse! @var{str} @var{start} @var{past}
@deffnx Macro string-reverse @var{S}
@deffnx Macro string-reverse! @var{S}
Reverse the string.  @func{%string-reverse} returns the result string
and does not alter its s parameter.  @func{%string-reverse!} is the
in--place side--effecting variant.
@end deffn

@c page
@node strings kmp
@subsection Knuth-Morris-Pratt searching


The Knuth--Morris--Pratt (@acronym{KMP}) string--search algorithm is a
method of rapidly scanning a sequence of text for the occurrence of some
fixed string.  It has the advantage of never requiring backtracking;
hence, it is useful for searching not just strings, but also other
sequences of text that do not support backtracking or random--access,
such as input ports.

For an explanation of the algorithm:

@center @url{http://en.wikipedia.org/wiki/Knuth-Morris-Pratt_algorithm}

The following routines package up the initialisation and searching
phases of the algorithm for general use.  They also support searching
through sequences of text that arrive in buffered chunks, in that
intermediate search state can be carried across applications of the
search loop from the end of one buffer application to the next.

A second critical property of @acronym{KMP} search is that it requires
the allocation of auxiliary memory proportional to the length of the
pattern, but constant in the size of the character type.  Alternate
searching algorithms frequently require the construction of a table with
an entry for every possible character; which can be prohibitively
expensive in a 16-bit or 32-bit character representation.

@menu
* strings kmp rv::              The restart vector.
* strings kmp step::            Single step of the search.
* strings kmp partial::         Partial string search.
* strings kmp string::          Full string search.
@end menu

@c page
@node strings kmp rv
@subsubsection The restart vector


When searching for a pattern string in a text string:

@example
(define pattern "ciao")
(define text    "Oh, ciao!")
@end example

@noindent
we start looking for the first char in the pattern:

@example
(define match-start
  (let loop ((i 0))
    (if (char=? (string-ref text i)
                (string-ref pattern 0))
        i
      (loop (+ 1 i)))))
@result{} 3
@end example

@noindent
now that we know a match for the first char, we go on looking for the
full pattern:

@example
(define match-past
  (let loop ((pi 1)                     ;; pattern index
             (ti (+ 1 match-start)))    ;; text index
    (cond
     ((= pi (string-length pattern)) pi)    ;; found
     ((= ti (string-length text))    #f)    ;; not found
     (else
      (if (char=? (string-ref ti text)
                  (string-ref pi pattern))
          (loop (+ 1 pi) (+ 1 ti))
        #f)))))                             ;; not found
@end example

@noindent
we have the following possible outcomes:

@itemize
@item
@code{match-past} is true: We have found a match in the substring
@code{[match-start, match-past)} of @code{text}.

@item
@code{match-past} is false: We have to restart matching @code{pattern}
from character @code{(+ 1 match-start)} of @code{text}.
@end itemize

The action ``restart matching'' involves backtracking (to go back) in
the @code{text} string; this can be avoided if we do the following
considerations.

@itemize
@item
Let's say we have to perform the search:

@example
(define pattern "ciao mamma")
(define text    "Oh, ciao papa e ciao mamma!")

;;               0123456789012345678901234567
;;               0         1         2
@end example

@noindent
we notice that the first char @code{c} is never repeated in the pattern;
so after we have matched the first occurrence of @code{"ciao "} in the
substring @math{[4, 9)} of @code{text}, we do not need to restart from
index @math{5}, we just try to match @code{pattern} from index @math{10}
of @code{text}.

@item
Let's say we have to perform the search:

@example
(define pattern "ciao ciao mamma")
(define text    "Oh, ciao ciao ciao mamma!")

;;               01234567890123456789012345
;;               0         1         2
@end example

@noindent
after we have matched the first occurrence of @code{"ciao ciao "} in the
substring @math{[4, 14)} of @code{text}, we find a @code{c} rather than
a @code{m}; we could restart matching @code{pattern} from index
@math{5}, but we notice that we already have matched substring @math{[0,
5)} of @code{pattern} in substring @math{[9, 14)} of @code{text}; so we
can avoid backtracking and go on matching index @math{5} of
@code{pattern} with index @math{14} of @code{text}.
@end itemize

Summary: We can implement an efficient algorithm if we precompute where
to restart from, after a partial match.  Of course, since the algorithm
needs no backtracking, we can also search for multiple patterns in
``parallel''.


@defun %kmp-make-restart-vector @var{char=} @var{pattern} @var{pattern-start} @var{pattern-past}
Build a @dfn{restart vector}, to be used to search character sequences
for the occurrence of the selected substring of @var{pattern}.

@var{char=} is a character--equality function used to construct the
restart vector.  It is typically @func{char=?} or @func{char-ci=?}.

The definition of the restart vector @math{V} for string @math{P} is: If
we have matched chars @math{0 ... i-1} of @math{P} against some search
string @math{T}:
@iftex
@tex
$$
P[i-1] = T[k]
P[i-2] = T[k-1]
...
P[0] = T[k-(i-1)]
$$
@end tex
@end iftex
@ifnottex

@example
P[i-1] = T[k]
P[i-2] = T[k-1]
...
P[0] = T[k-(i-1)]
@end example

@end ifnottex
and @math{P[i]} does not match @math{T[k]}, then reset @math{i := V[i]},
and try again to match @math{T[k]}.  If @math{V[i] = -1}: move on to
@math{T[k+1]} and @math{P[0]}.
@end defun

@c page
@node strings kmp step
@subsubsection Single step of the search


@defun %kmp-step @var{char=} @var{restart-vector} @var{next-char-from-text} @var{next-index-in-pattern} @var{pattern} @var{pattern-start}
This function encapsulates the work performed by one step of the
@acronym{KMP} string search; it can be used to scan strings, input
ports, or other on--line character sources for fixed strings.

@var{char=} is the character--equality function used to construct the
restart vector, typically @func{char=?} or @func{char-ci=?}.

@var{restart-vector} is the @acronym{KMP} restart vector for the
pattern, as constructed by @func{%kmp-make-restart-vector}.

@var{pattern} is the non--empty string specifying the text for which we
are searching.  The pattern begins at @var{pattern-start} and is
@code{(vector-length restart-vector)} characters long.

This function returns the new index in the pattern; that is, how much of
the pattern we have matched, including the given character from text.
@end defun


Searching for the pattern @code{"hello"} in the text @code{"ciao hello
salut"} looks like this:

@example
(let* ((text          "ciao hello salut")
       ;;              01234567890123456
       ;;              0         1
       (text-past     (string-length text))
       (pattern       "ciao")
       (pattern-start 0)
       (pattern-past  (string-length pattern))
       (rv            (%kmp-make-restart-vector char=?
                         pattern pattern-start pattern-past)))
  (let loop ((ti 0)
             (pi pattern-start))
    (or (and (= pi pattern-past) ti) ; found
        (and (not (= ti text-past))  ; not found
             (loop (+ 1 ti)
                   (%kmp-step char=? rv
                              (string-ref text ti)
                              pi pattern pattern-start))))))
@result{} 10
@end example

@noindent
if the pattern was not found the return value is @false{}; if the
pattern was found the return value is the index in the text of the
character past the last matched one.

Abstracting the search from a string to a generic source of characters,
we can write the following function, whose return value is the same as
the loop above:

@example
(define (return-match-past end-of-text? get-next-char
                           pattern pattern-start pattern-past)
  (let ((rv (%kmp-make-restart-vector char=?
               pattern pattern-start pattern-past)))
    (let loop ((ti 0)
               (pi pattern-start))
      (or (and (= pi pattern-past) ti) ; found
          (and (not (end-of-text?))    ; not found
               (loop (+ 1 ti)
                     (%kmp-step char=? rv (get-next-char)
                        pi pattern pattern-start)))))))
@end example

@noindent
a usage example with a string looks like this:

@example
(let* ((text           "ciao hello salut")
       ;;               01234567890123456
       ;;               0         1
       (pattern        "hello")
       (ti             0)
       (end-of-text?   (lambda ()
                         (= ti (string-length text))))
       (get-next-char  (lambda ()
                         (begin0
                             (string-ref text ti)
                           (set! ti (+ 1 ti))))))
  (return-match-past end-of-text? get-next-char
                     pattern 0 (string-length pattern)))
@result{} 10
@end example

@noindent
a usage example with an input port looks like this:

@example
(let* ((text           "ciao hello salut")
       ;;               01234567890123456
       ;;               0         1
       (pattern        "salut")
       (port           (open-string-input-port text))
       (end-of-text?   (lambda ()
                         (eof-object? (peek-char port))))
       (get-next-char  (lambda ()
                         (read-char port))))
  (return-match-past end-of-text? get-next-char
                     pattern 0 (string-length pattern)))
@result{} 16
@end example

@c page
@node strings kmp partial
@subsubsection Partial string search


@defun %kmp-string-partial-search @var{char=} @var{restart-vector} @var{next-index-in-pattern} @var{text} @var{text-start} @var{text-end} @var{pattern} @var{pattern-start}
Using this function is equivalent to apply @func{%kmp-step} across the
selected substring of @var{text} in search of the selected substring of
@var{pattern}; the pattern is @code{(vector-length rv)} characters long.

@var{char=} is the character--equality function used to construct the
restart vector, typically @func{char=?} or @func{char-ci=?}.

@itemize
@item
If the pattern is found: Return @math{-j} (a strictly negative integer)
where @math{j} is the index of the character past the one that last
matched the pattern in @var{text}.

@item
If the pattern is not found: Return the index in the restart vector (a
non negative integer) to be used in the next call to this function.
@end itemize

This utility is designed to allow searching for occurrences of a fixed
string that might extend across multiple buffers of text.  Notice that,
in this case, when the returned value is negative: It is the index in
the last buffer, not in the whole text.
@end defun


A simple one--shot search over a given string looks like the following:

@example
(let* ((text            "ciao hello salut")
       ;;                01234567890123456
       ;;                0         1
       (text-start      0)
       (text-past       (string-length text))
       (pattern         "hello")
       (pattern-start   0)
       (pattern-past    (string-length pattern))
       (restart-vector  (%kmp-make-restart-vector char=?
                           pattern pattern-start pattern-past)))
  (let ((i (%kmp-string-partial-search
              char=? restart-vector pattern-start
              text text-start text-past
              pattern pattern-start)))
    (or (<= 0 i) ;; not found
        (- i)))) ;; found, return match past index
@result{} 10
@end example

@noindent
if the pattern was not found: Return @false{}.  If the pattern was
found: Return the index in the string of the character past the one that
matched the end of the pattern.

Generalising this to a generic source of strings (represented by a list
of strings):

@smallexample
(let* ((strings         '("ciao h " "he hel h"
                          "ell hel" "lo salut"))
       (end-of-data?    (lambda ()
                          (null? strings)))
       (get-next-chunk  (lambda ()
                          (begin0
                              (car strings)
                            (set! strings (cdr strings))))))

  (let* ((pattern         "hello")
         (pattern-start   0)
         (pattern-past    (string-length pattern))
         (restart-vector  (%kmp-make-restart-vector char=?
                             pattern pattern-start pattern-past)))

    (let loop ((pi 0))
      (and (not (end-of-data?))          ; not found
           (let* ((buf (get-next-chunk))
                  (pi  (%kmp-string-partial-search
                          char=? restart-vector pi
                          buf 0 (string-length buf)
                          pattern pattern-start)))
             (if (< pi 0)
                 (cons buf (- pi)) ; found
               (loop pi)))))))
@result{} ("lo salut" . 2)
@end smallexample

@noindent
if the pattern was not found: Return @false{}.  If the pattern was
found: Return a cons whose car is the string chunk holding the end of
the text that matched, and whose cdr is the index in the chunk of the
character past the one that matched the end of the pattern.

@c page
@node strings kmp string
@subsubsection Full string search


@defun %kmp-search @var{char=} @var{text} @var{text-start} @var{text-past} @var{pattern} @var{pattern-start} @var{pattern-past}
Search the selected substring of @var{text} for the selected substring
of @var{pattern}.  Return the index of the character in @var{text} past
the one that matched the last character in the pattern.

@var{char=} is the character--equality function used to construct the
restart vector, typically @func{char=?} or @func{char-ci=?}.

This function is like @func{%string-contains} and
@func{%string-contains-ci}, in facts those are implemented as:

@smallexample
(define (%string-contains text text-beg text-past
                          pattern pattern-beg pattern-past)
  (%kmp-search char=?
               text text-beg text-past
               pattern pattern-beg pattern-past))

(define (%string-contains-ci text text-beg text-past
                             pattern pattern-beg pattern-past)
  (%kmp-search char-ci=?
               text text-beg text-past
               pattern pattern-beg pattern-past))
@end smallexample

@noindent
@func{%kmp-search} exposes the selection of a custom @var{char=}
comparison function.
@end defun

@c page
@node vectors
@section Vectors library


The @library{vectors} library extends vector operations available in the
base library (@pxref{baselib vectors, Vectors}).

@menu
* vectors intro::               Introduction.
* vectors view::                Views over vectors.
* vectors cons::                Constructors.
* vectors pred::                Predicates.
* vectors map::                 Mapping functions.
* vectors fold::                Folding and unfolding.
* vectors search::              Searching.
* vectors mutators::            Mutators.
* vectors conv::                Conversion.
@end menu

@c page
@node vectors intro
@subsection Introduction


In this section containing specifications of procedures, the following
notation is used to specify parameters and return values:

@table @code
@item (f arg1 arg2 ) -> something
Indicates a function @var{f} takes the parameters @var{arg1},
@var{arg2}, @dots{} and returns a value of the type @var{something}. If
@var{something} is unspecified, then what @var{f} returns is
implementation--dependant; this @srfi{} does not specify what it
returns, and in order to write portable code, the return value should be
ignored.

@item vec
The argument in this place must be a vector, i.e. it must satisfy the
predicate @func{vector?}.

@item i, j, start, size
The argument in this place must be a nonnegative integer, i.e. it must
satisfy the predicates @func{integer?} and either @func{zero?} or
@func{positive?}.  The third case of it indicates the index at which
traversal begins; the fourth case of it indicates the size of a vector.

@item end
The argument in this place must be a positive integer, i.e. it must
satisfy the predicates @func{integer?} and @func{positive?}.  This
indicates the index directly before which traversal will stop;
processing will occur until the the index of the vector is end.  It is
the closed right side of a range.

@item f
The argument in this place must be a function of one or more arguments,
returning exactly one value.

@item pred?
The argument in this place must be a function of one or more arguments
that returns one value, which is treated as a boolean.

@item x, y, z, seed, knil, fill, key, value
The argument in this place may be any Scheme value.

@item [something]
Indicates that @var{something} is an optional argument; it needn't
necessarily be applied.  Something needn't necessarily be one thing; for
example, this usage of it is perfectly valid:

@example
[start [end]]
@end example

@noindent
and is indeed used quite often.

@item something 
Indicates that zero or more somethings are allowed to be arguments.

@item something1 something2 
Indicates that at least one something must be arguments.

@item something1 something2  somethingn
Exactly equivalent to the previous argument notation, but this also
indicates that @var{n} will be used later in the procedure description.
@end table

It should be noted that all of the procedures that iterate across
multiple vectors in parallel stop iterating and produce the final result
when the end of the shortest vector is reached.  The sole exception is
@func{vector=}, which automatically returns @false{} if the vectors'
lengths vary.

@c page
@node vectors view
@subsection Views over vectors


Vector views are syntactic sugar to allow subvector specification with
minimum overhead.  Some low level vector functions act on subvectors
specified with triplets of arguments:

@itemize
@item
The full vector.

@item
The @dfn{start index}: The index of the first value included in the
selected subvector.

@item
The @dfn{past index}: The index of the value past the last value
included in the selected subvector.
@end itemize

To select the subvector @code{#(3 4 5)} from the vector @code{#(1 2 3 4
5 6 7)} we have to determine the half--open range of values, which is
@math{[3, 6)}, then apply a function to the arguments:

@example
(%the-function #(0 1 2 3 4 5 6 7) 3 6)
@end example

@noindent
while to select the whole vector we can do:

@example
(%the-function #(0 1 2 3 4 5 6 7) 0 8)
@end example

@noindent
or:

@example
(let ((vec #(0 1 2 3 4 5 6 7)))
  (%the-function vec 0 (vector-length vec)))
@end example

With the vector views implemented by @library{vectors}, the low level
function is wrapped by a high level syntax which can be invoked as:

@example
(the-function #(0 1 2 3 4 5 6 7))
        ; select the whole vector

(the-function (#(0 1 2 3 4 5 6 7)))
        ; select the whole vector

(the-function (#(0 1 2 3 4 5 6 7) 6))
        ; select the subvector [6, 8)

(the-function (#(0 1 2 3 4 5 6 7) 3 6))
        ; select the subvector [3, 6)
@end example

@noindent
additionally the following verbose syntaxes are supported:

@example
(the-function (view #(0 1 2 3 4 5 6 7)))
        ; select the whole vector

(the-function (view #(0 1 2 3 4 5 6 7)
                (start 5)))
        ; select the subvector [5, 8)

(the-function (view #(0 1 2 3 4 5 6 7)
                (start 3)
                (past 6)))
        ; select the subvector [3, 6)

(the-function (view #(0 1 2 3 4 5 6 7)
                (past 6)))
        ; select the subvector [0, 6)
@end example

@noindent
instead of a triplet of arguments, the vector view is a single argument
that can be:

@table @code
@item @meta{vector}
@itemx (@meta{vector})
@itemx (view @meta{vector})
The vector itself or an unquoted list holding the vector itself,
optionally prefixed by the @code{view} auxiliary syntax: It selects the
whole vector.

@item (@meta{vector} @meta{start-index})
@itemx (view @meta{vector} (start @meta{start-index}))
An unquoted list holding the vector and the start index, optionally with
auxiliary syntaxes @code{view} and @code{start}: It selects the
subvector from the start index to the end.

@item (view @meta{vector} (past @meta{past-index}))
An unquoted list holding the vector and the past index, with auxiliary
syntaxes @code{view} and @code{past}: It selects the subvector from zero
to the selected past index.

@item (@meta{vector} @meta{start-index} @meta{past-index})
@itemx (view @meta{vector} (start @meta{start-index}) (past @meta{past-index}))
An unquoted list holding the vector, the start index and the past index,
optionally with auxiliary syntaxes @code{view}, @code{start} and
@code{past}: It selects the subvector between the start and past
indexes.
@end table

@noindent
@meta{vector}, @meta{start-index} and @meta{past-index} can be arbitrary
Scheme expressions.  High level macros accepting two or more subvectors
as arguments support vector views for all of them.  When we are
concerned with the overhead of vector views, we can use the low level
functions directly.

In the following documentation: Low level functions and high level
macros are documented together; only the meaning of arguments to the low
level function are described, the meaning of high level arguments is
obvious.

@c page
@node vectors cons
@subsection Constructors


@deffn Function %vector-copy @var{fill} @var{vec} @var{start} @var{past}
@deffnx Macro vector-copy @var{V}
@deffnx Macro vector-copy @var{V} @var{fill}
Allocate a new vector and fill it with elements from the selected
subvector of @var{vec}.  If @var{past} extends beyond the length of
@var{vec}, the slots in the new vector that cannot be filled by elements
from @var{vec} are filled with @var{fill}, whose default value is
@false{}.

@example
(vector-copy '#(a b c d e f g h i))
@result{} #(a b c d e f g h i)

(vector-copy ('#(a b c d e f g h i) 6))
@result{} #(g h i)

(vector-copy ('#(a b c d e f g h i) 3 6))
@result{} #(d e f)

(vector-copy ('#(a b c d e f g h i) 6 12) 'x)
@result{} #(g h i x x x)
@end example
@end deffn


@deffn Function %vector-reverse-copy @var{vec} @var{start} @var{past}
@deffnx Macro vector-reverse-copy @var{V}
Like @func{vector-copy}, but copy the elements in the reverse order from
the selected subvector of @var{vec}.

@example
(vector-reverse-copy ('#(5 4 3 2 1 0) 1 5))
@result{} #(1 2 3 4)
@end example
@end deffn


@defun Function vector-append @var{vec} ...
Return a newly allocated vector that contains all elements in order from
the subsequent locations in the vector arguments.

@example
(vector-append '#(x) '#(y))
@result{} #(x y)

(vector-append '#(a) '#(b c d))
@result{} #(a b c d)

(vector-append '#(a #(b)) '#(#(c)))
@result{} #(a #(b) #(c))
@end example
@end defun


@defun vector-concatenate @var{vectors-list}
Append each vector in @var{vectors-list}.

@example
(vector-concatenate '(#(a b) #(c d)))
@result{} #(a b c d)
@end example
@end defun

@c page
@node vectors pred
@subsection Predicates


@deffn Function %vector-empty? @var{vec} @var{start} @var{past}
@deffnx Macro vector-empty? @var{V}
Return @true{} if the selected subvector of @var{vec} is empty.

@example
(vector-empty? '#(a))
@result{} #f

(vector-empty? '#(()))
@result{} #f

(vector-empty? '#(#()))
@result{} #f

(vector-empty? '#())
@result{} #t
@end example
@end deffn


@deffn Function %vector= @var{item=?} @var{vec1} @var{start1} @var{past1} @var{vec2} @var{start2} @var{past2}
@deffnx Macro vector= @var{item=?} @var{V1} @var{V2}
Compare the two subvectors and return true if their elements are equal
according to @var{item=?}.  The vectors must be of equal length.

The @var{item=?} predicate must be consistent with @func{eq?}: If
@func{eq?} would return true when applied to elements, @var{item=?}
must return true when applied to the same elements.

@example
(vector= eq? '#(a b c d) '#(a b c d))
@result{} #t

(vector= eq? '#(a b c d) '#(a b d c))
@result{} #f

(vector= = '#(1 2 3 4 5) '#(1 2 3 4))
@result{} #f

(vector= = '#(1 2 3 4) '#(1 2 3 4))
@result{} #t
@end example
@end deffn

@c page
@node vectors map
@subsection Mapping functions


@defun vector-map* @var{proc} @var{vec0} @var{vec} ...
Construct a new vector of the shortest size of the vector arguments;
this is an extension of the @func{vector-map} function.  Each element at
index @var{i} of the new vector is mapped from the old vectors by:

@example
(@var{proc} @var{i}
  (vector-ref @var{vec0} @var{i})
  (vector-ref @var{vec} @var{i})
  )
@end example

The dynamic order of application of @var{proc} is unspecified.

@example
(vector-map (lambda (i x) (* x x))
              (vector-unfold (lambda (i x)
                               (values x (+ x 1)))

@result{} #(1 4 9 16)

(vector-map (lambda (i x y) (* x y))
              (vector-unfold (lambda (i x)
                               (values x (+ x 1)))
                             5 1)
              (vector-unfold (lambda (i x)
                               (values x (- x 1)))
                             5 5))
@result{} #(5 8 9 8 5)

(let ((count 0))
  (vector-map (lambda (ignored-index ignored-elt)
                (set! count (+ count 1))
                count)
              '#(a b)))
@result{} #(1 2) OR #(2 1)

(vector-map (lambda (i elt)
              (+ i elt))
            '#(1 2 3 4))
@result{} #(1 3 5 7)
@end example
@end defun

@c ------------------------------------------------------------

@defun vector-map*! @var{proc} @var{vec0} @var{vec} ...
Similar to @func{vector-map*}, but rather than mapping the new elements
into a new vector, the new mapped elements are destructively inserted
into @var{vec0}.

The dynamic order of application of @var{proc} unspecified, so it is
dangerous for @var{proc} to apply either @func{vector-ref} or
@func{vector-set!} to @var{vec0} in @var{proc}.
@end defun

@c ------------------------------------------------------------

@defun vector-for-each* @var{proc} @var{vec0} @var{vec} ...
Simple vector iterator: apply @var{proc} to each index in the range
@math{[0, L)} (where @math{L} is the length of the smallest vector
argument passed) and the respective list of parallel elements from
@var{vec0}, @var{vec}, @dots{} at that index.

In contrast with @func{vector-map}, @var{proc} is reliably applied to
each subsequent elements, starting at index 0, in the vectors.

Example:

@example
(vector-for-each (lambda (i x)
                   (display x)
                   (newline))
  '#("foo" "bar" "baz" "quux" "zot"))
@print{} foo
@print{} bar
@print{} baz
@print{} quux
@print{} zot
@end example
@end defun

@c page
@node vectors fold
@subsection Folding and unfolding


@defun vector-fold @var{kons} @var{knil} @var{vec0} @var{vec} ...
The fundamental vector iterator.  @var{kons} is iterated left--to--right
over each index in all of the vectors, stopping at the end of the
shortest; @var{kons} is applied as:

@example
(@var{kons} @var{i} @var{state}
  (vector-ref @var{vec0} @var{i})
  (vector-ref @var{vec} @var{i})
  )
@end example

@noindent
where @var{state} is the current state value; the current state value
begins with @var{knil}, and becomes whatever @var{kons} returned at the
respective iteration; and @var{i} is the current index.

@example
;; Find the longest string's length in vector-of-strings.
(vector-fold (lambda (index len str)
               (max (string-length str) len))
             0 vector-of-strings)

;; Produce a list of the reversed elements of vec.
(vector-fold (lambda (index tail elt)
               (cons elt tail))
             '() vec)

;; Count the number of even numbers in vec.
(vector-fold (lambda (index counter n)
               (if (even? n) (+ counter 1) counter))
             0 vec)
@end example
@end defun

@c ------------------------------------------------------------

@defun vector-fold-right @var{kons} @var{knil} @var{vec0} @var{vec} ...
Similar to @func{vector-fold}, but it iterates right--to--left.

@example
;; Convert a vector to a list.
(vector-fold-right (lambda (index tail elt)
                     (cons elt tail))
                   '() '#(a b c d))
@result{} (a b c d)
@end example
@end defun

@c ------------------------------------------------------------

@defun vector-unfold @var{stop?} @var{seed->char} @var{make-seed} @var{first-seed}
@defunx vector-unfold @var{stop?} @var{seed->char} @var{make-seed} @var{first-seed} @var{base-vector}
@defunx vector-unfold @var{stop?} @var{seed->char} @var{make-seed} @var{first-seed} @var{base-vector} @var{make-final}
This is a fundamental constructor for vectors.  Arguments description
follows.

@table @var
@item make-seed
A map function used to generate a series of ``seed'' values from the
initial seed:

@example
@var{first-seed}
(@var{make-seed} @var{first-seed})            @result{} seed2
(@var{make-seed} seed2)                 @result{} seed3
(@var{make-seed} seed3)                 @result{} seed4
...
@end example

@item stop?
A predicate function telling when to stop generating characters; when it
returns true when applied to one of the seed values.

@item seed->char
Map function mapping each seed value to the corresponding character in
the result vector.  These chars are assembled into the vector in a
left--to--right order.

@item base
An optional vector which is used as initial/leftmost portion of the
constructed vector.  Defaults to the empty vector.

@item make-final
Optional function applied to the terminal seed value (on which
@var{stop?}  returns true) to produce the final/rightmost portion of the
constructed vector.  Defaults to @code{(lambda (x) "")}.
@end table

More precisely, the following (simple, inefficient) definitions hold:

@example
;; iterative
(define (vector-unfold stop? seed->char make-seed
                       first-seed base-vector make-final)
  (let loop ((seed    first-seed)
             (result  base-vector))
    (if (stop? seed)
        (vector-append result (make-final seed))
      (loop (make-seed seed)
            (vector-append result
                           (vector (seed->char seed)))))))

;; recursive
(define (vector-unfold stop? seed->char make-seed
                       first-seed base-vector make-final)
  (vector-append
     base-vector
     (let loop ((seed first-seed))
       (if (stop? seed)
           (make-final seed)
         (vector-append (vector (seed->char seed))
                        (loop (make-seed seed)))))))
@end example

This function is a fairly powerful vector constructor; we can use it to
convert a list to a vector, read a port into a vector, reverse a vector,
copy a vector, and so forth.  Examples:

@example
(port->vector p) = (vector-unfold eof-object? values
                                  (lambda (x) (read-char p))
                                  (read-char p))

(list->vector lis) = (vector-unfold null? car cdr lis)

(vector-tabulate f size) = (vector-unfold (lambda (i)
                                            (= i size))
                                          f add1 0)
@end example

@noindent
to map @var{seed->char} over a list @var{lis}, producing a vector:

@example
(vector-unfold null? (compose f car) cdr lis)
@end example

Interested functional programmers may enjoy noting that
@func{vector-fold-right} and @func{vector-unfold} are in some sense
inverses.  That is, given operations @var{knull?}, @var{kar}, @var{kdr},
@var{kons}, and @var{knil} satisfying:

@example
(kons (kar x) (kdr x)) @result{} x
(knull? knil) @result{} #t
@end example

@noindent
then:

@example
(vector-fold-right kons knil
                   (vector-unfold knull? kar kdr x))
@result{} x
@end example

@noindent
and:

@example
(vector-unfold knull? kar kdr
               (vector-fold-right kons knil s))
@result{} s
@end example

The final vector constructed does not share storage with either
@var{base-vector} or the value produced by @var{make-final}.
@end defun

@c ------------------------------------------------------------

@defun vector-unfold-right @var{stop?} @var{seed->char} @var{make-seed} @var{first-seed}
@defunx vector-unfold-right @var{stop?} @var{seed->char} @var{make-seed} @var{first-seed} @var{base-vector}
@defunx vector-unfold-right @var{stop?} @var{seed->char} @var{make-seed} @var{first-seed} @var{base-vector} @var{make-final}
This is a fundamental constructor for vectors.  The arguments are like
the ones of @func{vector-unfold}.  The difference from
@func{vector-unfold} is that this function builds the list from right to
left; more precisely, the following (simple, inefficient) definitions
hold:

@example
;; iterative
(define (vector-unfold-right
           stop? seed->char make-seed
           first-seed base-vector make-final)
  (let lp ((seed    first-seed)
           (result  base))
    (if (stop? seed)
        (vector-append (make-final seed) result)
      (loop (make-seed seed)
            (vector-append (vector (seed->char seed))
                           result)))))

;; recursive
(define (vector-unfold-right
           stop? seed->char make-seed
           first-seed base-vector make-final)
  (vector-append
     (let loop ((seed first-seed))
       (if (stop? seed)
           (make-final seed)
         (vector-append (loop (make-seed seed))
                        (vector (seed->char seed)))))
     base))
@end example

Interested functional programmers may enjoy noting that
@func{vector-fold} and @func{vector-unfold-right} are in some sense
inverses.  That is, given operations @var{knull?}, @var{kar}, @var{kdr},
@var{kons}, and @var{knil} satisfying:

@example
(kons (kar x) (kdr x)) @result{} x
(knull? knil) @result{} #t
@end example

@noindent
then:

@example
(vector-fold kons knil
             (vector-unfold-right knull? kar kdr x))
@result{} x
@end example

@noindent
and:

@example
(vector-unfold-right knull? kar kdr
                     (vector-fold kons knil s))
@result{} s
@end example

The final vector constructed does not share storage with either
@var{base-vector} or the value produced by @func{make-final}.
@end defun

@c ------------------------------------------------------------

@defun vector-tabulate @var{integer->char} @var{len}
Construct a vector of size @var{len} by applying @var{integer->char} to
each index in the range @math{[0, @var{len})} to produce the
corresponding vector element.  The order in which @var{integer->char} is
applied to the indices is not specified.
@end defun

@c page
@node vectors search
@subsection Searching


@defun vector-index @var{pred?} @var{vec0} @var{vec} ...
Find and return the index of the first elements in @var{vec0},
@var{vec}, @dots{} that satisfy @var{pred?}.  If no matching element is
found by the end of the shortest vector: Return @false{}.

@example
(vector-index even? '#(3 1 4 1 5 9))
@result{} 2

(vector-index < '#(3 1 4 1 5 9 2 5 6) '#(2 7 1 8 2))
@result{} 1

(vector-index = '#(3 1 4 1 5 9 2 5 6) '#(2 7 1 8 2))
@result{} #f
@end example
@end defun


@defun vector-index-right @var{pred?} @var{vec0} @var{vec} ...
Like @func{vector-index}, but it searches right--to--left and all of the
vectors must have the same length.
@end defun


@defun vector-skip @var{pred?} @var{vec0} @var{vec} ...
Find and return the index of the first elements in @var{vec0},
@var{vec}, @dots{} that do not satisfy @var{pred?}.  If all the values
in the vectors satisfy @var{pred?} until the end of the shortest vector:
Return @false{}.

@example
(vector-skip number? '#(1 2 a b 3 4 c d))
@result{} 2
@end example
@end defun


@defun vector-skip-right @var{pred?} @var{vec0} @var{vec} ...
Like @func{vector-skip}, but it searches for a non--matching element
right--to--left and all of the vectors must have the same length.
@end defun


@deffn Function %vector-binary-search @var{value} @var{cmp} @var{vec} @var{start} @var{past}
@deffnx Macro vector-binary-search @var{V} @var{value} @var{cmp}
Similar to @func{vector-index} and @func{vector-index-right}, but
instead of searching left to right or right to left, this performs a
binary search.  @var{cmp} should be a procedure of two arguments and
return:

@itemize
@item
A negative integer, indicating that its first argument is less than its
second.

@item
Zero, indicating that they are equal.

@item
A positive integer, indicating that the first argument is greater than
the second argument.
@end itemize

An example @var{cmp} might be:

@example
(lambda (char1 char2)
  (cond ((char<? char1 char2) -1)
        ((char=? char1 char2)  0)
        (else                  1)))
@end example
@end defun


@defun vector-any @var{pred?} @var{vec0} @var{vec} ...
Find the first set of elements in parallel from @var{vec0}, @var{vec},
@dots{} for which @var{pred?} returns a true value.  If such a parallel
set of elements exists: Return the value that @var{pred?}  returned for
that set of elements.  The iteration is strictly left--to--right.
@end defun


@defun vector-every @var{pred?} @var{vec0} @var{vec} ...
If, for every index @math{i} between @math{0} and the length of the
shortest vector argument, the set of elements:

@example
(vector-ref @var{vec0} i)
(vector-ref @var{vec} i)
 
@end example

@noindent
satisfies @var{pred?}: Return the value that @var{pred?}  returned for
the last set of elements, at the last index of the shortest vector.  The
iteration is strictly left--to--right.
@end defun


@defun vector-count @var{pred?} @var{vec0} @var{vec} ...
Count the number of parallel elements in the vectors that satisfy
@var{pred?}, which is applied (for each index @math{i} in the range
@math{[0, L)}, where @math{L} is the length of the smallest vector
argument) to @math{i} and each parallel element in the vectors at that
index, in order.

@example
(vector-count (lambda (i elt)
                (even? elt))
              '#(3 1 4 1 5 9 2 5 6))
@result{} 3

(vector-count (lambda (i x y)
                (< x y))
              '#(1 3 6 9)
              '#(2 4 6 8 10 12))
@result{} 2
@end example
@end defun

@c page
@node vectors mutators
@subsection Mutators


@defun vector-swap! @var{vec} @var{i} @var{j}
Swap or exchange the values of the locations in @var{vec} at @var{i} and
@var{j}.
@end defun


@deffn Function %vector-fill*! @var{fill} @var{vec} @var{start} @var{past}
@deffnx Macro vector-fill*! @var{V} @var{fill}
Store @var{fill} in every location of the selected subvector of
@var{vec}.
@end deffn


@deffn Function %vector-reverse! @var{vec} @var{start} @var{past}
@deffnx Macro vector-reverse! @var{V}
Destructively reverse the contents of the selected subvector of
@var{vec}.  Notice that this function does not deeply reverse.
@end deffn


@deffn Function %vector-copy! @var{vec-dst} @var{dst-start} @var{vec-src} @var{src-start} @var{src-past}
@deffnx Macro vector-copy! @var{V-dst} @var{V-src}
Copy the selected subvector of @var{vec-src} into @var{vec-dst} starting
at @var{dst-start}.  This function supports copying over the same
vector.

When using the vector view: The past index of the destination vector is
ignored.
@end deffn


@deffn Function %vector-reverse-copy! @var{dst-vec} @var{dst-start} @var{src-vec} @var{src-start} @var{src-past}
@deffnx Macro vector-reverse-copy! @var{V-dst} @var{V-src}
Like @func{vector-copy!}, but this copies the elements in the reverse
order.  This function supports copying over the same vector.
@end deffn

@c page
@node vectors conv
@subsection Conversion


@deffn Function %vector->list* @var{vec} @var{start} @var{past}
@deffnx Macro vector->list* @var{V}
Create a list containing the elements of the selected subvector of
@var{vec}.
@end deffn


@deffn Function %reverse-vector->list @var{vec} @var{start} @var{end}
@deffnx Macro reverse-vector->list @var{vec} @var{start} @var{end}
Like @func{vector->list}, but the resulting list contains the elements
in reverse order.
@end deffn


@defun reverse-list->vector @var{proper-list}
Like @func{list->vector}, but the resulting list contains the elements
of @var{proper-list} in reverse order.
@end defun

@c page
@node checks
@section Lightweight testing


The @library{checks} library provides a simple mechanism for testing
Scheme programs.  As a most primitive example, the expression:

@example
(check (+ 1 1) => 3)
@end example

@noindent
evaluates the expression @code{(+ 1 1)} and compares the result with the
expected result @code{3} provided after the syntactic keyword @code{=>}.
Then the outcome of this comparison is reported in human--readable form
by printing a message of the form:

@example
(+ 1 1) @result{} 2 ; *** failed ***
; expected result: 3
@end example


@menu
* checks select::               Selecting tests to be run.
* checks eval::                 Evaluating code.
* checks result::               Handling multiple results.
* checks debug::                Printing debug messages.
@end menu


@c page
@node checks select
@subsection Selecting tests to be run


As usage example, let's assume that the system environment variable
@env{CHECK_TEST_NAME} is set to @code{yellow}:

@example
(parameterise ((check-test-name 'yellow))

  ;; this is evaluated
  (check
      (do-something)
    => 123)

  ;; this is evaluated, too
  (check
      (do-some-other-thing)
    => 456))

(parameterise ((check-test-name 'blue))

  ;; this is NOT evaluated
  (check
      (do-something)
    => #t)

  ;; this is NOT evaluated
  (check
      (do-some-other-thing)
    => #f))

;; this is evaluated
(check "yellow-one"
    (do-something)
  => 123)

;; this is NOT evaluated
(check 'red
    (do-something)
  => #t)
@end example


@defvr {Environment Variable} CHECK_TEST_NAME
The system environment variable used to select tests in a test suite
file.  The value of the variable must be the name, or the prefix of the
name, or the suffix of the name, of one or more tests.
@end defvr


@deffn Parameter check-test-name
Holds the name of the current test or tests.  Must be set to @false{}, a
string or a symbol.
@end deffn


@c page
@node checks eval
@subsection Evaluating code


@deffn Syntax check @var{expr} (=> @var{equal}) @var{expected}
@deffnx Syntax check @var{expr} => @var{expected}
@deffnx Syntax check @var{name} @var{expr} (=> @var{equal}) @var{expected}
@deffnx Syntax check @var{name} @var{expr} => @var{expected}
Evaluate @var{expr} and compare the value to the value of @var{expected}
using the predicate @var{equal}, which is @func{equal?} when omitted.
Then a report is printed according to the current mode setting (see
below) and the outcome is recorded in a global state to be used in
@func{check-report}.

The precise order of evaluation is that first @var{equal} and
@var{expected} are evaluated (in unspecified order) and then @var{expr}
is evaluated.  Example: @code{(check (+ 1 1) => 2)}.

When @var{name} is given: it is the name of this test, @ref{checks
select, Selecting tests to be run}.
@end deffn


@deffn Syntax check-ec @var{qualifier*} @var{expr} (=> @var{equal}) @var{expected} (@var{argument*})
@deffnx Syntax check-ec @var{qualifier*} @var{expr} => @var{expected} (@var{argument*})
@deffnx Syntax check-ec @var{qualifier*} @var{expr} (=> @var{equal}) @var{expected}
@deffnx Syntax check-ec @var{qualifier*} @var{expr} => @var{expected}
An eager comprehension for executing a parametric sequence of checks.
To use this syntax we have to import the @library{loops} library.

Enumerate the sequence of bindings specified by @var{qualifier*}.  For
each binding evaluate @var{equal} and @var{expected} in unspecified
order. Then evaluate @var{expr} and compare the value obtained to the
value of @var{expected} using the value of @var{equal} as predicate,
which is @func{equal?} when omitted.

The comprehension stops after the first failed check, if there is any.
Then a report is printed according to the current mode setting (see
below) and the outcome is recorded in a global state to be used in
@func{check-report}.  The entire @func{check-ec} counts as a single
check.

In case the check fails @var{argument*} is used for constructing an
informative message with the argument values.  Use @var{argument*} to
list the relevant free variables of @var{expr} (see examples) that you
want to have printed.

A @var{qualifier} is any qualifier of an eager comprehension.

Examples:

@example
(check-ec (: e 100)
          (positive? (expt 2 e))
          => #t (e)) ; fails on fixnums

(check-ec (: e 100)
          (:let x (expt 2.0 e))
          (= (+ x 1) x)
          => #f (x)) ; fails

(check-ec (: x 10)
          (: y 10)
          (: z 10)
          (* x (+ y z))
          => (+ (* x y) (* x z))
             (x y z)) ; passes with 10^3 cases checked
@end example
@end deffn


@defun check-report
Print a summary and the first failed check, if there is any, depending
on the current mode settings.
@end defun


@defun check-set-mode! @var{mode}
Set the current mode to @var{mode}, which must be a symbol among:
@code{off}, @code{summary}, @code{report-failed}, @code{report}; the
default is @code{report}.  Note that you can change the mode at any
time, and that @func{check}, @func{check-ec} and @func{check-report} use
the current value.

The mode symbols have the following meaning:

@table @code
@item off
do not execute any of the checks;

@item summary
print only summary in @func{check-report} and nothing else;

@item report-failed
report failed checks when they happen, and in summary;

@item report
report every example executed.
@end table
@end defun


@defun check-reset!
Reset the global state (counters of correct/failed examples) to the
state immediately after loading the module for the first time, i.e. no
checks have been executed.
@end defun


@defun check-passed? @var{expected-total-count}
Return @true{} if there were no failed checks and
@var{expected-total-count} correct checks, @false{} otherwise.

Rationale: This procedure can be used in automatized tests by
terminating a test program with the statement:

@example
(exit (if (check-passed? n) 0 1))
@end example
@end defun


@deffn Syntax false-if-exception @var{form0} @var{form} ...
Evaluate all the @var{form}s as in @func{begin}.  If an exception is
raised: return @false{}.
@end deffn


@deffn Syntax check-for-true @var{expr}
@deffnx Syntax check-for-true @var{name} @var{expr}
@deffnx Syntax check-for-false @var{expr}
@deffnx Syntax check-for-false @var{name} @var{expr}
Evaluate @var{expr} in a @func{check} form, expecting a non--false or
@false{} value as result.

When @var{name} is given: it is the name of this test, @ref{checks
select, Selecting tests to be run}.
@end deffn

@c page
@node checks result
@subsection Handling multiple results


@deffn Syntax with-result @var{form0} @var{form} ...
Evaluate all the @var{form}s, as in @func{begin}, in a dynamic
environment in which an implicit result variable is defined.  The
implicit variable is handled as a list.

Return a list whose first value is the return value of the last
@var{form} and whose second value is the value of the implicit variable.
@end deffn


@defun add-result @var{value}
Append @var{value} to the list in the implicit variable defined by
@func{with-result}.
@end defun


@defun get-result
Return the current value of the implicit variable defined by
@func{with-result}.
@end defun


@c page
@node checks debug
@subsection Printing debug messages


@deffn Parameter debugging
When set to true @func{debug} will print its message.  It is initialised
to @false{}.
@end deffn


@defun debug @var{string}
@defunx debug @var{format-string} @var{arg} ...
If the @code{debugging} parameter is set to true, print @var{string} to
the current error port, or hand @var{format-string} and the optional
@var{arg} values to @func{format} and print the result to the current
error port.
@end defun


@defun debug-print-condition @var{message} @var{exc}
A wrapper for @func{debug} that prints @var{message} and does its best
to print @var{exc}, which must be a Scheme condition value.
@end defun



@c page
@node one-dimension
@section One dimensional extended ranges


The @library{one-dimension} library implements low level operations over
generalised ranges of value for a coordinate.  It can be used to operate
ranges of numbers or characters or whatever objects upon which we can
define an ordering.  @library{one-dimension} is the backbone of the
@library{char-sets} library.

@menu
* one-dimension intro::         Data types and conventions.
* one-dimension make::          Constructors.
* one-dimension pred::          Predicates.
* one-dimension inspect::       Inspection.
* one-dimension ops::           Operations.
@end menu

@c page
@node one-dimension intro
@subsection Data types and conventions


A @dfn{range} is a pair representing a half--open interval of items.
The car of the pair if the included left--limit, called @var{start}; the
cdr is the excluded right--limit, called @var{past}.  Empty ranges
(pairs with equal values) are @strong{not} valid.

A @dfn{domain} is a sorted list of ranges.  Empty domains are empty
lists.  Ranges in a domain do not overlap and are not contiguous.  A
domain has the following format:

@example
((start1 . past1) (start2 . past2) (start3 . past3) ...)
@end example

@noindent
with the constraints:

@example
start1 < past1 < start2 < past2 < start3 < past3 < ...
@end example

The range and domain functions accept the following arguments:

@table @var
@item obj
Any object.

@item range
A valid range according to @func{%range?}.

@item domain
A valid domain according to @func{%domain?}.

@item start
The included left--limit of a range.

@item past
The excluded right--limit of a range.

@item item?
A unary predicate returning @true{} if the argument is a valid item.

@item item=?
An n--ary predicate returning @true{} if the all the arguments are
equal.  @func{char=?} is an example of such a function.

@item item<?
An n--ary predicate returning @true{} if the all the arguments, in the
given order, are in strict increasing order.  @func{char<?} is an
example of such a function.

@item item<=?
An n--ary predicate returning @true{} if the all the arguments, in the
given order, are in non--strict increasing order.  @func{char<=?} is an
example of such a function.

@item item-?
A binary function returning the number of items between the first
argument (included) and the second argument (excluded).  The arithmetic
@func{-} applied to integers is an example of such a function; for
characters we can use:

@example
(lambda (past start)
  (- (char->integer past)
     (char->integer start)))
@end example

@item item-copy
A unary function returning a copy of an item.  For ranges holding atomic
values (like characters or integers) it is fine to use @code{(lambda (x)
x)}.

@item item-next
A unary function returning the next value with respect to the argument.
For integers it can be @code{(lambda (x) (+ 1 x))}; for characters it
can be:

@example
(lambda (ch) (integer->char (+ 1 (char->integer ch))))
@end example


@item lower-bound
@itemx upper-bound
Values representing the included lower bound and the excluded upper
bounds for the items in ranges and domains.

The lower bound is used as first argument to the @var{item=?},
@var{item<?} and @var{item<=?} predicates.  The upper bound is used as
last argument to the @var{item=?}, @var{item<?} and @var{item<=?}
predicates.
@end table

@c page
@node one-dimension make
@subsection Constructors


@defun %make-range @var{start} @var{past} @var{item?} @var{item<?}
Compose @var{start} and @var{past} into a range.  This function
validates the arguments to make sure the resulting range is valid.
@end defun


@defun %range-copy @var{range} @var{item-copy}
Return a clone of @var{range}.
@end defun


@defun %make-domain @var{item?} @var{item=?} @var{item<?} @var{item<=?} @var{item-next} . @var{args}
Build and return a new domain.

@var{args} may be the empty list or a mixed list of item and pairs of
items.  Pairs of items are interpreted as limits--inclusive ranges of
items (this is in contrast with the half--open representation of ranges
ranges).
@end defun


@defun %domain-copy @var{domain} @var{item-copy}
Return a newly allocated domain holding a copy of @var{domain}.
@end defun


@defun %domain-add-item @var{domain} @var{obj} @var{item?} @var{item=?} @var{item<?} @var{item<=?} @var{item-next}
Add the single value @var{obj} to @var{domain}, return the new domain.
The returned domain may share some structure with @var{domain}.
@end defun


@defun %domain-add-range @var{domain} @var{range} @var{item=?} @var{item<?} @var{item<=?}
Add @var{range} to @var{domain}, return the new domain.  The returned
domain may share some structure with @var{domain}.
@end defun



@c page
@node one-dimension pred
@subsection Predicates


@subsubheading Range predicates


@defun %range? @var{obj} @var{item?} @var{item<?}
Return @true{} if @var{obj} is a valid range.
@end defun


@defun %range-contains? @var{range} @var{obj} @var{item<?} @var{item<=?}
Return @true{} if @var{obj} is a member of @var{range}.
@end defun


@defun %range=? @var{range-a} @var{range-b} @var{item=?}
Return @true{} if the ranges are equal.
@end defun


@defun %range<? @var{range-a} @var{range-b} @var{item<=?}
Return @true{} if @var{range-a} has all members less than all members of
@var{range-b}.
@end defun


@defun %range<=? @var{range-a} @var{range-b} @var{item<=?}
Return @true{} if @var{range-a} has all members less than, or equal to,
all members of @var{range-b}.
@end defun


@defun %range-start<? @var{range-a} @var{range-b} @var{item<?}
Return @true{} if the start of @var{range-a} is less than the start of
@var{range-b}.
@end defun


@defun %range-start<=? @var{range-a} @var{range-b} @var{item<=?}
Return @true{} if the start of @var{range-a} is less than, or equal to,
the start of @var{range-b}.
@end defun


@defun %range-contiguous? @var{range-a} @var{range-b} @var{item=?}
Return @true{} if @var{range-a} and @var{range-b} are contiguous.  It
does not matter which range has start less than the other.
@end defun


@defun %range-overlapping? @var{range-a} @var{range-b} @var{item<?} @var{item<=?}
Return @true{} if the ranges have some elements in common.
@end defun


@defun %range-subset? @var{range-a} @var{range-b} @var{item<=?}
@defunx %range-strict-subset? @var{range-a} @var{range-b} @var{item<=?}
Return @true{} if @var{range-b} is a subset or a strict subset of
@var{range-a}.
@end defun

@c ------------------------------------------------------------

@subsubheading Domain predicates


@defun %domain? @var{domain} @var{item<?} @var{item<=?} @var{lower-bound} @var{upper-bound}
Return true if @var{domain} is a valid domain.
@end defun


@defun %domain-empty? @var{domain}
Return true if the domain is empty.
@end defun


@defun %domain-contains? @var{domain} @var{obj} @var{item<?} @var{item<=?}
Return true if @var{obj} is an element of @var{domain}.
@end defun


@defun %domain=? @var{domain-a} @var{domain-b} @var{item=?}
Return true if the arguments represent the same domain.
@end defun


@defun %domain<? @var{domain-a} @var{domain-b} @var{item<=?}
Return true if all the elements of @var{domain-a} are strictly less than
all the elements of @var{domain-b}.  Empty domains cannot be ordered, so
if an argument is empty the return value is @false{}.
@end defun


@defun %domain-subset? @var{domain-a} @var{domain-b} @var{item<=?}
@defunx %domain-strict-subset? @var{domain-a} @var{domain-b} @var{item<=?}
Return @true{} if @var{domain-b} is a subset or strict subset of
@var{domain-a}.
@end defun

@c page
@node one-dimension inspect
@subsection Inspection


@defun %range-length @var{range} @var{item-}
Return the number of items in the range.
@end defun


@defun %domain-size @var{domain} @var{item-}
Return the number of items in the domain.
@end defun

@c page
@node one-dimension ops
@subsection Operations


@subsubheading Set operations


@defun %range-intersection @var{range-a} @var{range-b} @var{item<?} @var{item<=?}
Return a range representing the intersection of the argument ranges.  It
does not matter which range has start less than the other.

Intersection is a closed operation on the space of ranges: The
intersection of two ranges is a range (possibly empty).  If the ranges
are not overlapping return @false{} to represent the empty range.
@end defun


@defun %range-union @var{range-a} @var{range-b} @var{item=?} @var{item<?} @var{item<=?}
Return two values representing the union of the argument ranges.  It
does not matter which range has start less than the other.

Union is @strong{not} a closed operation on the space of ranges: The
union of two contiguous or overlapping ranges is a single range, but the
union of two disjoint ranges is the set holding the two argument ranges.

If the argument ranges are disjoint and non--contiguous: Return two
values being @var{range-a} and @var{range-b}.  If the argument ranges
are contiguous or overlapping: Return two values being @false{} and a
new range representing the union.

This function should be called like this:

@example
(let-values (((head tail) (%range-union r-a r-b)))
  (when head ---)
  (when tail ---))
@end example
@end defun


@defun %range-difference @var{range-a} @var{range-b} @var{item=?} @var{item<?}
Return two values representing the difference between the argument
ranges; the difference is the set of elements present in one range and
not in the other.  It does not matter which range has start less than
the other.

Difference is @strong{not} a closed operation on the space of ranges: In
genreral the result is a couple of ranges.  The first one represents
values which are all less than the values in the second one.  Both the
returned values can be @false{}.

This function should be called like this:

@example
(let-values (((head tail) (%range-difference r-a r-b)))
  (when head ---)
  (when tail ---))
@end example
@end defun


@defun %range-in-first-only @var{range-a} @var{range-b} @var{item<?} @var{item<=?}
Return two values representing the elements of @var{range-a} that are
not in @var{range-b}.

This operation is @strong{not} a closed operation on the space of
ranges: In genreral the result is a couple of ranges.  The first one
represents values which are all less than the values in the second one.
Both the returned values can be @false{}.

This function should be called like this:

@example
(let-values (((head tail) (%range-in-first-only r-a r-b)))
  (when head ---)
  (when tail ---))
@end example
@end defun


@defun %domain-intersection @var{domain-a} @var{domain-b} @var{item=?} @var{item<?} @var{item<=?}
Return a new domain representing the intersection of the arguments.  The
intersection is the set of values present in both the arguments.

The returned value may share some structure with the arguments.
@end defun


@defun %domain-union @var{domain-a} @var{domain-b} @var{item=?} @var{item<?} @var{item<=?}
Return a new domain representing the union of the arguments.  The union
is the set of values present in one and/or the other argument.

The returned value may share some structure with the arguments.
@end defun


@defun %domain-difference @var{domain-a} @var{domain-b} @var{item=?} @var{item<?} @var{item<=?}
Return a new domain representing the difference of the arguments.  The
difference is the set of values present in one or the other argument.

The returned value may share some structure with the arguments.
@end defun


@defun %domain-complement @var{domain} @var{domain-universe} @var{item=?} @var{item<?} @var{item<=?}
Return a new domain representing the complement of @var{domain} in the
space defined by @var{domain-universe}.  The returned value holds all
the items from @var{domain-universe} that are not in @var{domain}.

The returned value may share some structure with the arguments.
@end defun

@c ------------------------------------------------------------

@subsubheading List operations


@defun %range-for-each @var{proc} @var{range} @var{item<=?} @var{item-next}
Apply @var{proc} to each value in the range.
@end defun


@defun %range-every @var{proc} @var{range} @var{item<?} @var{item-next}
Apply @var{proc} to each value in the range and return true if all the
return values are true.  The application stops at the first @false{}
return value.
@end defun


@defun %range-any @var{proc} @var{range} @var{item<?} @var{item-next}
Apply @var{proc} to each value in the range and return true if at least
one of the returned values is true.  The application stops at the first
true return value.
@end defun


@defun %range-fold @var{kons} @var{knil} @var{range} @var{item<?} @var{item-next}
Fold @var{kons} over the values in the range.
@end defun


@defun %range->list @var{range} @var{item<?} @var{item-next}
Return a list holding all the elements in the range.
@end defun


@defun %domain-for-each @var{proc} @var{domain} @var{item<?} @var{item-next}
Apply @var{proc} to each element of @var{domain}.
@end defun


@defun %domain-every @var{proc} @var{domain} @var{item<?} @var{item-next}
Apply @var{proc} to each value in the domain and return true if all the
return values are true.  The application stops at the first @false{}
return value.
@end defun


@defun %domain-any @var{proc} @var{domain} @var{item<?} @var{item-next}
Apply @var{proc} to each value in the domain and return true if at least
one of the returned values is true.  The application stops at the first
true return value.
@end defun


@defun %domain-fold @var{kons} @var{knil} @var{domain} @var{item<?} @var{item-next}
Fold @var{kons} over the values in the domain.
@end defun


@defun %domain->list @var{domain} @var{item<?} @var{item-next}
Return a list holding all the elements in the domain.
@end defun

@c ------------------------------------------------------------

@subsubheading Miscellaneous operations


@defun %range-concatenate @var{range-a} @var{range-b} @var{item<?}
Concatenate the ranges and return the resulting range.  It does not
matter which range has start less than the other.  It makes sense to
apply this function to ranges that satisfy @func{%range-contiguous?}.

The returned range may share some value with the original ranges.
@end defun

@c page
@node foreign
@chapter Foreign functions interface




@c page
@node binfmt
@appendix Launching programs directly


This appendix describes an optional feature of @value{PACKAGE} that
activates Scheme programs execution through the mechanism of the
@code{binfmt_misc} Linux kernel module.  It is available only on
Unix--like platforms running the Linux kernel.

By default the installation of the required files is disabled; to enable
it: at configuration time we must pass the @option{--binfmt} option to
the @command{configure} script.

The mechanism allows to run Scheme programs without explicitly invoking
the executable runtime program of the selected Scheme implementation.
It is a generalisation of the mechanism that runs shell scripts whose
file begins with @code{#!/bin/sh}.

@quotation
This appendix is derived from the original @code{binfmt_misc}
documentation text file by Richard Guenther.
@end quotation

@menu
* binfmt overview::             Overview of the mechanism.
* binfmt loading::              Loading the module.
* binfmt registry::             The registry.
* binfmt using::                Using the mechanism.
* binfmt control::              Controlling the mechanism.
* binfmt hints::                Hints.
* binfmt scheme::               Running Scheme programs.
@end menu


@c page
@node binfmt overview
@appendixsec Overview of the mechanism

@cindex Binary formats, kernel support
@cindex Binary executables, kernel support

This kernel feature allows us to invoke almost every program by simply
typing its name in the shell.  This includes for example compiled Java,
Python or Emacs programs, and of course Scheme programs.

The feature is implemented in the @code{binfmt_misc} kernel module; it
recognises the binary file type by matching some bytes at the beginning
of the file with a magic byte sequence (masking out specified bits) we
have supplied; it can also recognise a filename extension.


@c page
@node binfmt loading
@appendixsec Loading the module


If the module is installed on the system, we will find it in:

@example
/lib/modules/<version>/kernel/fs/binfmt_misc.ko
@end example

@noindent
and to load it we do (as root):

@example
/sbin/modprobe binfmt_misc
@end example

@noindent
for example on a Slackware Linux installation, loading can be done by
uncommenting the relevant line in @file{/etc/rc.d/rc.modules}.

Then we add the following line to @file{/etc/fstab}:

@example
none /proc/sys/fs/binfmt_misc binfmt_misc defaults 0 0
@end example

@noindent
so that the @code{binfmt_misc} directory can be mounted and will be
mounted when booting the system.  We can freely mount and unmount this
virtual file system with (as root):

@example
$ mount  /proc/sys/fs/binfmt_misc
$ umount /proc/sys/fs/binfmt_misc
@end example

@noindent
but this is rarely useful.  There are other means of enabling and
disabling the feature.


@c page
@node binfmt registry
@appendixsec The registry


To actually register a new binary type, we have to set up a string like:

@example
:name:type:offset:magic:mask:interpreter:
@end example

@noindent
and echo it to @file{/proc/sys/fs/binfmt_misc/register}.  We can choose
the @code{:} character upon our needs and we can omit a field by simply
using an empty string; for example if we omit the offset and the mask
fields:

@example
:name:type::magic::interpreter:
@end example

Fields description follows.

@table @code
@item name
It is an identifier string.  A new @file{/proc} file will be created
with this name under @file{/proc/sys/fs/binfmt_misc}, and we can use it
to query and reset the configuration status.

@item type
It is the type of file recognition.  If set to @code{M} activates first
bytes recognition (also known as ``magic'' recognition); if set to
@code{E} activates file name extension recognition.

@item offset
It is the zero--based offset of the magic/mask in the file, counted in
bytes.  It is used only with magic recognition.  It defaults to zero.

@item magic
It is the byte sequence @code{binfmt_misc} will look for file
recognition.

The magic string may contain hex--encoded characters like @code{\x0a} or
@code{\xA4}.  When echoing the record from a shell environment, we will
have to quote the backslash: @code{\\x0a}.

If we chose filename extension matching, this is the extension to be
recognised (without the @code{.}, the @code{\x0a} specials are not
allowed).  Extension matching is case sensitive.

@item mask
It is a bitmask.  We can mask out some bits from matching by supplying a
string with the same length of @code{magic}.  The mask is ANDed with the
byte sequence from the file.  It defaults to @code{0xff}.

@item interpreter
It is the program that should be invoked with the binary as first
argument.  @strong{We have to specify the full path.}
@end table

There are some restrictions:

@itemize
@item
the whole @file{register} string may not exceed 255 characters;

@item
the magic must reside in the first 128 bytes of the file, i.e.
offset+size(magic) has to be less than 128;

@item
the interpreter string may not exceed 127 characters.
@end itemize


@c page
@node binfmt using
@appendixsec Using the mechanism


We may want to add the binary formats in one of the shell scripts that
are executed at system boot time.  For example, on a Slackware Linux
system we can use @file{/etc/rc.d/rc.local}.

To do it we can add to the shell script a chunk of code like the
following, which activates the mechanism for Scheme programs compiled
with Larceny:

@example
if test -f /proc/sys/fs/binfmt_misc/register ; then
  test -f /proc/sys/fs/binfmt_misc/LARCENY && \
    echo -1 >/proc/sys/fs/binfmt_misc/LARCENY
  echo \
   ':LARCENY:M:2:fasl::/opt/larceny/last/scheme-script:' \
   >/proc/sys/fs/binfmt_misc/register
fi
@end example

@noindent
which does the following:

@enumerate
@item
verify if the @code{binfmt_misc} kernel module has been loaded by
testing the existence of the @file{register} file; if the module is not
there, nothing happens;

@item
remove the configuration from the registry if it is already there; this
is done by testing the existence of the special file:

@example
/proc/sys/fs/binfmt_misc/LARCENY
@end example

@noindent
echoing @code{-1} in it removes the Larceny record;

@item
echo the Larceny record into the registry with the following fields:

@table @code
@item LARCENY
It is the name of the special file representing the record in the
registry.

@item M
Activates first bytes recognition.

@item 2
The offset of the magic string into the file.

@item fasl
The magic string itself.

@item /opt/larceny/last/scheme-script
The full pathname to the Larceny @rnrs{6} runtime system executable.
@end table

We see that the @code{mask} field is set to the empty string, that is:
no mask is applied and @code{fasl} is matched literally.
@end enumerate

To determine the magic string for the Larceny compiled programs: compile
a program and read the compiled file, for example with @command{less} or
Emacs; we will see that the first bytes are @code{#!fasl}.

We have to care about the order of added entries: entries added later
are matched first.


@c page
@node binfmt control
@appendixsec Controlling the mechanism


@subsubheading Overall control

We can enable/disable the mechanism by echoing @code{0} (to disable) or
@code{1} (to enable) to the special file:

@example
/proc/sys/fs/binfmt_misc/status
@end example

@noindent
that is, at the shell prompt and with superuser privileges:

@example
# enable
$ echo 1 >/proc/sys/fs/binfmt_misc/status

# disable
$ echo 0 >/proc/sys/fs/binfmt_misc/status
@end example

Applying the command @command{cat} to the @file{status} special file
tells us the current status of @code{binfmt_misc}:

@example
$ cat /proc/sys/fs/binfmt_misc/status
enabled
@end example

We can remove all the entries from the registry echoing @code{-1} to
@file{status}; at the shell prompt and with superuser privileges:

@example
$ echo -1 >/proc/sys/fs/binfmt_misc/status
@end example

@noindent
removing the entries and disabling the mechanism are two distinct
operations.


@subsubheading Specific format control

We can enable/disable recognition of a single binary format by echoing
@code{0} or @code{1} to the format specific special file under
@file{/proc/sys/fs/binfmt_misc}.  For example for @code{LARCENY}, at the
shell prompt and with superuser privileges:

@example
# enable
$ echo 1 >/proc/sys/fs/binfmt_misc/LARCENY

# disable
$ echo 0 >/proc/sys/fs/binfmt_misc/LARCENY
@end example

@noindent
disabled formats are @strong{not} removed from the internal table.

Applying the command @command{cat} to the format's special file tells us
the current status of that format and its configuration; for example for
@code{LARCENY}, at the shell prompt:

@example
$ cat /proc/sys/fs/binfmt_misc/LARCENY
@end example

We can remove a specific format entry from the registry echoing
@code{-1} to its special file; for @code{LARCENY} at the shell prompt
and with superuser privileges:

@example
$ echo -1 >/proc/sys/fs/binfmt_misc/LARCENY
@end example

@noindent
removing the entry and disabling the mechanism for a format are two
distinct operations.


@c page
@node binfmt hints
@appendixsec Hints


If we want to pass command line arguments to the runtime executable, we
must write a wrapper script for it.  There is no way to embed the
arguments in the registry record.

The runtime must @strong{not} look in the @env{PATH} environment
variable for the filename; the kernel passes it the full filename to
use.  Using the @env{PATH} can cause unexpected behaviour and be a
security hazard.


@c page
@node binfmt scheme
@appendixsec Running Scheme programs


The @code{binfmt_misc} mechanism can be used to run both source and
compiled Scheme programs.  @value{PACKAGE} adheres to the following
rules, which we can change by customising the files in the distribution:

@enumerate
@item
Ikarus Scheme source programs are recognised if the file starts with the
string @code{;;;!ikarus}, the @code{binfmt_misc} configuration will look
for the string @code{!ikarus} at zero--based offset 3 in the file.

@item
Larceny Scheme source programs are recognised if the file starts with
the string @code{;;;!larceny}, the @code{binfmt_misc} configuration will
look for the string @code{!larceny} at zero--based offset 3 in the file.

@item
Mosh Scheme source programs are recognised if the file starts with the
string @code{;;;!mosh}, the @code{binfmt_misc} configuration will look
for the string @code{!mosh} at zero--based offset 3 in the file.

@item
Ypsilon Scheme source programs are recognised if the file starts with
the string @code{;;;!ypsilon}, the @code{binfmt_misc} configuration will
look for the string @code{!ypsilon} at zero--based offset 3 in the file.

@item
Larceny Scheme compiled programs are recognised if the file starts with
the string @code{#!fasl}, the @code{binfmt_misc} configuration will look
for the string @code{fasl} at zero--based offset 2 in the file.

@item
The @code{binfmt_misc} configuration is stored in a Bourne shell script
(@file{/bin/sh} interpreter) named @file{rc.scheme} installed in the
@code{$(sysconfdir)/rc.d} directory.  If the default configuration is
not changed, this ends up being @file{/usr/local/etc/rc.d/rc.scheme}.

The script can be executed at system boot time and again later any
number of times: at each run it resets and reinitialises the
configuration for Scheme programs.

@item
Ikarus Scheme source programs are run through a Bourne shell script
(@file{/bin/sh} interpreter) named @file{ikarus-scheme-script} installed
in the @code{$(bindir)} directory.  The script calls Ikarus with:

@example
exec "$IKARUS" --r6rs-script "$@@"
@end example

@noindent
where @code{$IKARUS} is the Ikarus executable discovered by the
@command{configure} script of @value{PACKAGE}.

@item
Larceny Scheme both source and compiled programs are run through a
Bourne shell script (@file{/bin/sh} interpreter) named
@file{larceny-scheme-script} installed in the @code{$(bindir)}
directory.  The script calls Larceny with:

@example
exec "$LARCENY" -r6rs -program "$@@"
@end example

@noindent
where @code{$LARCENY} is the Larceny executable discovered by the
@command{configure} script of @value{PACKAGE}.

@item
Mosh Scheme source programs are run through a Bourne shell script
(@file{/bin/sh} interpreter) named @file{mosh-scheme-script} installed
in the @code{$(bindir)} directory.  The script calls Mosh with:

@example
exec "$MOSH" "$@@"
@end example

@noindent
where @code{$MOSH} is the Mosh executable discovered by the
@command{configure} script of @value{PACKAGE}.

@item
Ypsilon Scheme source programs are run through a Bourne shell script
(@file{/bin/sh} interpreter) named @file{ypsilon-scheme-script}
installed in the @code{$(bindir)} directory.  The script calls Ypsilon
with:

@example
exec "$YPSILON" --r6rs --compatible "$@@"
@end example

@noindent
where @code{$YPSILON} is the Ypsilon executable discovered by the
@command{configure} script of @value{PACKAGE}.
@end enumerate

The reasons for installing custom scripts are:

@enumerate
@item
A wrapper script allows more customisation of the execution environment
by setting system environment variables, for example to select a library
search path.

@item
A wrapper allows the selection of command line arguments that cannot be
embedded in the @code{binfmt_misc} registry entry.

@item
Installation of specific Scheme implementation @command{scheme-script}
executables is problematic because of file name collision.  For example:
both Larceny and Ikarus try to install a @command{scheme-script}
program, which one is installed?

The approach of @value{PACKAGE} is to use none of those and rely on
custom scripts.
@end enumerate

The files installed by @value{PACKAGE} provide no way to run all the
(portable) @rnrs{6} Scheme programs with a single Scheme implementation.
But the installed files can be easily customised to do so.  Let's say
that we want to run all the @rnrs{6} Scheme programs with Ypsilon; we
can add the following chunk of code to @file{rc.scheme}:

@example
BINFMT_SCHEME=$@{BINFMT_DIR@}/R6RS_SCHEME

if test "$@{BINFMT_ENABLE_YPSILON@}" = yes ; then
    echo -n enabling binfmt R6RS Scheme scripts execution...
    test -f $@{BINFMT_SCHEME@} && echo -1 >$@{BINFMT_SCHEME@}
    echo ":R6RS_SCHEME:M:3:!scheme::$@{BINFMT_PROG_YPSILON@}:" \
        >$@{BINFMT_REGISTER@}
    echo done
else
    echo skipping binfmt R6RS Scheme programs execution
fi
@end example

@noindent
that way all the files starting with @code{;;;!scheme} are launched with
the Ypsilon executable.

@c page
@node credits
@appendix Credits


This appendix collects license notices and copyright assignments for
code and documentation included in @value{PACKAGE}.


@subsubheading Language and implementations

Scheme is a statically scoped and properly tail--recursive dialect of
the Lisp programming language invented by Guy Lewis Steele Jr. and
Gerald Jay Sussman.  It was designed to have an exceptionally clear and
simple semantics and few different ways to form expressions.

Several libraries depend upon the port to @rnrs{6} of the Scheme
Requests For Implementation (@srfi{}).  The original @srfi{} code is the
work of many authors, reference of which you can find the at the top of
the source code files; the port to @rnrs{6} included in this project is
the work of Derick Eddington.

Ikarus is an almost @rnrs{6} compliant implementation of the Scheme
programming language.  It is the creation of Abdulaziz Ghuloum.

Larceny is an also @rnrs{6} compliant implementation of the Scheme
programming language.  It is the creation of William D.  Clinger, Lars T
Hansen and Lightship Software, Incorporated, with contributions by other
people.

Mosh is an @rnrs{6} compliant implementation of the Scheme programming
language.  It is the creation of Taro Minowa (higepon).

Ypsilon is an @rnrs{6} compliant implementation of the Scheme
programming language.  It is the creation of Yoshikatsu Fujita at
LittleWing Company.

@value{PACKAGE} is the creation of Marco Maggi.  If this work exists, it
is because of the @gnu{} Software tools he uses all the time.


@subsubheading Documentation: language definition, base and standard library

The chapters @ref{lang,The Scheme language}, @ref{baselib,Base library}
and @ref{stdlib,Standard library} are derived from the original
``Revised^6 Report on the Algorithmic Language Scheme'' document and the
original ``Revised^6 Report on the Algorithmic Language Scheme ----
Standard Libraries'', authored by the following people:

@quotation
@noindent
MICHAEL SPERBER@*
R. KENT DYBVIG, MATTHEW FLATT, ANTON VAN STRAATEN@*
(Editors)

@noindent
RICHARD KELSEY, WILLIAM CLINGER, JONATHAN REES@*
(Editors, Revised^5 Report on the Algorithmic Language Scheme)

@noindent
ROBERT BRUCE FINDLER, JACOB MATTHEWS@*
(Authors, formal semantics)
@end quotation

@noindent
with various contributions by the following people:

@quotation
Hal Abelson, Norman Adams, David Bartley, Gary Brooks, William Clinger,
Daniel Friedman, Robert Halstead, Chris Hanson, Christopher Haynes,
Eugene Kohlbecker, Don Oxley, Kent Pitman, Jonathan Rees, Guillermo
Rozas, Guy L. Steele Jr., Gerald Jay Sussman, and Mitchell Wand.
@end quotation

@noindent
and a lot of discussions and debates by the Scheme community.  According
to the following quotation from the original document:

@quotation
We intend this report to belong to the entire Scheme community, and so
we grant permission to copy it in whole or in part without fee.  In
particular, we encourage implementors of Scheme to use this report as a
starting point for manuals and other documentation, modifying it as
necessary.
@end quotation

@noindent
the documents are meant to be in the public domain.  They are available
at:

@center @url{http://www.r6rs.org/}
@center @url{http://www.r6rs.org/r6rs-errata.html}


@subsubheading Documentation: Scheme requests for implementation

The original @srfi{} documents are the work of many authors and are
available at:

@center @url{http://srfi.schemers.org/}

@noindent
this document embeds and modifies some of them.  The original document
licenses are included in this document, and can be found in the relevant
sections.


@subsubheading @ansrfi{0}: Feature based conditional expansion construct

Copyright @copyright{} 1999 Marc Feeley .  All Rights Reserved.

This document and translations of it may be copied and furnished to
others, and derivative works that comment on or otherwise explain it or
assist in its implementation may be prepared, copied, published and
distributed, in whole or in part, without restriction of any kind,
provided that the above copyright notice and this paragraph are included
on all such copies and derivative works.  However, this document itself
may not be modified in any way, such as by removing the copyright notice
or references to the Scheme Request For Implementation process or
editors, except as needed for the purpose of developing SRFIs in which
case the procedures for copyrights defined in the SRFI process must be
followed, or as required to translate it into languages other than
English.

The limited permissions granted above are perpetual and will not be
revoked by the authors or their successors or assigns.

This document and the information contained herein is provided on an
``AS IS'' basis and THE AUTHOR AND THE SRFI EDITORS DISCLAIM ALL
WARRANTIES, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO ANY
WARRANTY THAT THE USE OF THE INFORMATION HEREIN WILL NOT INFRINGE ANY
RIGHTS OR ANY IMPLIED WARRANTIES OF MERCHANTABILITY OR FITNESS FOR A
PARTICULAR PURPOSE.


@subsubheading @ansrfi{1}: List library

Certain portions of the @ansrfi{1} document (the specific, marked
segments of text describing the R5RS procedures) were adapted with
permission from the @rnrs{5} report.

All other text is copyright @copyright{} 1998, 1999 Olin Shivers.  All
Rights Reserved.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the
``Software''), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ``AS IS'', WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


@subsubheading @ansrfi{2}: An @func{and} with local bindings

Copyright @copyright{} 1998 Oleg Kiselyov.  All Rights Reserved.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the
``Software''), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ``AS IS'', WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


@subsubheading @ansrfi{8}: Binding multiple values

Copyright @copyright{} 1999 John David Stone.  All Rights Reserved.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the
``Software''), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ``AS IS'', WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


@subsubheading @ansrfi{13}: Strings library

Certain portions of this document, the specific, marked segments of text
describing the @rnrs{5} procedures, were adapted with permission from
the @rnrs{5} report.

All other text is copyright @copyright{} 1998, 1999, 2000 Olin Shivers.
All Rights Reserved.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the
``Software''), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ``AS IS'', WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


@subsubheading @ansrfi{14}: Character sets

Certain portions of this document---the specific, marked segments of
text describing the @rnrs{5} procedures---were adapted with permission
from the @rnrs{5} report editors.

All other text is Copyright @copyright{} 1998, 1999, 2000 Olin Shivers.
All Rights Reserved.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the
``Software''), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ``AS IS'', WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.




@subsubheading @ansrfi{26}: Notation for specializing parameters without currying

Copyright @copyright{} 2002 Sebastian Egner.  All Rights Reserved.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the
``Software''), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ``AS IS'', WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


@subsubheading @ansrfi{31}: A special form for recursive evaluation

Copyright @copyright{} 2002 Dr. Mirko Luedde
@email{Mirko.Luedde@@SAP.com}.  All Rights Reserved.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the
``Software''), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ``AS IS'', WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


@subsubheading @ansrfi{38}: External representation for data with shared structure

Copyright @copyright{} 2003 Ray Dillinger.  All Rights Reserved.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the
``Software''), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ``AS IS'', WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


@subsubheading @ansrfi{39}: Parameter objects

Copyright @copyright{} 2002 Marc Feeley.  All Rights Reserved.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the
``Software''), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ``AS IS'', WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


@subsubheading @ansrfi{43}: Vectors library

Copyright @copyright{} 2003 Taylor Campbell.  All rights reserved.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the
``Software''), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ``AS IS'', WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


@subsubheading @ansrfi{78}: Library for lightweight test suites

Copyright @copyright{} 2005--2006 Sebastian Egner.  All Rights Reserved.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the
``Software''), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ``AS IS'', WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


@subsubheading @ansrfi{98}: Environment variables

Copyright @copyright{} 2008 Taro Minowa (Higepon).  All Rights Reserved.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the
``Software''), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ``AS IS'', WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

@c page
@c ------------------------------------------------------------
@c Licenses.
@c ------------------------------------------------------------

@include gpl-3.0.texiinc
@include fdl-1.3.texiinc


@c page
@node references
@appendix Bibliography and references


@noindent
@emph{Revised^6 Report on the Algorithmic Language Scheme.}  Michael
Sperber and R. Kent Dybvig and Matthew Flatt and Anton Van Straaten
(Editors).  2007.

@noindent
@emph{Revised^6 Report on the Algorithmic Language Scheme---Standard
Libraries.}  Michael Sperber and R. Kent Dybvig and Matthew Flatt and
Anton Van Straaten (Editors). 2007.

@noindent
@emph{Scheme Request for Implementation 41: Streams.}  Philip L. Bewig.
2007.  @url{http://srfi.schemers.org/srfi-41/srfi-41.html}

@noindent
@bibref{DYBGUA} @emph{Guardians in a generation--based garbage
collector}.  R. Kent Dybvig and Carl Bruggeman and David Eby.  PLDI '93,
pages 207--216, June 1993.  Introduces guardians and describes the
implementation of guardians and weak pairs in Chez Scheme.

@noindent
@emph{Don't stop the BiBOP: Flexible and efficient storage management
for dynamically--typed languages.}  R. Kent Dybvig and David Eby and
Carl Bruggeman.  Technical Report 400, Indiana University, March 1994.
Describes Chez Scheme's storage management system.

@noindent
@emph{Generation Friendly Eq Hash Tables.}  Abdulaziz Ghuloum and
R. Kent Dybvig. In @emph{Proceedings of the 2007 Workshop on Scheme and
Functional Programming}, pages 207--216.  Universite Laval Technical
Report DIUL-RT-0701, 2007.

@noindent
@emph{Extending the Scope of Syntactic Abstraction.}  Oscar Waddell.
PhD thesis, Indiana University Computer Science Department, August 1999.
@url{http://www.cs.indiana.edu/~owaddell/papers/thesis.ps.gz}

@noindent
@emph{Extending the Scope of Syntactic Abstraction.}  Oscar Waddell and
R. Kent Dybvig.  In @emph{Conference Record of POPL'99: The 26th ACM
SIGPLAN-SIGACT Symposium on Principles of Programming Languages}, pages
203--213, January 1999.
@url{http://www.cs.indiana.edu/~dyb/papers/popl99.ps.gz}

@noindent
@emph{Chez Scheme Version 7 User's Guide.}  R. Kent Dybvig.  Cadence
Research Systems, 2005.  User's guide and reference manual for Chez
Scheme Version 7.  @url{http://www.scheme.com/csug7/}

@noindent
@emph{Implicit phasing for R6RS libraries.}  Abdulaziz Ghuloum and
R. Kent Dybvig.  In @emph{ICFP '07: Proceedings of the 2007 ACM SIGPLAN
international conference on Functional programming}, pages 303--314.
New York, NY, USA, 2007.  ACM.



@c page
@node examples
@appendix Scheme programming examples


@menu
* examples macros::             Exploring macro mechanisms.
@end menu


@c page
@node examples macros
@appendixsec Exploring macro mechanisms


The examples in this section are meant neither to be useful nor to be
meaningful; their only purpose is to highlight how the
@func{define-syntax}, @func{syntax-case} and @func{syntax} mechanism
works.

When the examples are especially simple, or do not require particular
features of @func{syntax-case}, the @func{syntax-rules} transformer is
used.  This is the case of some of the pattern examples.


@menu
* examples macros phases::      Bits about phases.
* examples macros underscore::  About the underscore.
* examples macros patterns::    About writing patterns.
* examples macros literals::    About literal identifiers.
* examples macros syntax::      Syntax objects and you.
@end menu



@c page
@node examples macros phases
@appendixsubsec Bits about phases


@quotation
@strong{The following ramblings do not aim at being rigorous.}
@end quotation

@noindent
Let's look at this code:

@example
(import (rnrs))

(define (gasp arg)
  arg)

(define (gulp arg)
  (gasp arg))

(gulp 123)
@end example

@noindent
Oll Korrect: we define two functions, one of which invokes the other in
its body.  Now look at this:

@example
(import (rnrs))

(let ()
  (define (gasp arg)
    arg)
  #f)

(let ()
  (define (gulp arg)
    (gasp arg))
  (gulp 123))
@end example

@noindent
this will not work: when @func{gulp} is invoked @func{gasp} does not
exist anymore, because it is ``local'' to the first @func{let}.  The two
functions exist at different ``times'' (using correct jargon for this
case: they belong to distinct @emph{regions}).

Let's state this clearly: Scheme allows sets of functions to exist at
different times.  With more generality: Scheme allows sets of
identifier/value bindings to exist at different times.  Fine.  Now we
know that, technologically speaking, we have this option.

In the two--@func{let} example above the two ``times'', the two regions,
correspond to two distinct chunks of code: the @func{let} forms.  We ask
ourselves if it is possible to make two set of functions exist at
different times in the same chunk of code; with this feature, we could
use the first set to compute values and build code that are used by the
second set.  It would be similar to preprocessing C language files with
the C language preprocessor.  The answer is that: yes, it is possible.

Inhabitants of the Scheme Pantheon decided that, in the same way as
@func{let} ``times'' are called @emph{regions}, these other ``times''
are called @emph{phases}.

To do such a thing we have to mark, select, partition, discriminate,
paint with different colours the functions that are meant to belong to
different phases.  We can imagine that there are special versions of
@func{define} and @func{eval} that select a phase:

@example
;; Imaginary code.

(define-for-phase-1 (gasp arg1)
  (cons 1 arg1))

(define-for-phase-2 (gulp arg2)
  (cons 2 (gasp arg2)))

(evaluate-in-phase-2
  (gulp 9))
@end example

@noindent
so that after evaluation phase @code{1} is finished, we have code
equivalent to:

@example
;; Imaginary code.

(define-for-phase-2 (gulp arg)
  (cons 2 (cons 1 arg)))

(evaluate-in-phase-2
  (gulp 9))
@end example

@noindent
which is equivalent to:

@example
;; Imaginary code.

(define (gulp arg)
  (cons 2 (cons 1 arg)))

(gulp 9)
@end example

@noindent
which is ``ordinary'' Scheme code that computes the improper list
@code{(2 1 . 9)}.

It turns out that code like this is really ugly, and the Scheme Pantheon
decided that a chunk of code, by itself, should have only two phases
called @code{expand} and @code{run}.  The @code{run} phase is the normal
one: it is when the program should do whatever it is meant to do (talk
to the user, download web pages, etc).  The @code{expand} phase is the
special one: it is when the program does the precomputation of values
and code.

So it is enough to mark one set of functions, and the Pantheon decided
that it is the @code{expand} set that must be marked differently.  Let's
look at:

@example
(import (rnrs) (srfi lightweight-testing))
(check-set-mode! 'report-failed)

(define-syntax expand-time-func
  (lambda (syntax-object)        ;; this is the expand
    123))                        ;; phase function

(define (run-time-func arg)
  (expand-time-func arg))

(check
 (run-time-func 456)
 => 123)

(check-report)
@end example

@noindent
where we have marked @func{expand-time-func} to exist at @code{expand}
time using @func{define-syntax}.  After evaluation phase @code{expand}
is finished the code is equivalent to:

@example
(import (rnrs) (srfi lightweight-testing))
(check-set-mode! 'report-failed)

(define (run-time-func arg)
  123)

(check
 (run-time-func 456)
 => 123)

(check-report)
@end example

@noindent
and everything works as we expect.

The single argument that is handed to the @code{expand} time function is
a weird beast called ``syntax object''.  All this section about
@func{syntax-case} deals with using syntax objects.

In the example above, it looks like @func{expand-time-func} is applied
to the @code{arg} argument of @func{run-time-func}; but what is actually
handed to the function is a syntax object holding information about the
whole form @code{(expand-time-func arg)} and the context where it was
located.

There are functions to extract and use values in the syntax object.  For
example the following script evaluates successfully:

@example
(import (rnrs) (srfi lightweight-testing))
(check-set-mode! 'report-failed)

(define-syntax expand-time-func
  (lambda (syntax-object)
    (caddr (syntax->datum syntax-object))))

(define (run-time-func arg)
  (expand-time-func arg 123))

(check
 (run-time-func 456)
 => '123)

(check-report)
@end example



@c page
@node examples macros underscore
@appendixsubsec About the underscore


It is stated that:

@quotation
Underscores, @code{_}, also match arbitrary input subforms but are not
pattern variables and so cannot be used to refer to those elements.
Multiple underscores may appear in a @meta{pattern}.
@end quotation

@noindent
@xref{baselib transformers,syntax-rules}.  It means that the following
script will evaluate successfully:

@example
(import (rnrs) (srfi lightweight-testing))
(check-set-mode! 'report-failed)

(check
 (let ()
   (define-syntax doit
     (syntax-rules ()
       ((doit _) 1)))

   (list (doit '(a b c))
         (doit 2)
         (doit '(a (b (c)) d (e)))))
 => '(1 1 1))

(check
 (let ()
   (define-syntax doit
     (syntax-rules ()
       ((doit _ _) 1)))

   (list (doit '(a b c) '(d e f))
         (doit 1 2)))
 => '(1 1))

(check-report)
@end example

The use of @code{_} allows us not to invent pattern variable names for
subpatterns that we do not care about in a @meta{syntax rule}.  In the
following:

@example
(define-syntax doit
  (syntax-rules ()
    ((doit ?arg)
     1)))
@end example

@noindent
we do not care about the actual value of @code{?arg}, so why should we
be forced to invent a name for it?  Better to write:

@example
(define-syntax doit
  (syntax-rules ()
    ((doit _)
     1)))
@end example

It is customary to use @code{_} in the pattern of @meta{syntax rule}s as
a placeholder for the macro use name:

@example
(define-syntax doit
  (syntax-rules ()
    ((_ ?arg)
     (list ?arg))))
@end example

@noindent
but there is nothing special about this: it is convenient because one
has only one char to type.  Notice that when writing @func{syntax-case}
forms using @code{_} as first subpattern of patterns, while still
possible, may need to be avoided because there is the need to use the
macro use identifier, and it is not possible to do it with @code{_}.



@c page
@node examples macros patterns
@appendixsubsec About writing patterns


@subsubheading At least one element in a subpattern with ellipsis


When we want to specify a pattern or subpattern that matches at least
one element from the input form we have to use @code{?elm0 ?elm ...};
this is because the @code{?elm ...} subpattern matches @strong{zero} or
more elements.  So the following correctly returns @code{error} when
zero elements are used:

@example
(import (rnrs) (srfi lightweight-testing))
(check-set-mode! 'report-failed)

(define-syntax doit
  (syntax-rules ()
    ((_ ?elm0 ?elm ...)
     '(?elm0 ?elm ...))
    ((_ . ?rest)
     'error)))

(check (doit 1)         => '(1))
(check (doit 1 2 3)     => '(1 2 3))
(check (doit)           => 'error)

(check-report)
@end example

@noindent
while the following happily matches zero arguments to the macro:

@example
(import (rnrs) (srfi lightweight-testing))
(check-set-mode! 'report-failed)

(define-syntax doit
  (syntax-rules ()
    ((_ ?elm ...)
     '(?elm ...))
    ((_ . ?rest)
     'error)))

(check (doit 1)         => '(1))
(check (doit 1 2 3)     => '(1 2 3))
(check (doit)           => '())

(check-report)
@end example

@noindent
here the @code{(_ . ?rest)} is never matched.

@c ------------------------------------------------------------


@subsubheading Matching proper lists but not improper lists


When the @code{?arg ...} and @code{. ?args} subpatterns appear at the
end of a list, they both match all the other elements (the cdr), which
can be zero or more.  There is one difference: while @code{?arg ...}
matches only a proper list, @code{. ?args} matches both proper and
improper lists.  So the following works always:

@example
(import (rnrs) (srfi lightweight-testing))
(check-set-mode! 'report-failed)

(define-syntax doit
  (syntax-rules ()
    ((_ . ?args)
     '?args)))

(check (doit 1)         => '(1))
(check (doit 1 2 3)     => '(1 2 3))
(check (doit 1 . 2)     => '(1 . 2))

(check-report)
@end example

@noindent
but the following raises an ``invalid syntax'' error in the improper
list check:

@example
(import (rnrs) (srfi lightweight-testing))
(check-set-mode! 'report-failed)

(define-syntax doit
  (syntax-rules ()
    ((_ ?arg ...)
     '(?arg ...))))

(check (doit 1)         => '(1))
(check (doit 1 2 3)     => '(1 2 3))
(check (doit 1 . 2)     => '(1 . 2))

(check-report)
@end example

Notice that the @meta{formals} argument of a @func{lambda} can be an
improper list when we use the @code{(arg1 arg2 . args)} form to bind the
rest arguments to @code{args}.  So to properly match @meta{formals} the
general @meta{formals} we have to use the @code{?arg . ?args}
subpattern.



@c page
@node examples macros literals
@appendixsubsec About literal identifiers


It is stated that:

@quotation
A @meta{literal} identifier matches an input subform if and only if the
input subform is an identifier and either both its occurrence in the
input expression and its occurrence in the list of literals have the
same lexical binding, or the two identifiers have the same name and both
have no lexical binding.
@end quotation

@noindent
@xref{baselib transformers,syntax-rules}.  Literal identifiers are
useful to ``mark'' subforms of the input form that have special meaning.
In a way, @meta{literal}s can act as comments.

The following script will evaluate successfully:

@example
(import (rnrs) (srfi lightweight-testing))
(check-set-mode! 'report-failed)

;;;In this  example 'alpha'  has NO lexical  binding in  both the
;;;literals list of SYNTAX-RULES and the input expression.
(check
 (let ()
   (define-syntax doit
     (syntax-rules (alpha)
       ((_ ?arg1 (alpha ?arg2))
        (list ?arg1 ?arg2))))

   (doit 1 (alpha 2)))
 => '(1 2))

;;;In this example  'alpha' has THE SAME lexical  binding in both
;;;the literals list of SYNTAX-RULES and the input expression.
(check
 (let ((alpha 123))
   (define-syntax doit
     (syntax-rules (alpha)
       ((_ ?arg1 (alpha ?arg2))
        (list ?arg1 ?arg2))))

   (doit 1 (alpha 2)))
 => '(1 2))

(check-report)
@end example

@noindent
while both the following examples will fail with a compile time error,
because @code{alpha} has @strong{different} lexical bindings in the
literals list of @func{syntax-rules} and the input expression:

@example
(check
 (let ()
   (define-syntax doit
     (syntax-rules (alpha)
       ((_ ?arg1 (alpha ?arg2))
        (list ?arg1 ?arg2))))

   (let ((alpha 123))
     (doit 1 (alpha 2))))
 => '(1 2))

(check
 (let ()
   (define-syntax doit
     (let ((alpha 123))
       (syntax-rules (alpha)
         ((_ ?arg1 (alpha ?arg2))
          (list ?arg1 ?arg2)))))

     (doit 1 (alpha 2)))
 => '(1 2))
@end example

@noindent
quoting the literal makes no difference:

@example
(check
 (let ()
   (define-syntax doit
     (syntax-rules (alpha)
       ((_ ?arg1 ((quote alpha) ?arg2))
        (list ?arg1 ?arg2))))

   (let ((alpha 123))
     (doit 1 ((quote alpha) 2))))
 => '(1 2))
@end example

@noindent
it is still a compile time error.


@c page
@node examples macros syntax
@appendixsubsec Syntax objects and you


Syntax objects are constructed with @func{syntax}.


@menu
* examples macros syntax not::   What we do not do.
@end menu


@c page
@node examples macros syntax not
@appendixsubsubsec What we do not do


We have to make clear that we cannot extract at will informations from a
syntax object; only in particular context we can do something with
them.  So the following is useless:

@example
(define (doit stx)
  ---)

(let ((alpha 1)
      (beta  2)
      (delta 3))
  (doit (syntax (alpha beta delta))))
@end example

@noindent
the expansion of @func{syntax} (which is a syntax, not a function)
creates a syntax object that is used as argument for @func{doit}; but we
can do nothing useful with it in the body of @func{doit}, neither for
actual solution of programming problems, nor to show how syntax objects
work.

The following checks will work, but they only demonstrate that we can
shoot the breeze with @func{syntax} and @func{syntax->datum}:

@example
(check
    (let ()
      (define (doit stx)
        (syntax-case stx ()
          ((use ?arg0 ?arg1)
           (syntax->datum (syntax ?arg0)))))
      (let ((alpha 1)
            (beta  2)
            (delta 3))
        (doit (syntax (alpha beta delta)))))
  => 'beta)

(check
    (let ()
      (define (doit stx)
        (syntax-case stx ()
          ((use ?arg0 ?arg1)
           (list (syntax->datum (syntax use))
                 (syntax->datum (syntax ?arg0))
                 (syntax->datum (syntax ?arg1))))))
      (let ((alpha 1)
            (beta  2)
            (delta 3))
        (doit (syntax (alpha beta delta)))))
  => '(alpha beta delta))

(check
    (let ()
      (define (doit stx)
        (syntax-case stx ()
          ((use ?arg0 ?arg1)
           (syntax->datum (syntax (use ?arg0 ?arg1))))))
      (let ((alpha 1)
            (beta  2)
            (delta 3))
        (doit (syntax (alpha beta delta)))))
  => '(alpha beta delta))
@end example


@c page
@node concept index
@appendix An entry for each concept

@printindex cp

@node function index
@appendix An entry for each function.

@printindex fn

@node variable index
@appendix An entry for each variable.

@printindex vr

@node type index
@appendix An entry for each type.

@printindex tp

@contents
@bye

@c end of file
@c Local Variables:
@c mode: texinfo
@c TeX-master: t
@c End:
